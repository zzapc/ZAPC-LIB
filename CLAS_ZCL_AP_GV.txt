
class ZCL_AP_GV definition
  public
  create public .

public section.

  constants C_MOLGA type MOLGA value '04' ##NO_TEXT.
  data RETURN type BAPIRETURN .
  data FRAMEDATA type BAPITRMAIN .
  data STATUS type BAPITRSTAO .
  data I_RECEIPTS type BAPITRVREO_T .
  data I_COSTDIST_TRIP type BAPITRVCOT_ITAB .
  data I_AMOUNTS type BAPITRVSUM_T .
  data I_EMP_INFO type BAPITRVEMP_ITAB .
  data RECEIPT type BAPITRVREO .
  data AMOUNT type BAPITRVSUM .
  data COSTDIST_TRIP type BAPITRVCOT .
  data EMP_INFO type BAPITRVEMP .
  data PTK99 type PTK99 .
  data I_PTK99 type PTK99_T .
  data I_STOPOVER type BAPITRVSTO_ITAB .
  data STOPOVER type BAPITRVSTO .
  data I_ADVANCES type PTRV_ADVANCES_ITAB .
  data I_ADDINFO type BAPITRADDI_ITAB .
  data ADDINFO type BAPITRADDI .
  data TEXT type BAPITRTEXT .
  data I_TEXT type BAPITRTEXT_ITAB .

  class-methods GET_VALOR_PROP_CLASE
    importing
      !CLASE type ANY
      !FECHA type DATS default SY-DATUM
      !MOREI type ANY default C_MOLGA
    preferred parameter CLASE
    returning
      value(VALOR) type T706B2-BETRG .
  methods GET_DETAIL
    importing
      !PERNR type PERSNO
      !TRIPNO type BAPITRIP-TRIPNO
      !NO_BAPI type ABAP_BOOL default '' .
  class-methods GET_TABLA_USER_DATA
    importing
      !PERNR type PERSNO
      !TRIPNO type BAPITRIP-TRIPNO
      !SOLO_ULTIMO_PERIODO type ABAP_BOOL default 'X'
    returning
      value(I_USER) type PTK99_T .
  class-methods GET_USER_DATA
    importing
      !PERNR type PERSNO
      !TRIPNO type BAPITRIP-TRIPNO
      !SOLO_ULTIMO_PERIODO type ABAP_BOOL default 'X'
    returning
      value(USER) type PTK99 .
  class-methods VISUALIZAR
    importing
      !PERNR type PERSNO
      !TRIPNO type BAPITRIP-TRIPNO .
protected section.
private section.
endclass. "ZCL_AP_GV definition
class ZCL_AP_GV implementation.
METHOD get_detail.

  CLEAR: return, framedata, status,
         i_receipts, i_costdist_trip, i_amounts, i_emp_info, i_text.

  IF no_bapi IS INITIAL.
    CALL FUNCTION 'BAPI_TRIP_GET_DETAILS'
      EXPORTING
        employeenumber = pernr
        tripnumber     = tripno
*       LANGUAGE       = SY-LANGU
*       CALCULATE_AMOUNTS          = 'X'
*       GET_TRIP_FROM_MEMORY       = ' '
*       PERIODNUMBER   = '000'
      IMPORTING
        return         = return
        framedata      = framedata
        status         = status
      TABLES
        receipts       = i_receipts
        addinfo        = i_addinfo
        text           = i_text
*       MILEAGE        = MILEAGE
        stopover       = i_stopover
*       DEDUCTIONS     = DEDUCTIONS
        costdist_trip  = i_costdist_trip
*       COSTDIST_STOP  = COSTDIST_STOP
*       COSTDIST_RECE  = COSTDIST_RECE
*       COSTDIST_MILE  = COSTDIST_MILE
        amounts        = i_amounts
        emp_info       = i_emp_info.

    IF return-type = 'E'.
* Si la BAPI estándar falla al recuperar datos, devolvemos la lectura directa a tablas, aunque no es completa.
*     zcl_ap_temp=>set_st( clave = 'GV' subclave_auto = 'X' valor1 = pernr valor2 = tripno texto = return-message ).
      get_detail( pernr   = pernr
                  tripno  = tripno
                  no_bapi = 'X'
             ).
    ENDIF.
  ELSE.
* Acceso directos por tabla para optimizar tiempos
    DATA: t_head     TYPE ptrv_head,
          t_perio    TYPE ptrv_perio,
          l_emp_info TYPE bapitrvemp,
          i_trips    TYPE TABLE OF bapitrip,
          l_trip     TYPE bapitrip,
          l_amounts  TYPE bapitrvsum.

    SELECT * FROM ptrv_head
      INTO t_head
      UP TO 1 ROWS
     WHERE pernr = pernr
       AND reinr = tripno
     ORDER BY PRIMARY KEY.
    ENDSELECT.
    IF sy-subrc = 0.
      framedata-customer = t_head-kunde.
      framedata-location = t_head-zort1.
      framedata-country = t_head-zland.
      framedata-region = t_head-hrgio.
      framedata-out_date = t_head-dath1.
      framedata-out_time = t_head-uhrh1.
      framedata-ret_date = t_head-datr1.
      framedata-ret_time = t_head-uhrr1.
      framedata-ret_coun = t_head-agrz1.
      framedata-ret_rgio = t_head-grgio.
      framedata-ret_ttcs = t_head-grber.
      framedata-t_schema = t_head-schem.
      framedata-tt_comsp = t_head-berei.
      framedata-tt_statu = t_head-kzrea.
      framedata-t_actype = t_head-kztkt.
      framedata-tax_per_diem = t_head-tax_per_diem.
      framedata-tax_pd_man = t_head-tax_pd_man.
      framedata-tax_ov_man = t_head-tax_ov_man.

      SELECT abrec antrg anuep druck pdatb pdatv perio puhrb puhrv uebkz uebrf verpa FROM ptrv_perio
        INTO CORRESPONDING FIELDS OF t_perio
        UP TO 1 ROWS
       WHERE pernr = pernr
         AND reinr = tripno
       ORDER BY PRIMARY KEY.
      ENDSELECT.
      IF sy-subrc = 0.
        IF no_bapi = 'G'.
          CALL FUNCTION 'BAPI_TRIP_CHECK_STATUS'
            EXPORTING
              employeenumber    = pernr
              tripnumber        = tripno
              calculate_amounts = 'X'
              periodnumber      = t_perio-perio
            TABLES
              trips             = i_trips
              amounts           = i_amounts.
          READ TABLE i_trips INTO l_trip INDEX 1.
          IF sy-subrc = 0.
            MOVE-CORRESPONDING l_trip TO framedata.
            MOVE-CORRESPONDING l_trip TO status.
          ENDIF.
        ELSE.
          framedata-dep_date = t_perio-pdatv.
          framedata-dep_time = t_perio-puhrv.
          framedata-arr_date = t_perio-pdatb.
          framedata-arr_time = t_perio-puhrb.
          framedata-pd_meals = t_perio-verpa.
          framedata-pd_accom = t_perio-uebkz.
          framedata-no_night = t_perio-anuep.

          status-approved = t_perio-antrg.
          status-appr_txt = zcl_ap_utils=>get_texto_dominio( dominio = 'ANTRG' valor = status-approved ).
          status-account = t_perio-abrec.
          status-acc_text = zcl_ap_utils=>get_texto_dominio( dominio = 'ABREC' valor = status-account ).
          status-printed = t_perio-druck.
          status-prin_txt = zcl_ap_utils=>get_texto_dominio( dominio = 'P_PRINT' valor = status-printed ).
          status-tran_fi = t_perio-uebrf.
        ENDIF.
      ENDIF.
* Estadística viaje: importes del viaje
      CLEAR l_amounts.
      SELECT SINGLE currency addit_amnt sum_reimbu sum_advanc
                    sum_payout sum_paidco trip_total pd_food
                    pd_housing pd_mileage txf_food
        FROM ptrv_shdr
        INTO (l_amounts-currency, l_amounts-addit_amnt, l_amounts-sum_reimbu,
              l_amounts-sum_advanc,l_amounts-sum_payout , l_amounts-sum_paidco,
              l_amounts-trip_total, l_amounts-pd_food, l_amounts-pd_housing,
              l_amounts-pd_mileage, l_amounts-txf_food)
       WHERE pernr = pernr
         AND reinr = tripno.
      IF sy-subrc = 0.
        APPEND l_amounts TO i_amounts.
      ENDIF.

* Datoe de empleado
      CLEAR l_emp_info.
      SELECT SINGLE pernr ename bukrs werks kostl gsber FROM pa0001
        INTO (l_emp_info-pernr, l_emp_info-ename, l_emp_info-comp_code, l_emp_info-pers_area,
              l_emp_info-costcenter, l_emp_info-bus_area)
      WHERE pernr = pernr
        AND begda <= t_head-datb1
        AND endda >= t_head-datb1.
      IF sy-subrc = 0.
        APPEND l_emp_info TO i_emp_info.
      ENDIF.
    ENDIF.
  ENDIF.

  IF NOT framedata IS INITIAL.
    i_ptk99 = get_tabla_user_data( pernr = pernr tripno = tripno ).
    READ TABLE i_ptk99 INTO ptk99 INDEX 1.
  ENDIF.

ENDMETHOD.
METHOD get_tabla_user_data.
  DATA: l_te_key   TYPE ptp00,
        i_perio    TYPE TABLE OF ptrv_perio,
        l_perio    TYPE ptrv_perio,
        i_user_aux TYPE ptk99_t,
        l_ptk99    TYPE ptk99.

  SELECT pdvrs perio FROM ptrv_perio
    INTO CORRESPONDING FIELDS OF TABLE i_perio
   WHERE pernr = pernr
     AND reinr = tripno.

  LOOP AT i_perio INTO l_perio.
    l_te_key-pernr = pernr.
    l_te_key-reinr = tripno.
    l_te_key-perio = l_perio-perio.
    l_te_key-pdvrs = l_perio-pdvrs.

    TRY.
        IMPORT user TO i_user_aux
               FROM DATABASE pcl1(te) ID l_te_key.
      CATCH cx_sy_import_mismatch_error.

    ENDTRY.

    LOOP AT i_user_aux INTO l_ptk99.
      APPEND l_ptk99 TO i_user.
    ENDLOOP.

    IF solo_ultimo_periodo = 'X'.
      EXIT.
    ENDIF.
  ENDLOOP.

ENDMETHOD.
METHOD get_user_data.
  DATA:  i_user TYPE ptk99_t.

  i_user = get_tabla_user_data( pernr = pernr tripno = tripno solo_ultimo_periodo = solo_ultimo_periodo ).

  READ TABLE i_user INTO user INDEX 1.

ENDMETHOD.
METHOD get_valor_prop_clase.

  SELECT betrg FROM t706b2
    INTO valor
    UP TO 1 ROWS
   WHERE morei = morei
     AND spkzl = clase
     AND atype = 'A' "Valor de propuesta
     AND endda >= fecha
     AND begda <= fecha
   ORDER BY PRIMARY KEY.
  ENDSELECT.

ENDMETHOD.
METHOD visualizar.
  DATA o_bi TYPE REF TO zcl_ap_batch_input.
  DATA l_mensaje TYPE bapireturn1-message.
  CREATE OBJECT o_bi.

  o_bi->inicio( ).

* Overview of Trips
  o_bi->dynpro( program = 'SAPMP56T' dynpro = '1000').
  o_bi->campos( campo = 'BDC_OKCODE' valor = '=FILT').
  o_bi->campos( campo = 'PTP00-PERNR' valor = pernr ). " Número de personal

* Entry of selection criteria
  o_bi->dynpro( program = 'SAPMP56T' dynpro = '1100').
  o_bi->campos( campo = 'BDC_OKCODE' valor = '=ENTR').
  o_bi->campos( campo = 'PTKXX-SRENR' valor = tripno ). " Número de viaje

* Overview of Trips
  o_bi->dynpro( program = 'SAPMP56T' dynpro = '1000').
  o_bi->campos( campo = 'BDC_CURSOR' valor = 'PTP1000-REINR(01)').
  o_bi->campos( campo = 'BDC_OKCODE' valor = '=PICK').

  l_mensaje = o_bi->llamar_transaccion( tcode = 'PR05' modo = 'E').

ENDMETHOD.
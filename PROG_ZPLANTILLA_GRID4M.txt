***********************************************************************
* APLICACION : FI
* TIPO : LISTADO
* TITULO : ??
* *
* DESCRIPCION : ??
*
*
* AUTOR: Andrés Picazo                                FECHA: 06/02/2013
* ANALISTA: ??
*
* MODIFICACIONES
*
***********************************************************************
REPORT zplantilla_grid4m.

*------INCLUDES--------------------------------------------------------*

*------TABLAS/ESTRUCTURAS----------------------------------------------*
TABLES: zbps033, sscrfields.

*------TABLAS INTERNAS-------------------------------------------------*
DATA: i_listado TYPE TABLE OF zbps033_est,
      i_listado_aux TYPE TABLE OF zbps033_est,
      i_listado_ini TYPE TABLE OF zbps033_est,
      l_listado TYPE zbps033_est.

FIELD-SYMBOLS <listado> TYPE zbps033_est.

*------VARIABLES-------------------------------------------------------*
DATA: v_inicio, v_fplanning, v_fsap, v_flanzamiento.

*----------------------------------------------------------------------*
* CLASS lcl_alv DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_event_grid DEFINITION INHERITING FROM zcl_ap_alv_grid_eventos.
  PUBLIC SECTION.
    METHODS: double_click REDEFINITION,
             data_changed REDEFINITION,
             toolbar      REDEFINITION,
             user_command REDEFINITION.
ENDCLASS.                    "lcl_event_grid DEFINITION

DATA: o_alv TYPE REF TO zcl_ap_alv_grid,
      o_event TYPE REF TO lcl_event_grid,
      o_prog TYPE REF TO zcl_ap_dev.

*------PARAMETER/SELECT-OPTIONS EN PANTALLA----------------------------*
SELECTION-SCREEN BEGIN OF BLOCK 001 WITH FRAME.
SELECT-OPTIONS: s_pspnr FOR zbps033-pspnr,
                s_matnr FOR zbps033-hoja0,
                s_ente  FOR zbps033-ente,
                s_idnrk FOR zbps033-idnrk.
SELECTION-SCREEN SKIP.
PARAMETERS: p_vari LIKE disvariant-variant.
SELECTION-SCREEN END OF BLOCK 001.
SELECTION-SCREEN: FUNCTION KEY 1.
SELECTION-SCREEN: FUNCTION KEY 2.
SELECTION-SCREEN: FUNCTION KEY 3.




************************************************************************
*
* LOGICA DEL PROGRAMA
*
************************************************************************
CLASS lcl_event_grid IMPLEMENTATION.
  METHOD double_click.
    READ TABLE i_listado INTO l_listado INDEX e_row-index.
    IF sy-subrc = 0.
      IF e_column CS '_ENT'.
      ELSE.
*          zcl_pedido_sd=>visualizar( l_listado_color-vbeln ).
      ENDIF.
    ENDIF.

  ENDMETHOD.                    "double_click


  METHOD toolbar.
    DATA: ls_toolbar TYPE stb_button.

    CLEAR ls_toolbar.
    ls_toolbar-function  = 'BORRAR'.
    ls_toolbar-icon      = '@11@'.
    ls_toolbar-quickinfo = 'Borrar'.
    ls_toolbar-text      = 'Borrar'.
    APPEND ls_toolbar TO e_object->mt_toolbar.

    CLEAR ls_toolbar.
    ls_toolbar-function  = 'REFRESCAR'.
    ls_toolbar-icon      = '@42@'.
    ls_toolbar-quickinfo = text-ref. "Refrescar
    ls_toolbar-text      = text-ref.
    APPEND ls_toolbar TO e_object->mt_toolbar.

    CLEAR ls_toolbar.
    ls_toolbar-function  = 'EXCEL'.
    ls_toolbar-icon      = '@J2@'.
    ls_toolbar-quickinfo = 'Excel'.
    ls_toolbar-text      = 'Excel'.
    APPEND ls_toolbar TO e_object->mt_toolbar.

  ENDMETHOD.                    "toolbar

  METHOD user_command.
    DATA l_cont(7).
    CASE e_ucomm .
      WHEN 'REFRESCAR'.
        PERFORM leer_datos.
        o_alv->refrescar_grid( ).
      WHEN 'EXCEL'.
        o_alv->comprobar_cambios( ).
        o_alv->exportar_excel( CHANGING t_tabla = i_listado ).
      WHEN 'BORRAR'.
        o_alv->comprobar_cambios( ).
        o_alv->get_filas_sel( ).
        CLEAR i_listado_aux.
        LOOP AT o_alv->i_filas_sel INTO o_alv->fila.
          READ TABLE i_listado ASSIGNING <listado> INDEX o_alv->fila.
          IF sy-subrc = 0.
            APPEND <listado> TO i_listado_aux.
          ENDIF.
        ENDLOOP.
        l_cont = 0.
        LOOP AT i_listado_aux ASSIGNING <listado>.
          ADD 1 TO l_cont.
          DELETE FROM zbps033
           WHERE pspnr = <listado>-pspnr
             AND hoja0 = <listado>-hoja0
             AND ente  = <listado>-ente
             AND idnrk = <listado>-idnrk
             AND aennr = <listado>-aennr.
        ENDLOOP.
        MESSAGE s398(00) WITH 'Se han borrado' l_cont 'registros'.
        PERFORM leer_datos.
        o_alv->refrescar_grid( ).
    ENDCASE .

  ENDMETHOD.                    "USER_COMMAND

  METHOD data_changed.
  ENDMETHOD.                    "data_changed
ENDCLASS.                    "lcl_event_grid IMPLEMENTATION

*----------------------------------------------------------------------*
* INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

*+*+*+*+ PLANTILLA *+*+*+*+
  CREATE OBJECT o_prog
    EXPORTING
      status = 'INICIO'.

  o_prog->initialization( CHANGING sscrfields = sscrfields ).

  CREATE OBJECT o_event.
  CREATE OBJECT o_alv
    EXPORTING
      estructura = 'ZBPS033_EST'
      o_event    = o_event.

  p_vari = o_alv->get_default_layout( ).
*+*+*+*+ PLANTILLA *+*+*+*+


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.

  p_vari = o_alv->get_f4_layout( ).

************************************************************************
* AT SELECTION-SCREEN.
************************************************************************
AT SELECTION-SCREEN.

  o_prog->at_selection(  ).

*+*+*+*+ PLANTILLA *+*+*+*+
  zcl_ap_dev=>at_selection_screen( ).
*+*+*+*+ PLANTILLA *+*+*+*+

*----------------------------------------------------------------------
* START-OF-SELECTION.
*----------------------------------------------------------------------*
START-OF-SELECTION.

  PERFORM leer_datos.

  CALL SCREEN 0100.

************************************************************************
*
* FORMS ADICIONALES
*
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  LEER_DATOS
*&---------------------------------------------------------------------*
FORM leer_datos .

  o_prog->sgpi_texto( 'Leyendo datos' ).

  SELECT * FROM zbps033
    INTO CORRESPONDING FIELDS OF TABLE i_listado
   WHERE pspnr IN s_pspnr
     AND hoja0 IN s_matnr
     AND ente  IN s_ente
     AND idnrk IN s_idnrk
     and ENTE_PLANIF = 'X'.

  SORT i_listado.


  LOOP AT i_listado ASSIGNING <listado>.
    zcl_ap_alv_grid=>append_style_no_edit( EXPORTING campo = 'PSPNR,HOJA0,ENTE,IDNRK,AENNR'
                                           CHANGING  tabla_style = <listado>-style ).
  ENDLOOP.

  i_listado_ini = i_listado.

ENDFORM.                    " LEER_DATOS
*&---------------------------------------------------------------------*
*&      Module  STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.
  SET PF-STATUS 'ST_0100'.
  SET TITLEBAR '100'.


  IF v_inicio IS INITIAL.
    v_inicio = 'X'.

    o_alv->variant-variant = p_vari.
    o_alv->set_layout( style = 'STYLE'
                       edit  = 'X' ).

    o_alv->quitar_opciones( cl_gui_alv_grid=>mc_fc_loc_paste_new_row ).
    o_alv->quitar_opciones( cl_gui_alv_grid=>mc_fc_loc_copy_row ).
    o_alv->quitar_opciones( cl_gui_alv_grid=>mc_fc_loc_delete_row ).
    o_alv->quitar_opciones( cl_gui_alv_grid=>mc_fc_loc_paste ).
    o_alv->quitar_opciones( cl_gui_alv_grid=>mc_fc_loc_paste_new_row ).
    o_alv->quitar_opciones( cl_gui_alv_grid=>mc_fc_loc_cut ).

    o_alv->show( CHANGING tabla = i_listado ).
  ENDIF.

ENDMODULE.                 " STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_0100  INPUT
*&---------------------------------------------------------------------*
MODULE user_command_0100 INPUT.
  CASE sy-ucomm.
    WHEN 'BACK'.
      o_alv->comprobar_cambios( ).
      i_listado_aux = i_listado.
      SORT: i_listado_ini, i_listado_aux.
      IF i_listado_ini NE i_listado_aux.
        IF zcl_ap_popup=>confirmar( titulo = 'Datos sin guardar'
                                    texto  = 'Existen datos manuales no grabados'
                                    texto2   = '¿Está seguro de salir sin guardar?' ) = 'X'.
          LEAVE TO SCREEN 0.
        ENDIF.
      ELSE.
        LEAVE TO SCREEN 0.
      ENDIF.
    WHEN 'GRABAR'.
      o_alv->comprobar_cambios( ).
      LOOP AT i_listado ASSIGNING <listado>.
        READ TABLE i_listado_ini INTO l_listado WITH KEY pspnr = <listado>-pspnr
                                                         hoja0 = <listado>-hoja0
                                                         ente  = <listado>-ente
                                                         idnrk = <listado>-idnrk
                                                         aennr = <listado>-aennr.
        IF sy-subrc = 0.
          IF <listado> NE l_listado.
            UPDATE zbps033
               SET fplanning    = <listado>-fplanning
                   fsap         = <listado>-fsap
                   flanzamiento = <listado>-flanzamiento
                   AEDAT = sy-datum
                   AENAM = sy-uname
             WHERE pspnr = <listado>-pspnr
               AND hoja0 = <listado>-hoja0
               AND ente  = <listado>-ente
               AND idnrk = <listado>-idnrk
               AND aennr = <listado>-aennr.
            IF sy-subrc = 0.
              MESSAGE 'Se han grabado los cambios' TYPE 'S'.
            ENDIF.
          ENDIF.
        ELSE.
          CLEAR zbps033.
          MOVE-CORRESPONDING <listado> TO zbps033.
          zbps033-AEDAT = sy-datum.
          zbps033-AENAM = sy-uname.
          MODIFY zbps033.
        ENDIF.
      ENDLOOP.
      i_listado_ini = i_listado.

    WHEN 'MASS'.
      o_alv->comprobar_cambios( ).
      o_alv->get_filas_sel( ).
      IF o_alv->i_filas_sel IS INITIAL.
        MESSAGE 'Seleccione alguna fila' TYPE 'E'.
      ELSE.
        CALL SCREEN 0200 STARTING AT 3 3 ENDING AT 54 7.
      ENDIF.

  ENDCASE.
ENDMODULE.                 " USER_COMMAND_0100  INPUT
*&---------------------------------------------------------------------*
*&      Module  STATUS_9000  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE status_9000 OUTPUT.
  SET PF-STATUS 'ST_0200X'.
  SET TITLEBAR '200'.

  zcl_ap_dynpro=>screen_visible( campo = 'ZBPS033-FPLANNING' variable = v_fplanning ).
  zcl_ap_dynpro=>screen_visible( campo = 'ZBPS033-FSAP' variable = v_fsap ).
  zcl_ap_dynpro=>screen_visible( campo = 'ZBPS033-FLANZAMIENTO' variable = v_flanzamiento ).

ENDMODULE.                 " STATUS_9000  OUTPUT

*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_9000  INPUT
*&---------------------------------------------------------------------*
MODULE user_command_9000 INPUT.

  CASE sy-ucomm.
    WHEN 'MASS'.
      o_alv->get_filas_sel( ).
      LOOP AT o_alv->i_filas_sel INTO o_alv->fila.
        READ TABLE i_listado ASSIGNING <listado> INDEX o_alv->fila.
        IF sy-subrc = 0.
          IF v_fplanning = 'X'.
            <listado>-fplanning = zbps033-fplanning.
          ENDIF.
          IF v_fsap = 'X'.
            <listado>-fsap = zbps033-fsap.
          ENDIF.
          IF v_flanzamiento = 'X'.
            <listado>-flanzamiento = zbps033-flanzamiento.
          ENDIF.
        ENDIF.
      ENDLOOP.
      o_alv->refrescar_grid( ).
      LEAVE TO SCREEN 0.
    WHEN 'CANCELAR'.
      LEAVE TO SCREEN 0.
  ENDCASE.

ENDMODULE.                 " USER_COMMAND_9000  INPUT

***********************************************************************
* TIPO : LISTADO
* TITULO : Monitor IDOCS
* DESCRIPCION : Monitor IDOCS
*
* AUTOR: Andrés Picazo                                FECHA: 29/04/2015
* ANALISTA: Alfredo Garcia
*
* Doc. Tecnica: http://sap4.com/tareas?=&cliente=DAF&objeto=ZIDOCS
*
* MODIFICACIONES
* 29/04/2015 Tarea inicial: http://sap4.com/tareas?&ntask=1104
*
***********************************************************************
REPORT zidocs.

*------INCLUDES--------------------------------------------------------*

*------TABLAS/ESTRUCTURAS----------------------------------------------*
TABLES: edidc, kna1, edids, edp21, e1edl24, edid4, mara, lips, sscrfields,
        e071, zparametros,  ttxpi.

*------TABLAS INTERNAS-------------------------------------------------*
TYPES: BEGIN OF t_listado,
         check,
         lights,
         docnum	  TYPE edi_docnum,
         status	  TYPE edi_status,
         direct   TYPE edidc-direct,
         credat   TYPE edidc-credat,
         cretim   TYPE edidc-cretim,
         mestyp   TYPE edidc-mestyp,
         descrp   TYPE edimsgt-descrp,
         idoctp   TYPE edidc-idoctp,
         sndprt   TYPE edidc-sndprt,
         sndprn   TYPE edidc-sndprn,
         name1    TYPE kna1-name1,
         ablad    TYPE xblnr,
         pedido   TYPE vbeln_va,
         audat    TYPE audat,
         entrega  TYPE vbeln_vl,
         belnr    TYPE e1edk02-belnr,
         werks    TYPE werks_d,
         lifnr    TYPE e1edka1-lifnr,
         kunwe    TYPE kunwe,
         name1_we TYPE kna1-name1,
         message  TYPE bapi_msg,
         funcion  TYPE funcname,
       END OF t_listado.
DATA: i_listado TYPE TABLE OF t_listado,
      l_listado TYPE t_listado.

FIELD-SYMBOLS <listado> TYPE t_listado.

TYPES: BEGIN OF t_log,
         lights,
         logdat  TYPE edids-logdat,
         logtim  TYPE edids-logtim,
         mensaje TYPE bapi_msg,
       END OF t_log.
DATA: i_log TYPE TABLE OF t_log,
      l_log TYPE t_log.

DATA: BEGIN OF i_e1edl24 OCCURS 0.
        INCLUDE STRUCTURE e1edl24.
DATA:   materiales TYPE string,
        vbeln      TYPE lips-vbeln,
        posnl      TYPE lips-posnr,
        posiciones TYPE string,
        lights,
        END OF i_e1edl24.
*----------------------------------------------------------------------*
* CLASS lcl_alv DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv DEFINITION INHERITING FROM zcl_ap_alv_check.
  PUBLIC SECTION.
    METHODS: handle_double_click REDEFINITION.
    METHODS: handle_user_command REDEFINITION.
    METHODS: top_of_page REDEFINITION.
ENDCLASS. "lcl_alv DEFINITION

*----------------------------------------------------------------------*
*       CLASS zcl_report DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report DEFINITION INHERITING FROM zcl_ap_dev.
  PUBLIC SECTION.
    METHODS: main.

    METHODS:  listado,
      seleccionar_datos,
      enviar_mail,
      ver_posiciones IMPORTING docnum TYPE any.

ENDCLASS.                    "REPORT DEFINITION


*------VARIABLES-------------------------------------------------------*
DATA: o_prog TYPE REF TO zcl_report,
      o_alv  TYPE REF TO lcl_alv.

*------PARAMETER/SELECT-OPTIONS EN PANTALLA----------------------------*
SELECTION-SCREEN BEGIN OF BLOCK 001 WITH FRAME.
SELECT-OPTIONS: s_docnum  FOR edidc-docnum,
                s_status  FOR edidc-status, " DEFAULT '53' SIGN E,
                s_direct  FOR edidc-direct," DEFAULT '2',
                s_credat  FOR edidc-credat DEFAULT sy-datum,
                s_cretim  FOR edidc-cretim,
                s_mestyp  FOR edidc-mestyp,
                s_idoctp  FOR edidc-idoctp,
                s_sndprt  FOR edidc-sndprt,
                s_sndprn  FOR edidc-sndprn,
                s_funcio  FOR ttxpi-funcname,
                s_entreg  FOR lips-vbeln,
                s_belnr   FOR lips-vbeln.
SELECTION-SCREEN: SKIP 1.
PARAMETERS p_email TYPE string.
SELECTION-SCREEN: SKIP 1.
PARAMETERS: p_vari LIKE disvariant-variant.
SELECTION-SCREEN END OF BLOCK 001.
SELECTION-SCREEN: FUNCTION KEY 2,
                  FUNCTION KEY 3,
                  FUNCTION KEY 4.


************************************************************************
*
* LOGICA DEL PROGRAMA
*
************************************************************************

*----------------------------------------------------------------------*
* CLASS lcl_alv IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv IMPLEMENTATION.
  METHOD handle_double_click.
    DATA o_bi TYPE REF TO zcl_ap_batch_input.
    DATA l_mensaje TYPE bapireturn1-message.

    READ TABLE i_listado INTO l_listado INDEX row.
    IF sy-subrc = 0.
      CASE column.
        WHEN 'DOCNUM'.
          CREATE OBJECT o_bi.

          o_bi->inicio( ).

* Herramienta de test para el proceso de entrada IDOC
          o_bi->dynpro( program = 'SAPMSED7' dynpro = '0010').
          o_bi->campos( campo = 'BDC_OKCODE' valor = '=CREA').
          o_bi->campos( campo = 'MSED7START-SEL_EXIDOC' valor = 'X'). " Acceso mediante IDOC existente como modelo
          o_bi->campos( campo = 'MSED7START-EXIDOCNUM' valor = l_listado-docnum ). " Número del IDOC

          l_mensaje = o_bi->llamar_transaccion( tcode = 'WE19' modo = 'E').

        WHEN 'FUNCION'.
          e071-obj_name = l_listado-funcion.
          CALL FUNCTION 'TR_OBJECT_JUMP_TO_TOOL'
            EXPORTING
              iv_pgmid    = 'R3TR'
              iv_object   = 'FUNC'
              iv_obj_name = e071-obj_name
              iv_action   = 'SHOW'
            EXCEPTIONS
              OTHERS      = 1.
        WHEN 'LIGHTS' OR 'MESSAGE'.
          CLEAR i_log.
          SELECT * FROM edids
            INTO edids
           WHERE docnum = l_listado-docnum
             AND statyp NE ''.
            CLEAR l_log.
            MOVE-CORRESPONDING edids TO l_log.
            MESSAGE ID edids-stamid TYPE 'S' NUMBER edids-stamno WITH edids-stapa1 edids-stapa2 edids-stapa3 edids-stapa4
               INTO l_log-mensaje.
            CASE edids-statyp.
              WHEN 'E'. l_log-lights = zcl_ap_alv=>c_sem_rojo.
              WHEN 'W'. l_log-lights = zcl_ap_alv=>c_sem_rojo.
              WHEN 'I' OR 'S'. l_log-lights = zcl_ap_alv=>c_sem_rojo.
            ENDCASE.
            APPEND l_log TO i_log.
          ENDSELECT.
          IF sy-subrc = 0.
            DATA o_alv_log TYPE REF TO zcl_ap_alv.

            CREATE OBJECT o_alv_log
              EXPORTING
                tabla  = 'I_LOG'
                lights = 'LIGHTS'.

            o_alv_log->set_orden( 'LOGDAT,LOGTIM' ).
            o_alv_log->show_popup( end_column = 160 ).
          ENDIF.
        WHEN 'NAME1'.
          o_prog->ver_posiciones( l_listado-docnum ).
        WHEN OTHERS.
          zcl_ap_idoc=>visualizar( l_listado-docnum ).
      ENDCASE.
    ENDIF.
  ENDMETHOD. "handle_double_click
  METHOD handle_user_command.
    DATA: l_row    TYPE i,
          l_return,
          l_string TYPE text132.

    CASE e_salv_function.
      WHEN 'EXCEL'.
        exportar_excel( ).

      WHEN 'VER_POS'.
        get_seleccion( ).
        LOOP AT i_listado ASSIGNING <listado> WHERE check = 'X'.
          o_prog->ver_posiciones( <listado>-docnum ).
        ENDLOOP.

      WHEN 'ST_51'.
        get_seleccion( ).
        LOOP AT i_listado ASSIGNING <listado> WHERE status NE '51'.
          UPDATE edidc
             SET status = '51'
           WHERE docnum = <listado>-docnum.
          <listado>-lights = zcl_ap_alv=>c_sem_rojo.
          zcl_ap_temp=>set_st( clave = 'ZIDOCS' subclave = <listado>-docnum ).
        ENDLOOP.
        refresh( ).

      WHEN 'ST_53'.
        get_seleccion( ).
        LOOP AT i_listado ASSIGNING <listado> WHERE status NE '53'.
          UPDATE edidc
             SET status = '53'
           WHERE docnum = <listado>-docnum.
          <listado>-lights = zcl_ap_alv=>c_sem_verde.
        ENDLOOP.
        refresh( ).

      WHEN 'TEST'.
        get_seleccion( ).
        LOOP AT i_listado ASSIGNING <listado> WHERE check = 'X'.
          SELECT SINGLE * FROM zparametros
            INTO zparametros
           WHERE clave = 'ZIDOCS'
             AND campo = 'FUNCION'
             AND valor = <listado>-funcion.
          IF sy-subrc NE 0.
            MESSAGE 'Función no preparada para test' TYPE 'I'.
          ELSE.
            DATA: o_popup TYPE REF TO zcl_ap_matchcode_z.

            CREATE OBJECT o_popup
              EXPORTING
                tabname = 'ZPARAMETROS'.

            o_popup->add_field( field = 'VALOR2' selectflag = 'X' ).
            o_popup->add_field( field = 'COMENTARIO' ).

            SELECT * FROM  zparametros
              INTO zparametros
           WHERE clave = 'ZIDOCS'
             AND campo = 'FUNCION'
             AND valor = <listado>-funcion.

              o_popup->add_valor( zparametros-valor2 ).
              o_popup->add_valor( zparametros-comentario ).
            ENDSELECT.

            CLEAR zparametros.
            o_popup->matchcode( EXPORTING field   = 'VALOR2'
                                CHANGING  valor   = zparametros-valor2 ).
            IF NOT zparametros-valor2 IS INITIAL.
              SET PARAMETER ID 'TEST_ZIDOCS' FIELD zparametros-valor2.
              CALL FUNCTION 'IDOC_MANUAL_INPUT'
                EXPORTING
                  idoc_number                  = <listado>-docnum
                  input_exception              = '0'
                  no_dialog                    = 'X'
                EXCEPTIONS
                  idoc_not_in_database         = 1
                  no_input_function_found      = 2
                  no_function_parameters_found = 3
                  no_status_record_found       = 4
                  no_authorization             = 5
                  OTHERS                       = 6.
            ENDIF.
          ENDIF.
        ENDLOOP.
        SET PARAMETER ID 'TEST_ZIDOCS' FIELD ''.

      WHEN 'SAL_VAL'.
        get_seleccion( ).
        LOOP AT i_listado ASSIGNING <listado> WHERE check = 'X'.
          IF <listado>-funcion = 'ZZIDOC_INPUT_DELVRY_PCK_NOEMB'.
            zcl_ap_popup=>popup_usuario( EXPORTING campo1 = 'BOM_CLASS_LINK-ITM_LINK'
                                                   titulo = 'Seleccione posiciones de picos'
                                         IMPORTING return = l_return
                                         CHANGING  valor1 = l_string ).
            IF NOT l_string IS INITIAL.
              SET PARAMETER ID 'ZIDOCS_SAL_VAL' FIELD l_string.
              CALL FUNCTION 'IDOC_MANUAL_INPUT'
                EXPORTING
                  idoc_number                  = <listado>-docnum
                  input_exception              = '0'
                  no_dialog                    = 'X'
                EXCEPTIONS
                  idoc_not_in_database         = 1
                  no_input_function_found      = 2
                  no_function_parameters_found = 3
                  no_status_record_found       = 4
                  no_authorization             = 5
                  OTHERS                       = 6.
            ENDIF.
          ELSE.
            MESSAGE 'Opción no permitida' TYPE 'I'.
          ENDIF.
        ENDLOOP.
        SET PARAMETER ID 'ZIDOCS_SAL_VAL' FIELD ''.
      WHEN 'EJECUTAR'.
        get_seleccion( ).
        SET PARAMETER ID 'TEST_ZIDOCS' FIELD ''.
        LOOP AT i_listado ASSIGNING <listado> WHERE check = 'X'.
          CALL FUNCTION 'IDOC_MANUAL_INPUT'
            EXPORTING
              idoc_number                  = <listado>-docnum
              input_exception              = '0'
              no_dialog                    = 'X'
            EXCEPTIONS
              idoc_not_in_database         = 1
              no_input_function_found      = 2
              no_function_parameters_found = 3
              no_status_record_found       = 4
              no_authorization             = 5
              OTHERS                       = 6.
          IF sy-subrc <> 0.
            MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                    WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO <listado>-message.
          ELSE.
            SELECT SINGLE * FROM edidc
              INTO CORRESPONDING FIELDS OF <listado>
             WHERE docnum = <listado>-docnum.
            IF sy-subrc = 0.
              IF <listado>-status = '53'.
                <listado>-lights = zcl_ap_alv=>c_sem_verde.
              ELSEIF <listado>-status < '50'.
                <listado>-lights = zcl_ap_alv=>c_sem_ambar.
              ELSE.
                <listado>-lights = zcl_ap_alv=>c_sem_rojo.
              ENDIF.
              CLEAR edids.
              SELECT * FROM edids
                INTO edids
               WHERE docnum = <listado>-docnum.
              ENDSELECT.
              MESSAGE ID edids-stamid TYPE 'S' NUMBER edids-stamno WITH edids-stapa1 edids-stapa2 edids-stapa3 edids-stapa4
                 INTO <listado>-message.
            ENDIF.
          ENDIF.
        ENDLOOP.

      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD. "handle_USER_COMMAND
  METHOD top_of_page.
    CLEAR o_content.

    o_top_page->set_titulo( sy-title ).

    o_top_page->add_rango_auto( ).
    o_top_page->add_rango( texto = 'Nº Idoc' rango = s_docnum[] ).
    o_top_page->add_rango( texto = 'Fecha' rango = s_credat[] ).
    o_top_page->add_rango( texto = 'Tipo mensaje' rango = s_mestyp[] ).
    o_top_page->add_rango( texto = 'Status' rango = s_status[] ).

    o_top_page->crea_info_seleccion( ).

    o_content = o_top_page->get_grid( ).

  ENDMETHOD.                    "top_of_page
ENDCLASS. "lcl_alv IMPLEMENTATION

*----------------------------------------------------------------------*
*       CLASS zcl_report IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report IMPLEMENTATION.
  METHOD main.
    seleccionar_datos( ).

    IF p_email IS INITIAL.
      listado( ).
    ELSE.
      IF NOT i_listado IS INITIAL. "Sólo enviamos mail si hay datos
        enviar_mail( ).
      ENDIF.
    ENDIF.
  ENDMETHOD.                    "REPORT

  METHOD seleccionar_datos.
    DATA l_ntanf(40).

    sgpi_texto( 'Seleccionando datos' ).

    SELECT * FROM edidc
      INTO CORRESPONDING FIELDS OF TABLE i_listado
     WHERE docnum IN s_docnum
       AND status IN s_status
       AND credat IN s_credat
       AND direct IN s_direct
       AND cretim IN s_cretim
       AND mestyp IN s_mestyp
       AND idoctp IN s_idoctp
       AND sndprt IN s_sndprt
       AND sndprn IN s_sndprn.

    LOOP AT i_listado ASSIGNING <listado>.
      IF <listado>-status = '53'.
        <listado>-lights = zcl_ap_alv=>c_sem_verde.
      ELSEIF <listado>-status < '50'.
        <listado>-lights = zcl_ap_alv=>c_sem_ambar.
      ELSE.
        <listado>-lights = zcl_ap_alv=>c_sem_rojo.
      ENDIF.

      CLEAR edidc.
      SELECT SINGLE mescod mesfct mestyp sndpfc sndprn sndprt test FROM edidc
        INTO CORRESPONDING FIELDS OF edidc
       WHERE docnum = <listado>-docnum.
      IF sy-subrc = 0.
        SELECT SINGLE * FROM edp21
          INTO edp21
           WHERE sndprn = edidc-sndprn
                                     AND sndprt = edidc-sndprt
                                     AND sndpfc = edidc-sndpfc
                                     AND mestyp = edidc-mestyp
                                     AND mescod = edidc-mescod
                                     AND mesfct = edidc-mesfct
                                     AND test   = edidc-test.
        IF sy-subrc = 0.
          SELECT SINGLE funcname FROM tbd52
            INTO <listado>-funcion WHERE evcode = edp21-evcode.
        ENDIF.
      ENDIF.

      IF NOT <listado>-funcion IN s_funcio.
        DELETE i_listado.
        CONTINUE.
      ENDIF.

      SELECT SINGLE descrp FROM  edimsgt
        INTO <listado>-descrp
             WHERE  langua  = sy-langu
             AND    mestyp  = <listado>-mestyp.

      CASE <listado>-sndprt.
        WHEN 'KU'.
          SELECT SINGLE name1 FROM kna1
            INTO <listado>-name1
           WHERE kunnr = <listado>-sndprn.
        WHEN 'LI'.
          SELECT SINGLE name1 FROM lfa1
            INTO <listado>-name1
           WHERE lifnr = <listado>-sndprn.
      ENDCASE.

      l_ntanf = zcl_ap_idoc=>get_valor( docnum = <listado>-docnum segmento = 'E1EDT13' campo = 'NTANF' ).
      <listado>-audat = l_ntanf(8).

      <listado>-ablad = zcl_ap_idoc=>get_valor( docnum = <listado>-docnum segmento = 'E1EDL20' campo = 'ABLAD' ).
      IF NOT <listado>-ablad IS INITIAL.
        IF <listado>-ablad(1) CN '0123456789'.
          <listado>-ablad = <listado>-ablad+1.
        ENDIF.

        SELECT SINGLE ebeln FROM ekko
          INTO <listado>-pedido
         WHERE ebeln = <listado>-ablad
           AND bsart = <listado>-ablad.
        IF sy-subrc = 0.
          SELECT SINGLE belnr FROM ekbe
            INTO <listado>-entrega
           WHERE ebeln = <listado>-pedido
             AND vgabe = '8' "Entrega por traslado
             AND bewtp = 'L'. "Lfs
        ELSE.
          IF NOT <listado>-audat IS INITIAL.
            SELECT SINGLE vbeln FROM vbkd               "#EC CI_NOFIELD
              INTO <listado>-pedido
             WHERE bstkd = <listado>-ablad
              AND fkdat = <listado>-audat.
          ENDIF.
          IF <listado>-pedido IS INITIAL.
            SELECT SINGLE vbeln FROM vbkd               "#EC CI_NOFIELD
              INTO <listado>-pedido
             WHERE bstkd = <listado>-ablad
               AND gjahr = <listado>-audat(4).
          ENDIF.
          IF <listado>-pedido IS INITIAL.
            SELECT SINGLE vbeln FROM vbkd               "#EC CI_NOFIELD
              INTO <listado>-pedido
             WHERE bstkd = <listado>-ablad.
          ENDIF.
          IF sy-subrc = 0.
            SELECT SINGLE vbeln FROM  lips
              INTO <listado>-entrega
             WHERE vgbel = <listado>-pedido.
          ENDIF.
        ENDIF.
      ENDIF.
      IF <listado>-mestyp = 'INVOIC'.
        <listado>-belnr = zcl_ap_idoc=>get_valor( docnum = <listado>-docnum segmento = 'E1EDP02' campo = 'BELNR' ini_datos = '001' ).
        IF <listado>-belnr(1) = '4'.
          SELECT SINGLE werks FROM ekpo
            INTO <listado>-werks
           WHERE ebeln = <listado>-belnr.
        ENDIF.
      ELSEIF <listado>-mestyp = 'ZRECIDOC_TM'.
        <listado>-entrega = zcl_ap_idoc=>get_valor( docnum = <listado>-docnum segmento = 'ZRECAB' campo = 'NUMDOC'  ).
      ELSEIF <listado>-mestyp = 'DESADV'.
        <listado>-entrega = zcl_ap_idoc=>get_valor( docnum = <listado>-docnum segmento = 'E1EDK07' campo = 'VBELN'  ).
      ELSEIF <listado>-mestyp = 'ORDERS01'.
        <listado>-belnr = zcl_ap_idoc=>get_valor( docnum = <listado>-docnum segmento = 'E1EDK01' campo = 'BELNR' ).
      ENDIF.
      IF <listado>-belnr IS INITIAL.
        <listado>-belnr = zcl_ap_idoc=>get_valor( docnum = <listado>-docnum segmento = 'E1EDK02' campo = 'BELNR' ).
        IF <listado>-belnr IS INITIAL.
          <listado>-belnr = zcl_ap_idoc=>get_valor( docnum = <listado>-docnum segmento = 'E1EDL20' campo = 'VBELN' ).
        ELSEIF <listado>-belnr IS INITIAL.
          <listado>-belnr = zcl_ap_idoc=>get_valor( docnum = <listado>-docnum segmento = 'E1EDK01' campo = 'BELNR' ).
        ENDIF.
      ENDIF.

      IF NOT <listado>-entrega IN s_entreg OR
         NOT <listado>-belnr IN s_belnr.
        DELETE i_listado.
        CONTINUE.
      ENDIF.
      <listado>-lifnr = zcl_ap_idoc=>get_valor( docnum = <listado>-docnum segmento = 'E1EDKA1' campo = 'LIFNR' ini_datos = 'WE' ).
      IF NOT <listado>-lifnr IS INITIAL.
        SELECT SINGLE kunnr FROM edpar
          INTO <listado>-kunwe
         WHERE parvw = 'WE'
           AND expnr = <listado>-lifnr.
        IF sy-subrc = 0.
          SELECT SINGLE name1 FROM kna1
            INTO <listado>-name1_we
           WHERE kunnr = <listado>-kunwe.
        ENDIF.
      ENDIF.

      CLEAR edids.
      SELECT * FROM edids
        INTO edids
       WHERE docnum = <listado>-docnum.
      ENDSELECT.
      MESSAGE ID edids-stamid TYPE 'S' NUMBER edids-stamno WITH edids-stapa1 edids-stapa2 edids-stapa3 edids-stapa4
         INTO <listado>-message.
    ENDLOOP.



  ENDMETHOD.                    "seleccionar_datos


  METHOD ver_posiciones.
    DATA: l_e1ed124 LIKE LINE OF i_e1edl24,
          i_lips    TYPE TABLE OF lips,
          l_aux(40).

    CLEAR i_e1edl24. REFRESH i_e1edl24.
    SELECT * FROM edid4                       "#EC CI_ALL_FIELDS_NEEDED
      INTO edid4
     WHERE docnum = docnum
       AND segnam = 'E1EDL24'
     ORDER BY PRIMARY KEY.
      e1edl24 = edid4-sdata.
      CLEAR l_e1ed124.
      MOVE-CORRESPONDING e1edl24 TO l_e1ed124.

      IF NOT e1edl24-ean11 IS INITIAL.
        SELECT matnr FROM mara                          "#EC CI_NOFIELD
          INTO mara-matnr
         WHERE ean11 = e1edl24-ean11.
          zcl_ap_lista=>add( EXPORTING valor = mara-matnr quitar_ceros = 'X' CHANGING lista = l_e1ed124-materiales ).
        ENDSELECT.

        IF NOT l_e1ed124-matnr IS INITIAL.
          SELECT SINGLE mtart FROM mara
            INTO l_e1ed124-usr01
           WHERE matnr = l_e1ed124-matnr.
        ENDIF.

        IF NOT <listado>-entrega IS INITIAL.
          l_e1ed124-lights = zcl_ap_alv=>c_sem_rojo.
          l_e1ed124-vbeln = l_listado-entrega.
          SELECT posnr lips~matnr arktx FROM lips JOIN mara ON lips~matnr = mara~matnr
            INTO CORRESPONDING FIELDS OF TABLE i_lips
           WHERE vbeln = <listado>-entrega
             AND mara~ean11 = l_e1ed124-ean11
             AND posnr < '9000'
           ORDER BY vbeln posnr.
          IF sy-subrc = 0.
            READ TABLE i_lips INTO lips INDEX 1.
            l_e1ed124-posnl = lips-posnr.
            l_e1ed124-matnr = lips-matnr.
            l_e1ed124-orktx = lips-arktx.
            DESCRIBE TABLE i_lips LINES sy-tfill.
            IF sy-tfill = 1.
              l_e1ed124-lights = zcl_ap_alv=>c_sem_verde.
            ELSE.
              l_e1ed124-lights = zcl_ap_alv=>c_sem_ambar.
              LOOP AT i_lips INTO lips.
                zcl_ap_string=>quitar_ceros_c( CHANGING cadena = lips-posnr ).
                zcl_ap_string=>quitar_ceros_c( CHANGING cadena = lips-matnr ).
                CONCATENATE lips-posnr lips-matnr INTO l_aux SEPARATED BY '-'.
                zcl_ap_lista=>add( EXPORTING valor = l_aux quitar_ceros = 'X' CHANGING lista = l_e1ed124-posiciones ).
              ENDLOOP.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.
      APPEND l_e1ed124 TO i_e1edl24.
    ENDSELECT.
    IF sy-subrc = 0.
      DATA o_alv_pos TYPE REF TO zcl_ap_alv.

      CREATE OBJECT o_alv_pos
        EXPORTING
          tabla  = 'I_E1EDL24'
          lights = 'LIGHTS'.
      o_alv_pos->ocultar_columnas_vacias( ).
      o_alv_pos->set_field_text( campo = 'POSNR' valor = 'Pos.' ).
      o_alv_pos->set_field_text( campo = 'ARKTX' valor = 'Descripción artículo' ).
      o_alv_pos->set_field_text( campo = 'KDMAT' valor = 'Material origen' ).
      o_alv_pos->set_field_text( campo = 'CHARG' valor = 'Lote' ).
      o_alv_pos->set_field_text( campo = 'LFIMG' valor = 'Cantidad' ).
      o_alv_pos->set_field_text( campo = 'EAN11' valor = 'EAN11' ).
      o_alv_pos->set_field_text( campo = 'USR01' valor = 'T.M.' ).
      o_alv_pos->set_field_text( campo = 'MATNR' valor = 'Material' ).
      o_alv_pos->set_field_text( campo = 'MATNR_EXTERNAL' valor = 'EAN' ).
      o_alv_pos->set_field_text( campo = 'MATERIALES' valor = 'Posibles materiales por EAN11' ).
      o_alv_pos->set_field_text( campo = 'POSICIONES' valor = 'Posibles posiciones' ).
      o_alv_pos->show_popup( end_column = 160 ).
    ENDIF.

  ENDMETHOD.                    "VER_POSICIONES

  METHOD listado.

    sgpi_texto( 'Generando informe' ).

    o_alv->set_layout( p_vari ).

    o_alv->set_top_of_page( ).

    o_alv->set_seleccion( CHANGING t_tabla = i_listado ).

    o_alv->set_field_noout( 'IDOCTP,DIRECT' ).
    o_alv->set_field_text( campo = 'NAME1_WE' valor = 'Nombre destinatario' ).
    o_alv->set_field_text( 'FUNCION' ).

    o_alv->ocultar_columnas_vacias( ).
    o_alv->show( ).


  ENDMETHOD.                    "

  METHOD enviar_mail.
    DATA: o_mail     TYPE REF TO zcl_ap_envio_mail,
          l_string   TYPE string,
          l_asunto   TYPE string,
          l_begda    TYPE d, l_endda TYPE d,
          l_fini(10), l_ffin(10),
          l_aux(20).

    CREATE OBJECT o_mail.

    o_mail->cabecera_html( ).

    zcl_ap_fechas=>rango2fechas( EXPORTING r_fechas = s_credat[] IMPORTING fecha_desde = l_begda fecha_hasta = l_endda ).
    WRITE: l_begda TO l_fini,
           l_endda TO l_ffin.

    CONCATENATE 'IDOCs fallidos desde' l_fini 'a' l_ffin INTO l_asunto SEPARATED BY space.


    o_mail->add_parrafo_html( l_asunto ).

    o_mail->set_text( '<font size="2">' ).
    o_mail->inicio_tabla_html( longitud = 1024
                               c1 = 'NºIdoc'
                               c2 = 'Fecha'
                               c3 = 'Hora'
                               c4 = 'Tipo Idoc'
                               c5 = 'Interlocutor'
                               c6 = 'NºDocumento'
                               c7 = 'Dest.Mercancías' ).

    LOOP AT i_listado ASSIGNING <listado> WHERE lights = zcl_ap_alv=>c_sem_rojo.
      WRITE <listado>-docnum TO l_aux.
      o_mail->add_fila_html( c1 = l_aux
                             c2 = <listado>-credat
                             c3 = <listado>-cretim
                             c4 = <listado>-descrp
                             c5 = <listado>-name1
                             c6 = <listado>-belnr
                             c7 = <listado>-name1_we
                              ).
    ENDLOOP.

    o_mail->fin_tabla_html( ).
    o_mail->set_text( '</font>' ).
    o_mail->set_text( '</body></html>' ).

    o_mail->add_itab_alv_as_xls( EXPORTING descripcion = 'IDOCS' CHANGING itab = i_listado ).

    CALL METHOD o_mail->envio_mail
      EXPORTING
        subject             = l_asunto
        direccion           = p_email
        html                = 'X'
        forzar_mail_externo = 'X'.

    o_mail->free( ).
    CLEAR o_mail.

  ENDMETHOD.                    "enviar_mail

ENDCLASS.                    "REPORT IMPLEMENTATION

*----------------------------------------------------------------------*
* INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

  SET PARAMETER ID 'TEST_ZIDOCS' FIELD ''.
  CREATE OBJECT o_prog
    EXPORTING
      status        = 'INICIO'
      get_nombre_pc = 'X'
      no_param      = 'X'.

  CREATE OBJECT o_alv
    EXPORTING
      status = 'STANDARD'
      lights = 'LIGHTS'.
  p_vari = o_alv->get_default_layout( ).


  o_prog->initialization( EXPORTING nombre_pc = o_prog->nombre_pc CHANGING sscrfields = sscrfields ).

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.

  p_vari = o_alv->get_f4_layout( ).

************************************************************************
* AT SELECTION-SCREEN.
************************************************************************
AT SELECTION-SCREEN.

  o_prog->at_selection(  ).

AT SELECTION-SCREEN ON EXIT-COMMAND.
  o_prog->at_selection(  ).

*----------------------------------------------------------------------
* START-OF-SELECTION.
*----------------------------------------------------------------------*
START-OF-SELECTION.

  o_prog->main( ).

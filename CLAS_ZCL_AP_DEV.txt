type-pools abap.
CLASS zcl_ap_dev DEFINITION
  PUBLIC
  CREATE PUBLIC.

  PUBLIC SECTION.
    TYPES tt_exc TYPE TABLE OF text20.

    DATA o_sgpi            TYPE REF TO zcl_ap_sgpi.
    DATA o_par             TYPE REF TO zcl_ap_parametros.
    DATA o_log             TYPE REF TO zcl_ap_log.
    DATA nombre_pc         TYPE string.
    DATA titulo            TYPE string.
    DATA i_exc             TYPE tt_exc.
    DATA url_manual        TYPE string.
    DATA url_manual_edt    TYPE string.
    DATA url_plantilla     TYPE string.
    DATA url_plantilla_edt TYPE string.
    DATA proceso_log       TYPE zproceso_log.
    DATA no_break          TYPE abap_bool VALUE '' ##NO_TEXT.
    DATA cx_root           TYPE REF TO cx_root.
    DATA mensaje           TYPE bapi_msg.
    DATA string            TYPE string.
    DATA cont              TYPE i.
    DATA aux1              TYPE text80.
    DATA aux2              TYPE text80.
    DATA object            TYPE string.
    DATA inicio            TYPE xfeld.
    DATA fecha             TYPE dats.
    DATA ucomm             TYPE sy-ucomm.
    DATA ucomm_text        TYPE string.
    DATA ucomm_grabar      TYPE string.
    DATA o_cache           TYPE REF TO zcl_ap_cache.
    DATA modo_ct           TYPE bdcmode VALUE 'N' ##NO_TEXT.
    DATA status_prog       TYPE sy-cprog.
    DATA linea             TYPE string.
    DATA accion_ejecutada  TYPE abap_bool.
    DATA handle            TYPE slis_handl.
    DATA no_sgpi           TYPE abap_bool VALUE '' ##NO_TEXT.

    METHODS constructor
      IMPORTING prog                TYPE any       DEFAULT sy-cprog
                commit_work         TYPE abap_bool DEFAULT 'X'
                !status             TYPE any       DEFAULT 'INICIO'
                param_sap_all       TYPE abap_bool DEFAULT ''
                !object             TYPE any       DEFAULT ''
                subobject           TYPE any       DEFAULT ''
                no_param            TYPE abap_bool DEFAULT ''
                get_nombre_pc       TYPE abap_bool DEFAULT ''
                get_doc_from_report TYPE abap_bool DEFAULT 'X'
                guardar_logz        TYPE abap_bool DEFAULT ''
                proceso_log         TYPE any       DEFAULT sy-cprog
                no_break            TYPE abap_bool DEFAULT ''
                status_prog         TYPE any       DEFAULT ''
          PREFERRED PARAMETER prog.

    CLASS-METHODS get_objeto_como_texto
      IMPORTING tipo           TYPE any
                nombre         TYPE any
                modo           TYPE any DEFAULT ''
      RETURNING VALUE(i_tabla) TYPE tttext255.

    CLASS-METHODS texto_comparable
      CHANGING i_tabla TYPE table.

    CLASS-METHODS comparar_reports
      IMPORTING tabla1             TYPE table
                tabla2             TYPE table
      RETURNING VALUE(diferencias) TYPE vxabapt255_tab.

    CLASS-METHODS activar_objeto
      IMPORTING !object  TYPE e071-object
                obj_name TYPE e071-obj_name.

    CLASS-METHODS popup_cambio_clase_desarrollo
      IMPORTING !object  TYPE e071-object
                obj_name TYPE e071-obj_name
                devclass TYPE tadir-devclass.

    CLASS-METHODS borrar_programa
      IMPORTING !program TYPE sy-repid.

    CLASS-METHODS get_saplink_xml
      IMPORTING !object    TYPE any
                nombre     TYPE any
      RETURNING VALUE(xml) TYPE string.

    CLASS-METHODS get_fieldcatalog
      IMPORTING tabla              TYPE STANDARD TABLE OPTIONAL
                limpiar_tablas     TYPE abap_bool      DEFAULT 'X'
                desglosar_includes TYPE abap_bool      DEFAULT 'X'
                linea              TYPE any            OPTIONAL
                solo_tipos         TYPE any            DEFAULT ''
          PREFERRED PARAMETER tabla
      EXPORTING !message           TYPE bapi_msg
      RETURNING VALUE(campos)      TYPE abap_component_tab.

    CLASS-METHODS get_fieldcatalog_ddic
      IMPORTING tabla         TYPE STANDARD TABLE
      RETURNING VALUE(campos) TYPE ddfields.

    CLASS-METHODS get_atributos_campo
      IMPORTING tabla_campo  TYPE any OPTIONAL
                tabla        TYPE any OPTIONAL
                campo        TYPE any OPTIONAL
      RETURNING VALUE(dd03l) TYPE dd03l.

    CLASS-METHODS get_descripcion_campo
      IMPORTING campo         TYPE any      OPTIONAL
                tabla_campo   TYPE any      OPTIONAL
                tabla         TYPE any      OPTIONAL
                idioma        TYPE sy-langu DEFAULT sy-langu
      RETURNING VALUE(ddtext) TYPE dd04t-ddtext.

    CLASS-METHODS get_fieldcatalog_tabla
      IMPORTING tabla         TYPE any
                solo_campos   TYPE any DEFAULT ''
      RETURNING VALUE(campos) TYPE ddfields.

    CLASS-METHODS get_default_variant
      IMPORTING nombre_pc TYPE string OPTIONAL.

    CLASS-METHODS inserta_boton_gos.

    CLASS-METHODS at_selection_screen
      IMPORTING o_par           TYPE REF TO zcl_ap_parametros OPTIONAL
                url_manual      TYPE any                      DEFAULT ''
                url_plantilla   TYPE any                      DEFAULT ''
                zparametros_alv TYPE abap_bool                DEFAULT zcl_c=>zparametros_alv
          PREFERRED PARAMETER o_par.

    CLASS-METHODS initialization
      IMPORTING gos               TYPE abap_bool         DEFAULT 'X'
                nombre_pc         TYPE string            OPTIONAL
                url_manual        TYPE any               DEFAULT ''
                boton_manual      TYPE any               DEFAULT '@J7@ Manual usuario'(mus)
                boton_plantilla   TYPE any               DEFAULT '@J2@ Ejemplo fichero carga'(efc)
                get_default_param TYPE abap_bool         DEFAULT 'X'
                url_plantilla     TYPE any               DEFAULT ''
                tcode_doc         TYPE zdocumentos-tcode DEFAULT ''
                boton_doc_tecnica TYPE string            DEFAULT '@19@' "#EC * ICON_MESSAGE_INFORMATION
      CHANGING  sscrfields        TYPE sscrfields        OPTIONAL.

    CLASS-METHODS get_descripcion
      IMPORTING tabla         TYPE any
                valor         TYPE any
                valor2        TYPE any   DEFAULT ''
                nombre_campo  TYPE any   OPTIONAL
                nombre_campo2 TYPE any   OPTIONAL
                idioma        TYPE spras DEFAULT sy-langu
                campo_clave   TYPE any   OPTIONAL
                campo_idioma  TYPE any   DEFAULT ''
      RETURNING VALUE(texto)  TYPE string.

    CLASS-METHODS boton_gos_obj
      IMPORTING objeto TYPE swo_objtyp
                clave  TYPE any.

    METHODS at_selection
      IMPORTING !action         TYPE c                         DEFAULT 'U'
                zparametros_alv TYPE abap_bool                 DEFAULT zcl_c=>zparametros_alv
                tcode_doc       TYPE zdocumentos-tcode         DEFAULT ''
                boton_fc05      TYPE zdocumentos-clasificacion DEFAULT ''.

    METHODS sgpi_texto
      IMPORTING texto1       TYPE any       OPTIONAL
                texto2       TYPE any       OPTIONAL
                texto3       TYPE any       OPTIONAL
                quitar_ceros TYPE abap_bool DEFAULT ''
                cant_porc    TYPE i         DEFAULT 0
          PREFERRED PARAMETER texto1.

    CLASS-METHODS commit
      IMPORTING dequeue_all TYPE abap_bool DEFAULT ''.

    METHODS envio_mail
      IMPORTING campo                TYPE zcampo_param DEFAULT 'CORREO'
                subject              TYPE any          OPTIONAL
                texto                TYPE any          OPTIONAL
                direccion            TYPE any          DEFAULT ''
                urgente              TYPE abap_bool    DEFAULT 'X'
                html                 TYPE abap_bool    DEFAULT ''
                !commit              TYPE abap_bool    DEFAULT 'X'
                forzar_mail_externo  TYPE abap_bool    DEFAULT ''
                confirmacion_lectura TYPE bcs_rqst     DEFAULT 'A'
                emisor               TYPE any          DEFAULT ''
          PREFERRED PARAMETER campo.

    METHODS fin.

    METHODS msg
      IMPORTING msgty TYPE any    DEFAULT 'E'
                texto TYPE string OPTIONAL
          PREFERRED PARAMETER texto.

    METHODS buscar_datos.

    METHODS set_status
      IMPORTING !status TYPE any
                excluir TYPE any      DEFAULT ''
                cprog   TYPE sy-cprog DEFAULT sy-cprog.

    CLASS-METHODS relanzar_report
      IMPORTING !report              TYPE sy-cprog  DEFAULT sy-cprog
                via_selection_screen TYPE abap_bool DEFAULT ''
                parametros           TYPE any       DEFAULT ''
                param1               TYPE any       DEFAULT ''
                valor1               TYPE any       DEFAULT ''
                param2               TYPE any       DEFAULT ''
                valor2               TYPE any       DEFAULT ''
                param3               TYPE any       DEFAULT ''
                valor3               TYPE any       DEFAULT ''
                !return              TYPE abap_bool DEFAULT ''
                param4               TYPE any       DEFAULT ''
                valor4               TYPE any       DEFAULT ''.

    CLASS-METHODS get_fieldcatalog_tabla_alv
      IMPORTING tabla         TYPE ANY TABLE
                fix_ref       TYPE abap_bool DEFAULT ''
                tabla_ref     TYPE string    DEFAULT ''
      RETURNING VALUE(campos) TYPE lvc_t_fcat.

    CLASS-METHODS salir_si_no_cambios
      IMPORTING t1                     TYPE table
                t2                     TYPE table
                quitar_campos          TYPE any       DEFAULT 'LIGHTS,CHECK,MESSAGE,MENSAJE'
                solo_campos            TYPE any       DEFAULT ''
                rollback_si_no_cambios TYPE abap_bool DEFAULT ''
                relanzar_report        TYPE abap_bool DEFAULT ''.

    METHODS initialization_i
      IMPORTING gos               TYPE abap_bool                 DEFAULT 'X'
                boton_manual      TYPE any                       DEFAULT '@J7@ Manual usuario'(mus)
                boton_plantilla   TYPE any                       DEFAULT '@J2@ Ejemplo fichero carga'(efc)
                get_default_param TYPE abap_bool                 DEFAULT 'X'
                tcode_doc         TYPE zdocumentos-tcode         DEFAULT ''
                boton_doc_tecnica TYPE string                    DEFAULT '@19\Q*Documentación técnica@'
                boton_fc05        TYPE zdocumentos-clasificacion DEFAULT ''
      CHANGING  sscrfields        TYPE sscrfields                OPTIONAL.

    CLASS-METHODS break_condicional
      IMPORTING texto TYPE any      DEFAULT ''
                cprog TYPE sy-cprog DEFAULT sy-cprog
          PREFERRED PARAMETER texto.

    METHODS control_ejecucion_simultanea
      IMPORTING cprog TYPE any DEFAULT sy-cprog
                !job  TYPE any DEFAULT ''.

    CLASS-METHODS clear_global_data
      IMPORTING cprog TYPE sy-cprog DEFAULT sy-cprog.

    METHODS message
      IMPORTING !type                          TYPE msgty     DEFAULT 'E'
                p1                             TYPE any       DEFAULT ''
                p2                             TYPE any       DEFAULT ''
                p3                             TYPE any       DEFAULT ''
                p4                             TYPE any       DEFAULT ''
                p5                             TYPE any       DEFAULT ''
                p6                             TYPE any       DEFAULT ''
                p7                             TYPE any       DEFAULT ''
                meins                          TYPE meins     DEFAULT 'ST'
                clave_log                      TYPE any       DEFAULT ''
                p8                             TYPE any       DEFAULT ''
                p9                             TYPE any       DEFAULT ''
                !mail                          TYPE any       DEFAULT ''
                solo_log                       TYPE any       DEFAULT ''
                no_log                         TYPE any       DEFAULT ''
                usuarios_cambio_error_x_warnin TYPE any       DEFAULT ''
                solo_primera_vez               TYPE any       DEFAULT ''
                postponer                      TYPE abap_bool DEFAULT ''
                msgv1                          TYPE any       DEFAULT ''
                msgv2                          TYPE any       DEFAULT ''
                msgv3                          TYPE any       DEFAULT ''
                msgv4                          TYPE any       DEFAULT ''
                p10                            TYPE any       DEFAULT ''
                p11                            TYPE any       DEFAULT ''
          PREFERRED PARAMETER p1
      RETURNING VALUE(message)                 TYPE bapi_msg.

    CLASS-METHODS codigo_dinamico
      IMPORTING l1    TYPE any      DEFAULT ''
                l2    TYPE any      DEFAULT ''
                l3    TYPE any      DEFAULT ''
                l4    TYPE any      DEFAULT ''
                l5    TYPE any      DEFAULT ''
                l6    TYPE any      DEFAULT ''
                l7    TYPE any      DEFAULT ''
                l8    TYPE any      DEFAULT ''
                l9    TYPE any      DEFAULT ''
                cprog TYPE sy-cprog DEFAULT sy-cprog.

    CLASS-METHODS pruebas
      IMPORTING cprog       TYPE any DEFAULT sy-cprog
                texto       TYPE any DEFAULT '%TEST%'
      RETURNING VALUE(test) TYPE abap_bool.

    METHODS status_dynpro
      IMPORTING o_alv         TYPE REF TO zcl_ap_alv_grid OPTIONAL
                !status       TYPE any                    DEFAULT 'ST_0100'
                variant       TYPE any                    DEFAULT ''
                alv_standard  TYPE abap_bool              DEFAULT 'X'
                dynnr         TYPE dynnr                  DEFAULT '0100'
                campos_borrar TYPE any                    DEFAULT 'CHECK,TABIX'
                tabint        TYPE abap_bool              DEFAULT 'X'
                excluir       TYPE any                    DEFAULT ''
                !style        TYPE any                    DEFAULT ''
                !color        TYPE any                    DEFAULT ''
                cprog         TYPE any                    DEFAULT sy-cprog
                titulo        TYPE any                    DEFAULT ''
                lights        TYPE any                    DEFAULT 'LIGHTS'
      CHANGING  i_listado     TYPE table                  OPTIONAL.

    METHODS command_dynpro
      IMPORTING o_alv                  TYPE REF TO zcl_ap_alv_grid OPTIONAL
                dynnr                  TYPE dynnr                  DEFAULT '0100'
                seleccion              TYPE any                    DEFAULT ''
                solo_campos            TYPE any                    DEFAULT ''
                fila_activa            TYPE any                    DEFAULT ''
                rollback_si_no_cambios TYPE abap_bool              DEFAULT ''
                relanzar_report        TYPE abap_bool              DEFAULT ''
                quitar_campos          TYPE any                    OPTIONAL
      CHANGING  hay_sel                TYPE xfeld                  OPTIONAL
                i_listado              TYPE table                  OPTIONAL
                i_listado_ini          TYPE table                  OPTIONAL.

    METHODS set_status_list
      IMPORTING icono               TYPE any  DEFAULT ''
                !message            TYPE any  DEFAULT ''
                !color              TYPE any  DEFAULT ''
                campos_editables    TYPE any  DEFAULT ''
                campos_no_editables TYPE any  DEFAULT ''
                int                 TYPE int1 DEFAULT 0
                inv                 TYPE int4 DEFAULT 0
                criterio            TYPE any  DEFAULT ''
                campos_color        TYPE any  DEFAULT ''
                texto_icono         TYPE any  DEFAULT ''
                resaltar_campos     TYPE any  DEFAULT ''
                color_resalte       TYPE any  DEFAULT 'A'
      CHANGING  !list               TYPE any.

    METHODS set_status_list_stop
      IMPORTING icono               TYPE any  DEFAULT zcl_ap_alv=>c_ico_stop
                !message            TYPE any  DEFAULT ''
                !color              TYPE any  DEFAULT 'R'
                campos_editables    TYPE any  DEFAULT ''
                campos_no_editables TYPE any  DEFAULT ''
                int                 TYPE int1 DEFAULT 0
                inv                 TYPE int4 DEFAULT 0
                campos_color        TYPE any  DEFAULT ''
      CHANGING  !list               TYPE any.

    CLASS-METHODS marca_mod_estandard
      IMPORTING parar    TYPE abap_bool DEFAULT ''
                !uname   TYPE sy-uname  DEFAULT zcl_c=>usuario_ap
                !include TYPE any       DEFAULT ''
                !form    TYPE any       DEFAULT ''
                subclave TYPE any       DEFAULT ''
                cprog    TYPE sy-cprog  DEFAULT sy-cprog
                tcode    TYPE sy-tcode  DEFAULT sy-tcode.

    CLASS-METHODS marca_enhancement
      IMPORTING parar        TYPE abap_bool DEFAULT ''
                !uname       TYPE sy-uname  DEFAULT zcl_c=>usuario_ap
                !include     TYPE any       DEFAULT ''
                !form        TYPE any       DEFAULT ''
                subclave     TYPE any       DEFAULT ''
                cprog        TYPE sy-cprog  DEFAULT sy-cprog
                tcode        TYPE sy-tcode  DEFAULT sy-tcode
                !enhancement TYPE any       DEFAULT ''.

    METHODS get
      IMPORTING tabla         TYPE any
                clave         TYPE any
                clave2        TYPE any   DEFAULT ''
                clave3        TYPE any   DEFAULT ''
                nombre_campo  TYPE any   DEFAULT ''
                nombre_campo2 TYPE any   DEFAULT ''
                idioma        TYPE spras DEFAULT sy-langu
                campo_clave   TYPE any   DEFAULT ''
                campo_idioma  TYPE any   DEFAULT ''
      RETURNING VALUE(valor)  TYPE string.

    METHODS es_ucomm_grabar
      IMPORTING ucomm         TYPE sy-ucomm DEFAULT sy-ucomm
      RETURNING VALUE(return) TYPE abap_bool.

    METHODS mail_info
      IMPORTING direccion      TYPE any                    DEFAULT zcl_c=>dest_avisos_mail
                asunto         TYPE any                    DEFAULT ''
                tabla1         TYPE table                  OPTIONAL
                tabla2         TYPE table                  OPTIONAL
                tabla3         TYPE table                  OPTIONAL
                o_alv1         TYPE REF TO zcl_ap_alv      OPTIONAL
                tabla_alv1     TYPE table                  OPTIONAL
                nombre_tabla1  TYPE any                    OPTIONAL
                nombre_tabla2  TYPE any                    OPTIONAL
                nombre_tabla3  TYPE any                    OPTIONAL
                nombre_alv1    TYPE any                    OPTIONAL
                !commit        TYPE abap_bool              DEFAULT 'X'
                texto          TYPE any                    DEFAULT ''
                i_textos       TYPE rspc_t_text            OPTIONAL
                info_programa  TYPE abap_bool              DEFAULT ''
                info_variante  TYPE abap_bool              DEFAULT ''
                info_alv       TYPE abap_bool              DEFAULT ''
                info_log       TYPE abap_bool              DEFAULT ''
                info_variables TYPE abap_bool              DEFAULT ''
                o_alv2         TYPE REF TO zcl_ap_alv      OPTIONAL
                tabla_alv2     TYPE table                  OPTIONAL
                nombre_alv2    TYPE any                    OPTIONAL
                o_grid2        TYPE REF TO zcl_ap_alv_grid OPTIONAL
                tabla_grid2    TYPE table                  OPTIONAL
                nombre_grid2   TYPE any                    OPTIONAL
      RETURNING VALUE(message) TYPE bapi_msg.

    METHODS validar_seleccion_obligatoria
      IMPORTING cprog      TYPE sy-cprog DEFAULT sy-cprog
                campos_or  TYPE string   DEFAULT ''
                campos_and TYPE string   DEFAULT ''
                excluir    TYPE string   DEFAULT ''
                msgty      TYPE msgty    DEFAULT 'E'
                mensaje    TYPE msgty    DEFAULT ''.

    METHODS show_docu
      IMPORTING titulo TYPE any DEFAULT 'Documentacion'(doc)
                !id    TYPE dokhl-object.

    CLASS-METHODS exec_method_st
      IMPORTING clase            TYPE any
                metodo           TYPE any
                name1            TYPE any               OPTIONAL
                value1           TYPE any               OPTIONAL
                k1               TYPE any               DEFAULT cl_abap_objectdescr=>exporting
                name2            TYPE any               OPTIONAL
                value2           TYPE any               OPTIONAL
                k2               TYPE any               DEFAULT cl_abap_objectdescr=>exporting
                name3            TYPE any               OPTIONAL
                value3           TYPE any               OPTIONAL
                k3               TYPE any               DEFAULT cl_abap_objectdescr=>exporting
                name4            TYPE any               OPTIONAL
                value4           TYPE any               OPTIONAL
                k4               TYPE any               DEFAULT cl_abap_objectdescr=>exporting
                i_par            TYPE abap_parmbind_tab OPTIONAL
      EXPORTING VALUE(returning) TYPE any
                VALUE(message)   TYPE bapi_msg.

    METHODS get_val
      IMPORTING campo    TYPE any OPTIONAL
                linea    TYPE any DEFAULT ''
          PREFERRED PARAMETER campo
      EXPORTING valor    TYPE any
                !message TYPE bapi_msg.

    METHODS get_vals
      IMPORTING campo        TYPE any OPTIONAL
                linea        TYPE any DEFAULT ''
          PREFERRED PARAMETER campo
      RETURNING VALUE(valor) TYPE string.

    METHODS get_descripciones
      IMPORTING campos TYPE string
      CHANGING  !list  TYPE any.

    METHODS initialization_v2
      IMPORTING gos               TYPE abap_bool  DEFAULT 'X'
                boton_manual      TYPE any        DEFAULT '@J7@ Manual usuario'(mus)
                boton_plantilla   TYPE any        DEFAULT '@J2@ Ejemplo fichero carga'(efc)
                get_default_param TYPE abap_bool  DEFAULT 'X'
                batch             TYPE syst_batch OPTIONAL
      CHANGING  sscrfields        TYPE sscrfields OPTIONAL.

    CLASS-METHODS class_ok
      IMPORTING !class    TYPE any
      EXPORTING !message  TYPE string
      RETURNING VALUE(ok) TYPE abap_bool.

    CLASS-METHODS dif_valores_campos
      IMPORTING VALUE(tabla)     TYPE any
                VALUE(var1)      TYPE any
                VALUE(var2)      TYPE any
                campos_no        TYPE string DEFAULT ''
      RETURNING VALUE(dif)       TYPE string.

    METHODS command_dynpro_m
      IMPORTING ucomm  TYPE sy-ucomm
                o_grid TYPE REF TO zcl_ap_alv_grid OPTIONAL
                datos  TYPE table
                !title TYPE string                 DEFAULT ''.

    METHODS status_dynpro_m
      IMPORTING o_grid TYPE REF TO zcl_ap_alv_grid
                !title TYPE string DEFAULT ''
                datos  TYPE table.

    CLASS-METHODS mail_si_error
      IMPORTING !report   TYPE sy-cprog
                msgid     TYPE sy-msgid            DEFAULT ''
                msgno     TYPE sy-msgno            OPTIONAL
                mensaje   TYPE any                 DEFAULT ''
                clave     TYPE any                 DEFAULT ''
                variables TYPE apb_lpd_t_key_value OPTIONAL.

    CLASS-METHODS set_campo_z
      IMPORTING tabla TYPE zap_campos_z-tabla
                clave TYPE zap_campos_z-clave
                campo TYPE zap_campos_z-campo
                valor TYPE any.

    CLASS-METHODS get_campo_z
      IMPORTING tabla        TYPE zap_campos_z-tabla
                clave        TYPE zap_campos_z-clave
                campo        TYPE any
      RETURNING VALUE(valor) TYPE zap_campos_z-valor.

    CLASS-METHODS get_campos_z
      IMPORTING tabla TYPE zap_campos_z-tabla
                clave TYPE zap_campos_z-clave
      CHANGING  datos TYPE any.

  PROTECTED SECTION.

  PRIVATE SECTION.
    CONSTANTS c_lin_sep TYPE string VALUE '==================================================' ##NO_TEXT.
endclass. "ZCL_AP_DEV definition
class ZCL_AP_DEV implementation.
  METHOD activar_objeto.
    CALL FUNCTION 'RS_WORKING_OBJECT_ACTIVATE'
      EXPORTING
        object                     = object
        obj_name                   = obj_name
*       FORCE_ACTIVATION           = ' '
*       ACTIVATE_ONLY_THIS_OBJECT  =
*       OBJECT_SAVED               =
*       DICTIONARY_ONLY            = ' '
*       P_WB_MANAGER               =
*       P_CALLER_PROGRAM           =
*       CWB_MODE                   =
*       DISPLAY_SYSID              = ' '
* IMPORTING
*       BIND_ERROR_WINDOW          =
* TABLES
*       OBJECTS                    =
      EXCEPTIONS
        object_not_in_working_area = 1
        execution_error            = 2
        cancelled                  = 3
        insert_into_corr_error     = 4
        OTHERS                     = 5.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.
  ENDMETHOD.
  METHOD at_selection.
    DATA: l_ucomm       TYPE sy-ucomm,
          l_but         TYPE smp_dyntxt,
          l_ruta_ztasks TYPE string.

    l_ucomm = sy-ucomm.

    IF sy-ucomm(1) = 'M'.
      PERFORM get_button IN PROGRAM zap_status USING sy-ucomm CHANGING l_but.
      CASE l_but-text.
        WHEN 'Log'. l_ucomm = 'LOG'.
        WHEN 'Parámetro' OR 'Parámetros'. l_ucomm = 'PARAM'.
      ENDCASE.
    ENDIF.

    IF NOT tcode_doc IS INITIAL.
      DATA(l_tcode_doc) = tcode_doc.
    ELSE.
      l_tcode_doc = sy-cprog.
    ENDIF.

    CASE l_ucomm.
      WHEN 'FC02'.
        zcl_ap_documentos=>get_documentos_por_clas( EXPORTING tcode = l_tcode_doc
                                                              clasificacion = 'MANUAL'
                                                    IMPORTING i_documentos = DATA(i_documentos)
                                                              eliminados = DATA(l_eliminados) ).
        IF NOT i_documentos IS INITIAL OR l_eliminados > 0.
          zcl_ap_documentos=>popup_list( tcode = l_tcode_doc clasificacion = 'MANUAL' ).
        ELSEIF zcl_ap_documentos=>visualizar_documento( nombre = 'MANUAL' ) = ''.
          IF NOT url_manual IS INITIAL.
            zcl_ap_gos=>visualizar_fichero_st( url_manual ).
          ENDIF.
        ENDIF.
      WHEN 'FC03'.
        zcl_ap_documentos=>get_documentos_por_clas( EXPORTING tcode = l_tcode_doc
                                                              clasificacion = 'PLANTILLA'
                                                    IMPORTING i_documentos = i_documentos
                                                              eliminados = l_eliminados ).
        IF NOT i_documentos IS INITIAL OR l_eliminados > 0.
          zcl_ap_documentos=>popup_list( tcode = l_tcode_doc clasificacion = 'PLANTILLA' ).
        ELSEIF zcl_ap_documentos=>visualizar_documento( nombre = 'PLANTILLA' ) = ''.
          IF NOT url_plantilla IS INITIAL.
            zcl_ap_gos=>visualizar_fichero_st( url_plantilla ).
          ENDIF.
        ENDIF.
      WHEN 'FC04'.
        zcl_ap_documentos=>popup_list( tcode = l_tcode_doc clasificacion = 'DOC_TEC' ).

      WHEN 'FC05'.
        IF NOT boton_fc05 IS INITIAL.
          zcl_ap_documentos=>popup_list( tcode = l_tcode_doc clasificacion = boton_fc05 ).
        ENDIF.
      WHEN 'CDOCU'.
        SUBMIT zdocumentos                               "#EC CI_SUBMIT
          AND RETURN
               WITH s_tcode = sy-cprog.
      WHEN 'DOC_TEC'.
        IF zcl_ap_documentos=>visualizar_documento( nombre = 'Documentacion Tecnica'(dtc) ) = ''.
          CONCATENATE zcl_c=>rutas_tasks sy-cprog INTO l_ruta_ztasks.
          zcl_ap_gos=>visualizar_fichero_st( l_ruta_ztasks ).
        ENDIF.
      WHEN 'LOG'.
        IF NOT object IS INITIAL.
          o_log->ver_logs( object = o_log->s_log-object subobject = o_log->s_log-subobject ).
        ELSEIF NOT me->proceso_log IS INITIAL.
          SUBMIT (zcl_c=>prog_zlog)                      "#EC CI_SUBMIT
                 VIA SELECTION-SCREEN
                 WITH s_proces = proceso_log
                 WITH s_progra = sy-cprog
                 WITH s_fecha  = sy-datum.
        ENDIF.
      WHEN 'PARAM'.
        IF NOT o_par IS INITIAL.
          o_par->mantenimiento( action = action alv = zparametros_alv ).
          o_par->inicio( ).
          relanzar_report( via_selection_screen = 'X' ).
        ENDIF.
      WHEN OTHERS.
        IF l_ucomm(5) = 'DOCU_'.
          show_docu( id = CONV #( l_ucomm+5 ) ).
        ELSE.
          zcl_ap_utils=>mantener_tabla( sy-ucomm ).
        ENDIF.
    ENDCASE.
  ENDMETHOD.
  METHOD at_selection_screen.
    DATA l_ruta_ztasks TYPE string.

    CASE sy-ucomm.
      WHEN 'FC02'.
        IF zcl_ap_documentos=>visualizar_documento( nombre = 'MANUAL' ) = ''.
          IF NOT url_manual IS INITIAL.
            zcl_ap_gos=>visualizar_fichero_st( url_manual ).
          ENDIF.
        ENDIF.
      WHEN 'FC03'.
        IF zcl_ap_documentos=>visualizar_documento( nombre = 'PLANTILLA' ) = ''.
          IF NOT url_plantilla IS INITIAL.
            zcl_ap_gos=>visualizar_fichero_st( url_plantilla ).
          ENDIF.
        ENDIF.
      WHEN 'FC04'.
        zcl_ap_documentos=>popup_list( ).
      WHEN 'CDOCU'.
        SUBMIT zdocumentos                               "#EC CI_SUBMIT
          AND RETURN
               WITH s_tcode = sy-cprog.
      WHEN 'DOC_TEC'.
        IF zcl_ap_documentos=>visualizar_documento( nombre = 'Documentacion Tecnica'(dtc) ) = ''.
          CONCATENATE zcl_c=>rutas_tasks sy-cprog INTO l_ruta_ztasks.
          zcl_ap_gos=>visualizar_fichero_st( l_ruta_ztasks ).
        ENDIF.
      WHEN 'PARAM'.
        IF NOT o_par IS INITIAL.
          o_par->mantenimiento( alv = zparametros_alv ).
        ENDIF.
    ENDCASE.
  ENDMETHOD.
  METHOD borrar_programa.
    CALL FUNCTION 'RS_DELETE_PROGRAM'
      EXPORTING
        program            = program
      EXCEPTIONS
        enqueue_lock       = 1
        object_not_found   = 2
        permission_failure = 3
        reject_deletion    = 4
        OTHERS             = 5.
    IF sy-subrc <> 0.
      MESSAGE 'Error borrando programa'(ebp) TYPE 'E'.
    ENDIF.
  ENDMETHOD.
  METHOD boton_gos_obj.
    DATA: l_obj     TYPE borident,
          l_manager TYPE REF TO cl_gos_manager.

* Set object Key
    l_obj-objtype = objeto.
    l_obj-objkey  = clave.

* GOS toolbar
    CREATE OBJECT l_manager
      EXPORTING
        is_object    = l_obj
        ip_no_commit = ''
      EXCEPTIONS
        OTHERS       = 1.
    IF sy-subrc = 0.
      IF clave IS INITIAL.
        IF NOT l_manager IS INITIAL.
          l_manager->unpublish( ).
          CLEAR l_manager.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD break_condicional.
    DATA: l_seguir   TYPE c LENGTH 1,
          i_breaks   TYPE TABLE OF zap_break,
          l_variable TYPE string.

    FIELD-SYMBOLS: <break>    TYPE zap_break,
                   <variable> TYPE any.

    DEFINE breakp.
      IF <break>-solo_codigo IS INITIAL.
        IF sy-batch IS INITIAL.
          BREAK-POINT.                                      ""#EC *
        ELSEIF NOT <break>-fondo IS INITIAL.
          zcl_ap_utils=>parar_fondo( ).
        ENDIF.
      ENDIF.

      IF NOT <break>-abap1 IS INITIAL.
        codigo_dinamico( l1 = <break>-abap1
                         l2 = <break>-abap2
                         l3 = <break>-abap3
                         l4 = <break>-abap4
                         l5 = <break>-abap5
                         l6 = <break>-abap6
                         l7 = <break>-abap7
                         l8 = <break>-abap8
                         l9 = <break>-abap9 ).
      ENDIF.
    END-OF-DEFINITION.

    IF sy-uname <> zcl_c=>usuario_ap.
      IF zcl_c=>break_condicional <> 'X'.
        RETURN.
      ENDIF.
    ENDIF.

    GET PARAMETER ID 'ZBREAKC' FIELD l_seguir ##EXISTS.
    IF l_seguir <> 'X'.
      RETURN.
    ENDIF.

    TRY.
        SELECT * FROM zap_break
          INTO TABLE i_breaks
         WHERE cprog    = cprog
           AND uname    = sy-uname
           AND inactivo = ''.
      CATCH cx_root. "#EC *
        RETURN.
    ENDTRY.

    LOOP AT i_breaks ASSIGNING <break>.
      IF texto NS <break>-texto.
        CONTINUE.
      ENDIF.

      IF <break>-variables IS INITIAL.
        breakp.                                             "#EC *
      ELSE.
        TRY.
            CONCATENATE '(' sy-cprog ')' <break>-variables INTO l_variable.
            ASSIGN (l_variable) TO <variable>.
            IF sy-subrc = 0.
              CASE <break>-op.
                WHEN '' OR 'EQ' OR '='.
                  IF <variable> = <break>-valor.
                    breakp.                                 "#EC *
                  ENDIF.
                WHEN 'NE' OR '<>'.
                  IF <variable> <> <break>-valor.
                    breakp.                                 "#EC *
                  ENDIF.
                WHEN 'GT' OR '>'.
                  IF <variable> > <break>-valor.
                    breakp.                                 "#EC *
                  ENDIF.
                WHEN 'GE' OR '>='.
                  IF <variable> >= <break>-valor.
                    breakp.                                 "#EC *
                  ENDIF.
                WHEN 'LT' OR '<'.
                  IF <variable> < <break>-valor.
                    breakp.                                 "#EC *
                  ENDIF.
                WHEN 'LE' OR '<='.
                  IF <variable> <= <break>-valor.
                    breakp.                                 "#EC *
                  ENDIF.
                WHEN 'LIKE'.
                  IF <variable> CS <break>-valor.
                    breakp.                                 "#EC *
                  ENDIF.
                WHEN 'NOT LIKE'.
                  IF NOT <variable> CS <break>-valor.
                    breakp.                                 "#EC *
                  ENDIF.
              ENDCASE.
            ENDIF.
          CATCH cx_root. "#EC *
            MESSAGE i398(00) WITH 'Error evaluando variable'(eev) <break>-variables '' ''.
        ENDTRY.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.
  METHOD buscar_datos.
  ENDMETHOD.
  METHOD class_ok.
    DATA: source_code TYPE TABLE OF string,
          dir         TYPE trdir,
          " TODO: variable is assigned but never used (ABAP cleaner)
          line        TYPE i,
          " TODO: variable is assigned but never used (ABAP cleaner)
          word        TYPE string.

    CLEAR ok.
    DATA(include_name) = cl_oo_classname_service=>get_cs_name( CONV #( class ) ).
    READ REPORT include_name INTO source_code.
    IF source_code IS INITIAL.
      include_name+30 = 'CP'.
      READ REPORT include_name INTO source_code.
    ENDIF.

    SELECT SINGLE * FROM trdir
      INTO dir
     WHERE name = include_name.
    IF sy-subrc <> 0.
      message = |No existe el programa { include_name }|.
    ELSE.
      SYNTAX-CHECK FOR source_code MESSAGE message LINE line WORD word DIRECTORY ENTRY dir.
      ok = xsdbool( sy-subrc = 0 ).
    ENDIF.
  ENDMETHOD.
  METHOD clear_global_data.
* http://abap.ninja/making-programs-forget/

    DATA: lt_globals   TYPE TABLE OF rfieldlist,
          lv_fieldname TYPE string.

    FIELD-SYMBOLS: <global> LIKE LINE OF lt_globals,
                   <table>  TYPE ANY TABLE,
                   <field>  TYPE any.

    "// Get all the global variables' names
    CALL FUNCTION 'GET_GLOBAL_SYMBOLS'
      EXPORTING
        program   = cprog
      TABLES
        fieldlist = lt_globals.

    "// Clear them
    LOOP AT lt_globals ASSIGNING <global>                "#EC CI_STDSEQ
         WHERE
                   name(1) NA '<%' "// Don't touch field-symbols
               AND name    <> 'SY'
               AND name    <> 'SYST'
               AND flitl   IS INITIAL. "// neither read-only variables
      "// Cross-program field-symbols access, format: '(PROGRAM)VARIABLE'
      CONCATENATE '(' cprog ')' <global>-name INTO lv_fieldname.
      CASE <global>-type.
        WHEN cl_abap_typedescr=>typekind_table. "// Table
          ASSIGN (lv_fieldname) TO <table>.
          IF sy-subrc = 0.
            FREE <table>.
          ENDIF.
        WHEN OTHERS. "// Anything else
          ASSIGN (lv_fieldname) TO <field>.
          IF sy-subrc = 0.
            FREE <field>.
          ENDIF.
      ENDCASE.
    ENDLOOP.

    cl_abap_memory_utilities=>do_garbage_collection( ).
  ENDMETHOD.
  METHOD codigo_dinamico.
    DATA: i_source             TYPE TABLE OF text255,
          program_name         TYPE sy-cprog,
          syntax_check_message TYPE c LENGTH 128,
          line_no              TYPE i,
          l_source             TYPE text255,
          l_variable           TYPE text255,
          l_type               TYPE text255.

    DEFINE add_linea.
      l_source = &1.
      IF l_source(7) = ':DEFINE'.
        REPLACE ALL OCCURRENCES OF '.' IN l_source WITH ''.
      ENDIF.
      IF l_source(13) = ':DEFINE_TABLE'.
        l_variable = l_source+14.
        CONCATENATE 'FIELD-SYMBOLS <' l_variable '> TYPE TABLE.' INTO l_source.
        APPEND l_source TO i_source.
        CONCATENATE 'ASSIGN (''(' cprog ')' l_variable ''') TO <' l_variable '>.' INTO l_source.
        APPEND l_source TO i_source.
      ELSEIF l_source(7) = ':DEFINE'.
        SPLIT l_source+8 AT ' ' INTO l_variable l_type.
        IF l_type IS INITIAL.
          l_type = 'ANY'.
        ENDIF.
        CONCATENATE '> TYPE' l_type INTO l_type SEPARATED BY space.
        CONCATENATE 'FIELD-SYMBOLS <' l_variable l_type '.' INTO l_source.
        APPEND l_source TO i_source.
        CONCATENATE 'ASSIGN (''(' cprog ')' l_variable ''') TO <' l_variable '>.' INTO l_source.
        APPEND l_source TO i_source.
      ELSEIF NOT &1 IS INITIAL.
        IF zcl_ap_string=>ultimo_caracter( &1 ) = '.'.
          APPEND &1 TO i_source.
        ELSE.
          CONCATENATE &1 '.' INTO l_source.
          APPEND l_source TO i_source.
        ENDIF.
      ENDIF.
    END-OF-DEFINITION.

    add_linea: 'REPORT ZCOMANDO',
               'FORM COMANDO',
               l1, l2, l3, l4, l5, l6, l7, l8, l9,
               'ENDFORM'.

    IF i_source IS INITIAL.
      RETURN.
    ENDIF.

    GENERATE SUBROUTINE POOL i_source                  "#EC CI_GENERATE
             NAME program_name
             MESSAGE syntax_check_message
             LINE line_no.
    IF sy-subrc <> 0.
      IF sy-uname = zcl_c=>usuario_ap.
        zcl_ap_utils=>message( p1 = 'Error de sintaxis, mensaje'(esm) p2 = syntax_check_message p3 = 'en linea'(eli) p4 = line_no ).
      ENDIF.
    ELSE.
      PERFORM comando IN PROGRAM (program_name).
    ENDIF.
  ENDMETHOD.
  METHOD command_dynpro.
    " TODO: parameter DYNNR is never used (ABAP cleaner)
    " TODO: parameter QUITAR_CAMPOS is never used (ABAP cleaner)

    DATA l_fila_activa TYPE c LENGTH 1.

    FIELD-SYMBOLS <tabla> TYPE table.

    CLEAR: accion_ejecutada,
           ucomm_text.
    ucomm = sy-ucomm.
    IF ( ucomm(1) = 'F' OR ucomm(1) = 'M' ) AND strlen( ucomm ) = 3.
      IF NOT o_alv IS INITIAL.
        ASSIGN o_alv->i_botones[ boton = ucomm ] TO FIELD-SYMBOL(<boton>).
        IF sy-subrc = 0.
          ucomm_text = <boton>-text.
          IF NOT <boton>-ucomm IS INITIAL.
            ucomm = <boton>-ucomm.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.
    CLEAR hay_sel.

    CASE ucomm.
      WHEN 'BACK' OR '&F15' OR 'ECANC'.
        accion_ejecutada = 'X'.
        IF i_listado_ini IS SUPPLIED.
          IF NOT o_alv IS INITIAL.
            o_alv->comprobar_cambios( ).
          ENDIF.
          salir_si_no_cambios( t1                     = i_listado
                               t2                     = i_listado_ini
                               solo_campos            = solo_campos
                               relanzar_report        = relanzar_report
                               rollback_si_no_cambios = rollback_si_no_cambios ).
        ELSE.
          IF relanzar_report = 'X'.
            relanzar_report( via_selection_screen = 'X' ).
          ELSE.
            LEAVE TO SCREEN 0.
          ENDIF.
        ENDIF.

      WHEN 'GRABAR'.
        IF NOT o_alv IS INITIAL.
          o_alv->comprobar_cambios( ).
          IF seleccion = 'X' OR seleccion CS ucomm.
            IF fila_activa = 'X' OR fila_activa CS ucomm.
              l_fila_activa = 'X'.
            ENDIF.
            o_alv->set_marca_filas_sel( EXPORTING validar_seleccion = 'X' fila_activa = l_fila_activa CHANGING t_tabla = i_listado hay_sel = hay_sel ).
          ENDIF.
        ENDIF.

      WHEN 'VISIBLE'.
        accion_ejecutada = 'X'.
        zcl_ap_batch_input=>cambiar_modo( CHANGING modo = modo_ct ).
        CLEAR ucomm.

      WHEN 'EXCEL'.
        IF NOT o_alv IS INITIAL.
          ASSIGN o_alv->table_ref->* TO <tabla>.
          IF sy-subrc = 0.
            SELECT SINGLE clsname FROM seoclass
              " TODO: variable is assigned but never used (ABAP cleaner)
              INTO @DATA(l_clase)
             WHERE clsname = 'ZCL_AP_ABAP2XLS'.
            IF sy-subrc = 0.
              exec_method_st( EXPORTING clase   = 'ZCL_AP_ABAP2XLS'
                                        metodo  = 'ALV_2_XLS'
                                        name1   = 'ALV'
                                        value1  = o_alv->o_grid
                                        name2   = 'TABLA'
                                        value2  = <tabla>
                                        name3   = 'ABRIR'
                                        value3  = 'X'
                                        name4   = 'NOMBRE_FICHERO'
                                        value4  = sy-title
                              IMPORTING message = DATA(msg) ).
              IF NOT msg IS INITIAL.
                MESSAGE msg TYPE 'I'.
              ENDIF.
            ELSE.
              o_alv->exportar_excel( ).
            ENDIF.
          ENDIF.
        ENDIF.

      WHEN OTHERS.
        IF seleccion <> '' AND ucomm <> ''.
          IF seleccion = 'X' OR seleccion CS ucomm.
            IF fila_activa = 'X' OR fila_activa CS ucomm.
              l_fila_activa = 'X'.
            ENDIF.
            IF NOT o_alv IS INITIAL.
              o_alv->set_marca_filas_sel( EXPORTING validar_seleccion = 'X' fila_activa = l_fila_activa CHANGING t_tabla = i_listado hay_sel = hay_sel ).
            ENDIF.
          ENDIF.
        ENDIF.

*    WHEN 'UBICAR'.
*      o_alv->set_marca_filas_sel( EXPORTING validar_seleccion = 'X' CHANGING t_tabla = i_listado hay_sel = l_hay_sel ).
*      IF l_hay_sel = 'X'.
*      LOOP AT i_listado ASSIGNING <listado> WHERE check = 'X'.
*        o_prog->ubicar( CHANGING listado = <listado> ).
*      ENDLOOP.
*      o_alv->refrescar_grid( ).
*      o_alv->set_seleccion( CHANGING t_tabla = i_listado ).
*        ENDIF.
*      WHEN 'GRABAR'.
*        o_alv->comprobar_cambios( ).
*        o_prog->message( p1 = 'Se han guardado los cambios' type = 'S' no_log = '' ).
*        o_alv->refrescar_grid( ).
*        i_listado_ini = i_listado.
    ENDCASE.
  ENDMETHOD.
  METHOD command_dynpro_m.
  ENDMETHOD.
  METHOD commit.
* Hacemos el Commit y esperamos a que se actualice todo
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.
    COMMIT WORK AND WAIT.
*  CALL FUNCTION 'C14Z_WAIT_ON_COMMIT'.

    IF dequeue_all = 'X'.
      CALL FUNCTION 'DEQUEUE_ALL'.
    ENDIF.
  ENDMETHOD.
  METHOD comparar_reports.
    DATA: i_tab_old TYPE TABLE OF trdir,
          i_tab_new TYPE TABLE OF trdir,
          i_tab_del TYPE TABLE OF xtrdir.

    CLEAR diferencias.
    CALL FUNCTION 'SVRS_COMPUTE_DELTA_REPS'
      EXPORTING
        compare_mode            = 1
        ignore_case_differences = 'X'
      TABLES
        texttab_old             = tabla1
        texttab_new             = tabla2
        trdirtab_old            = i_tab_old
        trdirtab_new            = i_tab_new
        trdir_delta             = i_tab_del
        text_delta              = diferencias.
  ENDMETHOD.
  METHOD constructor.
    DATA: l_status_prog TYPE sy-cprog,
          " TODO: variable is assigned but never used (ABAP cleaner)
          l_obj_name    TYPE tadir-obj_name,
          i_excluir     TYPE TABLE OF char40,
          l_no_param    TYPE c LENGTH 1,
          l_status      TYPE sy-pfkey,
          l_status_ok   TYPE c LENGTH 1,
          l_subobject   TYPE balsubobj,
          l_its         TYPE c LENGTH 1,
          i_source      TYPE TABLE OF text255,
          l_source      TYPE text255.

    IF status_prog IS INITIAL.
      l_status_prog = sy-cprog.
    ELSE.
      l_status_prog = status_prog.
    ENDIF.
    me->status_prog = l_status_prog.

    o_par = NEW #( clave = prog ).

    o_sgpi = NEW #( commit_work = commit_work ).

    SELECT SINGLE obj_name FROM tadir
      INTO l_obj_name
     WHERE pgmid    = 'R3TR'
       AND object   = 'CLAS'
       AND obj_name = 'ZCL_CACHE'.
    IF sy-subrc <> 0.
      o_cache = NEW #( ).
    ELSE.
      CREATE OBJECT o_cache TYPE ('ZCL_CACHE').
    ENDIF.

    IF NOT status IS INITIAL.
      IF no_param = 'X'.
        APPEND 'PARAM' TO i_excluir.
        l_no_param = 'X'.
      ELSE.
        IF param_sap_all = 'X'.
          IF zcl_ap_autorizacion=>es_sapall( ) = ''.
            APPEND 'PARAM' TO i_excluir.
            l_no_param = 'X'.
          ENDIF.
        ENDIF.
      ENDIF.
      IF object IS INITIAL AND guardar_logz IS INITIAL.
        APPEND 'LOG' TO i_excluir.
      ENDIF.

      l_status = status.
      IF status = 'INICIO' AND l_status_prog <> 'ZAP_STATUS'.
        IF l_no_param = 'X' AND object IS INITIAL.
          l_status = 'INICIO'.
        ELSEIF l_no_param = '' AND NOT object IS INITIAL.
          l_status = 'INICIO_PARAM_LOG'.
        ELSEIF l_no_param = '' AND object IS INITIAL.
          l_status = 'INICIO_PARAM'.
        ELSE.
          l_status = 'INICIO_LOG'.
        ENDIF.

        SELECT obj_code FROM rsmptexts                  "#EC CI_GENBUFF
          INTO l_status
          UP TO 1 ROWS
         WHERE progname = l_status_prog
           AND sprsl    = sy-langu
           AND obj_type = 'C'
           AND obj_code = l_status
          ORDER BY PRIMARY KEY.
        ENDSELECT.
        IF sy-subrc <> 0.
          SELECT obj_code FROM rsmptexts                "#EC CI_GENBUFF
             INTO l_status
             UP TO 1 ROWS
            WHERE progname = l_status_prog
              AND obj_type = 'C'
              AND obj_code = l_status
             ORDER BY PRIMARY KEY.
          ENDSELECT.
          IF sy-subrc <> 0.
            l_status = status.
          ENDIF.
        ENDIF.
      ENDIF.

      IF l_status_ok IS INITIAL.
        SELECT obj_code FROM rsmptexts                  "#EC CI_GENBUFF
          INTO l_status
          UP TO 1 ROWS
         WHERE progname = l_status_prog
           AND sprsl    = sy-langu
           AND obj_type = 'C'
           AND obj_code = l_status
          ORDER BY PRIMARY KEY.
        ENDSELECT.
        IF sy-subrc <> 0.
          SELECT obj_code FROM rsmptexts                "#EC CI_GENBUFF
            INTO l_status
            UP TO 1 ROWS
           WHERE progname = l_status_prog
             AND obj_type = 'C'
             AND obj_code = l_status
            ORDER BY PRIMARY KEY.
          ENDSELECT.
        ENDIF.
      ENDIF.
      IF sy-subrc = 0 OR l_status_ok = 'X'.
*      SET PF-STATUS l_status EXCLUDING i_excluir.
        CALL FUNCTION 'RS_SET_SELSCREEN_STATUS'
          EXPORTING
            p_status  = l_status
            p_program = l_status_prog
          TABLES
            p_exclude = i_excluir.
      ELSE.
        SELECT obj_code FROM rsmptextsi                 "#EC CI_GENBUFF
          INTO l_status
          UP TO 1 ROWS
         WHERE progname = l_status_prog
           AND obj_type = 'C'
           AND obj_code = l_status
          ORDER BY PRIMARY KEY.
        ENDSELECT.
        IF sy-subrc = 0.
*        SET PF-STATUS l_status OF PROGRAM sy-cprog EXCLUDING i_excluir.
          CALL FUNCTION 'RS_SET_SELSCREEN_STATUS'
            EXPORTING
              p_status  = l_status
              p_program = l_status_prog
            TABLES
              p_exclude = i_excluir.
        ENDIF.
      ENDIF.
    ENDIF.

    IF NOT object IS INITIAL.
      me->object = object.
      IF subobject IS INITIAL.
        l_subobject = prog.
      ELSE.
        l_subobject = subobject.
      ENDIF.
      o_log = NEW #( object                = object
                     subobject             = l_subobject
                     grabar_siempre        = ''
                     guardar_tabla_interna = guardar_logz ).
    ELSEIF NOT proceso_log IS INITIAL.
      o_log = NEW #( object                = proceso_log
                     guardar_tabla_interna = guardar_logz ).
    ENDIF.

    IF get_nombre_pc = 'X'.
      IF sy-batch IS INITIAL AND sy-binpt IS INITIAL.
        CALL FUNCTION 'GUI_IS_ITS'
          IMPORTING
            return = l_its.
        IF l_its IS INITIAL.
          cl_gui_frontend_services=>get_computer_name( CHANGING   computer_name        = nombre_pc
                                                       EXCEPTIONS cntl_error           = 1
                                                                  error_no_gui         = 2
                                                                  not_supported_by_gui = 3
                                                                  OTHERS               = 4 ).
          IF sy-subrc = 0.
            cl_gui_cfw=>flush( ).
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.

    titulo = sy-title.

    IF get_doc_from_report = 'X'.
      i_source = zcl_ap_dev=>get_objeto_como_texto( tipo = 'PROG' nombre = sy-cprog ).
      LOOP AT i_source INTO l_source.
        IF l_source(10) = '*->MANUAL:'.
          url_manual = l_source+10.
          REPLACE ALL OCCURRENCES OF '*' IN url_manual WITH ''.
          SPLIT url_manual AT ',' INTO url_manual url_manual_edt.
          IF NOT url_manual_edt IS INITIAL.
            IF sy-uname = zcl_c=>usuario_ap.
              url_manual = url_manual_edt.
            ELSE.
              IF zcl_ap_lista=>es_elemento( lista = zcl_c=>usuarios_sistemas elemento = sy-uname ) = 'X'.
                url_manual = url_manual_edt.
              ENDIF.
            ENDIF.
          ENDIF.
        ELSEIF l_source(13) = '*->PLANTILLA:'.
          url_plantilla = l_source+13.
          REPLACE ALL OCCURRENCES OF '*' IN url_plantilla WITH ''.
          SPLIT url_plantilla AT ',' INTO url_plantilla url_plantilla_edt.
          IF NOT url_manual_edt IS INITIAL.
            IF sy-uname = zcl_c=>usuario_ap.
              url_plantilla = url_plantilla_edt.
            ELSE.
              IF zcl_ap_lista=>es_elemento( lista = zcl_c=>usuarios_sistemas elemento = sy-uname ) = 'X'.
                url_plantilla = url_plantilla_edt.
              ENDIF.
            ENDIF.
          ENDIF.
          EXIT.
        ENDIF.
      ENDLOOP.
    ENDIF.

    IF NOT guardar_logz IS INITIAL.
      IF proceso_log IS INITIAL.
        me->proceso_log = sy-cprog.
      ELSE.
        me->proceso_log = proceso_log.
      ENDIF.
    ENDIF.

    me->no_break = no_break.
  ENDMETHOD.
  METHOD control_ejecucion_simultanea.
    DATA: l_num_jobs TYPE i,
          l_cprog    TYPE sy-cprog.

    IF NOT job IS INITIAL.
      l_num_jobs = zcl_ap_jobs=>num_job_activos( job ).

      IF    ( sy-batch = 'X' AND l_num_jobs > 1 )
         OR ( sy-batch = ''  AND l_num_jobs > 0 ).
        MESSAGE 'Saliendo debido a que hay otro job activo'(spa) TYPE 'I'.
        LEAVE PROGRAM.
      ENDIF.
    ENDIF.

    IF NOT cprog IS INITIAL.
      l_cprog = cprog.
      IF zcl_ap_utils=>bloquear_programa( cprog = l_cprog ) = 'X'.
        MESSAGE 'Saliendo debido a que el programa ya se está ejecutando.'(spe) TYPE 'I'.
        LEAVE PROGRAM.
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD dif_valores_campos.
    DATA aux1 TYPE string.

    CLEAR dif.
    DATA(i_campos) = get_fieldcatalog_tabla( tabla = tabla ).
    DATA(r_campos_no) = zcl_ap_lista=>lista2rango( campos_no ).

    LOOP AT i_campos ASSIGNING FIELD-SYMBOL(<campo>) WHERE NOT fieldname IN r_campos_no.
      ASSIGN COMPONENT <campo>-fieldname OF STRUCTURE var1 TO FIELD-SYMBOL(<v1>).
      IF sy-subrc <> 0.
        aux1 = |{ <campo>-fieldname } no existe en VAR1|.
      ELSE.
        ASSIGN COMPONENT <campo>-fieldname OF STRUCTURE var2 TO FIELD-SYMBOL(<v2>).
        IF sy-subrc <> 0.
          aux1 = |{ <campo>-fieldname } no existe en VAR2|.
        ELSE.
          IF <v1> <> <v2>.
            aux1 = |{ <campo>-fieldname }({ <v1> }<>{ <v2> })|.
          ENDIF.
        ENDIF.
      ENDIF.
      IF NOT aux1 IS INITIAL.
        IF dif IS INITIAL.
          dif = aux1.
        ELSE.
          dif = |{ dif } - { aux1 }|.
        ENDIF.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.
  METHOD envio_mail.
    DATA: i_tabla TYPE zt_parametros,
          o_mail  TYPE REF TO zcl_ap_envio_mail,
          l_texto TYPE string,
          l_param TYPE zv_parametros.

    if o_par IS INITIAL.
      return.
    endif.

    i_tabla = o_par->get_tabla_campo( campo ).

    IF i_tabla IS INITIAL.
      RETURN.
    ENDIF.

    o_mail = NEW #( usar_clases = 'X' ).

    IF texto IS INITIAL.
      l_texto = subject.
    ELSE.
      l_texto = texto.
    ENDIF.

    o_mail->set_text( l_texto ).

    LOOP AT i_tabla INTO l_param.
      o_mail->add_destinatario( l_param-valor ).
    ENDLOOP.

    o_mail->envio_mail( subject              = subject
                        direccion            = direccion
                        urgente              = urgente
                        html                 = html
                        commit               = commit
                        forzar_mail_externo  = forzar_mail_externo
                        confirmacion_lectura = confirmacion_lectura
                        emisor               = emisor ).

    o_mail->free( ).
  ENDMETHOD.
  METHOD es_ucomm_grabar.
    return = zcl_ap_lista=>es_elemento( lista = ucomm_grabar elemento = ucomm ).
  ENDMETHOD.
  METHOD exec_method_st.
    DATA: ptab   TYPE abap_parmbind_tab,
          tab    TYPE abap_parmbind,
          o_root TYPE REF TO cx_root.

    CLEAR message.

    ptab = i_par.

    IF NOT name1 IS INITIAL.
      CLEAR tab.
      tab-name = name1.
      tab-kind = k1.
      IF k1 = cl_abap_objectdescr=>returning.
        tab-value = REF #( returning ).
      ELSE.
        tab-value = REF #( value1 ).
      ENDIF.
      INSERT tab INTO TABLE ptab.
    ENDIF.
    IF NOT name2 IS INITIAL.
      CLEAR tab.
      tab-name = name2.
      tab-kind = k2.
      IF k2 = cl_abap_objectdescr=>returning.
        tab-value = REF #( returning ).
      ELSE.
        tab-value = REF #( value2 ).
      ENDIF.
      INSERT tab INTO TABLE ptab.
    ENDIF.
    IF NOT name3 IS INITIAL.
      CLEAR tab.
      tab-name = name3.
      tab-kind = k3.
      IF k3 = cl_abap_objectdescr=>returning.
        tab-value = REF #( returning ).
      ELSE.
        tab-value = REF #( value3 ).
      ENDIF.
      INSERT tab INTO TABLE ptab.
    ENDIF.
    IF NOT name4 IS INITIAL.
      CLEAR tab.
      tab-name = name4.
      tab-kind = k4.
      IF k4 = cl_abap_objectdescr=>returning.
        tab-value = REF #( returning ).
      ELSE.
        tab-value = REF #( value4 ).
      ENDIF.
      INSERT tab INTO TABLE ptab.
    ENDIF.

    TRY.
        CALL METHOD (clase)=>(metodo) PARAMETER-TABLE ptab.

      CATCH cx_root INTO o_root. "#EC *
        message = o_root->get_longtext( ).
        IF message IS INITIAL.
          message = o_root->get_text( ).
        ENDIF.
    ENDTRY.

*zcl_ap_dev=>exec_method_st( exporting clase = 'ZCL_AP_PEDIDO_SD' metodo = 'VISUALIZAR'  name1 = 'VBELN' value1 = CONV vbeln_va( '0090670810' ) IMPORTING message = data(msg) ).
*data l_returning type bapi_msg.
*zcl_ap_dev=>exec_method_st( exporting clase = 'ZCL_AP_UTILS' metodo = 'MESSAGE'
*                                      name1 = 'P1' value1 = 'parametro1' name2 = 'P2' value2 = 'parametro2' name3 = 'TYPE' value3 = 'S'
*                                      name4 = 'MESSAGE' k4 = CL_ABAP_OBJECTDESCR=>receiving
*                            IMPORTING returning = l_Returning
*                                      message = data(msg) ).
  ENDMETHOD.
  METHOD fin.
    IF NOT o_log IS INITIAL.
      o_log->grabar( ).
    ENDIF.
  ENDMETHOD.
  METHOD get.
    valor = o_cache->get( tabla         = tabla
                          clave         = clave
                          clave2        = clave2
                          clave3        = clave3
                          nombre_campo  = nombre_campo
                          nombre_campo2 = nombre_campo2
                          idioma        = idioma
                          campo_clave   = campo_clave
                          campo_idioma  = campo_idioma ).
  ENDMETHOD.
  METHOD get_atributos_campo.
    DATA: l_tabla TYPE string,
          l_campo TYPE string,
          " TODO: variable is assigned but never used (ABAP cleaner)
          l_aux   TYPE string.

    CLEAR dd03l.
    IF campo IS INITIAL AND tabla_campo CS '-'.
      SPLIT tabla_campo AT '-' INTO l_tabla l_campo.
      IF l_campo CS '('.
        SPLIT l_campo AT '(' INTO l_campo l_aux.
      ENDIF.
    ELSE.
      l_campo = campo.
      l_tabla = tabla.
    ENDIF.

    SELECT * FROM dd03l
      INTO dd03l
      UP TO 1 ROWS
     WHERE tabname   = l_tabla
       AND fieldname = l_campo
      ORDER BY PRIMARY KEY.
    ENDSELECT.
  ENDMETHOD.
  METHOD get_campo_z.
    TRY.
        CLEAR valor.
        SELECT SINGLE valor FROM zap_campos_z
          INTO valor
         WHERE tabla = tabla
           AND clave = clave
           AND campo = campo.
      CATCH cx_root INTO DATA(o_root). "#EC *
        MESSAGE |Error { o_root->get_text( ) }| TYPE 'S'.
    ENDTRY.
  ENDMETHOD.
  METHOD get_campos_z.
    TRY.
        SELECT campo, valor FROM zap_campos_z
          INTO TABLE @DATA(i_campos)
         WHERE tabla = @tabla
           AND clave = @clave.

        LOOP AT i_campos ASSIGNING FIELD-SYMBOL(<campo>).
          ASSIGN COMPONENT <campo>-campo OF STRUCTURE datos TO FIELD-SYMBOL(<fs>).
          IF sy-subrc = 0.
            <fs> = <campo>-valor.
          ENDIF.
        ENDLOOP.
      CATCH cx_root INTO DATA(o_root). "#EC *
        MESSAGE |Error { o_root->get_text( ) }| TYPE 'S'.
    ENDTRY.
  ENDMETHOD.
  METHOD get_default_variant.
    DATA h_repid            TYPE rsvar-report.
    DATA h_variant          TYPE rsvar-variant.
    DATA h_subrc            TYPE sy-subrc.
    DATA l_inicio_inmediato TYPE c LENGTH 1.

    IF sy-slset IS NOT INITIAL.
      RETURN.
    ENDIF.

    h_repid = sy-cprog.
    CLEAR h_variant.
    h_variant = 'U_'.
    WRITE sy-uname TO h_variant+2.
*--- User specific variant
    CALL FUNCTION 'RS_VARIANT_EXISTS'
      EXPORTING
        report  = h_repid
        variant = h_variant
      IMPORTING
        r_c     = h_subrc.
    IF h_subrc = 0.
      IF sy-batch = 'X'.
        h_subrc = 4.
        CLEAR h_variant.
      ENDIF.
    ELSE.
      CLEAR h_variant.
    ENDIF.
    IF sy-batch = '' AND h_variant IS INITIAL.
      h_variant = 'U_'.
      WRITE sy-uname TO h_variant+2.
      CONCATENATE h_variant '#' INTO h_variant.
      CALL FUNCTION 'RS_VARIANT_EXISTS'
        EXPORTING
          report  = h_repid
          variant = h_variant
        IMPORTING
          r_c     = h_subrc.
      IF h_subrc = 0.
        l_inicio_inmediato = 'X'.
      ENDIF.
    ENDIF.

    IF NOT h_subrc IS INITIAL.
      IF sy-uname = zcl_c=>usuario_ap.
        IF NOT nombre_pc IS INITIAL.
          IF zcl_c=>nombre_pc CS nombre_pc.
            h_variant = '_PC'.
            CALL FUNCTION 'RS_VARIANT_EXISTS'
              EXPORTING
                report  = h_repid
                variant = h_variant
              IMPORTING
                r_c     = h_subrc.
          ENDIF.
        ENDIF.
        IF NOT h_subrc IS INITIAL.
          h_variant = 'P'.
          CALL FUNCTION 'RS_VARIANT_EXISTS'
            EXPORTING
              report  = h_repid
              variant = h_variant
            IMPORTING
              r_c     = h_subrc.
          IF h_subrc <> 0.
            IF sy-tcode = 'SADT_START_WB_URI'.
              h_variant = 'ECLIPSE'.
              CALL FUNCTION 'RS_VARIANT_EXISTS'
                EXPORTING
                  report  = h_repid
                  variant = h_variant
                IMPORTING
                  r_c     = h_subrc.
            ENDIF.
            IF h_subrc <> 0.
              IF sy-tcode = 'SE38' OR sy-tcode = 'SADT_START_WB_URI'.
                h_variant = 'SE38'.
                CALL FUNCTION 'RS_VARIANT_EXISTS'
                  EXPORTING
                    report  = h_repid
                    variant = h_variant
                  IMPORTING
                    r_c     = h_subrc.
              ENDIF.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.
      IF NOT h_subrc IS INITIAL.
*--- customizing variant
        CLEAR h_variant.
        h_variant = 'SAP_TCODE_'.
        WRITE sy-tcode TO h_variant+10.
        CALL FUNCTION 'RS_VARIANT_EXISTS'
          EXPORTING
            report  = h_repid
            variant = h_variant
          IMPORTING
            r_c     = h_subrc.
        IF NOT h_subrc IS INITIAL.
*--- system variant
          CLEAR h_variant.
          h_variant = 'SAP&TCODE_'.
          WRITE sy-tcode TO h_variant+10.
          CALL FUNCTION 'RS_VARIANT_EXISTS'
            EXPORTING
              report  = h_repid
              variant = h_variant
            IMPORTING
              r_c     = h_subrc.
        ENDIF.
        IF NOT h_subrc IS INITIAL.
*--- system variant
          CLEAR h_variant.
          h_variant = 'DEFAULT'.
          CALL FUNCTION 'RS_VARIANT_EXISTS'
            EXPORTING
              report  = h_repid
              variant = h_variant
            IMPORTING
              r_c     = h_subrc.
        ENDIF.
        IF NOT h_subrc IS INITIAL.
*--- system variant
          CLEAR h_variant.
          h_variant = sy-tcode.
          CALL FUNCTION 'RS_VARIANT_EXISTS'
            EXPORTING
              report  = h_repid
              variant = h_variant
            IMPORTING
              r_c     = h_subrc.
          IF h_subrc <> 0.
            CLEAR h_variant.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.

    IF sy-batch = 'X'.
      h_subrc = 4.
      CLEAR h_variant.
    ENDIF.

    IF NOT h_variant IS INITIAL.
      IF l_inicio_inmediato IS INITIAL.
        CALL FUNCTION 'RS_SUPPORT_SELECTIONS'
          EXPORTING
            report               = h_repid
            variant              = h_variant
          EXCEPTIONS
            variant_not_existent = 01
            variant_obsolete     = 02.
        IF sy-subrc <> 0.
          IF sy-uname = zcl_c=>usuario_ap.
            MESSAGE 'Error recuperando variante'(erv) TYPE 'S'.
          ENDIF.
        ENDIF.
      ELSE.
        SUBMIT (sy-cprog) USING SELECTION-SET h_variant. "#EC CI_SUBMIT
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD get_descripcion.
    DATA: i_campos             TYPE ddfields,
          l_campo              TYPE dfies,
          l_campo_idioma       TYPE string,
          l_where              TYPE string,
          l_nombre_campo_texto TYPE string,
          l_ncampos            TYPE i,
          o_cx_root            TYPE REF TO cx_root,
          l_string             TYPE string,
          l_campo_texto2       TYPE string.

    i_campos = get_fieldcatalog_tabla( tabla ).
    DELETE i_campos WHERE fieldname = 'MANDT'.           "#EC CI_STDSEQ

    IF campo_idioma IS INITIAL.
      LOOP AT i_campos INTO l_campo WHERE datatype = 'LANG'. "#EC CI_STDSEQ
        l_campo_idioma = l_campo-fieldname.
        DELETE i_campos.
        CONCATENATE l_campo_idioma ' = ''' idioma ''' AND ' INTO l_where.
        EXIT.
      ENDLOOP.
    ELSEIF campo_idioma = 'NO'.                    "#EC EMPTY_IF_BRANCH

    ELSE.
      CONCATENATE campo_idioma ' = ''' idioma ''' AND ' INTO l_where.
    ENDIF.

    l_nombre_campo_texto = nombre_campo.

    IF NOT campo_clave IS INITIAL.
      CONCATENATE l_where campo_clave INTO l_where SEPARATED BY space.
      CONCATENATE l_where ' = ''' valor '''' INTO l_where.
    ELSE.
      l_ncampos = lines( i_campos ).
      IF l_ncampos >= 2.
        READ TABLE i_campos INTO l_campo INDEX 1.
        CONCATENATE l_where l_campo-fieldname INTO l_where SEPARATED BY space.
        CONCATENATE l_where ' = ''' valor '''' INTO l_where.

        IF NOT valor2 IS INITIAL.
          READ TABLE i_campos INTO l_campo INDEX 2.
          CONCATENATE l_where 'AND' l_campo-fieldname INTO l_where SEPARATED BY space.
          CONCATENATE l_where ' = ''' valor2 '''' INTO l_where.
          DELETE i_campos INDEX 2.
        ENDIF.

        IF l_nombre_campo_texto IS INITIAL.
          READ TABLE i_campos INTO l_campo WITH KEY rollname = 'CVTEXT'.
          IF sy-subrc = 0.
            l_nombre_campo_texto = l_campo-fieldname.
          ELSE.
            READ TABLE i_campos INTO l_campo INDEX 2.
            l_nombre_campo_texto = l_campo-fieldname.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.

    IF line_exists( i_campos[ fieldname = 'BEGDA' ] ) AND line_exists( i_campos[ fieldname = 'ENDDA' ] ).
      l_where = |{ l_where } AND BEGDA <= '{ sy-datum }' AND ENDDA >= '{ sy-datum }'|.
    ENDIF.

    TRY.
        SELECT SINGLE (l_nombre_campo_texto) FROM (tabla)
          INTO texto
         WHERE (l_where).
      CATCH cx_root INTO o_cx_root.                         "#EC *
        IF sy-uname = zcl_c=>usuario_ap.
          l_string = o_cx_root->get_longtext( ).
          MESSAGE l_string TYPE 'I'.
        ENDIF.
    ENDTRY.

    IF NOT nombre_campo2 IS INITIAL.
      SELECT SINGLE (nombre_campo2) FROM (tabla)
        INTO l_campo_texto2
       WHERE (l_where).
      IF sy-subrc = 0.
        CONCATENATE texto l_campo_texto2 INTO texto SEPARATED BY space.
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD get_descripcion_campo.
    DATA l_dd03l TYPE dd03l.

    l_dd03l = get_atributos_campo( campo       = campo
                                   tabla       = tabla
                                   tabla_campo = tabla_campo ).
    IF NOT l_dd03l IS INITIAL.
      SELECT ddtext FROM dd04t
        INTO ddtext
        UP TO 1 ROWS
       WHERE ddlanguage = idioma
         AND rollname   = l_dd03l-rollname
        ORDER BY PRIMARY KEY.
      ENDSELECT.
    ENDIF.
  ENDMETHOD.
  METHOD get_descripciones.
    DATA: i_campos    TYPE TABLE OF string,
          l_campo_ref TYPE string.

    FIELD-SYMBOLS: <campo_ref>  TYPE any,
                   <valor>      TYPE any,
                   <campo_ref2> TYPE any.

    SPLIT campos AT ',' INTO TABLE i_campos.
    LOOP AT i_campos ASSIGNING FIELD-SYMBOL(<campo>).
      CLEAR l_campo_ref.
      CASE <campo>.
        WHEN 'STTXT' OR 'USTXT'.
          ASSIGN COMPONENT 'OBJNR' OF STRUCTURE list TO <campo_ref>.
          IF sy-subrc = 0.
            l_campo_ref = 'OBJNR'.
          ELSE.
            ASSIGN COMPONENT 'AUFNR' OF STRUCTURE list TO <campo_ref>.
            IF sy-subrc = 0.
              l_campo_ref = 'AUFNR'.
            ENDIF.
          ENDIF.
        WHEN 'EQKTX'. l_campo_ref = 'EQUNR'.
        WHEN 'PLTXT'. l_campo_ref = 'TPLNR'.
        WHEN 'MAKTX' OR 'MEINS'. l_campo_ref = 'MATNR'.
        WHEN 'HKONT_T'. l_campo_ref = 'HKONT'.
        WHEN 'NAME1_PROV'. l_campo_ref = 'LIFNR'.
        WHEN 'ENAME'. l_campo_ref = 'PERNR'.
        WHEN 'DMBTR' OR 'WRBTR' OR 'MENGE'. l_campo_ref = 'SHKZG'.
        WHEN 'NOMBRE_USUARIO' OR 'EMAIL_USUARIO'.
          l_campo_ref = 'UNAME'.
          ASSIGN COMPONENT l_campo_ref OF STRUCTURE list TO <campo_ref>.
          IF sy-subrc <> 0.
            l_campo_ref = 'USER_ID'.
          ENDIF.
        WHEN OTHERS.
          IF <campo> CS `_T-D `.
            SPLIT <campo> AT `_T-D ` INTO aux1 aux2.
            ASSIGN COMPONENT aux1 OF STRUCTURE list TO <campo_ref>.
            IF sy-subrc = 0.
              CONCATENATE aux1 '_T' INTO aux1.
              ASSIGN COMPONENT aux1 OF STRUCTURE list TO <valor>.
              IF sy-subrc = 0.
                CONCATENATE 'D' aux2 INTO aux2 SEPARATED BY space.
                <valor> = get( tabla = aux2 clave = <campo_ref> ).
              ENDIF.
            ENDIF.
          ENDIF.
      ENDCASE.
      IF NOT l_campo_ref IS INITIAL.
        ASSIGN COMPONENT l_campo_ref OF STRUCTURE list TO <campo_ref>.
        IF sy-subrc = 0.
          ASSIGN COMPONENT <campo> OF STRUCTURE list TO <valor>.
          IF sy-subrc = 0.
            CASE <campo>.
              WHEN 'STTXT'.
                CASE l_campo_ref.
                  WHEN 'OBJNR'. <valor> = zcl_ap_orden_pp=>get_status_st( objnr = CONV #( <campo_ref> ) ).
                  WHEN 'AUFNR'. <valor> = zcl_ap_orden_pp=>get_status_st( aufnr = <campo_ref> ).
                ENDCASE.
              WHEN 'USTXT'.
                CASE l_campo_ref.
                  WHEN 'OBJNR'. <valor> = zcl_ap_orden_pp=>get_status_st( objnr = CONV #( <campo_ref> ) usuario = 'X' ).
                  WHEN 'AUFNR'. <valor> = zcl_ap_orden_pp=>get_status_st( aufnr = <campo_ref> usuario = 'X' ).
                ENDCASE.
              WHEN 'EQKTX'. <valor> = get( tabla = 'EQKT' clave = <campo_ref> ).
              WHEN 'PLTXT'. <valor> = get( tabla = 'IFLOTX' clave = <campo_ref> ).
              WHEN 'MAKTX'. <valor> = get( tabla = 'MAKT' clave = <campo_ref> ).
              WHEN 'MEINS'. <valor> = get( tabla = 'MEINS' clave = <campo_ref> ).
              WHEN 'HKONT_T'.
                ASSIGN COMPONENT 'KTOPL' OF STRUCTURE list TO <campo_ref2>.
                IF sy-subrc = 0.
                  <valor> = get( tabla = 'SKAT' clave = <campo_ref> clave2 = <campo_ref2> ).
                ELSE.
                  <valor> = get( tabla = 'SKAT' clave = <campo_ref> ).
                ENDIF.
              WHEN 'ENAME'. <valor> = get( tabla = 'ENAME' clave = <campo_ref> ).
              WHEN 'NAME1_PROV'. <valor> = get( tabla = 'LFA1' clave = <campo_ref> ).
              WHEN 'NOMBRE_USUARIO'. <valor> = get( tabla = 'NOMBRE_USUARIO' clave = <campo_ref> ).
              WHEN 'EMAIL_USUARIO'. <valor> = get( tabla = 'EMAIL_USUARIO' clave = <campo_ref> ).
              WHEN 'DMBTR' OR 'WRBTR' OR 'MENGE'.
                IF <campo_ref> = 'H'.
                  <valor> = - <valor>.
                ENDIF.
            ENDCASE.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.
  METHOD get_fieldcatalog.
    DATA: lr_data         TYPE REF TO data,
          lo_tabdescr     TYPE REF TO cl_abap_structdescr,
          l_linea         TYPE abap_componentdescr,
          lr_tipo         TYPE REF TO cl_abap_typedescr,
          i_mas_campos    TYPE abap_component_tab,
          i_mas_campos2   TYPE abap_component_tab,
          i_nuevos_campos TYPE abap_component_tab.

    IF NOT tabla IS INITIAL.
      CREATE DATA lr_data LIKE LINE OF tabla.
      lo_tabdescr ?= cl_abap_structdescr=>describe_by_data_ref( lr_data ).
    ELSE.
      TRY.
          CREATE DATA lr_data LIKE linea.
          lo_tabdescr ?= cl_abap_structdescr=>describe_by_data_ref( lr_data ).
        CATCH cx_sy_move_cast_error.
          RETURN.
      ENDTRY.
    ENDIF.
    campos = lo_tabdescr->get_components( ).

    IF limpiar_tablas = 'X'.
      DELETE campos WHERE type->type_kind = 'h'.         "#EC CI_STDSEQ
    ENDIF.

    IF desglosar_includes = 'X'.
      LOOP AT campos INTO l_linea WHERE NOT name IS INITIAL. "#EC CI_STDSEQ
        TRY.
            lr_tipo ?= l_linea-type.
            IF lr_tipo->type_kind = 'h'.
              DELETE campos.
            ENDIF.
          CATCH cx_sy_move_cast_error INTO DATA(o_cast).
            message = o_cast->get_text( ).
        ENDTRY.
      ENDLOOP.

      LOOP AT campos INTO l_linea WHERE name IS INITIAL AND as_include = 'X'. "#EC CI_STDSEQ
        lo_tabdescr ?= l_linea-type.
        i_mas_campos = lo_tabdescr->get_components( ).

        LOOP AT i_mas_campos INTO l_linea.
          IF l_linea-name IS INITIAL AND l_linea-as_include = 'X'.
            lo_tabdescr ?= l_linea-type.
            i_mas_campos2 = lo_tabdescr->get_components( ).
            LOOP AT i_mas_campos2 INTO l_linea.
              APPEND l_linea TO i_nuevos_campos.
            ENDLOOP.
          ELSE.
            APPEND l_linea TO i_nuevos_campos.
          ENDIF.
        ENDLOOP.
      ENDLOOP.

      LOOP AT i_nuevos_campos INTO l_linea.
        APPEND l_linea TO campos.
      ENDLOOP.
    ENDIF.

    IF NOT solo_tipos IS INITIAL.
      LOOP AT campos INTO l_linea WHERE NOT name IS INITIAL. "#EC CI_STDSEQ
        TRY.
            lr_tipo ?= l_linea-type.
            IF lr_tipo->type_kind CN solo_tipos.
              DELETE campos.
            ENDIF.
          CATCH cx_sy_move_cast_error INTO o_cast.
            message = o_cast->get_text( ).
        ENDTRY.
      ENDLOOP.
    ENDIF.
  ENDMETHOD.
  METHOD get_fieldcatalog_ddic.
    DATA: lr_data     TYPE REF TO data,
          lo_tabdescr TYPE REF TO cl_abap_structdescr.

    CREATE DATA lr_data LIKE LINE OF tabla.

    lo_tabdescr ?= cl_abap_structdescr=>describe_by_data_ref( lr_data ).

*  ls_header = lo_tabdescr->get_ddic_header( ).

*  campos = lo_tabdescr->get_ddic_field_list( ).

    lo_tabdescr->get_ddic_field_list(
*    EXPORTING
*                                                 p_langu      = SY-LANGU
*                                                 p_including_substructres = ABAP_FALSE
      RECEIVING
        p_field_list = campos
      EXCEPTIONS
        not_found    = 1
        no_ddic_type = 2
        OTHERS       = 3 ).
    IF sy-subrc <> 0.
      CLEAR campos.
    ENDIF.
  ENDMETHOD.
  METHOD get_fieldcatalog_tabla.
    " TODO: variable is assigned but never used (ABAP cleaner)
    DATA: l_tabname   TYPE tabname,
          lo_tabdescr TYPE REF TO cl_abap_structdescr.
    DATA i_soloc TYPE TABLE OF fieldname.

    SELECT SINGLE tabname FROM dd02l
      INTO l_tabname
     WHERE tabname = tabla.

    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    TRY.
        lo_tabdescr ?= cl_abap_structdescr=>describe_by_name( tabla ).
      CATCH cx_root. "#EC *
        RETURN.
    ENDTRY.
*  ls_header = lo_tabdescr->get_ddic_header( ).

    campos = lo_tabdescr->get_ddic_field_list( ).

    IF NOT solo_campos IS INITIAL.
      SPLIT solo_campos AT ',' INTO TABLE i_soloc.
      DATA(i_c) = campos.
      CLEAR campos.
      LOOP AT i_soloc ASSIGNING FIELD-SYMBOL(<soloc>).
        ASSIGN i_c[ fieldname = <soloc> ] TO FIELD-SYMBOL(<campo>).
        IF sy-subrc = 0.
          APPEND <campo> TO campos.
        ENDIF.
      ENDLOOP.

*    DATA(r_campos) = zcl_ap_lista=>lista2rango( solo_campos ).
*    DELETE campos WHERE NOT fieldname IN r_campos.
    ENDIF.
  ENDMETHOD.
  METHOD get_fieldcatalog_tabla_alv.
    DATA: table        TYPE REF TO data,
          salv_table   TYPE REF TO cl_salv_table,
          columns      TYPE REF TO cl_salv_columns_table,
          aggregations TYPE REF TO cl_salv_aggregations.

    FIELD-SYMBOLS <tabla> TYPE any.

    CREATE DATA table LIKE tabla.
    ASSIGN table->* TO <tabla>.
    TRY.
        cl_salv_table=>factory( IMPORTING r_salv_table = salv_table
                                CHANGING  t_table      = <tabla> ).

        columns = salv_table->get_columns( ).
        aggregations = salv_table->get_aggregations( ).
        campos = cl_salv_controller_metadata=>get_lvc_fieldcatalog( r_columns      = columns " ALV Filter
                                                                    r_aggregations = aggregations ). " ALV Aggregations

        IF tabla_ref <> ''.
          LOOP AT campos ASSIGNING FIELD-SYMBOL(<campo>).
            SELECT fieldname
              FROM dd03l
              WHERE tabname   = @tabla_ref
                AND fieldname = @<campo>-fieldname
              ORDER BY PRIMARY KEY
              INTO @<campo>-ref_field
              UP TO 1 ROWS.
            ENDSELECT.
            IF sy-subrc = 0.
              <campo>-ref_table = tabla_ref.
            ENDIF.
          ENDLOOP.
        ENDIF.

        IF fix_ref = 'X'.
          LOOP AT campos ASSIGNING <campo> WHERE rollname <> '' AND ref_table IS INITIAL.
            CASE <campo>-rollname.
              WHEN 'MAKTX'.
                <campo>-ref_table = 'MAKT'.
                <campo>-ref_field = 'MAKTX'.
              WHEN 'NAME1_GP'.
                <campo>-ref_table = 'LFA1'.
                <campo>-ref_field = 'NAME1'.
              WHEN 'BAPI_MSG'.
                <campo>-ref_table = 'BAPIRET2'.
                <campo>-ref_field = 'MESSAGE'.
              WHEN OTHERS.
                IF NOT <campo>-domname IS INITIAL.
                  SELECT entitytab
                    FROM dd01l
                    WHERE domname = @<campo>-domname
                    ORDER BY PRIMARY KEY
                    INTO @<campo>-ref_table
                    UP TO 1 ROWS.
                  ENDSELECT.
                  IF NOT <campo>-ref_table IS INITIAL.
                    <campo>-ref_field = <campo>-domname.
                  ENDIF.
                ENDIF.
            ENDCASE.
          ENDLOOP.
        ENDIF.

* APC20221128 En nueva version de Hana da problmeas
        LOOP AT campos ASSIGNING <campo> WHERE ref_field <> '' AND ref_table IS INITIAL.
          CLEAR <campo>-ref_field.
        ENDLOOP.

      CATCH cx_root INTO DATA(o_root).                      "#EC *
        MESSAGE o_root->get_text( ) TYPE 'E'.
    ENDTRY.
  ENDMETHOD.
  METHOD get_objeto_como_texto.
    TYPES: BEGIN OF t_trdir,
             name      TYPE trdir-name,
             name_orig TYPE trdir-name,
           END OF t_trdir.
    TYPES: BEGIN OF t_inc_sort,
             orden TYPE string.
             INCLUDE TYPE seop_method_w_include.
    TYPES: END OF t_inc_sort.

    DATA:
      source      TYPE seop_source_string,
      cifref      TYPE REF TO if_oo_clif_incl_naming,
      clsref      TYPE REF TO if_oo_class_incl_naming,
      pool_source TYPE seop_source_string,
      source_line TYPE LINE OF seop_source_string,
      tabix       TYPE sytabix,
      includes    TYPE seop_methods_w_include,
      include     TYPE seop_method_w_include,
      intref      TYPE REF TO if_oo_interface_incl_naming.
    DATA: l_string  TYPE string,
          l_name    TYPE ddobjname,
          " TODO: variable is assigned but never used (ABAP cleaner)
          got_state TYPE ddgotstate,
          ddxx      TYPE dd02v,
          " TODO: variable is assigned but never used (ABAP cleaner)
          ddxx2     TYPE dd09l,
          ddtb_tab  TYPE TABLE OF dd03p,
          ddtb      TYPE dd03p.
    DATA i_inc_sort TYPE TABLE OF t_inc_sort.
    DATA: l_tfdir TYPE tfdir,
          l_tabla TYPE text255.
*        l_report TYPE string,
*        i_tabla2 TYPE tttext255,
*        l_aux    TYPE text255,
*        l_tabix  TYPE i.
    DATA: l_trdir TYPE t_trdir,
          l_dd40l TYPE dd40l.
*        i_trdir TYPE TABLE OF t_trdir.
    DATA o_bi      TYPE REF TO zcl_ap_batch_input.
    " TODO: variable is assigned but never used (ABAP cleaner)
    DATA l_mensaje TYPE bapireturn1-message.
    DATA xml       TYPE string.

    CHECK NOT nombre IS INITIAL.

    CLEAR i_tabla.
    CASE tipo.
      WHEN 'PROG'.
        READ REPORT nombre INTO i_tabla.
      WHEN 'DDLS'.
        SELECT source
          FROM ddddlsrc
          WHERE ddlname = @nombre
          ORDER BY PRIMARY KEY
          INTO @l_string
          UP TO 1 ROWS.
        ENDSELECT.
        zcl_ap_string=>string2tabla( EXPORTING string = l_string longitud = 255 CHANGING tabla = i_tabla ).
      WHEN 'CLAS'.
        TYPE-POOLS:
          seoc,
          seop.

        CLASS cl_oo_include_naming DEFINITION LOAD.

        cl_oo_include_naming=>get_instance_by_cifkey( EXPORTING  cifkey = CONV #( nombre )
                                                      RECEIVING  cifref = cifref
                                                      EXCEPTIONS OTHERS = 1 ).
        IF sy-subrc <> 0.
          RETURN.
        ENDIF.

        CASE cifref->clstype.
          WHEN seoc_clstype_class.
            clsref ?= cifref.
            READ REPORT clsref->class_pool
                 INTO pool_source.
            LOOP AT pool_source INTO source_line.
              IF    source_line CS 'CLASS-POOL'
                 OR source_line CS 'class-pool'.

                IF source_line = 'class-pool .' OR source_line = ''. "#EC EMPTY_IF_BRANCH
                ELSE.
                  APPEND source_line TO i_tabla.
                ENDIF.
                tabix = sy-tabix.
                EXIT.
              ENDIF.
            ENDLOOP.

            READ REPORT clsref->locals_old
                 INTO source.
            LOOP AT source
                 INTO source_line.
              IF source_line NS '*"*' AND source_line <> ''.
                APPEND source_line TO i_tabla.
              ENDIF.
            ENDLOOP.
            READ REPORT clsref->locals_def
                 INTO source.
            LOOP AT source
                 INTO source_line.
              IF source_line NS '*"*' AND source_line <> ''.
                APPEND source_line TO i_tabla.
              ENDIF.
            ENDLOOP.
            READ REPORT clsref->locals_imp
                 INTO source.
            LOOP AT source
                 INTO source_line.
              IF source_line NS '*"*' AND source_line <> ''.
                IF source_line = 'type-pools abap .'. "#EC EMPTY_IF_BRANCH
                ELSE.
                  APPEND source_line TO i_tabla.
                ENDIF.
              ENDIF.
            ENDLOOP.

            READ REPORT clsref->macros
                 INTO source.
            LOOP AT source
                 INTO source_line.
              IF source_line NS '*"*'.
                APPEND source_line TO i_tabla.
              ENDIF.
            ENDLOOP.
            READ REPORT clsref->public_section
                 INTO source.
            LOOP AT source
                 INTO source_line.
              IF source_line NS '*"*'.
                APPEND source_line TO i_tabla.
              ENDIF.
            ENDLOOP.
            READ REPORT clsref->protected_section
                 INTO source.
            LOOP AT source
                 INTO source_line.
              IF source_line NS '*"*'.
                APPEND source_line TO i_tabla.
              ENDIF.
            ENDLOOP.
            READ REPORT clsref->private_section
                 INTO source.
            LOOP AT source
                 INTO source_line.
              IF source_line NS '*"*'.
                APPEND source_line TO i_tabla.
              ENDIF.
            ENDLOOP.
            CONCATENATE 'CLASS' nombre 'IMPLEMENTATION' INTO l_string SEPARATED BY space.
            LOOP AT pool_source
                 FROM tabix
                 INTO source_line.
              IF source_line CS 'ENDCLASS'.
                APPEND source_line TO i_tabla.
              ENDIF.
              IF source_line CS l_string.
                APPEND source_line TO i_tabla.
                tabix = sy-tabix.
                EXIT.
              ENDIF.
            ENDLOOP.
* method implementation
            includes = clsref->get_all_method_includes( ).
            MOVE-CORRESPONDING includes TO i_inc_sort.
            LOOP AT i_inc_sort ASSIGNING FIELD-SYMBOL(<inc>).
              READ REPORT <inc>-incname INTO source.
              READ TABLE source INTO source_line INDEX 1.
              IF sy-subrc = 0.
                <inc>-orden = to_lower( source_line ).
                CONDENSE <inc>-orden NO-GAPS.
              ENDIF.
            ENDLOOP.
            SORT i_inc_sort.
            CLEAR includes.
            MOVE-CORRESPONDING i_inc_sort TO includes.

            LOOP AT includes
                 INTO include.
              READ REPORT include-incname
                   INTO source.
              LOOP AT source
                   INTO source_line.
                APPEND source_line TO i_tabla.
              ENDLOOP.
            ENDLOOP.
            LOOP AT pool_source
                 FROM tabix
                 INTO source_line.
              IF source_line CS 'ENDCLASS'.
                APPEND source_line TO i_tabla.
              ENDIF.
            ENDLOOP.

          WHEN seoc_clstype_interface.
            intref ?= cifref.
            READ REPORT intref->interface_pool
                 INTO source.
            LOOP AT source INTO source_line.
              APPEND source_line TO i_tabla.
            ENDLOOP.
            READ REPORT intref->public_section
                 INTO source.
            LOOP AT source INTO source_line.
              APPEND source_line TO i_tabla.
            ENDLOOP.

        ENDCASE.
*      SUBMIT seo_class_output
*       AND RETURN
*       WITH cifkey = nombre
*      EXPORTING LIST TO MEMORY.
*
*      DATA list_tab TYPE TABLE OF abaplist.
*      CALL FUNCTION 'LIST_FROM_MEMORY'
*        TABLES
*          listobject = list_tab
*        EXCEPTIONS
*          not_found  = 1
*          OTHERS     = 2.
*
*      IF sy-subrc = 0.
*        DATA i_texto TYPE  list_string_table.
*        CALL FUNCTION 'LIST_TO_DAT'
*          EXPORTING
*            list_index         = -1
*          CHANGING
*            listobject         = list_tab
*            listdat            = i_texto
*          EXCEPTIONS
*            empty_list         = 1
*            list_index_invalid = 2.
*
*        DATA l_aux6(6).
*        LOOP AT i_texto ASSIGNING FIELD-SYMBOL(<tab>).
*          l_aux6 = <tab>.
*          TRANSLATE l_aux6 TO UPPER CASE.
*          IF l_aux6 = 'CLASS'.
*            EXIT.
*          ELSE.
*            DELETE i_texto.
*          ENDIF.
*        ENDLOOP.
*        i_tabla = i_texto.
*      ENDIF.

*      CONCATENATE nombre '=%' INTO l_report.
*      SELECT * FROM trdir
*        INTO CORRESPONDING FIELDS OF TABLE i_trdir
*       WHERE name LIKE l_report
*         AND name NOT LIKE '%=CS'.
*      IF sy-subrc NE 0.
*        CONCATENATE nombre '%' INTO l_report.
*        SELECT * FROM trdir
*          INTO CORRESPONDING FIELDS OF TABLE i_trdir
*         WHERE name LIKE l_report.
*      ENDIF.
*
*      LOOP AT i_trdir INTO l_trdir.
*        l_trdir-name_orig = l_trdir-name.
*        READ REPORT l_trdir-name INTO  i_tabla2.
*        IF sy-subrc = 0.
*          READ TABLE i_tabla2 INTO l_tabla INDEX 1.
*          IF sy-subrc = 0.
*            l_tabix = sy-tabix.
*            TRANSLATE l_tabla TO UPPER CASE.
*            CLEAR l_aux.
*            SPLIT l_tabla AT 'METHOD' INTO l_tabla l_aux.
*            IF NOT l_aux IS INITIAL.
*              REPLACE '.' WITH ' ' INTO l_aux.
*              CONDENSE l_aux NO-GAPS.
*              CONCATENATE nombre '=' l_aux INTO l_trdir-name.
*            ENDIF.
*          ENDIF.
*        ENDIF.
*        MODIFY i_trdir FROM l_trdir.
*      ENDLOOP.
*      SORT i_trdir.
*
*      LOOP AT i_trdir INTO l_trdir.
*        l_tabla = c_lin_sep.
*        APPEND l_tabla TO i_tabla.
*        APPEND l_trdir-name TO i_tabla.
*        l_tabla = c_lin_sep.
*        APPEND l_tabla TO i_tabla.
*        READ REPORT l_trdir-name_orig INTO  i_tabla2.
*        LOOP AT i_tabla2 INTO l_tabla.
*          APPEND l_tabla TO i_tabla.
*        ENDLOOP.
*      ENDLOOP.

      WHEN 'FUGR'.
        SELECT SINGLE * FROM tfdir
          INTO l_tfdir
         WHERE funcname = nombre.
        CONCATENATE l_tfdir-pname+3 'U' l_tfdir-include INTO l_trdir-name.
        READ REPORT l_trdir-name INTO i_tabla.
      WHEN 'TABL'.

        l_name = nombre.
        CALL FUNCTION 'DDIF_TABL_GET'
          EXPORTING
            name          = l_name
            state         = 'M'
            langu         = sy-langu
          IMPORTING
            gotstate      = got_state
            dd02v_wa      = ddxx
            dd09l_wa      = ddxx2
          TABLES
            dd03p_tab     = ddtb_tab
*           DD12V_TAB     = DD12V_TAB
          EXCEPTIONS
            illegal_input = 1.
        IF sy-subrc = 0.
          CONCATENATE 'TABLA:' ddxx-tabname ddxx-ddtext
                      INTO l_tabla SEPARATED BY space.
          APPEND l_tabla TO i_tabla.
          LOOP AT ddtb_tab INTO ddtb.
            IF modo <> 'C'.
              CONCATENATE ddtb-fieldname ';' ddtb-rollname ';'
                          ddtb-inttype ';' ddtb-leng ';'
                          ddtb-ddtext INTO l_tabla.
            ELSE.
              CONCATENATE ddtb-fieldname ';' ddtb-rollname ';'
                          ddtb-inttype ';' ddtb-leng INTO l_tabla.
            ENDIF.
            APPEND l_tabla TO i_tabla.
          ENDLOOP.
        ENDIF.
      WHEN 'TTYP'.
        SELECT rowtype FROM  dd40l
          INTO l_dd40l-rowtype
          UP TO 1 ROWS
         WHERE typename = nombre
          ORDER BY PRIMARY KEY.
        ENDSELECT.
        IF sy-subrc = 0.
          CONCATENATE 'TIPO TABLA:' l_dd40l-rowtype
                      INTO l_tabla SEPARATED BY space.
          APPEND l_tabla TO i_tabla.
        ENDIF.
      WHEN 'SSFO'.
        IF modo <> 'C'.
          o_bi = NEW #( ).

          o_bi->inicio( ).

* SAP Smart Forms: Initial screen
          o_bi->dynpro( program = 'SAPMSSFO' dynpro = '0100' ).
          o_bi->campos( campo = 'BDC_OKCODE' valor = '=XMLDOWN' ).

* Smart Forms: Determine form name (create, change, ...)
          o_bi->dynpro( program = 'SAPLSTXB' dynpro = '3000' ).
          o_bi->campos( campo = 'BDC_OKCODE' valor = '=ENTER' ).
          o_bi->campos( campo = 'SSFSCREEN-FNAME' valor = nombre ). " Smart Forms: Nombre formulario

* SAP Smart Forms: Initial screen
          o_bi->dynpro( program = 'SAPMSSFO' dynpro = '0100' ).
          o_bi->campos( campo = 'BDC_OKCODE' valor = '=BACK' ).
          o_bi->campos( campo = 'RB_SF' valor = 'X' ).
          o_bi->campos( campo = 'SSFSCREEN-FNAME' valor = 'c:\temp\smartforms' ). " Smart Forms: Nombre formulario

          l_mensaje = o_bi->llamar_transaccion( tcode = 'SMARTFORMS' modo = 'E' ).
        ENDIF.
      WHEN OTHERS.
        xml = get_saplink_xml( object = tipo nombre = nombre ).

        IF NOT xml IS INITIAL.
          zcl_ap_string=>string2tabla( EXPORTING string   = xml
                                                 longitud = 255 ##NUMBER_OK
                                       CHANGING  tabla    = i_tabla ).

        ENDIF.
    ENDCASE.

    IF modo = 'C'.
      texto_comparable( CHANGING i_tabla = i_tabla ).
    ENDIF.
  ENDMETHOD.
  METHOD get_saplink_xml.
    TYPES: BEGIN OF t_objecttable,
             classname TYPE string,
             object    TYPE ko100-object,
             text      TYPE ko100-text,
           END OF t_objecttable.

    DATA l_objtable   TYPE TABLE OF t_objecttable.
    DATA l_objline    TYPE t_objecttable.
    DATA _objname     TYPE string.
    DATA targetobject TYPE REF TO zsaplink.
    DATA ixmldocument TYPE REF TO if_ixml_document.
    DATA excclass     TYPE REF TO zcx_saplink.
    " TODO: variable is assigned but never used (ABAP cleaner)
    DATA errormsg     TYPE string.

    CHECK NOT object IS INITIAL AND NOT nombre IS INITIAL.

    zsaplink=>getplugins( CHANGING objecttable = l_objtable ).

    READ TABLE l_objtable INTO l_objline WITH KEY object = object. "#EC CI_STDSEQ
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    _objname = nombre.
    CREATE OBJECT targetobject
      TYPE
        (l_objline-classname)
      EXPORTING name = _objname.
    TRY.
        ixmldocument = targetobject->createixmldocfromobject( ).
      CATCH zcx_saplink INTO excclass.
        errormsg = excclass->get_text( ).
      CATCH cx_root. "#EC *
        errormsg = 'Error generando SLNK'(egs).
    ENDTRY.

    IF NOT ixmldocument IS INITIAL.
      TRY.
          xml = zsaplink=>convertixmldoctostring( ixmldocument ).
        CATCH zcx_saplink INTO excclass.
          errormsg = excclass->get_text( ).
        CATCH cx_root. "#EC *
          errormsg = 'Error generando SLNK'(egs).
      ENDTRY.
    ENDIF.
  ENDMETHOD.
  METHOD get_val.
    FIELD-SYMBOLS <fs> TYPE any.

    IF linea IS INITIAL.
      IF me->linea IS INITIAL.
        message = 'No hay definido línea'.
        RETURN.
      ELSE.
        ASSIGN COMPONENT campo OF STRUCTURE me->linea TO <fs>.
      ENDIF.
    ELSE.
      ASSIGN COMPONENT campo OF STRUCTURE linea TO <fs>.
    ENDIF.

    IF <fs> IS ASSIGNED.
      valor = <fs>.
    ELSE.
      message = |Estructura no contiene campo { campo }|.
    ENDIF.
  ENDMETHOD.
  METHOD get_vals.
    get_val( EXPORTING campo   = campo
                       linea   = linea
             IMPORTING valor   = valor
                      " TODO: variable is assigned but never used (ABAP cleaner)
                       message = DATA(l_message) ).
  ENDMETHOD.
  METHOD initialization.
    DATA l_no_var_def TYPE c LENGTH 1.

    IF sy-batch IS INITIAL AND get_default_param = 'X'.
      GET PARAMETER ID 'Z_NO_VAR_DEF' FIELD l_no_var_def ##EXISTS.
      IF l_no_var_def IS INITIAL.
        zcl_ap_dev=>get_default_variant( nombre_pc = nombre_pc ).
      ENDIF.
    ENDIF.

    IF gos = 'X'.
      zcl_ap_dev=>inserta_boton_gos( ).
    ENDIF.

    IF NOT tcode_doc IS INITIAL.
      DATA(l_tcode_doc) = tcode_doc.
    ELSE.
      l_tcode_doc = sy-cprog.
    ENDIF.

    IF NOT boton_manual IS INITIAL.
      zcl_ap_documentos=>get_documentos_por_clas( EXPORTING tcode = l_tcode_doc
                                                            clasificacion = 'MANUAL'
                                                  IMPORTING i_documentos = DATA(i_documentos)
                                                            eliminados = DATA(l_eliminados) ).
      IF NOT i_documentos IS INITIAL.
        sscrfields-functxt_02 = boton_manual.
        IF boton_manual = '@J7@ Manual usuario'(mus).
          LOOP AT i_documentos ASSIGNING FIELD-SYMBOL(<documento>) WHERE boton <> ''.
            sscrfields-functxt_02 = <documento>-boton.
            EXIT.
          ENDLOOP.
        ENDIF.
      ELSEIF l_eliminados = 0.
        IF    zcl_ap_documentos=>existe_doc( tcode = l_tcode_doc
                                             nombre = 'MANUAL'
                                             clasificacion = '' ) " Documentacion formato antiguo sin permisos
              = 'X'
           OR url_manual <> ''.
          sscrfields-functxt_02 = boton_manual.
        ENDIF.
      ENDIF.
    ENDIF.

    IF NOT boton_plantilla IS INITIAL.
      IF zcl_ap_documentos=>existe_doc( 'PLANTILLA' ) = 'X' OR url_plantilla <> ''.
        sscrfields-functxt_03 = boton_plantilla.
      ENDIF.
    ENDIF.

    IF NOT boton_plantilla IS INITIAL.
      zcl_ap_documentos=>get_documentos_por_clas( EXPORTING tcode = l_tcode_doc
                                                            clasificacion = 'PLANTILLA'
                                                  IMPORTING i_documentos = i_documentos
                                                            eliminados = l_eliminados ).
      IF NOT i_documentos IS INITIAL.
        sscrfields-functxt_03 = boton_plantilla.
        IF boton_plantilla = '@J2@ Ejemplo fichero carga'(efc).
          LOOP AT i_documentos ASSIGNING <documento> WHERE boton <> ''.
            sscrfields-functxt_03 = <documento>-boton.
            EXIT.
          ENDLOOP.
        ENDIF.
      ELSEIF l_eliminados = 0.
        IF    zcl_ap_documentos=>existe_doc( tcode = l_tcode_doc
                                             nombre = 'PLANTILLA'
                                             clasificacion = '' ) " Documentacion formato antiguo sin permisos
              = 'X'
           OR url_plantilla <> ''.
          sscrfields-functxt_03 = boton_plantilla.
        ENDIF.
      ENDIF.
    ENDIF.

    IF NOT boton_doc_tecnica IS INITIAL.
      zcl_ap_documentos=>get_documentos_por_clas( EXPORTING tcode = l_tcode_doc
                                                            clasificacion = 'DOC_TEC'
                                                  IMPORTING i_documentos = i_documentos
                                                            eliminados = l_eliminados ).
      IF NOT i_documentos IS INITIAL OR l_eliminados > 0.
        sscrfields-functxt_04 = boton_doc_tecnica.
        IF boton_doc_tecnica = icon_message_information.
          LOOP AT i_documentos ASSIGNING <documento> WHERE boton <> ''.
            sscrfields-functxt_04 = <documento>-boton.
            EXIT.
          ENDLOOP.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD initialization_i.
    DATA l_no_var_def TYPE c LENGTH 1.

    IF sy-batch IS INITIAL AND get_default_param = 'X'.
      GET PARAMETER ID 'Z_NO_VAR_DEF' FIELD l_no_var_def ##EXISTS.
      IF l_no_var_def IS INITIAL.
        get_default_variant( nombre_pc = me->nombre_pc ).
      ENDIF.
    ENDIF.

    IF gos = 'X'.
      inserta_boton_gos( ).
    ENDIF.

    IF NOT tcode_doc IS INITIAL.
      DATA(l_tcode_doc) = tcode_doc.
    ELSE.
      l_tcode_doc = sy-cprog.
    ENDIF.

    IF NOT boton_manual IS INITIAL.
      zcl_ap_documentos=>get_documentos_por_clas( EXPORTING tcode = l_tcode_doc
                                                            clasificacion = 'MANUAL'
                                                  IMPORTING i_documentos = DATA(i_documentos)
                                                            eliminados = DATA(l_eliminados) ).
      IF NOT i_documentos IS INITIAL.
        sscrfields-functxt_02 = boton_manual.
        IF boton_manual = '@J7@ Manual usuario'(mus).
          LOOP AT i_documentos ASSIGNING FIELD-SYMBOL(<documento>) WHERE boton <> ''.
            sscrfields-functxt_02 = <documento>-boton.
            EXIT.
          ENDLOOP.
        ENDIF.
      ELSEIF l_eliminados = 0.
        IF    zcl_ap_documentos=>existe_doc( tcode = l_tcode_doc
                                             nombre = 'MANUAL'
                                             clasificacion = '' ) " Documentacion formato antiguo sin permisos
              = 'X'
           OR me->url_manual <> ''.
          sscrfields-functxt_02 = boton_manual.
        ENDIF.
      ENDIF.
    ENDIF.

    IF NOT boton_plantilla IS INITIAL.
      zcl_ap_documentos=>get_documentos_por_clas( EXPORTING tcode = l_tcode_doc
                                                            clasificacion = 'PLANTILLA'
                                                  IMPORTING i_documentos = i_documentos
                                                            eliminados = l_eliminados ).
      IF NOT i_documentos IS INITIAL.
        sscrfields-functxt_03 = boton_plantilla.
        IF boton_plantilla = '@J2@ Ejemplo fichero carga'(efc).
          LOOP AT i_documentos ASSIGNING <documento> WHERE boton <> ''.
            sscrfields-functxt_03 = <documento>-boton.
            EXIT.
          ENDLOOP.
        ENDIF.
      ELSEIF l_eliminados = 0.
        IF    zcl_ap_documentos=>existe_doc( tcode = l_tcode_doc
                                             nombre = 'PLANTILLA'
                                             clasificacion = '' ) " Documentacion formato antiguo sin permisos
              = 'X'
           OR me->url_plantilla <> ''.
          sscrfields-functxt_03 = boton_plantilla.
        ENDIF.
      ENDIF.
    ENDIF.

    IF NOT boton_doc_tecnica IS INITIAL.
      zcl_ap_documentos=>get_documentos_por_clas( EXPORTING tcode = l_tcode_doc
                                                            clasificacion = 'DOC_TEC'
                                                  IMPORTING i_documentos = i_documentos
                                                            eliminados = l_eliminados ).
      IF NOT i_documentos IS INITIAL.
        sscrfields-functxt_04 = boton_doc_tecnica.
        IF boton_doc_tecnica = icon_message_information.
          LOOP AT i_documentos ASSIGNING <documento> WHERE boton <> ''.
            sscrfields-functxt_04 = <documento>-boton.
            EXIT.
          ENDLOOP.
        ENDIF.
      ENDIF.
    ENDIF.

    IF NOT boton_fc05 IS INITIAL.
      zcl_ap_documentos=>get_documentos_por_clas( EXPORTING tcode = CONV #( sy-cprog )
                                                            clasificacion = boton_fc05
                                                  IMPORTING i_documentos = i_documentos ).
      LOOP AT i_documentos ASSIGNING <documento> WHERE boton <> ''.
        sscrfields-functxt_05 = <documento>-boton.
        EXIT.
      ENDLOOP.
    ENDIF.

    IF me->status_prog = 'ZAP_STATUS'.
      PERFORM set_sscrfields IN PROGRAM zap_status USING sscrfields IF FOUND.
    ENDIF.
  ENDMETHOD.
  METHOD initialization_v2.
*    DATA l_no_var_def.

*    IF batch IS INITIAL AND get_default_param = 'X'.
*      GET PARAMETER ID 'Z_NO_VAR_DEF' FIELD l_no_var_def.   "#EC EXISTS
*      IF l_no_var_def IS INITIAL.
*        get_default_variant( nombre_pc = me->nombre_pc ).
*      ENDIF.
*    ENDIF.

    IF gos = 'X'.
      inserta_boton_gos( ).
    ENDIF.

    IF NOT boton_manual IS INITIAL.
      IF zcl_ap_documentos=>existe_doc( 'MANUAL' ) = 'X' OR me->url_manual <> ''.
        sscrfields-functxt_02 = boton_manual.
      ENDIF.
    ENDIF.

    IF NOT boton_plantilla IS INITIAL.
      IF zcl_ap_documentos=>existe_doc( 'PLANTILLA' ) = 'X' OR me->url_plantilla <> ''.
        sscrfields-functxt_03 = boton_plantilla.
      ENDIF.
    ENDIF.

    IF me->status_prog = 'ZAP_STATUS'.
      PERFORM set_sscrfields IN PROGRAM zap_status USING sscrfields IF FOUND.
    ENDIF.
  ENDMETHOD.
  METHOD inserta_boton_gos.
    boton_gos_obj( objeto = 'ZREP_APC'
                   clave  = sy-cprog ).
  ENDMETHOD.
  METHOD mail_info.
    DATA: lt_globals   TYPE TABLE OF rfieldlist,
          lv_fieldname TYPE string.
    DATA: subject    TYPE string,
          tab        TYPE abap_parmbind,
          ptab       TYPE abap_parmbind_tab,
          l_texto    TYPE solisti1,
          i_textosl  TYPE rspc_t_text,
          o_variante TYPE REF TO zcl_ap_variante,
          l_var      TYPE rsparams,
          l_metodo   TYPE string                 VALUE 'MAIL',
          l_clase    TYPE string                 VALUE 'ZCL_AP_ABAP2XLS'.
    DATA l_tipo_alv TYPE string.
    DATA l_jobname  TYPE tbtcm-jobname.

    FIELD-SYMBOLS: <tabla> TYPE table,
                   <alv>   TYPE any.
    FIELD-SYMBOLS: <global> LIKE LINE OF lt_globals,
                   <field>  TYPE any.

    DEFINE set_par.
      IF NOT &1 IS INITIAL.
        tab-name  = '&1'.
        tab-value = REF #( &1 ).
        INSERT tab INTO TABLE ptab.
      ENDIF.
    END-OF-DEFINITION.

    TRY.
        IF asunto IS INITIAL.
          subject = sy-cprog.
        ELSE.
          subject = asunto.
        ENDIF.

        set_par: direccion, subject,
                 tabla1, tabla2, tabla3,
                 nombre_tabla1, nombre_tabla2, nombre_tabla3,
                 commit, texto,
                 o_alv2, tabla_alv2, nombre_alv2,
                 o_grid2, tabla_grid2, nombre_grid2.

        IF NOT info_alv IS INITIAL.
          ASSIGN ('ME->I_LISTADO') TO <tabla>.
          IF sy-subrc = 0.
            ASSIGN ('ME->O_ALV') TO <alv>.
            IF sy-subrc = 0.
              IF NOT <alv> IS INITIAL.

                l_tipo_alv = cl_abap_classdescr=>get_class_name( <alv> ).
                IF l_tipo_alv CS 'ZCL_AP_ALV_GRID'.
                  tab-name  = 'O_GRID1'.
                  tab-value = REF #( <alv> ).
                  INSERT tab INTO TABLE ptab.

                  tab-name  = 'TABLA_GRID1'.
                  tab-value = REF #( <tabla> ).
                  INSERT tab INTO TABLE ptab.

                  tab-name = 'NOMBRE_GRID1'.

                ELSE.
                  tab-name  = 'O_ALV1'.
                  tab-value = REF #( <alv> ).
                  INSERT tab INTO TABLE ptab.

                  tab-name  = 'TABLA_ALV1'.
                  tab-value = REF #( <tabla> ).
                  INSERT tab INTO TABLE ptab.

                  tab-name = 'NOMBRE_ALV1'.
                ENDIF.
                IF nombre_alv1 IS INITIAL.
                  tab-value = REF #( 'LISTADO' ).
                ELSE.
                  tab-value = REF #( nombre_alv1 ).
                ENDIF.
                INSERT tab INTO TABLE ptab.
              ENDIF.
            ENDIF.
          ENDIF.
        ENDIF.

        IF NOT info_log IS INITIAL.
          UNASSIGN <tabla>.
          ASSIGN ('ME->O_LOG->I_LOG') TO <tabla>.
          IF sy-subrc = 0.
            IF NOT <tabla> IS INITIAL.
              DATA(o_alv_log) = NEW zcl_ap_alv( ).
              o_alv_log->constructor_tabla( CHANGING t_tabla = <tabla> ).
              o_alv_log->set_field_quitar( 'MANDT,PROCESO,PROGNAME,USUARIO,INDICE' ).
              o_alv_log->set_field_quitar( 'MSGV1,MSGV2,MSGV3,MSGV4,MSGNO,MSGID,FICHERO' ).

              tab-name  = 'O_ALV2'.
              tab-value = REF #( o_alv_log ).
              INSERT tab INTO TABLE ptab.

              tab-name  = 'TABLA_ALV2'.
              tab-value = REF #( <tabla> ).
              INSERT tab INTO TABLE ptab.

              tab-name = 'NOMBRE_ALV2'.
              IF nombre_alv2 IS INITIAL.
                tab-value = REF #( 'LOG' ).
              ELSE.
                tab-value = REF #( nombre_alv2 ).
              ENDIF.
              INSERT tab INTO TABLE ptab.
            ENDIF.
          ENDIF.
        ENDIF.

        IF info_programa = 'X'.
          CONCATENATE 'Programa:' sy-cprog 'Transaccion:'(tra) sy-tcode INTO l_texto SEPARATED BY space ##NO_TEXT.
          APPEND l_texto TO i_textosl.
          CONCATENATE 'Usuario:' sy-uname 'Idioma:' sy-langu INTO l_texto SEPARATED BY space ##NO_TEXT.
          APPEND l_texto TO i_textosl.
          CONCATENATE 'Sistema:' sy-sysid 'Mandante:' sy-mandt INTO l_texto SEPARATED BY space ##NO_TEXT.
          APPEND l_texto TO i_textosl.

          IF sy-batch = 'X'.
            CALL FUNCTION 'GET_JOB_RUNTIME_INFO'
              IMPORTING
                jobname = l_jobname.

            l_texto-line = |Ejecucion en fondo. Job { l_jobname }. Variante { sy-slset }|.
            APPEND l_texto-line TO i_textosl ##NO_TEXT.
          ENDIF.
          IF sy-binpt = 'X'.
            APPEND 'Ejecucion en batch input' TO i_textosl ##NO_TEXT.
          ENDIF.
        ENDIF.

        IF info_variante = 'X'.
          IF NOT sy-slset IS INITIAL.
            CONCATENATE 'Variante:' sy-slset INTO l_texto SEPARATED BY space ##NO_TEXT.
            APPEND l_texto TO i_textosl.
          ENDIF.

          o_variante = NEW #( ).
          o_variante->get_variante_actual( ).
          LOOP AT o_variante->i_valores INTO l_var.
            IF l_var-kind = 'P'.
              CONCATENATE l_var-selname '=' l_var-low INTO l_texto SEPARATED BY space.
            ELSE.
              CONCATENATE l_var-selname l_var-sign l_var-option l_var-low l_var-high INTO l_texto SEPARATED BY space.
            ENDIF.
            APPEND l_texto TO i_textosl.
          ENDLOOP.
        ENDIF.

        IF info_variables = 'X'.
          CALL FUNCTION 'GET_GLOBAL_SYMBOLS'
            EXPORTING
              program   = sy-cprog
            TABLES
              fieldlist = lt_globals.

          LOOP AT lt_globals ASSIGNING <global> WHERE name(1) <> '%' AND type <> 'r' AND type <> 'u' AND type <> 'h' AND type <> 'v' AND name(3) <> 'SY-' AND name(5) <> 'SYST-' AND name(5) <> 'ICON_' AND name(2) <> 'TC_' AND flitl = ''.
            IF o_variante IS INITIAL.
              CONCATENATE '(' sy-cprog ')' <global>-name INTO lv_fieldname.
              ASSIGN (lv_fieldname) TO <field>.
              IF sy-subrc = 0.
                l_texto-line = |{ <global>-name } = { <field> }|.
                APPEND l_texto TO i_textosl.
              ENDIF.
            ELSE.
              IF NOT line_exists( o_variante->i_valores[ selname = <global>-name ] ).
                CONCATENATE '(' sy-cprog ')' <global>-name INTO lv_fieldname.
                ASSIGN (lv_fieldname) TO <field>.
                IF sy-subrc = 0.
                  l_texto-line = |{ <global>-name } = { <field> }|.
                  APPEND l_texto TO i_textosl.
                ENDIF.
              ENDIF.
            ENDIF.
          ENDLOOP.

        ENDIF.

        APPEND LINES OF i_textos TO i_textosl.

        IF NOT i_textosl IS INITIAL.
          tab-name  = 'I_TEXTOS'.
          tab-value = REF #( i_textosl ).
          INSERT tab INTO TABLE ptab.
        ENDIF.

        TRY.
            l_metodo = 'MAIL'.
            CALL METHOD (l_clase)=>(l_metodo) PARAMETER-TABLE ptab.
          CATCH cx_root INTO cx_root. "#EC *
            IF sy-uname = zcl_c=>usuario_ap.
              MESSAGE cx_root->get_longtext( ) TYPE 'I'.
            ENDIF.
        ENDTRY.

      CATCH cx_root INTO cx_root. "#EC *
        IF sy-uname = zcl_c=>usuario_ap.
          MESSAGE cx_root->get_longtext( ) TYPE 'I'.
        ENDIF.
    ENDTRY.
  ENDMETHOD.
  METHOD mail_si_error.
    DATA: zap_textos_mail TYPE zap_textos_mail,
          l_message       TYPE bapi_msg.

    CLEAR zap_textos_mail.
    zap_textos_mail-grupo = 'ERROR'.

    IF NOT mensaje IS INITIAL.
      " TODO: variable is assigned but never used (ABAP cleaner)
      DATA(l_msg) = |%{ mensaje }%|.
      SELECT emails, asunto, cuerpo, proceso, mensaje FROM zap_mail_errores
        INTO TABLE @DATA(i_mails)
       WHERE report   = @report
         AND mensaje <> ''.
      LOOP AT i_mails ASSIGNING FIELD-SYMBOL(<mail>).
        IF NOT mensaje CS <mail>-mensaje.
          DELETE i_mails.
        ENDIF.
      ENDLOOP.
    ENDIF.

    IF NOT msgno IS INITIAL.
      SELECT emails, asunto, cuerpo, proceso FROM zap_mail_errores
        APPENDING CORRESPONDING FIELDS OF TABLE @i_mails
       WHERE report = @report
         AND msgid  = @msgid
         AND msgno  = @msgno.
    ENDIF.

    LOOP AT i_mails ASSIGNING <mail>.
      zap_textos_mail-codigo = <mail>-proceso.
      zap_textos_mail-asunto = <mail>-asunto.
      zap_textos_mail-texto  = <mail>-cuerpo.
      LOOP AT variables ASSIGNING FIELD-SYMBOL(<var>).
        REPLACE ALL OCCURRENCES OF <var>-key IN zap_textos_mail-texto WITH <var>-value IGNORING CASE.

        REPLACE ALL OCCURRENCES OF <var>-key IN zap_textos_mail-asunto WITH <var>-value IGNORING CASE.
      ENDLOOP.
      CONDENSE zap_textos_mail-asunto.

* Añadimos info adicional.
      DATA(l_tcode) = |Transaccion: { sy-tcode }|.
      DATA(l_report) = |Report: { report }|.
      DATA(l_uname) = |Usuario: { sy-uname }|.

      CONCATENATE zap_textos_mail-texto cl_abap_char_utilities=>cr_lf l_tcode l_report l_uname
                  INTO zap_textos_mail-texto SEPARATED BY cl_abap_char_utilities=>cr_lf.

      zcl_ap_envio_mail=>mail( EXPORTING direccion = <mail>-emails
                                         plantilla = zap_textos_mail
                                         clave     = clave
                                         variables = variables
                               IMPORTING message   = DATA(l_msg_m) ).

      IF <mail>-proceso IS INITIAL.
        CONTINUE.
      ENDIF.

      IF NOT mensaje IS INITIAL.
        l_message = mensaje.
      ELSE.
        MESSAGE ID msgid TYPE 'S' NUMBER msgno INTO l_message.
      ENDIF.
      zcl_ap_log=>set_log( proceso = <mail>-proceso
                           clave   = clave
                           message = l_message ).

      IF NOT l_msg_m IS INITIAL.
        zcl_ap_log=>set_log( proceso = <mail>-proceso
                             clave   = clave
                             message = |Error enviando mail { l_msg_m }| ).
      ENDIF.
    ENDLOOP.
  ENDMETHOD.
  METHOD marca_enhancement.
    DATA l_subclave TYPE ztemp-subclave.

    IF NOT subclave IS INITIAL.
      l_subclave = subclave.
    ELSE.
      IF NOT include IS INITIAL AND NOT form IS INITIAL.
        CONCATENATE include form INTO l_subclave SEPARATED BY '-'.
      ELSE.
        CONCATENATE cprog include form INTO l_subclave SEPARATED BY '-'.
      ENDIF.
    ENDIF.

    zcl_ap_temp=>set_st( clave      = 'ENHANCEMEN'
                         subclave   = l_subclave
                         valor1     = tcode
                         valor2     = cprog
                         valor3     = enhancement
                         texto      = include
                         texto2     = form
                         permanente = 'X' ).
    IF parar = 'X' AND ( sy-uname = uname OR uname = '*' ).
      BREAK-POINT.                                          "#EC *
    ENDIF.
  ENDMETHOD.
  METHOD marca_mod_estandard.
    DATA: l_subclave TYPE ztemp-subclave,
          l_zbc003   TYPE zbc003.

    IF NOT subclave IS INITIAL.
      l_subclave = subclave.
    ELSE.
      IF NOT include IS INITIAL AND NOT form IS INITIAL.
        CONCATENATE include form INTO l_subclave SEPARATED BY '-'.
      ELSEIF NOT form IS INITIAL.
        CONCATENATE cprog form INTO l_subclave SEPARATED BY '-'.
      ELSEIF NOT include IS INITIAL.
        CONCATENATE cprog include INTO l_subclave SEPARATED BY '-'.
      ENDIF.
    ENDIF.

    zcl_ap_temp=>set_st( clave      = 'MOD_STAND'
                         subclave   = l_subclave
                         valor1     = tcode
                         valor2     = cprog
                         texto      = include
                         texto2     = form
                         permanente = 'X' ).
    IF parar = 'X' AND ( sy-uname = uname OR uname = '*' ).
      BREAK-POINT.                                          "#EC *
    ELSEIF NOT include IS INITIAL OR NOT cprog IS INITIAL.
      IF NOT include IS INITIAL.
        SELECT * FROM zbc003
          INTO l_zbc003
          UP TO 1 ROWS
         WHERE name = include
          ORDER BY PRIMARY KEY.
        ENDSELECT.
      ELSE.
        SELECT * FROM zbc003
          UP TO 1 ROWS
          INTO l_zbc003
         WHERE name = cprog
          ORDER BY PRIMARY KEY.
        ENDSELECT.
      ENDIF.
      IF l_zbc003 IS INITIAL.
        CLEAR l_zbc003.
        IF NOT include IS INITIAL.
          l_zbc003-name = include.
        ELSE.
          l_zbc003-name = cprog.
        ENDIF.
        l_zbc003-tcode      = tcode.
        l_zbc003-estado     = 'M'.
        l_zbc003-asignado_a = uname.
        l_zbc003-grupo      = 'AUTO'.
        INSERT zbc003 FROM l_zbc003.
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD message.
    DATA: l_guardar_log TYPE c LENGTH 1,
          l_solo_log    TYPE c LENGTH 1,
          l_no_log      TYPE c LENGTH 1,
          zlog          TYPE zlog_ap.

    IF NOT me->proceso_log IS INITIAL AND no_log IS INITIAL.
      l_guardar_log = 'X'.
    ENDIF.

    l_solo_log = solo_log.
    IF postponer = 'X' AND NOT o_log IS INITIAL.
      l_solo_log = 'X'.
    ENDIF.
    message = zcl_ap_utils=>message( p1               = p1
                                     p2               = p2
                                     p3               = p3
                                     p4               = p4
                                     p5               = p5
                                     p6               = p6
                                     p7               = p7
                                     p8               = p8
                                     p9               = p9
                                     p10              = p10
                                     p11              = p11
                                     meins            = meins
                                     solo_log         = l_solo_log
                                     solo_primera_vez = solo_primera_vez
                                     type             = type
                                     guardar_log      = l_guardar_log
                                     proceso_log      = me->proceso_log
                                     clave_log        = clave_log
                                     mail             = mail
                                     no_break         = no_break
                                     msgv1            = msgv1
                                     msgv2            = msgv2
                                     msgv3            = msgv3
                                     msgv4            = msgv4 ).

    IF postponer <> 'X' OR o_log IS INITIAL.
      RETURN.
    ENDIF.

    IF solo_primera_vez = 'X'.
      IF line_exists( o_log->i_log[ message = message
                                    msgty   = type
                                    msgv1   = msgv1
                                    msgv2   = msgv2
                                    msgv3   = msgv3
                                    msgv4   = msgv4 ] ).
        l_no_log = 'X'.
      ENDIF.
    ENDIF.

    IF l_no_log IS INITIAL.
      CLEAR zlog.
      zlog-message  = message.
      zlog-proceso  = me->proceso_log.
      zlog-clave    = clave_log.
      zlog-progname = sy-cprog.
      zlog-fecha    = sy-datum.
      zlog-hora     = sy-uzeit.
      zlog-usuario  = sy-uname.
      zlog-msgty    = type.
      zlog-msgv1    = msgv1.
      zlog-msgv2    = msgv2.
      zlog-msgv3    = msgv3.
      zlog-msgv4    = msgv4.
      IF type = 'Y'. zlog-msgty = 'E'. ENDIF.
      APPEND zlog TO o_log->i_log.
    ENDIF.
  ENDMETHOD.
  METHOD msg.
    IF o_log IS INITIAL.
      CASE msgty.
        WHEN 'E'. o_log->msg_error( msgv1 = texto ).
        WHEN 'W'. o_log->msg_warning( msgv1 = texto ).
        WHEN 'I'. o_log->msg_info( msgv1 = texto ).
        WHEN 'S'. o_log->msg_succes( msgv1 = texto ).
      ENDCASE.
    ENDIF.

    MESSAGE texto TYPE msgty.
  ENDMETHOD.
  METHOD popup_cambio_clase_desarrollo.
    CALL FUNCTION 'TR_TADIR_POPUP_ENTRY_E071'
      EXPORTING
        wi_e071_pgmid             = 'R3TR'
        wi_e071_object            = object
        wi_e071_obj_name          = obj_name
        wi_tadir_devclass         = devclass
      EXCEPTIONS
        display_mode              = 1
        exit                      = 2
        global_tadir_insert_error = 3
        no_repair_selected        = 4
        no_systemname             = 5
        no_systemtype             = 6
        no_tadir_type             = 7
        reserved_name             = 8
        tadir_enqueue_failed      = 9
        devclass_not_found        = 10
        tadir_not_exist           = 11
        object_exists             = 12
        internal_error            = 13
        object_append_error       = 14
        tadir_modify_error        = 15
        object_locked             = 16
        no_object_authority       = 17
        OTHERS                    = 18.

    IF sy-subrc <> 0.
      MESSAGE 'Error cambiando clase de desarrollo'(ecc) TYPE 'E'.
    ENDIF.
  ENDMETHOD.
  METHOD pruebas.
    " TODO: variable is assigned but never used (ABAP cleaner)
    DATA l_cprog TYPE sy-cprog.

    CLEAR test.
    TRY.
        SELECT SINGLE cprog FROM zap_break
          INTO l_cprog
         WHERE cprog    = cprog
           AND uname    = sy-uname
           AND texto    = texto
           AND inactivo = ''.
        IF sy-subrc = 0.
          test = 'X'.
        ENDIF.
      CATCH cx_root INTO DATA(o_root).                      "#EC *
        MESSAGE o_root->get_text( ) TYPE 'E'.
    ENDTRY.
  ENDMETHOD.
  METHOD relanzar_report.
    DATA: o_var        TYPE REF TO zcl_ap_variante,
          l_parametros TYPE string,
          i_param      TYPE zap_lista_t,
          l_valor      TYPE rsparams.

    FIELD-SYMBOLS: <param> TYPE zap_lista,
                   <valor> TYPE rsparams.

    o_var = NEW #( ).

    o_var->get_variante_actual( report = report ).

    l_parametros = parametros.
    IF NOT param1 IS INITIAL.
      zcl_ap_lista=>add_lista( EXPORTING valor = param1 subvalor = valor1 CHANGING lista = l_parametros ).
    ENDIF.
    IF NOT param2 IS INITIAL.
      zcl_ap_lista=>add_lista( EXPORTING valor = param2 subvalor = valor2 CHANGING lista = l_parametros ).
    ENDIF.
    IF NOT param3 IS INITIAL.
      zcl_ap_lista=>add_lista( EXPORTING valor = param3 subvalor = valor3 CHANGING lista = l_parametros ).
    ENDIF.
    IF NOT param4 IS INITIAL.
      zcl_ap_lista=>add_lista( EXPORTING valor = param4 subvalor = valor4 CHANGING lista = l_parametros ).
    ENDIF.

    IF NOT l_parametros IS INITIAL.
      SET PARAMETER ID 'Z_NO_VAR_DEF' FIELD 'X' ##EXISTS.
      i_param = zcl_ap_lista=>get_valores_lista( lista = l_parametros descomponer_subvalores = 'X' ).
      LOOP AT i_param ASSIGNING <param>.
        ASSIGN o_var->i_valores[ selname = <param>-valor ] TO <valor>.
        IF sy-subrc <> 0.
          CONTINUE.
        ENDIF.

        IF <valor>-low IS INITIAL.
          <valor>-low = <param>-subvalor.
          IF <valor>-kind = 'S'.
            <valor>-sign   = 'I'.
            <valor>-option = 'EQ'.
          ENDIF.
        ELSE.
          l_valor = <valor>.
          l_valor-low = <param>-subvalor.
          APPEND l_valor TO o_var->i_valores.
        ENDIF.
      ENDLOOP.
    ENDIF.

    IF via_selection_screen IS INITIAL.
      IF return IS INITIAL.
        SUBMIT (report)                                  "#EC CI_SUBMIT
               WITH SELECTION-TABLE o_var->i_valores.
      ELSE.
        SUBMIT (report)                                  "#EC CI_SUBMIT
               WITH SELECTION-TABLE o_var->i_valores
               AND RETURN.
      ENDIF.
    ELSE.
      IF sy-batch = 'X'.
        RETURN. " En fondo la ejecucion es inmediata y se pueden hacer bucles
      ENDIF.
      IF return IS INITIAL.
        SUBMIT (report)                                  "#EC CI_SUBMIT
               VIA SELECTION-SCREEN
               WITH SELECTION-TABLE o_var->i_valores.
      ELSE.
        SUBMIT (report)                                  "#EC CI_SUBMIT
               VIA SELECTION-SCREEN
               WITH SELECTION-TABLE o_var->i_valores
               AND RETURN.
      ENDIF.
    ENDIF.

    IF NOT l_parametros IS INITIAL.
      SET PARAMETER ID 'Z_NO_VAR_DEF' FIELD '' ##EXISTS.
    ENDIF.
  ENDMETHOD.
  METHOD salir_si_no_cambios.
    IF zcl_ap_itab=>tablas_iguales( t1            = t1
                                    t2            = t2
                                    quitar_campos = quitar_campos
                                    solo_campos   = solo_campos ) = 'X'.
      IF rollback_si_no_cambios = 'X'.
        ROLLBACK WORK. "#EC CI_ROLLBACK
      ENDIF.
      IF relanzar_report = 'X'.
        relanzar_report( via_selection_screen = 'X' ).
      ELSE.
        LEAVE TO SCREEN 0.
      ENDIF.
    ELSE.
      IF zcl_ap_popup=>confirmar_grabacion( ) = 'X'.
        IF rollback_si_no_cambios = 'X'.
          ROLLBACK WORK. "#EC CI_ROLLBACK
        ENDIF.
        IF relanzar_report = 'X'.
          relanzar_report( via_selection_screen = 'X' ).
        ELSE.
          LEAVE TO SCREEN 0.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD set_campo_z.
    DATA: l_valor_c    TYPE zap_campos_z-valor,
          zap_campos_z TYPE zap_campos_z.

    TRY.
        l_valor_c = valor.
        SELECT SINGLE valor FROM zap_campos_z
          INTO @DATA(l_valor)
         WHERE tabla = @tabla
           AND clave = @clave
           AND campo = @campo.
        IF l_valor <> l_valor_c.
          IF valor IS INITIAL.
            DELETE FROM zap_campos_z
             WHERE tabla = tabla
               AND clave = clave.
          ELSE.
            CLEAR zap_campos_z.
            zap_campos_z-tabla = tabla.
            zap_campos_z-clave = clave.
            zap_campos_z-campo = campo.
            zap_campos_z-valor = valor.
            zap_campos_z-erdat = sy-datum.
            zap_campos_z-erzet = sy-uzeit.
            zap_campos_z-ernam = sy-uname.
            zap_campos_z-tcode = sy-tcode.
            MODIFY zap_campos_z FROM zap_campos_z.
          ENDIF.
        ENDIF.
      CATCH cx_root INTO DATA(o_root). "#EC *
        MESSAGE |Error { o_root->get_text( ) }| TYPE 'S'.
    ENDTRY.
  ENDMETHOD.
  METHOD set_status.
    CLEAR i_exc.
    SPLIT excluir AT ',' INTO TABLE i_exc.
    SET PF-STATUS status OF PROGRAM cprog EXCLUDING i_exc.
  ENDMETHOD.
  METHOD set_status_list.
    DATA: l_message      TYPE string,
          l_message_ok   TYPE string,
          l_check        TYPE xfeld,
          l_icono        TYPE icon_d,
          l_color        TYPE string,
          l_campos_color TYPE string.

    FIELD-SYMBOLS <fs> TYPE any.

    IF NOT message IS INITIAL.
      l_message = message.
      ASSIGN ('LIST-MESSAGE') TO <fs>.
      IF sy-subrc = 0.
        <fs> = message.
      ENDIF.
    ELSE.
      ASSIGN ('LIST-MESSAGE') TO <fs>.
      IF sy-subrc = 0.
        l_message = <fs>.
      ENDIF.
    ENDIF.

    ASSIGN ('LIST-MESSAGE_OK') TO <fs>.
    IF sy-subrc = 0.
      l_message_ok = <fs>.
    ENDIF.

    ASSIGN ('LIST-CHECK') TO <fs>.
    IF sy-subrc = 0.
      l_check = <fs>.
    ENDIF.

    IF NOT icono IS INITIAL.
      l_icono = icono.
    ELSEIF criterio = 'A'.
      IF l_message IS INITIAL.
        l_icono = zcl_ap_alv=>c_ico_ambar.
      ELSE.
        l_icono = zcl_ap_alv=>c_ico_rojo.
      ENDIF.
    ELSEIF criterio = 'V'.
      IF l_message IS INITIAL.
        l_icono = zcl_ap_alv=>c_ico_verde.
      ELSE.
        l_icono = zcl_ap_alv=>c_ico_rojo.
      ENDIF.
    ELSEIF criterio = 'R'.
      IF NOT l_message IS INITIAL.
        l_icono = zcl_ap_alv=>c_ico_rojo.
      ELSEIF NOT l_message_ok IS INITIAL.
        l_icono = zcl_ap_alv=>c_ico_verde.
      ENDIF.
    ELSEIF criterio = 'C'.
      IF l_message IS INITIAL.
        IF l_check IS INITIAL.
          l_icono = zcl_ap_alv=>c_ico_verde.
        ELSE.
          l_icono = zcl_ap_alv=>c_ico_ambar.
        ENDIF.
      ELSE.
        l_icono = zcl_ap_alv=>c_ico_rojo.
      ENDIF.
    ENDIF.

    IF NOT l_icono IS INITIAL.
      ASSIGN ('LIST-LIGHTS') TO <fs>.
      IF sy-subrc = 0.
        IF texto_icono IS INITIAL.
          IF l_message IS INITIAL AND NOT l_message_ok IS INITIAL.
            <fs> = zcl_ap_alv=>set_icono( icono = l_icono mensaje = l_message_ok ).
          ELSE.
            <fs> = zcl_ap_alv=>set_icono( icono = l_icono mensaje = l_message ).
          ENDIF.
        ELSE.
          <fs> = zcl_ap_alv=>set_icono( icono = l_icono mensaje = texto_icono ).
        ENDIF.
      ENDIF.
    ENDIF.

    IF NOT color IS INITIAL.
      l_color = color.
    ELSEIF NOT criterio IS INITIAL.
      sy-tfill = strlen( criterio ).
      IF sy-tfill = 2.
        IF criterio+1(1) = 'C'.
          IF NOT l_message IS INITIAL.
            l_color = 'R'.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.

    IF l_color IS INITIAL AND NOT resaltar_campos IS INITIAL AND campos_color IS INITIAL.
      l_campos_color = resaltar_campos.
      l_color = color_resalte.
    ELSE.
      l_campos_color = campos_color.
    ENDIF.

    IF NOT l_color IS INITIAL OR NOT int IS INITIAL OR NOT inv IS INITIAL.
      UNASSIGN <fs>.
      ASSIGN ('LIST-COLOR_ALV') TO <fs>.
      IF sy-subrc <> 0.
        ASSIGN ('LIST-COLOR') TO <fs>.
      ENDIF.
      IF <fs> IS ASSIGNED.
        zcl_ap_alv_grid=>append_color( EXPORTING campo = l_campos_color colorc = l_color int = int inv = inv CHANGING tabla_color = <fs> ).
      ENDIF.
    ENDIF.

    IF NOT campos_editables IS INITIAL.
      ASSIGN ('LIST-STYLE') TO <fs>.
      IF sy-subrc = 0.
        zcl_ap_alv_grid=>append_style_edit( EXPORTING campo = campos_editables CHANGING tabla_style = <fs> ).
      ENDIF.
    ENDIF.

    IF NOT campos_no_editables IS INITIAL.
      ASSIGN ('LIST-STYLE') TO <fs>.
      IF sy-subrc = 0.
        zcl_ap_alv_grid=>append_style_no_edit( EXPORTING campo = campos_no_editables CHANGING tabla_style = <fs> ).
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD set_status_list_stop.
    set_status_list( EXPORTING color               = color
                               icono               = icono
                               message             = message
                               campos_editables    = campos_editables
                               campos_no_editables = campos_no_editables
                               campos_color        = campos_color
                               int                 = int
                               inv                 = inv
                     CHANGING  list                = list ).
  ENDMETHOD.
  METHOD sgpi_texto.
    DATA l_texto TYPE string.

    IF no_sgpi = 'X'.
      RETURN.
    ENDIF.

    l_texto = zcl_ap_utils=>concat( p1 = texto1 p2 = texto2 p3 = texto3 ).

    IF cant_porc IS INITIAL.
      o_sgpi->texto( texto1       = texto1
                     texto2       = texto2
                     texto3       = texto3
                     quitar_ceros = quitar_ceros ).
    ELSE.
      o_sgpi->porcentaje( texto = l_texto cantidad = cant_porc ).
    ENDIF.

    IF no_break IS INITIAL.
      break_condicional( texto = l_texto ).
    ENDIF.
  ENDMETHOD.
  METHOD show_docu.
* La documentacion se mantiene desde SE61, clase de documentos "Texto en dialogo"

*SELECTION-SCREEN BEGIN OF LINE.
*  PARAMETERS: p_incd   AS CHECKBOX MODIF ID sup.
*  SELECTION-SCREEN COMMENT 2(26) TEXT-inc FOR FIELD p_incd.
*  SELECTION-SCREEN PUSHBUTTON 30(4) b_incd USER-COMMAND incd.
*SELECTION-SCREEN END OF LINE.

*SELECTION-SCREEN BEGIN OF LINE.
*SELECTION-SCREEN COMMENT 1(31) TEXT-bus.
*PARAMETERS: p_busl   LIKE zap_exits_est-id2.
*SELECTION-SCREEN PUSHBUTTON 76(4) b_busl USER-COMMAND docu_zexits_busqueda.
*SELECTION-SCREEN END OF LINE.

    CALL FUNCTION 'POPUP_DISPLAY_TEXT'
      EXPORTING
        popup_title    = titulo
        text_object    = id
      EXCEPTIONS
        text_not_found = 1
        OTHERS         = 2.
    IF sy-subrc <> 0.
      MESSAGE |No se ha entrado el texto { id }| TYPE 'I'.
    ENDIF.
  ENDMETHOD.
  METHOD status_dynpro.
    set_status( status = status excluir = excluir cprog = cprog ).

    IF titulo IS INITIAL.
      SET TITLEBAR '100' OF PROGRAM cprog WITH me->titulo.
    ELSE.
      SET TITLEBAR '100' OF PROGRAM cprog WITH titulo.
    ENDIF.

    IF o_alv IS INITIAL.
      RETURN.
    ENDIF.

    IF inicio IS NOT INITIAL.
      RETURN.
    ENDIF.

    inicio = 'X'.

    o_alv->variant-variant = variant.

    IF alv_standard = 'X'.
      o_alv->set_layout( no_rowmove = 'X'
                         no_rowins  = 'X'
                         style      = style
                         colort     = color ).
      o_alv->quitar_opciones( cl_gui_alv_grid=>mc_fc_refresh ).
      IF tabint = 'X'.
        o_alv->set_campos_tabint( i_listado ).
      ENDIF.
      IF NOT lights IS INITIAL.
        o_alv->set_field( campo = lights op = 'FIX' ).
      ENDIF.
    ENDIF.

    sgpi_texto( 'Generando informe'(gin) ).
    o_alv->show( CHANGING tabla = i_listado ).
    IF alv_standard = 'X'.
      o_alv->actualiza_campos_grid( campos_borrar = campos_borrar ).
      o_alv->set_seleccion( CHANGING t_tabla = i_listado ).
    ENDIF.
  ENDMETHOD.
  METHOD status_dynpro_m.
  ENDMETHOD.
  METHOD texto_comparable.
    DATA l_tabla TYPE text255.

    LOOP AT i_tabla INTO l_tabla.
      IF    l_tabla    IS INITIAL           OR l_tabla CS '"NOLIB'
         OR l_tabla(1)  = '*'               OR l_tabla  = c_lin_sep
         OR l_tabla(4)  = 'ZCL_'
         OR l_tabla    CS 'DEFINITION LOAD'.
        DELETE i_tabla.
      ELSE.
        SPLIT l_tabla AT ` "EC` INTO l_tabla DATA(l_aux).
        SPLIT l_tabla AT ` ##` INTO l_tabla l_aux.
        IF l_aux CS '.'.
          CONCATENATE l_tabla '.' INTO l_tabla.
        ENDIF.
        REPLACE ALL OCCURRENCES OF 'í' IN l_tabla WITH 'i'.
        MODIFY i_tabla FROM l_tabla.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.
  METHOD validar_seleccion_obligatoria.
    DATA: selection_table TYPE STANDARD TABLE OF rsparams,
          r_campos        TYPE rstt_t_range_string,
          l_ok            TYPE c LENGTH 1,
          textpooltable   TYPE STANDARD TABLE OF textpool,
          sel             TYPE rsparams,
          texto           TYPE textpool,
          l_descr_campos  TYPE string.

    CHECK NOT campos_or IS INITIAL OR NOT campos_and IS INITIAL.

    CALL FUNCTION 'RS_REFRESH_FROM_SELECTOPTIONS'
      EXPORTING
        curr_report     = cprog
      TABLES
        selection_table = selection_table
      EXCEPTIONS
        not_found       = 1
        no_report       = 2.

    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    IF NOT campos_or IS INITIAL.
      IF campos_or <> '*'.
        r_campos = zcl_ap_lista=>lista2rango( lista = campos_or option = 'E*' ).
      ELSE.
        IF NOT excluir IS INITIAL.
          r_campos = zcl_ap_lista=>lista2rango( lista = excluir sign = 'E' option = 'E*' ).
        ENDIF.
      ENDIF.

      LOOP AT selection_table TRANSPORTING NO FIELDS WHERE     selname IN r_campos
                                                           AND ( low <> '' OR option <> '' ).
        l_ok = 'X'.
        EXIT.
      ENDLOOP.

      IF l_ok IS INITIAL.
        IF campos_or = '*'.
          IF NOT mensaje IS INITIAL.
            MESSAGE mensaje TYPE msgty.
          ELSE.
            IF msgty = 'E'.
              MESSAGE 'Informe algun criterio de seleccion'(ics) TYPE msgty ##NO_TEXT.
            ELSE.
              MESSAGE 'Deberia informar algun criterio de seleccion'(dic) TYPE msgty ##NO_TEXT.
            ENDIF.
          ENDIF.
        ELSE.
          IF NOT mensaje IS INITIAL.
            MESSAGE mensaje TYPE msgty.
          ELSE.
            READ TEXTPOOL cprog INTO textpooltable LANGUAGE sy-langu.

            LOOP AT selection_table INTO sel WHERE selname IN r_campos. "#EC CI_STDSEQ
              CLEAR texto.
              READ TABLE textpooltable INTO texto WITH KEY key = sel-selname. "#EC CI_STDSEQ
              IF sy-subrc = 0.
                IF texto-entry+1 = '       .'.
                  CLEAR texto.
                ENDIF.
              ENDIF.

              IF NOT texto-entry IS INITIAL.
                CONDENSE texto-entry.
                __add_lista l_descr_campos texto-entry.
              ELSE.
                __add_lista l_descr_campos sel-selname.
              ENDIF.
            ENDLOOP.

            CONCATENATE 'Informe uno de estos filtros:' l_descr_campos INTO l_descr_campos SEPARATED BY space ##NO_TEXT.
            MESSAGE l_descr_campos TYPE msgty.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.

    IF NOT campos_and IS INITIAL.
      r_campos = zcl_ap_lista=>lista2rango( campos_and ).

      LOOP AT selection_table INTO sel WHERE     selname IN r_campos "#EC CI_STDSEQ
                                             AND ( low <> '' OR option <> '' ).
        DELETE r_campos WHERE low = sel-selname.         "#EC CI_STDSEQ
      ENDLOOP.

      IF NOT r_campos IS INITIAL.
        IF NOT mensaje IS INITIAL.
          MESSAGE mensaje TYPE msgty.
        ELSE.

          IF textpooltable IS INITIAL.
            READ TEXTPOOL cprog INTO textpooltable LANGUAGE sy-langu.
          ENDIF.

          LOOP AT selection_table INTO sel WHERE selname IN r_campos. "#EC CI_STDSEQ
            READ TABLE textpooltable INTO texto WITH KEY key = sel-selname. "#EC CI_STDSEQ
            IF sy-subrc = 0.
              IF texto-entry+1 = '       .'.
                CLEAR texto.
              ENDIF.
            ENDIF.

            IF NOT texto-entry IS INITIAL.
              __add_lista l_descr_campos texto-entry.
            ELSE.
              __add_lista l_descr_campos sel-selname.
            ENDIF.
          ENDLOOP.

          CONCATENATE 'Informe todos estos filtros:' l_descr_campos INTO l_descr_campos SEPARATED BY space ##NO_TEXT.
          MESSAGE l_descr_campos TYPE msgty.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDMETHOD.

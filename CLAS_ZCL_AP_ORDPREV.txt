CLASS zcl_ap_ordprev DEFINITION
  PUBLIC
  CREATE PUBLIC.

  PUBLIC SECTION.
    CLASS-METHODS visualizar
      IMPORTING plnum TYPE plnum.

    CLASS-METHODS modificar
      IMPORTING plnum TYPE plnum.

    CLASS-METHODS borrar
      IMPORTING plnum          TYPE plnum
                !commit        TYPE abap_bool DEFAULT 'X'
      RETURNING VALUE(message) TYPE bapiret2-message.

    CLASS-METHODS crear
      IMPORTING pasch    TYPE pasch     DEFAULT 'LA'
                plscn    TYPE plscn     DEFAULT '000'
                matnr    TYPE matnr
                plwrk    TYPE plwrk
                pwwrk    TYPE pwwrk     DEFAULT ''
                gsmng    TYPE gsmng
                meins    TYPE meins     DEFAULT ''
                !commit  TYPE abap_bool DEFAULT 'X'
                psttr    TYPE psttr
                plafx    TYPE plafx     DEFAULT ''
                termx    TYPE termx     DEFAULT ''
                verid    TYPE verid     OPTIONAL
                ekorg    TYPE ekorg     OPTIONAL
                flief    TYPE flief     OPTIONAL
      EXPORTING plnum    TYPE plnum
                !message TYPE bapi_msg.

    CLASS-METHODS fijar
      IMPORTING plnum          TYPE plnum
                !commit        TYPE abap_bool DEFAULT 'X'
      RETURNING VALUE(message) TYPE bapiret2-message.

    CLASS-METHODS modificar_ctd
      IMPORTING plnum          TYPE plnum
                !commit        TYPE abap_bool DEFAULT 'X'
                gsmng          TYPE gsmng     OPTIONAL
                plafx          TYPE plafx     OPTIONAL
      RETURNING VALUE(message) TYPE bapiret2-message.

  PROTECTED SECTION.
  PRIVATE SECTION.
endclass. "ZCL_AP_ORDPREV definition
class ZCL_AP_ORDPREV implementation.
  METHOD borrar.
    DATA l_return TYPE bapireturn1.

    CALL FUNCTION 'BAPI_PLANNEDORDER_DELETE'
      EXPORTING
        plannedorder          = plnum
*         USE_COLL_UPDATE       = ' '
*         LAST_ORDER            = ' '
     IMPORTING
       return                = l_return.

    IF l_return-type = 'E'.
      message = l_return-message.
      CALL FUNCTION 'DEQUEUE_ALL'.
    ELSE.
      IF commit = 'X'.
        zcl_ap_dev=>commit( ).
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD crear.
    DATA: l_headerdata TYPE bapiplaf_i1,
          l_return     TYPE bapireturn1.

    CLEAR l_headerdata.
    l_headerdata-pldord_profile   = pasch.  " Perfil de orden previsional
    l_headerdata-plng_scenario_lt = plscn.  " Escenario de planificación a largo plazo.
    l_headerdata-material         = matnr.
    l_headerdata-plan_plant       = plwrk.  " Centro de planificación
    IF pwwrk IS INITIAL.
      l_headerdata-prod_plant = plwrk.      " Centro de producción en la orden previsional
    ELSE.
      l_headerdata-prod_plant = pwwrk.
    ENDIF.
    IF meins IS INITIAL.
      l_headerdata-base_uom = zcl_ap_material=>get_unidad_base( matnr ).
    ELSE.
      l_headerdata-base_uom = meins.
    ENDIF.
    l_headerdata-total_plord_qty  = gsmng.   " Cantidad total de la orden previsional
    l_headerdata-order_start_date = psttr.   " Fecha de inicio extrema en orden previsional
    l_headerdata-firming_ind      = plafx.   " Indicador de fijación para datos de la orden previsional
    l_headerdata-det_schedule     = termx.   " Programa del ciclo de fabricación (planificación detallada)
    l_headerdata-version          = verid.
    l_headerdata-purch_org        = ekorg.
    l_headerdata-fixed_vend       = flief.

    CALL FUNCTION 'BAPI_PLANNEDORDER_CREATE'
      EXPORTING
        headerdata                  = l_headerdata
      IMPORTING
        return                      = l_return
        plannedorder                = plnum
*         CREATEDHEADERDATA           =
*         CAPACITYHEADERDATA1         =
*         CAPACITYHEADERDATA2         =
*         CAPACITYHEADERDATA3         =
*       TABLES
*         COMPONENTSDATA              =
*         CREATEDCOMPONENTSDATA       =
*         CAPACITYDATA1               =
*         CAPACITYDATA2               =
*         CAPACITYDATA3               =
              .

    IF l_return-type = 'E'.
      message = l_return-message.
      CALL FUNCTION 'DEQUEUE_ALL'.
    ELSE.
      IF commit = 'X'.
        zcl_ap_dev=>commit( ).
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD fijar.
    DATA: l_headerdata  TYPE bapiplaf_i2,
          l_headerdatax TYPE bapiplaf_i2x,
          l_return      TYPE bapireturn1.

    l_headerdata-firming_ind = 'X'.
    l_headerdatax-firming_ind = 'X'.

    CALL FUNCTION 'BAPI_PLANNEDORDER_CHANGE'
      EXPORTING
        plannedorder              = plnum
        headerdata                = l_headerdata
        headerdatax               = l_headerdatax
      IMPORTING
        return                    = l_return
*   CHANGEDHEADERDATA         =
*   CAPACITYHEADERDATA1       =
*   CAPACITYHEADERDATA2       =
*   CAPACITYHEADERDATA3       =
* TABLES
*   COMPONENTSDATA            =
*   CAPACITYDATA1             =
*   CAPACITYDATA2             =
*   CAPACITYDATA3             =
              .

    IF l_return-type = 'E'.
      message = l_return-message.
      CALL FUNCTION 'DEQUEUE_ALL'.
    ELSE.
      IF commit = 'X'.
        zcl_ap_dev=>commit( ).
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD modificar.
    SET PARAMETER ID 'PAF' FIELD plnum.
    CALL TRANSACTION 'MD12' AND SKIP FIRST SCREEN.

*BAPI_PLANNEDORDER_CHANGE
  ENDMETHOD.
  METHOD modificar_ctd.
    DATA: headerdata  TYPE bapiplaf_i2,
          headerdatax TYPE bapiplaf_i2x,
          return      TYPE bapireturn1.

    headerdata-total_plord_qty = gsmng.
    headerdatax-total_plord_qty = 'X'.
    IF plafx IS SUPPLIED.
      headerdata-firming_ind = plafx.
      headerdatax-firming_ind = 'X'.
    ENDIF.
    CALL FUNCTION 'BAPI_PLANNEDORDER_CHANGE'
      EXPORTING
        plannedorder = plnum
        headerdata   = headerdata
        headerdatax  = headerdatax
      IMPORTING
        return       = return.

    IF return-type = 'E'.
      message = return-message.
      CALL FUNCTION 'DEQUEUE_ALL'.
    ELSE.
      IF commit = 'X'.
        zcl_ap_dev=>commit( ).
      ENDIF.
    ENDIF.
  ENDMETHOD.
  METHOD visualizar.
    SET PARAMETER ID 'PAF' FIELD plnum.
    CALL TRANSACTION 'MD13' AND SKIP FIRST SCREEN.
  ENDMETHOD.

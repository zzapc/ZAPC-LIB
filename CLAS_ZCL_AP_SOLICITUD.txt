class ZCL_AP_SOLICITUD definition
  public
  create public .

public section.

  constants C_OBJECTCLAS type CDHDR-OBJECTCLAS value 'EINKBELEG' ##NO_TEXT.
  constants C_OBJETO type SRGBTBREL-TYPEID_A value 'BUS2105' ##NO_TEXT.

  class-methods URLS_GOS_ST
    importing
      !BANFN type EBAN-BANFN
    returning
      value(TABLA) type ZTAB_URL_GOS .
  class-methods INSERTAR_URL_GOS_ST
    importing
      !BANFN type EBAN-BANFN
      !URL type ANY
      !TITULO type ANY .
  class-methods VISUALIZAR_SOLICITUD_ST
    importing
      !BANFN type EBAN-BANFN
      !BNFPO type EBAN-BNFPO optional
      !EDIT type XFELD default '' .
  class-methods GET_URL_POR_TITULO_ST
    importing
      !BANFN type EBAN-BANFN
      !TITULO type STRING
    returning
      value(URL) type STRING .
  class-methods ATTA_GOS_ST
    importing
      !BANFN type EBAN-BANFN
    returning
      value(TABLA) type ZTAB_URL_GOS .
  class-methods BORRAR_POSICION
    importing
      !BANFN type BANFN
      !BNFPO type BNFPO
      !MODO type BDC_MODE default 'N'
    returning
      value(MENSAJE) type BAPIRETURN1-MESSAGE .
  class-methods GET_ESTRATEGIAS
    importing
      !BANFN type EBAN-BANFN
      !BNFPO type EBAN-BNFPO optional
      !INFO_USUARIOS type ABAP_BOOL default ''
    returning
      value(T_COD) type ZTAB_COD .
  class-methods ES_LIBERADO_COD
    importing
      !T_COD type ZTAB_COD
    returning
      value(LIBERADO) type ABAP_BOOL .
  class-methods SIGUIENTE_ESTRATEGIA
    importing
      !BANFN type EBAN-BANFN
      !BNFPO type EBAN-BNFPO
    returning
      value(FRGCO) type FRGCO .
  class-methods ES_SIGUIENTE_ESTRATEGIA
    importing
      !BANFN type EBAN-BANFN
      !BNFPO type EBAN-BNFPO
      value(FRGCO) type FRGCO
    returning
      value(SI) type ABAP_BOOL .
protected section.
private section.
endclass. "ZCL_AP_SOLICITUD definition
class ZCL_AP_SOLICITUD implementation.
method ATTA_GOS_ST.
 DATA l_clave TYPE srgbtbrel-instid_a.

 l_clave = banfn.
 tabla = zcl_gos=>atta_gos_st( tipo = c_objeto clave = l_clave ).

endmethod.
METHOD borrar_posicion.
  DATA o_bi TYPE REF TO zcl_ap_batch_input.

  CREATE OBJECT o_bi.

  o_bi->inicio( ).

* Pantalla de llamada modificar/visualizar/liberar SolPed
  o_bi->dynpro( program = 'SAPMM06B' dynpro = '0105').
  o_bi->campos( campo = 'BDC_OKCODE' valor = '/00').
  o_bi->campos( campo = 'EBAN-BANFN' valor = banfn ). " Número de la solicitud de pedido

* Resumen de posición una línea
  o_bi->dynpro( program = 'SAPMM06B' dynpro = '0106').
  o_bi->campos( campo = 'BDC_OKCODE' valor = '/00').
  o_bi->campos( campo = 'RM06B-BNFPO' valor = bnfpo ). " Campo para posicionamiento en pantalla de resumen

* Resumen de posición una línea
  o_bi->dynpro( program = 'SAPMM06B' dynpro = '0106').
  o_bi->campos( campo = 'BDC_OKCODE' valor = '=DL').
  o_bi->campos( campo = 'RM06B-TCSELFLAG(01)' valor = 'X'). " Casilla selec.ctrl.tablas

* Resumen de posición una línea
  o_bi->dynpro( program = 'SAPMM06B' dynpro = '0106').
  o_bi->campos( campo = 'BDC_OKCODE' valor = '=BU').

  mensaje = o_bi->llamar_transaccion( tcode = 'ME52' modo = 'E').

ENDMETHOD.
method ES_LIBERADO_COD.
data l_cod type zest_cod.

  clear liberado.
  describe table t_cod lines sy-tfill.
  check sy-tfill > 0.

  read table t_cod into l_cod index sy-tfill.
  liberado = l_cod-liberada.

endmethod.
METHOD es_siguiente_estrategia.
  DATA: l_frgco TYPE frgco.

  l_frgco = siguiente_estrategia( banfn = banfn bnfpo = bnfpo ).

  IF frgco = l_frgco.
    si = 'X'.
  ELSE.
    CLEAR si.
  ENDIF.

ENDMETHOD.
method GET_ESTRATEGIAS.
  data: i_rel type table of BAPIRLCORQ,
        l_rel type BAPIRLCORQ,
        i_relp type table of BAPIRLCORQ,
        l_relp type BAPIRLCORQ,
        l_cod  type zest_cod.

  CALL FUNCTION 'BAPI_REQUISITION_GETRELINFO'
    EXPORTING
      NUMBER                       = banfn
      ITEM                         = bnfpo
*   REL_CODE                     =
    TABLES
*   GENERAL_RELEASE_INFO         =
*   RELEASE_PREREQUISITES        =
      RELEASE_ALREADY_POSTED       = i_relp
      RELEASE_FINAL                = i_rel
*   RETURN                       =
            .


  define get_rel.
    clear l_cod.
    if not l_rel-rel_code&1 is initial.
      l_cod-frgco = l_rel-rel_code&1.
      if l_relp-rel_code1 = l_rel-rel_code&1 or l_relp-rel_code2 = l_rel-rel_code&1 or l_relp-rel_code3 = l_rel-rel_code&1 or
         l_relp-rel_code4 = l_rel-rel_code&1 or l_relp-rel_code5 = l_rel-rel_code&1 or l_relp-rel_code6 = l_rel-rel_code&1 or
         l_relp-rel_code7 = l_rel-rel_code&1 or l_relp-rel_code8 = l_rel-rel_code&1.
        l_cod-liberada = 'X'.
      endif.
      append l_cod to t_cod.
    endif.
  end-of-definition.

read table i_relp into l_relp index 1.
  loop at i_rel into l_rel.
    get_rel: 1, 2, 3, 4, 5, 6, 7, 8.
  endloop.

  if info_usuarios = 'X'.
    data: i_cdpos type iscdpos_tab,
          l_cdpos type iscdpos,
          l_ind type i,
          l_lib(10),
          l_key(16).

    CONCATENATE banfn bnfpo into l_key.
    i_cdpos =
     zcl_ap_control_cambios=>get_cdpos( objectclas = c_objectclas
                                        objectid   = l_key
                                        tabname    = 'EBAN'
                                        fname      = 'FRGZU' ).
    clear l_cdpos.
    loop at t_cod into l_cod where liberada = 'X'.
      l_ind = sy-tabix.
      clear l_lib.
      do l_ind times.
        concatenate 'X' l_lib into l_lib.
      enddo.
      read table i_cdpos into l_cdpos with key value_new = l_lib.
      if sy-subrc = 0.
        l_cod-liberado_por = l_cdpos-username.
        l_cod-liberado_el  = l_cdpos-udate.
        l_cod-liberado_a   = l_cdpos-utime.
        modify t_cod from l_cod.
      endif.
    endloop.

  endif.

endmethod.
method GET_URL_POR_TITULO_ST.
 DATA: l_clave TYPE srgbtbrel-instid_a.

 l_clave = banfn  .
 url = zcl_gos=>get_url_por_titulo_st( tipo   = C_OBJETO
                                       clave  = l_clave
                                       titulo = titulo ).

endmethod.
METHOD insertar_url_gos_st.
  DATA: l_clave  TYPE srgbtbrel-instid_a,
        l_titulo TYPE string,
        l_url    TYPE string.

  l_clave  = banfn.
  l_titulo = titulo.
  l_url    = url.
  zcl_gos=>insertar_url_gos_st( tipo   = c_objeto
                                clave  = l_clave
                                titulo = l_titulo
                                url    = l_url ).

ENDMETHOD.
method SIGUIENTE_ESTRATEGIA.
  data: i_cod type ZTAB_COD,
        l_cod type zest_cod.

  i_cod = GET_ESTRATEGIAS( banfn = banfn bnfpo = bnfpo ).

  read table i_cod into l_cod with key LIBERADA = ''.
  if sy-subrc = 0.
    FRGCO = l_cod-FRGCO.
  endif.

endmethod.
method URLS_GOS_ST.
 DATA l_clave TYPE srgbtbrel-instid_a.

 l_clave = banfn.
 tabla = zcl_gos=>urls_gos_st( tipo = C_OBJETO clave = l_clave ).

endmethod.
METHOD VISUALIZAR_SOLICITUD_ST.
  DATA l_display_only.

*  SET PARAMETER ID 'BAN' FIELD banfn.
*
*  IF edit IS INITIAL.
*    CALL TRANSACTION 'ME53N' AND SKIP FIRST SCREEN.
*  ELSE.
*    CALL TRANSACTION 'ME52N' AND SKIP FIRST SCREEN.
*  ENDIF.

  IF edit IS INITIAL.
    l_display_only = 'X'.
  ELSE.
    CLEAR l_display_only.
  ENDIF.

  CALL FUNCTION 'MMPUR_REQUISITION_DISPLAY'
    EXPORTING
      im_banfn        = banfn
      im_bnfpo        = bnfpo
      im_change       = edit
      im_display_only = l_display_only
    EXCEPTIONS
      no_authority    = 1
      OTHERS          = 2.

ENDMETHOD.

***********************************************************************
* TIPO : LISTADO
* TITULO : Listado certificados
* DESCRIPCION : Basado en SSF_ALERT_CERTEXPIRE
*
* AUTOR: Andrés Picazo                                FECHA: 31/08/2023
*
***********************************************************************
REPORT zap_strust.

*------TABLAS/ESTRUCTURAS----------------------------------------------*
INCLUDE ssf_alert_certexpire_top.
INCLUDE ssf_alert_certexpire_func.
*------TABLAS INTERNAS-------------------------------------------------*
*----------------------------------------------------------------------*
* CLASS lcl_alv DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv DEFINITION INHERITING FROM zcl_ap_alv_check FINAL.
  PUBLIC SECTION.
    METHODS: handle_user_command REDEFINITION.
    METHODS: visualizar_objeto REDEFINITION.
ENDCLASS.                    "lcl_alv DEFINITION

*----------------------------------------------------------------------*
*       CLASS zcl_report DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report DEFINITION INHERITING FROM zcl_ap_dev FINAL.
  PUBLIC SECTION.
    TYPES: BEGIN OF t_listado,
             check        TYPE xfeld,
             lights       TYPE zico_estado_mensaje,
             context      TYPE psetype-context,
             applic       TYPE strustssl-applic,
             subject(512) TYPE c,
             issuer(512)  TYPE c,
             date         TYPE dats,
             diff         TYPE i,
             dominio      TYPE si_lfa1-lfurl,
             message      TYPE bapi_msg,
           END OF t_listado,
           tt_listado TYPE STANDARD TABLE OF t_listado.
    DATA: i_listado TYPE tt_listado,
          o_alv     TYPE REF TO lcl_alv.                    "#EC NEEDED


    METHODS: main.

    METHODS:  listado,
      seleccionar_datos.

ENDCLASS.                    "REPORT DEFINITION

*------VARIABLES-------------------------------------------------------*
DATA: o_prog TYPE REF TO zcl_report.

DATA: strustssl TYPE strustssl,
      psetype   TYPE psetype,
      dominio   TYPE text255.
*------PARAMETER/SELECT-OPTIONS EN PANTALLA----------------------------*
SELECTION-SCREEN BEGIN OF BLOCK b01 WITH FRAME TITLE TEXT-sel.
SELECT-OPTIONS: s_contex FOR psetype-context,
                s_applic FOR strustssl-applic,
                s_domini FOR dominio.
SELECTION-SCREEN: SKIP 1.
PARAMETERS: p_dias  TYPE i DEFAULT 30,
            p_solo  AS CHECKBOX,
            p_email TYPE text255.
SELECTION-SCREEN: SKIP 1.
PARAMETERS: p_vari LIKE disvariant-variant.
SELECTION-SCREEN END OF BLOCK b01.
__botones_plantilla.


************************************************************************
*
* LOGICA DEL PROGRAMA
*
************************************************************************

*----------------------------------------------------------------------*
* CLASS lcl_alv IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv IMPLEMENTATION.

  METHOD visualizar_objeto.
    DATA l_list TYPE o_prog->t_listado.
    l_list = list.
    CASE column.
*      WHEN 'BUKRS'.
*        MESSAGE l_list-bukrs TYPE 'I'.
      WHEN OTHERS. message = 'No implementado'.
    ENDCASE.
  ENDMETHOD. "handle_double_click


  METHOD handle_user_command.
    DATA: l_ok  TYPE abap_bool.

    check_ucomm_sel = 'F02'.

    super->handle_user_command( e_salv_function ).

    CASE ucomm.
      WHEN 'F01'.
        CALL TRANSACTION 'STRUST'.
      WHEN 'F02'.
        DATA(o_python) = NEW zcl_ap_python( info = '' ).
        IF NOT o_python->message IS INITIAL.
          MESSAGE o_python->message TYPE 'I'.
        ELSE.
          LOOP AT o_prog->i_listado ASSIGNING FIELD-SYMBOL(<listado>) WHERE check = 'X'.
            zcl_ap_popup=>popup_usuario( EXPORTING campo1 = 'SI_LFA1-LFURL'
                                                   titulo = 'Confirme que desea descargar certificado'
                                         IMPORTING return = l_ok
                                         CHANGING  valor1 = <listado>-dominio ).
            IF l_ok NE 'A' AND NOT <listado>-dominio IS INITIAL.
              o_python->grabar_script( EXPORTING codigo  = 'GET_CERT'
                                       IMPORTING message = DATA(l_msg)
                                                 fichero = DATA(l_fichero) ).
              IF NOT l_msg IS INITIAL.
                message l_msg type 'I'.
              ELSE.
                DATA l_cer TYPE string.
                l_cer = <listado>-dominio.
                REPLACE ALL OCCURRENCES OF '.' IN l_cer WITH '_'.
                o_python->ejecutar_script( EXPORTING fichero    = l_fichero
                                                     parametros = |{ <listado>-dominio } --output { l_cer }.cer|
                                           IMPORTING message    = l_msg
                                                     salida     = DATA(l_salida) ).
                IF NOT l_msg IS INITIAL.
                  MESSAGE o_python->message TYPE 'I'.
                ELSEIF NOT l_salida IS INITIAL.
                  MESSAGE l_salida TYPE 'I'.
                ELSE.
                  MESSAGE 'Error descargando certificado' TYPE 'I'.
                ENDIF.

              ENDIF.
            ENDIF.
          ENDLOOP.
        ENDIF.
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD. "handle_USER_COMMAND
ENDCLASS. "lcl_alv IMPLEMENTATION

*----------------------------------------------------------------------*
*       CLASS zcl_report IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report IMPLEMENTATION.
  METHOD main.
    seleccionar_datos( ).
    listado( ).

    IF NOT p_email IS INITIAL.
      LOOP AT i_listado TRANSPORTING NO FIELDS WHERE lights(3) NE icon_green_light(3).
        EXIT.
      ENDLOOP.
      IF sy-subrc = 0.
        DATA(l_xstring) = zcl_ap_abap2xls=>alv_2_xls( alv = o_alv->o_alv tabla = i_listado quitar_campos = 'LIGHTS,LOGS' ).

        zcl_ap_envio_mail=>mail( EXPORTING subject        = |Certificados a actualizar { sy-sysid } { sy-datum DATE = USER }|
                                           direccion      = p_email
                                           xstring        = l_xstring
                                           nombre_fichero = 'Certificados.xlsx'
                                           conversion_sap = 'Y'
                                 IMPORTING message        = DATA(message)
                                          ).
      ENDIF.
    ENDIF.
  ENDMETHOD.                    "REPORT

  METHOD seleccionar_datos.
    DATA:  g_cert      TYPE xstring.
    DATA:  gt_pklist   TYPE ssfbintab.
    DATA:  l_utctime(15)  TYPE c. "ssfutc.
    DATA l_listado TYPE t_listado.

    sgpi_texto( 'Seleccionando datos'(sda) ).

    PERFORM create_pse_list CHANGING gt_pse.

    LOOP AT gt_pse ASSIGNING FIELD-SYMBOL(<pse>) WHERE context IN s_contex
                                                   AND applic  IN s_applic.
      CLEAR l_listado.
      MOVE-CORRESPONDING <pse> TO l_listado.
      CALL FUNCTION 'SSFP_GET_PSEINFO'
        EXPORTING
          context           = <pse>-context
          applic            = <pse>-applic
          accept_no_cert    = 'X'
        IMPORTING
          certificate       = g_cert
          certificatelist   = gt_pklist
        EXCEPTIONS
          ssf_no_ssflib     = 1
          ssf_krn_error     = 2
          ssf_invalid_par   = 3 "PSE does not exist
          ssf_unknown_error = 4
          OTHERS            = 5.
      IF sy-subrc = 0.
        CALL FUNCTION 'SSFC_PARSE_CERTIFICATE'
          EXPORTING
            certificate         = g_cert
          IMPORTING
            subject             = l_listado-subject
            issuer              = l_listado-issuer
*           SERIALNO            =
*           VALIDFROM           =
            validto             = l_utctime
*           ALGID               =
*           FINGERPRINT         =
*           SUMMARY             =
*           ALL                 =
          EXCEPTIONS
            ssf_krn_error       = 1
            ssf_krn_nomemory    = 2
            ssf_krn_nossflib    = 3
            ssf_krn_invalid_par = 4
            OTHERS              = 5.

        IF sy-subrc = 0.
          l_listado-date = l_utctime(8).
          APPEND l_listado TO i_listado.

          LOOP AT gt_pklist INTO DATA(l_cacert).
            CALL FUNCTION 'SSFC_PARSE_CERTIFICATE'
              EXPORTING
                certificate         = l_cacert
              IMPORTING
                subject             = l_listado-subject
                issuer              = l_listado-issuer
*               SERIALNO            =
*               VALIDFROM           =
                validto             = l_utctime
*               ALGID               =
*               FINGERPRINT         =
*               SUMMARY             =
*               ALL                 =
              EXCEPTIONS
                ssf_krn_error       = 1
                ssf_krn_nomemory    = 2
                ssf_krn_nossflib    = 3
                ssf_krn_invalid_par = 4
                OTHERS              = 5.

            IF sy-subrc = 0.
              l_listado-date = l_utctime(8).

              APPEND l_listado TO i_listado.
            ENDIF.
          ENDLOOP.
        ENDIF.
      ENDIF.
    ENDLOOP.

    LOOP AT i_listado ASSIGNING FIELD-SYMBOL(<listado>).
      IF <listado>-subject CS 'OU='.
        DATA(l_sep) = 'OU='.
      ELSEIF <listado>-subject CS 'CN='.
        l_sep = 'CN='.
      ENDIF.
      SPLIT <listado>-subject AT l_sep INTO DATA(l_aux) <listado>-dominio.
      SPLIT <listado>-dominio AT ',' INTO <listado>-dominio l_aux.
      IF NOT <listado>-dominio CS '.' AND <listado>-issuer CS 'OU='.
        SPLIT <listado>-issuer AT l_sep INTO l_aux <listado>-dominio.
        SPLIT <listado>-dominio AT ',' INTO <listado>-dominio l_aux.
      ENDIF.

      IF NOT <listado>-dominio IN s_domini.
        DELETE i_listado.
      ELSE.
        <listado>-diff = <listado>-date - sy-datum.
        IF <listado>-diff > p_dias.
          IF p_solo = 'X'.
            DELETE i_listado.
            CONTINUE.
          ELSE.
            <listado>-lights = zcl_ap_alv=>set_icono( icono = icon_green_light mensaje = |Quedan { <listado>-diff } hasta que caduque el certificado| ).
          ENDIF.
        ELSEIF <listado>-diff > 0.
          <listado>-lights = zcl_ap_alv=>set_icono( icono = icon_yellow_light mensaje = |Sólo faltan { <listado>-diff } hasta que caduque el certificado| ).
        ELSE.
          <listado>-message = 'Certificado caducado'.
          <listado>-lights = zcl_ap_alv=>set_icono( icono = icon_red_light mensaje = <listado>-message ).
        ENDIF.
      ENDIF.

    ENDLOOP.


  ENDMETHOD.                    "seleccionar_datos


  METHOD listado.

    sgpi_texto( 'Generando informe'(gin) ).

    o_alv->add_button( button = 'F01' text = 'STRUST' icon = icon_execute_object ).
    o_alv->add_button( button = 'F02' text = 'Descargar certificado' icon = icon_url ).

    o_alv->set_layout( p_vari ).

    o_alv->set_top_of_page( ).

*    o_alv->set_field_hotspot( campo = 'EBELN' auto = 'X' ).

    o_alv->set_field( campo = 'LIGHTS' op = 'KEY' ).
    o_alv->set_field_quitar( 'CHECK' ).
    o_alv->set_field_text( campo = 'DIFF' valor = 'Dias validez' ).

*    o_alv->set_orden( '' ).
    o_alv->get_datos_layout( EXPORTING reordenar_tabla = 'X' tabla_ref = 'X' CHANGING t_tabla = i_listado ).
    o_alv->set_seleccion( CHANGING t_tabla = i_listado ).

    o_alv->show( ).


  ENDMETHOD.                    "

ENDCLASS.                    "REPORT IMPLEMENTATION

*----------------------------------------------------------------------*
* INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

  o_prog = NEW #( status       = 'INICIO_DYN'
                  status_prog  = 'ZAP_STATUS'
                  no_param     = 'X'
                  guardar_logz = '' ).

  PERFORM add_button IN PROGRAM zap_status USING 'M01' 'Analisis' '' ''.

  o_prog->o_alv = NEW #( status             = 'STANDARD_ALV_DYN'
                         status_prog        = 'ZAP_STATUS'
                         top_of_page_auto   = 'X'
                         top_of_page_titulo = 'X'
                         o_dev              = o_prog ).


  p_vari = o_prog->o_alv->get_default_layout( ).

  o_prog->initialization_i( CHANGING sscrfields = sscrfields ).

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  p_vari = o_prog->o_alv->get_f4_layout( ).

************************************************************************
* AT SELECTION-SCREEN.
************************************************************************
AT SELECTION-SCREEN.
  CASE sy-ucomm.
    WHEN 'M01'.
      SUBMIT ssf_alert_certexpire AND RETURN.
    WHEN 'ONLI'.
      o_prog->validar_seleccion_obligatoria( campos_or = '*' msgty = 'W' ).
    WHEN OTHERS.
      o_prog->at_selection( ).
  ENDCASE.

AT SELECTION-SCREEN ON EXIT-COMMAND.
  o_prog->at_selection( ).

*----------------------------------------------------------------------
* START-OF-SELECTION.
*----------------------------------------------------------------------*
START-OF-SELECTION.

  o_prog->main( ).

==================================================
ZCL_AP_MAIL===================CCDEF
==================================================
*"* use this source file for any type declarations (class
*"* definitions, interfaces or data types) you need for method
*"* implementation or private method's signature
==================================================
ZCL_AP_MAIL===================CCIMP
==================================================
*"* local class implementation for public class
*"* use this source file for the implementation part of
*"* local helper classes
==================================================
ZCL_AP_MAIL===================CCMAC
==================================================
*"* use this source file for any macro definitions you need
*"* in the implementation part of the class
==================================================
ZCL_AP_MAIL===================CI
==================================================
*"* private components of class ZCL_AP_MAIL
*"* do not include other source files here!!!
private section.

  data L_TEXTO type SOLISTI1 .
  data O_SENDER type ref to IF_SENDER_BCS .
  data O_RECIPIENT type ref to IF_RECIPIENT_BCS .
==================================================
ZCL_AP_MAIL===================CO
==================================================
*"* protected components of class ZCL_AP_MAIL
*"* do not include other source files here!!!
protected section.
==================================================
ZCL_AP_MAIL===================CP
==================================================
class-pool .
*"* class pool for class ZCL_AP_MAIL

*"* local type definitions
include ZCL_AP_MAIL===================ccdef.

*"* class ZCL_AP_MAIL definition
*"* public declarations
  include ZCL_AP_MAIL===================cu.
*"* protected declarations
  include ZCL_AP_MAIL===================co.
*"* private declarations
  include ZCL_AP_MAIL===================ci.
endclass. "ZCL_AP_MAIL definition

*"* macro definitions
include ZCL_AP_MAIL===================ccmac.
*"* local class implementation
include ZCL_AP_MAIL===================ccimp.

class ZCL_AP_MAIL implementation.
*"* method's implementations
  include methods.
endclass. "ZCL_AP_MAIL implementation

==================================================
ZCL_AP_MAIL===================CT
==================================================
*"* dummy include to reduce generation dependencies between
*"* class ZCL_AP_MAIL and it's users.
*"* touched if any type reference has been changed
==================================================
ZCL_AP_MAIL===================CU
==================================================
class ZCL_AP_MAIL definition
  public
  create public .

*"* public components of class ZCL_AP_MAIL
*"* do not include other source files here!!!
public section.

  data I_TEXTO_MAIL type SRM_T_SOLISTI1 .
  data I_DESTINATARIOS type SWFTLISTI1 .
  data O_SEND_REQUEST type ref to CL_BCS_MESSAGE .
  data O_DOCUMENT type ref to CL_DOCUMENT_BCS .
  data I_ADJUNTOS type RMPS_T_POST_CONTENT .
  data I_ADJUNTOS_BIN type BCST_FILE .

  methods CONSTRUCTOR .
  methods ENVIO_MAIL
    importing
      !SUBJECT type ANY
      !DIRECCION type ANY optional
      !URGENTE type ABAP_BOOL default 'X'
      !HTML type ABAP_BOOL default ''
      !FORZAR_MAIL_EXTERNO type ABAP_BOOL default ''
      !CONFIRMACION_LECTURA type BCS_RQST default 'E'
      !EMISOR type ANY default ''
      !LISTA_DISTRIBUCION type ABAP_BOOL default ''
      !DEST_COPIA type ANY default ''
      !DEST_COPIA_OCULTA type ANY default ''
      !SHOW_LOG type ABAP_BOOL default ''
      !UPDATE_TASK type ABAP_BOOL default ''
    returning
      value(ERROR) type ABAP_BOOL .
  methods SET_TEXT
    importing
      !TEXTO type ANY optional
      !TEXTO2 type ANY optional
      !TEXTO3 type ANY optional
      !TEXTO4 type ANY optional
      !TABLA type SRM_T_SOLISTI1 optional
    preferred parameter TEXTO .
  methods INI_TEXTOS .
  methods CABECERA_HTML
    importing
      !CHARSET type STRING default 'Windows-1252' .
  methods INICIO_TABLA_HTML
    importing
      !LONGITUD type ANY default '800'
      !C1 type ANY
      !C2 type ANY optional
      !C3 type ANY optional
      !C4 type ANY optional
      !C5 type ANY optional
      !C6 type ANY optional
      !C7 type ANY optional
      !C8 type ANY optional
      !C9 type ANY optional
      !C10 type ANY optional
      !C11 type ANY optional
      !C12 type ANY optional
      !C13 type ANY optional
      !C14 type ANY optional
      !BORDER type I default 0
      !C15 type ANY optional
      !C16 type ANY optional .
  methods ADD_FILA_HTML
    importing
      !COLOR type ANY optional
      !C1 type ANY
      !C2 type ANY optional
      !C3 type ANY optional
      !C4 type ANY optional
      !C5 type ANY optional
      !C6 type ANY optional
      !C7 type ANY optional
      !C8 type ANY optional
      !C9 type ANY optional
      !C10 type ANY optional
      !C11 type ANY optional
      !C12 type ANY optional
      !C13 type ANY optional
      !C14 type ANY optional
      !ALIGN_C1 type ANY optional
      !ALIGN_C2 type ANY optional
      !ALIGN_C3 type ANY optional
      !ALIGN_C4 type ANY optional
      !ALIGN_C5 type ANY optional
      !ALIGN_C6 type ANY optional
      !ALIGN_C7 type ANY optional
      !ALIGN_C8 type ANY optional
      !ALIGN_C9 type ANY optional
      !ALIGN_C10 type ANY optional
      !ALIGN_C11 type ANY optional
      !ALIGN_C12 type ANY optional
      !ALIGN_C13 type ANY optional
      !ALIGN_C14 type ANY optional
      !C15 type ANY optional
      !ALIGN_C15 type ANY optional
      !C16 type ANY optional
      !ALIGN_C16 type ANY optional .
  methods FIN_TABLA_HTML .
  methods ADD_PARRAFO_HTML
    importing
      !TEXTO type ANY
      !PARTIR_SIEMPRE type ABAP_BOOL default '' .
  methods ADD_DESTINATARIO
    importing
      !DESTINATARIO type ANY
      !URGENTE type ABAP_BOOL default 'X'
      !FORZAR_MAIL_EXTERNO type ABAP_BOOL default ''
      !LISTA_DISTRIBUCION type ABAP_BOOL default ''
      !COPIA type ABAP_BOOL default ''
      !COPIA_OCULTA type ABAP_BOOL default '' .
  methods SET_TEXT_AS_STRING
    importing
      !TEXTO type ANY
    preferred parameter TEXTO .
  methods FREE .
  class-methods CONSULTA_MAILS_ENVIADOS
    importing
      !FECHA type DATUM optional
      !FECHA_HASTA type DATUM optional
      !R_FECHA type SXDATRNGT
      !HORA type UZEIT optional
      !HORA_HASTA type UZEIT optional
    returning
      value(I_SNDRECS) type SOXSP2TAB .
  class-methods GET_DIR_ENVIO_FROM_ADRNR
    importing
      !ADRNR type ADRNR
    returning
      value(EMAIL) type STRING .
  class-methods CONSULTA_SOST
    importing
      !FECHA type DATUM optional
      !FECHA_HASTA type DATUM optional
      !R_FECHA type SXDATRNGT
      !HORA type UZEIT optional
      !HORA_HASTA type UZEIT optional
    returning
      value(I_SOST) type ZSOST_T .
  class-methods USR2EMAIL
    importing
      !UNAME type ANY
    returning
      value(EMAIL) type STRING .
  class-methods MAIL
    importing
      !SUBJECT type ANY
      !DIRECCION type ANY
      !URGENTE type ABAP_BOOL default 'X'
      !HTML type ABAP_BOOL default ''
      !FORZAR_MAIL_EXTERNO type ABAP_BOOL default ''
      !CONFIRMACION_LECTURA type BCS_RQST default 'N'
      !EMISOR type ANY default ''
      !TEXTO type ANY optional
      !PDF type FPCONTENT optional
      !NOMBRE_FICHERO type ANY default ''
      !I_TEXTOS type RSPC_T_TEXT optional
      !LISTA_DISTRIBUCION type ABAP_BOOL default ''
      !I_ADJUNTOS type RMPS_T_POST_CONTENT optional
      !ZIP type ABAP_BOOL default ''
      !NOMBRE_FICHERO_TABLA type ANY default ''
      !TABLA_COMO_ALV type ABAP_BOOL default 'X'
      !DEST_COPIA type ANY default ''
      !DEST_COPIA_OCULTA type ANY default ''
      !O_ALV_ORIGEN type ref to ZCL_AP_ALV optional
    changing
      !I_TABLA type TABLE optional .
  class-methods NOMBRE_USUARIO
    importing
      !UNAME type ANY
    returning
      value(NOMBRE) type STRING .
  methods SET_TEXTO_ESTANDAR_HTML
    importing
      !ID type ANY
      !NAME type ANY
      !SPRAS type STXH-TDSPRAS optional
      !OBJECT type ANY .
  methods ADD_ADJUNTO
    importing
      !TIPO type SOODK-OBJTP default 'BIN'
      !TITULO type ANY default ''
      !LONGITUD type ANY optional
      !BINARIO type SOLIX_TAB optional
      !HEADER type SOLI_TAB optional
      !XSTRING type XSTRING optional
      !STRING type STRING optional .
  methods ADD_OTF
    importing
      !I_OTFDATA type TT_ITCOO
      !TITULO type ANY .
  methods ADD_FICHERO_SERV
    importing
      !FICHERO type ANY
      !LEGACY type ABAP_BOOL default ''
      !MODO_TEXTO type ABAP_BOOL default 'X'
      !CODEPAGE type ABAP_ENCODING default ''
    exporting
      !MENSAJE type BAPI_MSG .
  methods ADD_ITAB_AS_XLS
    importing
      !ITAB type DATA
      !DESCRIPCION type SO_OBJ_DES
      !TIPO type ANY default 'XLS'
      !ZIP type ABAP_BOOL default '' .
  methods ADD_ITAB_ALV_AS_XLS
    importing
      !DESCRIPCION type ANY
      !QUITAR_CARACTERES_EXTRANOS type ABAP_BOOL default ''
      !O_ALV_ORIGEN type ref to ZCL_AP_ALV optional
      !ZIP type ABAP_BOOL default ''
      !TIPO type ANY default 'XLS'
    changing
      !ITAB type TABLE .
  methods ADD_FICHERO_LOCAL
    importing
      !FICHERO type ANY
      !SELECCIONAR_FICHERO type ABAP_BOOL default ''
    preferred parameter FICHERO .
  methods ADD_PDF
    importing
      !PDF type FPCONTENT
      !TITULO type ANY .
  methods ADD_ADJUNTO_ZIP
    importing
      !TITULO type ANY default ''
      !XSTRING type XSTRING optional
      !STRING type STRING optional
      !TABLA type TABLE optional
      !REEMPLAZAR_EXTENSION type ABAP_BOOL default 'X' .
  methods ADD_URL_HTML
    importing
      !TEXTO type ANY default ''
      !URL type ANY
      !PARRAFO type ABAP_BOOL default ''
      !CODIGO_PARRAFO type STRING default 'P'
    preferred parameter TEXTO .
==================================================
ZCL_AP_MAIL=ADD_ADJUNTO
==================================================
method ADD_ADJUNTO.
  DATA: l_adjunto TYPE rmps_post_content,
        l_size    TYPE so_obj_len.

  CLEAR l_adjunto.
  l_adjunto-doc_type = tipo.
  l_adjunto-docsize  = longitud.

  IF tipo = 'XLS' AND NOT string IS INITIAL.
    l_adjunto-doc_type = tipo.
    l_adjunto-binary   = 'X'.
    cl_bcs_convert=>string_to_solix(
      EXPORTING
        iv_string   = string
        iv_codepage = '4103'  "suitable for MS Excel, leave empty
        iv_add_bom  = 'X'     "for other doc types
      IMPORTING
        et_solix  = l_adjunto-cont_hex
        ev_size   = l_size ).
    l_adjunto-docsize = l_size.
  ELSEIF tipo = 'BIN' or tipo = 'XLS' or tipo = 'PDF'.
    l_adjunto-doc_type = tipo.
    l_adjunto-binary   = 'X'.
    IF NOT binario IS INITIAL.
      l_adjunto-cont_hex = binario.
    ELSEIF NOT xstring IS INITIAL.
      CALL FUNCTION 'SCMS_XSTRING_TO_BINARY'
        EXPORTING
          buffer          = xstring
          append_to_table = ' '
        IMPORTING
          output_length   = l_adjunto-docsize
        TABLES
          binary_tab      = l_adjunto-cont_hex.
    ENDIF.
  ELSE.
    IF tipo = 'BTX'.
      l_adjunto-doc_type = 'BIN'.
    ENDIF.
    l_adjunto-cont_text = header.
  ENDIF.
  l_adjunto-subject  = titulo.
  APPEND l_adjunto TO i_adjuntos.

endmethod.
==================================================
ZCL_AP_MAIL=ADD_ADJUNTO_ZIP
==================================================
method ADD_ADJUNTO_ZIP.
  DATA: l_string TYPE string,
        l_xstring TYPE xstring,
        l_nombre_fichero TYPE string,
        o_zip TYPE REF TO cl_abap_zip.

  IF NOT xstring IS INITIAL.
    l_xstring = xstring.
  ELSEIF NOT string IS INITIAL.
    l_xstring = zcl_ap_string=>string2xstring( string ).
  ELSEIF NOT tabla IS INITIAL.
    l_string = zcl_ap_string=>tabla2string( tabla ).
    l_xstring = zcl_ap_string=>string2xstring( l_string ).
  ENDIF.

  CREATE OBJECT o_zip.
  l_string = titulo.
  o_zip->add( name    = l_string
              content = l_xstring ).

  l_xstring = o_zip->save( ).

  l_nombre_fichero = titulo.
  IF reemplazar_extension IS INITIAL.
    CONCATENATE l_nombre_fichero '.ZIP' INTO l_nombre_fichero.
  ELSE.
    l_string = zcl_ap_ficheros=>get_extension( titulo ).
    IF l_string IS INITIAL.
      CONCATENATE titulo '.ZIP' INTO l_nombre_fichero.
    ELSE.
      REPLACE l_string WITH 'ZIP' INTO l_nombre_fichero.
    ENDIF.
  ENDIF.

  add_adjunto( titulo = l_nombre_fichero xstring = l_xstring ).

endmethod.
==================================================
ZCL_AP_MAIL=ADD_DESTINATARIO
==================================================
METHOD add_destinatario.
  DATA: l_dest         TYPE solisti1,
        l_destinatario TYPE string,
        l_usr21        TYPE usr21,
        i_dest         TYPE TABLE OF string,
        l_desti        TYPE so_obj_nam,
        l_long         TYPE i,
        l_lm,
        l_copia        TYPE bcs_copy.

  DATA iv_address      TYPE bcs_address.
  DATA iv_commtype     TYPE bcs_commtype.
  DATA iv_visible_name TYPE bcs_visname.
  DATA iv_copy         TYPE bcs_copy.
  DATA iv_fax_country  TYPE bcs_fax_country.



  SPLIT destinatario AT ',' INTO TABLE i_dest.

  LOOP AT i_dest INTO l_destinatario.
    CONDENSE l_destinatario NO-GAPS.
    IF forzar_mail_externo = 'X' AND
       lista_distribucion IS INITIAL AND
       l_destinatario(1) NE '$' AND
      ( zcl_c=>salida_mail_desarrollo = 'X' OR
        zcl_c=>entorno_produccion = sy-sysid ).
      TRANSLATE l_destinatario TO UPPER CASE.
      SELECT SINGLE * FROM usr21
        INTO l_usr21
       WHERE bname = l_destinatario.
      IF sy-subrc = 0.
        SELECT SINGLE smtp_addr FROM adr6
          INTO l_destinatario
         WHERE addrnumber = l_usr21-addrnumber
           AND persnumber = l_usr21-persnumber.
      ENDIF.
    ENDIF.

    l_long = strlen( l_destinatario ).
    IF l_destinatario CS '@'.
      DATA l_email TYPE adr6-smtp_addr.
      iv_address  = l_destinatario.
      iv_commtype = 'INT'.
    ELSE.
      l_lm = l_destinatario.
      IF lista_distribucion = 'X' OR l_lm = '$'.
        IF l_lm = '$'.
          l_destinatario = l_destinatario+1.
        ENDIF.
        l_desti = l_destinatario.
        TRANSLATE l_desti TO UPPER CASE.
        iv_address  = l_desti.
      ELSEIF l_destinatario CO '0123456789 +' AND l_long >= 9.
        DATA l_number TYPE ad_pagnmbr.
*   Configuracion del receptor del SMS
        l_number = l_destinatario.
        iv_address  = l_number.
        iv_commtype = 'SMS'.
      ELSE.
        DATA l_uname TYPE uname.
        l_uname = l_destinatario.
        TRANSLATE l_uname TO UPPER CASE.
        iv_address  = l_uname.
        iv_commtype = 'USR'.
      ENDIF.
    ENDIF.

    IF copia = 'X'.
      l_copia = cl_bcs_message=>gc_cc.
    ELSEIF copia_oculta = 'X'.
      l_copia = cl_bcs_message=>gc_bcc.
    ENDIF.

    TRY.
        o_send_request->add_recipient(
            iv_address      = iv_address
            iv_commtype     = iv_commtype
*            iv_visible_name = iv_visible_name
             iv_copy         = l_copia
*            iv_fax_country  = iv_fax_country
               ).

      CATCH cx_root .
        MESSAGE e398(00) WITH 'Error al añadir destinatario' destinatario.
    ENDTRY.
  ENDLOOP.

ENDMETHOD.
==================================================
ZCL_AP_MAIL=ADD_FICHERO_LOCAL
==================================================
METHOD add_fichero_local.
  DATA lt_files TYPE filetable.
  DATA ls_file TYPE file_table.
  DATA lv_rc TYPE i.
  DATA lv_action TYPE i.
  DATA lv_len TYPE i.
  DATA lt_name_parts  TYPE TABLE OF string.
  DATA lv_lines TYPE i.
  DATA lt_solix TYPE solix_tab.
  DATA lv_file_separator TYPE c.
  DATA rs_file TYPE bcss_file.

  IF seleccionar_fichero = 'X'.
    cl_gui_frontend_services=>file_open_dialog(
      CHANGING
        file_table              = lt_files
        user_action             = lv_action
        rc                      = lv_rc
      EXCEPTIONS
        file_open_dialog_failed = 1
        cntl_error              = 2
        error_no_gui            = 3
        not_supported_by_gui    = 4
        OTHERS                  = 5 ).
    IF sy-subrc <> 0 OR lv_rc < 0.
      MESSAGE e005(sbcs_send).
    ENDIF.

* if user chose cancel get out
    IF lv_action = cl_gui_frontend_services=>action_cancel.
      RETURN.
    ENDIF.

    READ TABLE lt_files INTO ls_file INDEX 1.
* have to convert to type string
    rs_file-filename = ls_file-filename.
  ELSE.
    rs_file-filename = fichero.
  ENDIF.

  cl_gui_frontend_services=>gui_upload(
    EXPORTING
      filename                = rs_file-filename
      filetype                = 'BIN'
*      virus_scan_profile      = iv_vsi_profile
    IMPORTING
      filelength              = lv_len "will be converted to size
    CHANGING
      data_tab                = lt_solix
    EXCEPTIONS
      file_open_error         = 1
      file_read_error         = 2
      no_batch                = 3
      gui_refuse_filetransfer = 4
      no_authority            = 5
      unknown_error           = 6
      bad_data_format         = 7
      access_denied           = 8
      OTHERS                  = 9 ).
  IF sy-subrc <> 0.
    MESSAGE e005(sbcs_send).
  ENDIF.

* convert to xstring
  rs_file-contents = cl_bcs_convert=>solix_to_xstring(
    it_solix = lt_solix
    iv_size  = lv_len ).

* determine filename from path and filename
  cl_gui_frontend_services=>get_file_separator(
    CHANGING
      file_separator       = lv_file_separator
    EXCEPTIONS
      cntl_error           = 1
      error_no_gui         = 2
      not_supported_by_gui = 3
      OTHERS               = 4 ).
  IF sy-subrc = 0.
    SPLIT rs_file-filename AT lv_file_separator INTO TABLE lt_name_parts.
    lv_lines = lines( lt_name_parts ).
    IF lv_lines > 1.
      READ TABLE lt_name_parts INDEX lv_lines INTO rs_file-filename.
    ENDIF.
  ENDIF.

  append rs_file to i_adjuntos_bin.


ENDMETHOD.
==================================================
ZCL_AP_MAIL=ADD_FICHERO_SERV
==================================================
method ADD_FICHERO_SERV.
  TYPES: t_linea(65535).
  DATA: i_att_cont2 TYPE TABLE OF t_linea,
        l_string TYPE string,
        l_linea TYPE string,
        l_adjunto TYPE rmps_post_content,
        i_ttext TYPE soli_tab.

  CLEAR l_adjunto.
  zcl_ap_ficheros=>lee_fich_servidor( EXPORTING fichero = fichero
                                                modo_texto = modo_texto
                                                legacy  = legacy
                                                codepage = codepage
                                      IMPORTING longitud = l_adjunto-docsize
                                                mensaje  = mensaje
                                      CHANGING  tabla   = i_att_cont2 ).

  IF mensaje IS INITIAL.
    LOOP AT i_att_cont2 INTO l_linea.
      IF l_string IS INITIAL.
        l_string = l_linea.
      ELSE.
        CONCATENATE l_string cl_abap_char_utilities=>cr_lf l_linea INTO l_string.
      ENDIF.
    ENDLOOP.

    CALL FUNCTION 'SCMS_STRING_TO_FTEXT'
      EXPORTING
        text      = l_string
      IMPORTING
        length    = l_adjunto-docsize
      TABLES
        ftext_tab = i_ttext.

    IF codepage = '4103' AND modo_texto = 'X'.
      l_adjunto-docsize = 2 * l_adjunto-docsize.
    ENDIF.

    l_adjunto-doc_type = 'EXT'.
    l_adjunto-binary   = ''.
    l_adjunto-subject  = fichero.
    l_adjunto-cont_text = i_ttext.
*  IF legacy = 'X'.
*    l_adjunto-docsize = l_adjunto-docsize * 4.
*  ENDIF.
    APPEND l_adjunto TO i_adjuntos.
  ENDIF.

endmethod.
==================================================
ZCL_AP_MAIL=ADD_FILA_HTML
==================================================
method ADD_FILA_HTML.
  DATA: l_texto TYPE solisti1,
        l_td TYPE string,
        l_tipo.

  DEFINE set_td.
    if align_&1 is initial.
      describe field &1 type l_tipo.
      if l_tipo = 'P' or l_tipo = 'i'.
        l_td = '  <td align="right">'.
      else.
        l_td = '  <td>'.
      endif.
    else.
      concatenate '  <td align="' align_&1 '">' into l_td.
    endif.
  END-OF-DEFINITION.

  IF NOT color IS INITIAL.
    CONCATENATE '<tr bgcolor="' color '">' INTO l_texto.
    APPEND l_texto TO i_texto_mail.
  ELSE.
    set_text(  '  <tr>' ).
  ENDIF.

  set_td c1.
  set_text( texto  = l_td texto2 = c1
            texto3 = '</td>' ).
  IF c2 IS SUPPLIED.
    set_td c2.
    set_text( texto  = l_td texto2 = c2
              texto3 = '</td>' ).
  ENDIF.
  IF c3 IS SUPPLIED.
    set_td c3.
    set_text( texto  = l_td texto2 = c3
              texto3 = '</td>' ).
  ENDIF.
  IF c4 IS SUPPLIED.
    set_td c4.
    set_text( texto  = l_td texto2 = c4
              texto3 = '</td>' ).
  ENDIF.
  IF c5 IS SUPPLIED.
    set_td c5.
    set_text( texto  = l_td texto2 = c5
              texto3 = '</td>' ).
  ENDIF.
  IF c6 IS SUPPLIED.
    set_td c6.
    set_text( texto  = l_td texto2 = c6
              texto3 = '</td>' ).
  ENDIF.
  IF c7 IS SUPPLIED.
    set_td c7.
    set_text( texto  = l_td texto2 = c7
              texto3 = '</td>' ).
  ENDIF.
  IF c8 IS SUPPLIED.
    set_td c8.
    set_text( texto  = l_td texto2 = c8
              texto3 = '</td>' ).
  ENDIF.
  IF c9 IS SUPPLIED.
    set_td c9.
    set_text( texto  = l_td texto2 = c9
              texto3 = '</td>' ).
  ENDIF.
  IF c10 IS SUPPLIED.
    set_td c10.
    set_text( texto  = l_td texto2 = c10
              texto3 = '</td>' ).
  ENDIF.
  IF c11 IS SUPPLIED.
    set_td c11.
    set_text( texto  = l_td texto2 = c11
              texto3 = '</td>' ).
  ENDIF.
  IF c12 IS SUPPLIED.
    set_td c12.
    set_text( texto  = l_td texto2 = c12
              texto3 = '</td>' ).
  ENDIF.
  IF c13 IS SUPPLIED.
    set_td c13.
    set_text( texto  = l_td texto2 = c13
              texto3 = '</td>' ).
  ENDIF.
  IF c14 IS SUPPLIED.
    set_td c14.
    set_text( texto  = l_td texto2 = c14
              texto3 = '</td>' ).
  ENDIF.
  IF c15 IS SUPPLIED.
    set_td c15.
    set_text( texto  = l_td texto2 = c15
              texto3 = '</td>' ).
  ENDIF.
  IF c16 IS SUPPLIED.
    set_td c16.
    set_text( texto  = l_td texto2 = c16
              texto3 = '</td>' ).
  ENDIF.

  set_text(  '  </tr>' ).

endmethod.
==================================================
ZCL_AP_MAIL=ADD_ITAB_ALV_AS_XLS
==================================================
method ADD_ITAB_ALV_AS_XLS.
  DATA: l_adjunto TYPE rmps_post_content,
        o_alv TYPE REF TO zcl_ap_alv,
        x_excelx TYPE solix_tab,
        l_string TYPE string,
        l_long TYPE i,
        l_extension TYPE string,
        l_fichero TYPE string.

  l_fichero = descripcion.
  l_extension = zcl_ap_ficheros=>get_extension( l_fichero ).
  IF l_extension IS INITIAL.
    CONCATENATE l_fichero '.' tipo INTO l_fichero.
  ENDIF.


  IF o_alv_origen IS INITIAL.
    CREATE OBJECT o_alv
      EXPORTING
        tabla = ''.

    o_alv->constructor_tabla( CHANGING t_tabla = itab ).
  ELSE.
    o_alv = o_alv_origen.
  ENDIF.

  o_alv->get_csv_as_string( EXPORTING quitar_caracteres_extranos = quitar_caracteres_extranos
                            IMPORTING string_csv = l_string
                            CHANGING t_tabla = itab ).

  IF zip IS INITIAL.
    l_long = STRLEN( l_string ).
    x_excelx = zcl_ap_string=>string_to_binary_tab( l_string ).

    add_adjunto( titulo = l_fichero tipo = 'XLS' binario = x_excelx longitud = l_long ).

*    add_adjunto( titulo = l_fichero tipo = 'XLS' string = l_string ).
  ELSE.
    add_adjunto_zip( titulo = l_fichero string = l_string ).
  ENDIF.


endmethod.
==================================================
ZCL_AP_MAIL=ADD_ITAB_AS_XLS
==================================================
method ADD_ITAB_AS_XLS.
  TYPE-POOLS: truxs.

  DATA: lv_string TYPE string,
        wa_field TYPE string,
        lv_is_dd_object TYPE boolean,
        lv_dd_header TYPE x030l,
        lv_dd_object TYPE dd_x031l_table,
        lt_filetable TYPE solix_tab,
        lr_data TYPE REF TO data,
        lr_structdescr TYPE REF TO cl_abap_structdescr,
        lr_tabledescr TYPE REF TO cl_abap_datadescr,
        lx_comp TYPE abap_component_tab,
        x_excelx TYPE solix_tab,
        l_adjunto TYPE rmps_post_content,
        o_typedes TYPE REF TO cl_abap_typedescr,
        l_long TYPE i,
        l_extension TYPE string,
        l_fichero TYPE string.

  l_fichero = descripcion.
  l_extension = zcl_ap_ficheros=>get_extension( l_fichero ).
  IF l_extension IS INITIAL.
    CONCATENATE l_fichero '.' tipo INTO l_fichero.
  ENDIF.


  FIELD-SYMBOLS:
                 <dyn_wa> TYPE ANY,
                 <fs_field> TYPE ANY,
                 <fs_table> TYPE ANY TABLE,
                 <fs_dd_tab> TYPE x031l,
                 <fs_comp> TYPE abap_componentdescr.



* Deduce data type of the imported table using RTTS
  ASSIGN itab TO <fs_table>.
  CREATE DATA lr_data LIKE LINE OF <fs_table>.

* Get the table structure
  lr_structdescr ?= cl_abap_structdescr=>describe_by_data_ref( lr_data ).

* Check if this is a DD object or not
  lv_is_dd_object = lr_structdescr->is_ddic_type( ).

* If table is typed on a DD object, we can examine the fields
  IF lv_is_dd_object = abap_true.

* (Not really needed - just to show how to get header info)
    lv_dd_header = lr_structdescr->get_ddic_header( ).
* Retrieves DD info of table/structure
    lv_dd_object = lr_structdescr->get_ddic_object( ).

* Move results to table LX_COMP for ease of use below.
    LOOP AT lv_dd_object ASSIGNING <fs_dd_tab>.
      APPEND INITIAL LINE TO lx_comp ASSIGNING <fs_comp>.
      MOVE <fs_dd_tab>-fieldname TO <fs_comp>-name.
    ENDLOOP.

* If not a DD object, use GET_COMPONENTS.
* Does not fully work for DD objects, because does not
* handle embedded include's.
  ELSE.
    lx_comp = lr_structdescr->get_components( ).
  ENDIF.

* Now we're ready to move the contents from the imported
* table into a string for converting to xls attachment
  IF NOT <fs_table>[] IS INITIAL.

* Retrieve values for each "cell" in the source table, move to excel table:
* Get table row

* First, get the header line into the final string
    LOOP AT lx_comp ASSIGNING <fs_comp>.
      CONCATENATE lv_string <fs_comp>-name wa_field cl_abap_char_utilities=>horizontal_tab
                 INTO lv_string.
    ENDLOOP.

    CONCATENATE lv_string cl_abap_char_utilities=>cr_lf INTO lv_string.

* Then, add contents of table into final string
    LOOP AT <fs_table> ASSIGNING <dyn_wa>.        " New record
      LOOP AT lx_comp ASSIGNING <fs_comp>. " For each field in the record
        IF NOT <fs_comp> IS INITIAL.
          ASSIGN COMPONENT <fs_comp>-name OF STRUCTURE <dyn_wa>
          TO <fs_field>.
          IF <fs_field> IS ASSIGNED.
            MOVE <fs_field> TO wa_field.
            CONCATENATE lv_string wa_field cl_abap_char_utilities=>horizontal_tab
           INTO lv_string.
          ENDIF.
        ENDIF.
      ENDLOOP.
      CONCATENATE lv_string cl_abap_char_utilities=>cr_lf INTO lv_string.
    ENDLOOP.
*
    IF zip IS INITIAL.
      l_long = STRLEN( lv_string ).
      x_excelx = zcl_ap_string=>string_to_binary_tab( lv_string ).

      add_adjunto( titulo = l_fichero tipo = 'BIN' binario = x_excelx longitud = l_long ).
    ELSE.
      add_adjunto_zip( titulo = l_fichero string = lv_string ).
    ENDIF.

  ENDIF.

endmethod.
==================================================
ZCL_AP_MAIL=ADD_OTF
==================================================
method ADD_OTF.
  DATA: l_long_pdf TYPE i,
        i_pdf TYPE solix_tab.

  zcl_ap_smartforms=>get_pdf_from_otfdata( EXPORTING i_otfdata = i_otfdata
                                        IMPORTING i_pdf = i_pdf
                                                  longitud_pdf = l_long_pdf ).

  add_adjunto( titulo = titulo
               longitud = l_long_pdf
               binario = i_pdf ).

endmethod.
==================================================
ZCL_AP_MAIL=ADD_PARRAFO_HTML
==================================================
method ADD_PARRAFO_HTML.
  DATA: l_long TYPE i,
        i_lineas TYPE TABLE OF text255,
        l_linea TYPE text255.

  l_long = STRLEN( texto ).

  IF l_long < 248 and PARTIR_SIEMPRE = ''.
    set_text( texto = '<P>' texto2 = texto texto3 = '</P>' ).
  ELSE.
    zcl_ap_string=>string2tabla( EXPORTING string = texto
                                           longitud = 248
                                 CHANGING  tabla = i_lineas ).

    LOOP AT i_lineas INTO l_linea.
      set_text( texto = '<P>' texto2 = l_linea texto3 = '</P>' ).
    ENDLOOP.
  ENDIF.

endmethod.
==================================================
ZCL_AP_MAIL=ADD_PDF
==================================================
method ADD_PDF.
  DATA: l_long_pdf TYPE i,
        i_pdf TYPE solix_tab.

  CALL FUNCTION 'SCMS_XSTRING_TO_BINARY'
    EXPORTING
      buffer        = pdf
    IMPORTING
      output_length = l_long_pdf
    TABLES
      binary_tab    = i_pdf.

  add_adjunto( titulo = titulo
               longitud = l_long_pdf
               binario = i_pdf
               TIPO    = 'PDF' ).

endmethod.
==================================================
ZCL_AP_MAIL=ADD_URL_HTML
==================================================
method ADD_URL_HTML.
  DATA: l_texto          TYPE string,
        l_linea          TYPE string,
        l_inicio_parrafo TYPE string,
        l_fin_parrafo    TYPE string.

  IF texto IS INITIAL.
    l_texto = url.
  ELSE.
    l_texto = texto.
  ENDIF.

  CONCATENATE '<a href="' url '">' l_texto '</a>' INTO l_linea.
  IF parrafo = 'X'.
    CONCATENATE '<' codigo_parrafo '>' INTO l_inicio_parrafo.
    CONCATENATE '</' codigo_parrafo '>' INTO l_fin_parrafo.
    CONCATENATE l_inicio_parrafo l_linea l_fin_parrafo INTO l_linea.
  ENDIF.

  set_text( l_linea ).

endmethod.
==================================================
ZCL_AP_MAIL=CABECERA_HTML
==================================================
method CABECERA_HTML.

  data l_meta type string.
  set_text( '<head>' ).
  set_text( '<title>Documento sin t&iacute;tulo</title>' ).
  concatenate '<meta http-equiv="Content-Type" content="text/html;'
              'charset=' charset '">' into l_meta separated by space.
  set_text( l_meta ).
  set_text( '<style type="text/css">' ).
  set_text( '<!--' ).
  set_text( 'Estilo1 {font-family: Arial, Helvetica, sans-serif}' ).
  set_text( '-->' ).
  set_text( '</style>' ).
  set_text( '</head>' ).

  set_text( '<body class="Estilo1">' ).

endmethod.
==================================================
ZCL_AP_MAIL=CONSTRUCTOR
==================================================
METHOD constructor.

  CREATE OBJECT o_send_request.

ENDMETHOD.
==================================================
ZCL_AP_MAIL=CONSULTA_MAILS_ENVIADOS
==================================================
method CONSULTA_MAILS_ENVIADOS.
  DATA: i_snd_date TYPE  sxdatrngt,
        l_snd_date TYPE sxdatrngl,
        i_snd_time TYPE  sxtimrngt,
        l_snd_time TYPE sxtimrngl,
        l_status  TYPE  soststatus,
        l_sndrec TYPE soxsp2.

  IF NOT r_fecha IS INITIAL.
    i_snd_date = r_fecha.
  ELSE.
    IF NOT fecha IS INITIAL.
      CLEAR l_snd_date.
      l_snd_date-sign   = 'I'.
      l_snd_date-low    = fecha.

      IF fecha_hasta IS INITIAL.
        l_snd_date-option = 'EQ'.
      ELSE.
        l_snd_date-option = 'BT'.
        l_snd_date-high    = fecha_hasta.
      ENDIF.

      APPEND l_snd_date TO i_snd_date.
    ENDIF.
  ENDIF.

  IF NOT hora IS INITIAL.
    CLEAR l_snd_time.
    l_snd_time-sign   = 'I'.
    l_snd_time-low    = hora.

    IF hora_hasta IS INITIAL.
      l_snd_time-option = 'EQ'.
    ELSE.
      l_snd_time-option = 'BT'.
      l_snd_time-high    = hora_hasta.
    ENDIF.

    APPEND l_snd_time TO i_snd_time.
  ENDIF.

  l_status = 'XXXXXXXX '.
  CALL FUNCTION 'SX_SNDREC_SELECT'
    EXPORTING
*         SND_ART             =
          snd_date            = i_snd_date
          snd_time            = i_snd_time
*         DEL_DATE            =
*         DEL_TIME            =
      status              = l_status
          notifications       = 'X'
*         SENDER              =
*         MAXSEL              =
          all_waiting         = ' '
   IMPORTING
      sndrecs             = i_sndrecs.

endmethod.
==================================================
ZCL_AP_MAIL=CONSULTA_SOST
==================================================
method CONSULTA_SOST.
  DATA: i_sndrecs TYPE soxsp2tab,
        l_sndrec TYPE soxsp2,
        l_sost TYPE zsost.

  i_sndrecs = consulta_mails_enviados( fecha = fecha
                                       fecha_hasta = fecha_hasta
                                       r_fecha = r_fecha
                                       hora  = hora
                                       hora_hasta  = hora_hasta ).

  LOOP AT i_sndrecs INTO l_sndrec.
    CLEAR l_sost.

    IF l_sndrec-msgty = 'E'.
      l_sost-status = '@5C@'.
    ELSE.
      l_sost-status = '@5B@'.
    ENDIF.

    CASE l_sndrec-sndart.
      WHEN 'INT'.
        l_sost-forma_envio = 'Email'.
      WHEN 'PAG'.
        l_sost-forma_envio = 'SMS'.
      WHEN OTHERS.
        l_sost-forma_envio =  l_sndrec-sndart.
    ENDCASE.

    l_sost-titulo = l_sndrec-titel.
    l_sost-emisor = l_sndrec-usernam.
    l_sost-destinatario = get_dir_envio_from_adrnr( l_sndrec-adrnr ).
    l_sost-fecha_envio = l_sndrec-stat_date.
    l_sost-hora_envio = l_sndrec-stat_time.

    APPEND l_sost TO i_sost.

  ENDLOOP.

endmethod.
==================================================
ZCL_AP_MAIL=ENVIO_MAIL
==================================================
METHOD envio_mail.
  DATA: l_adjunto      TYPE rmps_post_content,
        i_recipients   TYPE bcsy_re,
        l_destinatario TYPE solisti1,
        l_smtp_addr    TYPE adr6-smtp_addr,
        l_uname        TYPE uname,
        l_result       TYPE os_boolean,
        ls_file        TYPE bcss_file.

*http://abapblog.com/articles/tricks/104-send-mail-in-badi-or-user-exit-without-commiting
*RSBCS_EXAMPLE_EMAIL

  CLEAR error.

  SET PARAMETER ID 'ZMAIL' FIELD 'X'.

  DATA: l_mailtext    TYPE soli_tab,
        l_mailhex     TYPE solix_tab,
        l_title       TYPE sood-objdes,
        l_tipo(3),
        l_subject     TYPE string,
        l_importancia TYPE bcs_docimp.

  DATA iv_address      TYPE bcs_address.
  DATA iv_commtype     TYPE bcs_commtype.
  DATA iv_visible_name TYPE bcs_visname.
  DATA iv_fax_country  TYPE bcs_fax_country.

  IF html IS INITIAL.
    l_tipo = 'RAW'.
  ELSE.
    l_tipo = 'HTM'.
  ENDIF.

  l_title = subject.
  l_mailtext = i_texto_mail.

  IF urgente = 'X'.
    l_importancia = '1'.
  ENDIF.

  TRY.
      CALL METHOD cl_document_bcs=>create_document
        EXPORTING
          i_type       = l_tipo
          i_subject    = l_title
*         i_length     =
*         i_language   = SPACE
          i_importance = l_importancia
*         i_sensitivity =
          i_text       = l_mailtext
*         i_hex        =
*         i_header     =
*         i_sender     =
        RECEIVING
          result       = o_document.
    CATCH cx_document_bcs .
      MESSAGE 'Error creando mail' TYPE 'E'.
  ENDTRY.

*    o_document = cl_document_bcs=>create_document(
*        i_type = l_tipo
*        i_text = l_mailtext
*        i_subject = l_title ).

  IF o_send_request IS INITIAL.
    error = 'X'.
  ELSE.
    l_subject = subject.
    TRY.
        o_send_request->set_subject( l_subject ).
      CATCH cx_root.
        MESSAGE i398(00) WITH 'Error definiendo asunto:' l_subject.
    ENDTRY.


    IF emisor IS INITIAL.
      TRY.
          CALL METHOD cl_sapuser_bcs=>create
            EXPORTING
              i_user = sy-uname
            RECEIVING
              result = o_sender.
        CATCH cx_address_bcs .
          MESSAGE e398(00) WITH 'Error al definir emisor para usuario:' sy-uname.
      ENDTRY.
    ELSE.
      IF emisor CS '@'.
        l_smtp_addr = emisor.
        iv_address = l_smtp_addr.
        iv_commtype = 'INT'.
      ELSE.
        l_uname = emisor.
        iv_address = l_uname.
        iv_commtype = 'USR'.
      ENDIF.
    ENDIF.

    IF NOT direccion IS INITIAL.
      add_destinatario( destinatario = direccion
                        urgente      = urgente
                        forzar_mail_externo = forzar_mail_externo
                        lista_distribucion = lista_distribucion ).
    ELSEIF NOT i_destinatarios IS INITIAL.
      LOOP AT i_destinatarios INTO l_destinatario.
        add_destinatario( destinatario = l_destinatario
                          urgente      = urgente
                          forzar_mail_externo = forzar_mail_externo ).
      ENDLOOP.
    ENDIF.

    IF NOT dest_copia IS INITIAL.
      add_destinatario( destinatario = dest_copia
                        copia        = 'X'
                        urgente      = urgente
                        forzar_mail_externo = forzar_mail_externo
                        lista_distribucion = lista_distribucion ).
    ENDIF.

    IF NOT dest_copia_oculta IS INITIAL.
      add_destinatario( destinatario = dest_copia
                        copia_oculta = 'X'
                        urgente      = urgente
                        forzar_mail_externo = forzar_mail_externo
                        lista_distribucion = lista_distribucion ).
    ENDIF.


    o_send_request->set_sender(
        iv_address      = iv_address
        iv_commtype     = iv_commtype
*        iv_visible_name = iv_visible_name
*        iv_fax_country  = iv_fax_country
           ).


    IF urgente = 'X'.
      o_send_request->set_send_immediately( abap_true ).
      o_send_request->set_importance( cl_bcs=>cp_priority_normal ).
    ENDIF.

    o_send_request->set_requested_status( confirmacion_lectura ).

    DATA: o_document_bcs TYPE REF TO cx_document_bcs,
          l_long         TYPE i,
          l_titulo       TYPE bcs_description,
          l_error        TYPE string,
          l_xstring      TYPE xstring,
          l_string       TYPE string.

    LOOP AT i_adjuntos INTO l_adjunto.
      l_long = l_adjunto-docsize.

      IF NOT l_adjunto-cont_text[] IS INITIAL.
        l_string = cl_bcs_convert=>raw_to_string( l_adjunto-cont_text ).
      ENDIF.

      IF NOT l_adjunto-cont_hex[] IS INITIAL.
        l_xstring = cl_bcs_convert=>solix_to_xstring( l_adjunto-cont_hex ).
      ENDIF.

      TRY.
          l_titulo = l_adjunto-subject.
          o_send_request->add_attachment(
     iv_filename     = l_titulo
     iv_contents_bin = l_xstring
     ).
        CATCH cx_root INTO DATA(ex_at).
          MESSAGE ex_at->get_text( ) TYPE 'E'.
      ENDTRY.
    ENDLOOP.

    LOOP AT i_adjuntos_bin INTO ls_file.
      TRY.
          o_send_request->add_attachment(
            iv_filename     = ls_file-filename
            iv_contents_bin = ls_file-contents ).

        CATCH cx_root INTO DATA(ex_at2).
          MESSAGE ex_at2->get_text( ) TYPE 'E'.
      ENDTRY.
    ENDLOOP.

    IF NOT l_mailtext IS INITIAL.
      l_string = cl_bcs_convert=>raw_to_string( l_mailtext ).
    ENDIF.

    o_send_request->set_main_doc( l_string ).

    IF update_task = 'X'.
      o_send_request->set_update_task( update_task ).
    ENDIF.

    TRY.
        o_send_request->send( ).
        IF show_log = 'X'.
          o_send_request->show_send_log( ).
        ENDIF.
      CATCH cx_bcs_send INTO DATA(ex).
        MESSAGE ex->get_text( ) TYPE 'E'.
    ENDTRY.
  ENDIF.

  SET PARAMETER ID 'ZMAIL' FIELD ''.

ENDMETHOD.
==================================================
ZCL_AP_MAIL=FIN_TABLA_HTML
==================================================
method FIN_TABLA_HTML.

  set_text( '</table>' ).

endmethod.
==================================================
ZCL_AP_MAIL=FREE
==================================================
method FREE.

  CLEAR: i_texto_mail, i_destinatarios,
         o_send_request, o_document, o_sender, o_recipient,
         i_adjuntos.

endmethod.
==================================================
ZCL_AP_MAIL=GET_DIR_ENVIO_FROM_ADRNR
==================================================
method GET_DIR_ENVIO_FROM_ADRNR.

  SELECT SINGLE g~name_text
         INTO email
         FROM adcp AS f JOIN adrp AS g
         ON  f~persnumber = g~persnumber
         AND f~date_from  = g~date_from
         AND f~nation     = g~nation
  WHERE f~so_key = adrnr.

endmethod.
==================================================
ZCL_AP_MAIL=INICIO_TABLA_HTML
==================================================
method INICIO_TABLA_HTML.
  DATA: l_long(10),
        l_border(3),
        l_aux(40).

  l_long = longitud.
  WRITE border TO l_border.
  CONDENSE l_border NO-GAPS.

  CONCATENATE '" border="' l_border '">' INTO l_aux.
  set_text( texto  = '<table width="' texto2 = l_long
            texto3 = l_aux ).
  set_text(  '  <tr bgcolor="#6699FF">' ).
  set_text( texto  = '  <th scope="col">' texto2 = c1
            texto3 = '</th>' ).
  IF c2 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c2
              texto3 = '</th>' ).
  ENDIF.
  IF c3 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c3
              texto3 = '</th>' ).
  ENDIF.
  IF c4 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c4
              texto3 = '</th>' ).
  ENDIF.
  IF c5 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c5
              texto3 = '</th>' ).
  ENDIF.
  IF c6 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c6
              texto3 = '</th>' ).
  ENDIF.
  IF c7 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c7
              texto3 = '</th>' ).
  ENDIF.
  IF c8 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c8
              texto3 = '</th>' ).
  ENDIF.
  IF c9 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c9
              texto3 = '</th>' ).
  ENDIF.
  IF c10 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c10
              texto3 = '</th>' ).
  ENDIF.
  IF c11 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c11
              texto3 = '</th>' ).
  ENDIF.
  IF c12 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c12
              texto3 = '</th>' ).
  ENDIF.
  IF c13 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c13
              texto3 = '</th>' ).
  ENDIF.
  IF c14 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c14
              texto3 = '</th>' ).
  ENDIF.
  IF c15 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c15
              texto3 = '</th>' ).
  ENDIF.
  IF c16 IS SUPPLIED.
    set_text( texto  = '  <th scope="col">' texto2 = c16
              texto3 = '</th>' ).
  ENDIF.
  set_text(  '  </tr>' ).

endmethod.
==================================================
ZCL_AP_MAIL=INI_TEXTOS
==================================================
method INI_TEXTOS.
  clear I_TEXTO_MAIL.
endmethod.
==================================================
ZCL_AP_MAIL=MAIL
==================================================
method MAIL.
  DATA: o_mail TYPE REF TO zcl_ap_mail,
        l_texto TYPE solisti1,
        l_titulo TYPE string,
        l_aux(10).

  CREATE OBJECT o_mail.

  IF NOT texto IS INITIAL.
    o_mail->set_text( texto ).
  ENDIF.

  LOOP AT i_textos INTO l_texto.
    o_mail->set_text( l_texto ).
  ENDLOOP.

  o_mail->i_adjuntos = i_adjuntos.

  IF NOT pdf IS INITIAL.
    IF NOT nombre_fichero IS INITIAL.
      l_titulo = nombre_fichero.
    ELSE.
      l_titulo = 'ADJUNTO.PDF'.
    ENDIF.
    o_mail->add_pdf( pdf = pdf titulo = l_titulo ).
  ELSEIF NOT nombre_fichero IS INITIAL.
    o_mail->add_fichero_local( fichero = nombre_fichero ).
  ENDIF.

  IF NOT i_tabla IS INITIAL.
    DESCRIBE TABLE i_tabla LINES sy-tfill.
    IF sy-tfill > 20000. "Si tiene más de 20000 líneas lo más securo es que casque por rendimiento, así que en lugar de enviar la tabla enviamos un mensaje diciendo que no se envía
      l_aux = sy-tfill.
      CONCATENATE 'Tabla demasiado grande ('(tdg) l_aux '). No es posible adjuntar contenido'(npa) INTO l_texto.
      o_mail->set_text( l_texto ).
    ELSE.
      IF nombre_fichero_tabla IS INITIAL.
        IF tabla_como_alv IS INITIAL.
          o_mail->add_itab_as_xls( descripcion = 'TABLA.XLS'  zip = zip itab = i_tabla[] ).
        ELSE.
          o_mail->add_itab_alv_as_xls( EXPORTING descripcion = 'TABLA.XLS'  zip = zip quitar_caracteres_extranos = 'X' O_ALV_ORIGEN = O_ALV_ORIGEN CHANGING itab = i_tabla[] ).
        ENDIF.
      ELSE.
        IF tabla_como_alv IS INITIAL.
          o_mail->add_itab_as_xls( descripcion = nombre_fichero_tabla  zip = zip itab = i_tabla[] ).
        ELSE.
          o_mail->add_itab_alv_as_xls( EXPORTING descripcion = nombre_fichero_tabla  zip = zip quitar_caracteres_extranos = 'X' O_ALV_ORIGEN = O_ALV_ORIGEN CHANGING itab = i_tabla[] ).
        ENDIF.
      ENDIF.
    ENDIF.
  ENDIF.

  o_mail->envio_mail( subject    = subject
                      direccion  = direccion
                      dest_copia = dest_copia
                      urgente = urgente
                      html    = html
                      forzar_mail_externo = forzar_mail_externo
                      confirmacion_lectura = confirmacion_lectura
                      emisor  = emisor
                      lista_distribucion = lista_distribucion ).

  o_mail->free( ).

endmethod.
==================================================
ZCL_AP_MAIL=NOMBRE_USUARIO
==================================================
method NOMBRE_USUARIO.

  SELECT SINGLE adrp~name_text
    INTO nombre
   FROM usr21 JOIN adrp ON usr21~persnumber EQ adrp~persnumber
   WHERE usr21~bname = uname.

endmethod.
==================================================
ZCL_AP_MAIL=SET_TEXT
==================================================
METHOD SET_TEXT.
  DATA: l_string TYPE string,
        l_texto TYPE solisti1,
        l_texto2 TYPE solisti1,
        l_tipo TYPE c,
        l_long TYPE i,
        l_long2 TYPE i,
        i_tt TYPE TABLE OF solisti1.

  IF tabla IS INITIAL.

    DESCRIBE FIELD texto TYPE l_tipo.
    IF l_tipo = 'C' or l_tipo = 'g'.
      l_long = STRLEN( texto ).
    ENDIF.
    IF texto2 IS SUPPLIED.
      DESCRIBE FIELD texto2 TYPE l_tipo.
      IF l_tipo = 'C' or l_tipo = 'g'.
        l_long2 = STRLEN( texto2 ).
      ENDIF.
    ENDIF.
    IF l_long <= 255 AND l_long2 <= 255.
      WRITE texto TO l_texto.
      IF texto2 IS SUPPLIED.
        DESCRIBE FIELD texto2 TYPE l_tipo.
        WRITE texto2 TO l_texto2.
        CASE l_tipo.
          WHEN 'P'.
            WRITE texto2 TO l_texto2(15).
          WHEN 'D'.
            IF texto2 = '00000000'.
              CLEAR l_texto2.
            ELSE.
              WRITE texto2 TO l_texto2.
            ENDIF.
          WHEN 'T'.
            IF texto2 = '000000'.
              CLEAR l_texto2.
            ELSE.
              WRITE texto2 TO l_texto2.
            ENDIF.
        ENDCASE.
        CONCATENATE l_texto l_texto2 INTO l_texto SEPARATED BY space.
      ENDIF.
      IF texto3 IS SUPPLIED.
        WRITE texto3 TO l_texto2.
        CONCATENATE l_texto l_texto2 INTO l_texto SEPARATED BY space.
      ENDIF.
      IF texto4 IS SUPPLIED.
        WRITE texto4 TO l_texto2.
        CONCATENATE l_texto l_texto2 INTO l_texto SEPARATED BY space.
      ENDIF.

      APPEND l_texto TO i_texto_mail.
    ELSE.
      CONCATENATE texto texto2 texto3 texto4 INTO l_string SEPARATED BY space.

      zcl_ap_string=>string2tabla( EXPORTING string = l_string longitud = 250 CHANGING tabla = i_tt ).
      LOOP AT i_tt INTO l_texto.
        APPEND l_texto TO i_texto_mail.
      ENDLOOP.
    ENDIF.
  ELSE.
    i_texto_mail = tabla.
  ENDIF.

ENDMETHOD.
==================================================
ZCL_AP_MAIL=SET_TEXTO_ESTANDAR_HTML
==================================================
method SET_TEXTO_ESTANDAR_HTML.
  DATA: i_lineas TYPE tlinetab,
        l_tline TYPE tline,
        i_tdline TYPE TABLE OF tdline,
        l_format_ant TYPE tdformat,
        l_first, l_fin,
        l_texto TYPE solisti1,
        l_texto_ant TYPE solisti1.

  i_lineas = zcl_ap_textos=>get_texto( id     = id
                                    name   = name
                                    spras  = spras
                                    object = object ).

  IF NOT i_lineas IS INITIAL.
    l_texto = '<P>'.
    LOOP AT i_lineas INTO l_tline.
      l_texto_ant = l_texto.
      CLEAR: l_first, l_fin.
      AT FIRST.
        l_first = 'X'.
      ENDAT.
      AT LAST.
        l_fin = 'X'.
      ENDAT.
      IF l_first = ''.
        IF NOT l_tline-tdformat IS INITIAL.
          CONCATENATE l_texto '</P>' INTO l_texto.
          set_text( l_texto ).
          l_texto = '<P>'.
        ENDIF.
      ENDIF.
      CONCATENATE l_texto l_tline-tdline INTO l_texto.
      l_format_ant = l_tline-tdformat.

      IF STRLEN( l_texto ) > 250.
        CONCATENATE l_texto_ant '</P>' into l_texto_ant.
        set_text( l_texto_ant ).
        CONCATENATE '<P>' l_tline-tdline INTO l_texto.
      ENDIF.
    ENDLOOP.
    IF l_texto NE '<P>'.
      CONCATENATE l_texto '</P>' INTO l_texto.
      IF STRLEN( l_texto ) > 250.
        CONCATENATE l_texto_ant '</P>' into l_texto_ant.
        set_text( l_texto_ant ).
        CONCATENATE '<P>' l_tline-tdline INTO l_texto.
      ENDIF.
      set_text( l_texto ).
    ENDIF.
  ENDIF.

endmethod.
==================================================
ZCL_AP_MAIL=SET_TEXT_AS_STRING
==================================================
method SET_TEXT_AS_STRING.

  zcl_ap_string=>string2tablastring( exporting string = texto
                                               longitud = 255
                                     changing  tabla = i_texto_mail ).

endmethod.
==================================================
ZCL_AP_MAIL=USR2EMAIL
==================================================
method USR2EMAIL.

  SELECT SINGLE adr6~smtp_addr
    INTO email
   FROM usr21 JOIN adr6 ON usr21~persnumber EQ adr6~persnumber
   WHERE usr21~bname EQ uname.

endmethod.

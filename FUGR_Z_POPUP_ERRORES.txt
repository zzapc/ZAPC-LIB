FUNCTION z_popup_errores.
*"----------------------------------------------------------------------
*"*"Interfase local
*"  IMPORTING
*"     REFERENCE(SHOW_MSGV1) TYPE  ANY DEFAULT ''
*"     REFERENCE(SHOW_MSGV2) TYPE  ANY DEFAULT ''
*"     REFERENCE(TITULO) TYPE  ANY DEFAULT ''
*"  TABLES
*"      T_ERRORES STRUCTURE  ZEST_AP_ERRORES
*"----------------------------------------------------------------------
  DATA: l_long     TYPE i VALUE 90,
        l_alto     TYPE i VALUE 10,
        l_long_msg TYPE i,
        l_num_msg  TYPE i.

  FIELD-SYMBOLS <error> TYPE zest_ap_errores.

  CHECK NOT t_errores[] IS INITIAL.

  DESCRIBE TABLE t_errores LINES l_num_msg.

  v_show_msgv1 = show_msgv1.
  v_show_msgv2 = show_msgv2.
  v_titulo     = titulo.

  REFRESH i_errores.
  LOOP AT t_errores ASSIGNING <error>.
    CLEAR i_errores.
    MOVE-CORRESPONDING <error> TO i_errores.
    IF i_errores-lights IS INITIAL.
      CASE i_errores-msgty.
        WHEN 'I' OR 'S'.
          i_errores-lights = zcl_ap_alv=>c_sem_verde.
        WHEN 'W'.
          i_errores-lights = zcl_ap_alv=>c_sem_ambar.
        WHEN '?'.
          CLEAR i_errores-lights.
        WHEN OTHERS.
          i_errores-lights = zcl_ap_alv=>c_sem_rojo.
      ENDCASE.
    ENDIF.

    IF i_errores-message IS INITIAL AND NOT i_errores-msgno IS INITIAL.
      MESSAGE ID i_errores-msgid TYPE 'S' NUMBER i_errores-msgno
            WITH i_errores-msgv1 i_errores-msgv2 i_errores-msgv3 i_errores-msgv4
            INTO i_errores-message.
    ENDIF.

    l_long_msg = strlen( i_errores-message ).
    IF l_long_msg > l_long.
      IF l_long_msg > 120.
        l_long = 120.
      ELSE.
        l_long = l_long_msg + 4.
      ENDIF.
    ENDIF.

    IF NOT <error>-color IS INITIAL.
      zcl_ap_alv=>append_color( EXPORTING campo = '' colorc = <error>-color
                                CHANGING  tabla_color = i_errores-color_alv ).
    ENDIF.

    APPEND i_errores.
  ENDLOOP.

  CLEAR v_hay_objeto.
  LOOP AT i_errores WHERE objeto NE ''.
    v_hay_objeto = 'X'.
    ADD 20 TO l_long.
    EXIT.
  ENDLOOP.

  ADD 6 TO l_num_msg.
  IF l_num_msg > 20.
    l_alto = 20.
  ELSE.
    l_alto = l_num_msg.
  ENDIF.

  IF sy-batch IS INITIAL.
    IF NOT o_alv_error IS INITIAL.
      o_alv_error->free( ).
      CLEAR o_alv_error.
    ENDIF.
    CALL SCREEN 0200 STARTING AT 3 3 ENDING AT l_long l_alto.
  ELSE.
    LOOP AT i_errores.
      IF i_errores-msgty = 'I' OR i_errores-msgty = 'S'.
        MESSAGE i_errores-message TYPE i_errores-msgty.
      ELSE.
        MESSAGE s398(00) WITH i_errores-msgty i_errores-message(40) i_errores-message+40(40) i_errores-message+80(40).
      ENDIF.
    ENDLOOP.
  ENDIF.

ENDFUNCTION.
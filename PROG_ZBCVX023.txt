************************************************************************
* MÓDULO      : BC                                                     *
* TIPO        : Utilidad
* TITULO      : Descarga SPOOL y LOG job a ficheros
* DESCRIPCION : Descarga SPOOL y LOG job a ficheros
* AUTOR: Andres Picazo                          FECHA: 25/04/2013      *
* MODIFICACIONES                                                       *
* --------------                                                       *
* FECHA        NOMBRE            DESCRIPCION                           *
* -------------------------------------------------------------------- *
************************************************************************
REPORT  zbcvx023.

*------I N C L U D E S ------------------------------------------------*

*------TABLAS/ESTRUCTURAS----------------------------------------------*
TABLES: tbtco.

*------TABLAS INTERNAS-------------------------------------------------*
DATA: o_job TYPE REF TO zcl_ap_jobs,
      i_log_job TYPE btc_t_job_log,
      i_spools TYPE TABLE OF tbtc_spoolid WITH HEADER LINE,
      v_fichero TYPE string,
*      i_buffer TYPE TABLE OF text255,
      i_buffer TYPE list_string_table,
      v_index TYPE i,
      v_csv.

*------PARAMETER/SELECT-OPTIONS EN PANTALLA----------------------------*
SELECTION-SCREEN BEGIN OF BLOCK zpse WITH FRAME TITLE text-001.
PARAMETERS: p_job LIKE tbtco-jobname OBLIGATORY.
SELECT-OPTIONS: s_fecha FOR tbtco-strtdate,
                s_hora  FOR tbtco-strttime.
SELECTION-SCREEN END OF BLOCK zpse.
SELECTION-SCREEN BEGIN OF BLOCK zps2 WITH FRAME TITLE text-002.
PARAMETERS: p_dir    TYPE string DEFAULT 'C:\',
            p_log    TYPE string DEFAULT 'LOG.TXT'.

SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (20) text-sp1.
SELECTION-SCREEN POSITION 33.
PARAMETERS: p_spool1 TYPE string DEFAULT 'SPOOL1.TXT'.
SELECTION-SCREEN COMMENT (3) text-csv FOR FIELD p_csv1.
PARAMETERS: p_csv1 AS CHECKBOX.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (20) text-sp2.
SELECTION-SCREEN POSITION 33.
PARAMETERS: p_spool2 TYPE string DEFAULT 'SPOOL2.TXT'.
SELECTION-SCREEN COMMENT (3) text-csv FOR FIELD p_csv2.
PARAMETERS: p_csv2 AS CHECKBOX.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (20) text-sp3.
SELECTION-SCREEN POSITION 33.
PARAMETERS: p_spool3 TYPE string DEFAULT 'SPOOL3.TXT'.
SELECTION-SCREEN COMMENT (3) text-csv FOR FIELD p_csv3.
PARAMETERS: p_csv3 AS CHECKBOX.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK zps2.


************************************************************************
*
*                  LOGICA DEL PROGRAMA
*
************************************************************************

*----------------------------------------------------------------------*
* INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

  CLEAR s_fecha.
  s_fecha-option = 'BT'.
  s_fecha-sign   = 'I'.
  s_fecha-low    = sy-datum - 15.
  s_fecha-high   = sy-datum.
  APPEND s_fecha.

*----------------------------------------------------------------------*
* START-OF-SELECTION.
*----------------------------------------------------------------------*
START-OF-SELECTION.

* Seleccionamos el ÚLTIMO job que cumpla las condiciones de selección
  SELECT * FROM tbtco
    UP TO 1 ROWS
   WHERE jobname = p_job
     AND strtdate IN s_fecha
     AND strttime IN s_hora
     AND status = 'F' "Status finalizado
   ORDER BY strtdate DESCENDING strttime DESCENDING.
  ENDSELECT.

  IF tbtco IS INITIAL.
    MESSAGE 'No se ha encontrado ejecución del job' TYPE 'E'.
  ENDIF.

  CREATE OBJECT o_job
    EXPORTING
      jobname = p_job.

  o_job->tbtco = tbtco.

* Grabamos el log del job en fichero
  IF NOT p_log IS INITIAL.
    i_log_job = o_job->get_log( ).

    IF NOT i_log_job IS INITIAL.
      v_fichero = zcl_ap_ficheros=>concat_ruta( directorio = p_dir
                                                fichero    = p_log ).
      zcl_ap_ficheros=>grabar( EXPORTING fichero = v_fichero
                               CHANGING  tabla   = i_log_job[] ).
    ENDIF.
  ENDIF.

* Verificamos si existen ordenes de spool
  SELECT * FROM tbtc_spoolid
    INTO TABLE i_spools
   WHERE jobname  = tbtco-jobname
     AND jobcount = tbtco-jobcount.
  LOOP AT i_spools.
    v_index = sy-tabix.

    CLEAR: v_fichero, v_csv.
    CASE v_index.
      WHEN 1.
        IF NOT p_spool1 IS INITIAL.
          v_fichero = zcl_ap_ficheros=>concat_ruta( directorio = p_dir
                                                    fichero    = p_spool1 ).
          v_csv = p_csv1.
        ENDIF.
      WHEN 2.
        IF NOT p_spool2 IS INITIAL.
          v_fichero = zcl_ap_ficheros=>concat_ruta( directorio = p_dir
                                                    fichero    = p_spool2 ).
          v_csv = p_csv2.
        ENDIF.
      WHEN 3.
        IF NOT p_spool3 IS INITIAL.
          v_fichero = zcl_ap_ficheros=>concat_ruta( directorio = p_dir
                                                    fichero    = p_spool3 ).
          v_csv = p_csv3.
        ENDIF.
    ENDCASE.
    IF NOT v_fichero IS INITIAL.
      CLEAR i_buffer.
      IF v_csv IS INITIAL.
        i_buffer = zcl_ap_spool=>get_txt_from_spool( i_spools-spoolid ).
      ELSE.
        i_buffer = zcl_ap_spool=>get_csv_from_spool( i_spools-spoolid ).
      ENDIF.
      IF NOT i_buffer IS INITIAL.
        zcl_ap_ficheros=>grabar( EXPORTING fichero = v_fichero
                                 CHANGING  tabla   = i_buffer[] ).
      ENDIF.
    ENDIF.
  ENDLOOP.
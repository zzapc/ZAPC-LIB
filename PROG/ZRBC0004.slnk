<?xml version="1.0" encoding="utf-8"?>
<PROG NAME="ZRBC0004" VARCL="X" SUBC="1" RMAND="100" RLOAD="S" FIXPT="X" UCCHECK="X">
 <textPool>
  <language SPRAS="E">
   <textElement ID="I" KEY="BTS" ENTRY="        Search texts without element" LENGTH="36 "/>
   <textElement ID="I" KEY="CLA" ENTRY="        Key" LENGTH="15 "/>
   <textElement ID="I" KEY="CTR" ENTRY="        Upload Translation" LENGTH="26 "/>
   <textElement ID="I" KEY="DCP" ENTRY="        Download proposals" LENGTH="26 "/>
   <textElement ID="I" KEY="GIN" ENTRY="        Generating report" LENGTH="25 "/>
   <textElement ID="I" KEY="GTR" ENTRY="Record Translation" LENGTH="18 "/>
   <textElement ID="I" KEY="IDD" ENTRY="        Target Language" LENGTH="23 "/>
   <textElement ID="I" KEY="IDO" ENTRY="        Source language" LENGTH="23 "/>
   <textElement ID="I" KEY="IMP" ENTRY="        Import Proposals" LENGTH="24 "/>
   <textElement ID="I" KEY="ISE" ENTRY="Data Item Selection Report" LENGTH="39 "/>
   <textElement ID="I" KEY="ISP" ENTRY="Program Selection Report" LENGTH="30 "/>
   <textElement ID="I" KEY="LOG" ENTRY="                Log" LENGTH="19 "/>
   <textElement ID="I" KEY="LTB" ENTRY="        The table" LENGTH="17 "/>
   <textElement ID="I" KEY="NET" ENTRY="        Table doesn&apos;t exists" LENGTH="28 "/>
   <textElement ID="I" KEY="NIC" ENTRY="has no language field in the key" LENGTH="36 "/>
   <textElement ID="I" KEY="PDA" ENTRY="                                Processing data" LENGTH="47 "/>
   <textElement ID="I" KEY="PNF" ENTRY="        This program cannot be run in background" LENGTH="86 "/>
   <textElement ID="I" KEY="PRO" ENTRY="Proposal" LENGTH="9 "/>
   <textElement ID="I" KEY="PTE" ENTRY="Propose Text Items" LENGTH="27 "/>
   <textElement ID="I" KEY="SDA" ENTRY="                                Selecting data" LENGTH="46 "/>
   <textElement ID="I" KEY="SEL" ENTRY="                                Selection criteria" LENGTH="50 "/>
   <textElement ID="I" KEY="TAB" ENTRY="        Table" LENGTH="13 "/>
   <textElement ID="I" KEY="TRA" ENTRY="Translate" LENGTH="9 "/>
   <textElement ID="I" KEY="TXD" ENTRY="        Target Text:" LENGTH="23 "/>
   <textElement ID="I" KEY="TXO" ENTRY="        Source text" LENGTH="19 "/>
   <textElement ID="R" ENTRY="                Translations" LENGTH="70 "/>
   <textElement ID="S" KEY="P_DTEL" ENTRY="                        Data Elements" LENGTH="38 "/>
   <textElement ID="S" KEY="P_FICHE" ENTRY="                File" LENGTH="38 "/>
   <textElement ID="S" KEY="P_LANGD" ENTRY="                Target language" LENGTH="38 "/>
   <textElement ID="S" KEY="P_LANGO" ENTRY="                Source language" LENGTH="38 "/>
   <textElement ID="S" KEY="P_PROG" ENTRY="                        Programs" LENGTH="38 "/>
   <textElement ID="S" KEY="P_VARI" ENTRY="        Layout" LENGTH="38 "/>
   <textElement ID="S" KEY="S_DEVCL" ENTRY="        Package" LENGTH="38 "/>
   <textElement ID="S" KEY="S_DTEL" ENTRY="        Data Elements" LENGTH="38 "/>
   <textElement ID="S" KEY="S_NAME" ENTRY="        Program" LENGTH="38 "/>
  </language>
  <language SPRAS="F">
   <textElement ID="I" KEY="EJE" ENTRY="        Exécuter" LENGTH="18 "/>
   <textElement ID="I" KEY="GIN" ENTRY="        Génération du rapport" LENGTH="29 "/>
   <textElement ID="I" KEY="LOG" ENTRY="        Journal" LENGTH="15 "/>
   <textElement ID="I" KEY="PDA" ENTRY="        Traitement des données" LENGTH="30 "/>
   <textElement ID="I" KEY="SDA" ENTRY="        Sélection des données" LENGTH="29 "/>
   <textElement ID="I" KEY="SEL" ENTRY="        Critères de sélection" LENGTH="29 "/>
   <textElement ID="R" ENTRY="        Traductions" LENGTH="70 "/>
   <textElement ID="S" KEY="P_DTEL" ENTRY="        Éléments de données" LENGTH="38 "/>
   <textElement ID="S" KEY="P_FICHE" ENTRY="        Fichier" LENGTH="38 "/>
   <textElement ID="S" KEY="P_LANGD" ENTRY="         Langue cible" LENGTH="38 "/>
   <textElement ID="S" KEY="P_LANGO" ENTRY="        Langue source" LENGTH="38 "/>
   <textElement ID="S" KEY="P_PROG" ENTRY="        Programmes" LENGTH="38 "/>
   <textElement ID="S" KEY="P_VARI" ENTRY="D       ." LENGTH="14 "/>
   <textElement ID="S" KEY="S_DTEL" ENTRY="D       ." LENGTH="9 "/>
   <textElement ID="S" KEY="S_NAME" ENTRY="D       ." LENGTH="9 "/>
  </language>
  <language SPRAS="I">
   <textElement ID="I" KEY="EJE" ENTRY="Execute" LENGTH="18 "/>
   <textElement ID="I" KEY="GIN" ENTRY="Generating ALV output" LENGTH="27 "/>
   <textElement ID="I" KEY="LOG" ENTRY="Log" LENGTH="13 "/>
   <textElement ID="I" KEY="PDA" ENTRY="Processing data" LENGTH="26 "/>
   <textElement ID="I" KEY="SDA" ENTRY="Selecting data" LENGTH="29 "/>
   <textElement ID="I" KEY="SEL" ENTRY="Selection criteria" LENGTH="22 "/>
   <textElement ID="R" ENTRY="ALV Template" LENGTH="70 "/>
   <textElement ID="S" KEY="P_VARI" ENTRY="D       ." LENGTH="14 "/>
   <textElement ID="S" KEY="S_VBELN" ENTRY="D       ." LENGTH="15 "/>
  </language>
  <language SPRAS="S">
   <textElement ID="I" KEY="CLA" ENTRY="Clave" LENGTH="15 "/>
   <textElement ID="I" KEY="LOG" ENTRY="Log" LENGTH="13 "/>
   <textElement ID="I" KEY="PDA" ENTRY="Procesando datos" LENGTH="26 "/>
   <textElement ID="I" KEY="PNF" ENTRY="Este programa no se puede ejecutar en fondo" LENGTH="86 "/>
   <textElement ID="I" KEY="SDA" ENTRY="Seleccionando datos" LENGTH="29 "/>
   <textElement ID="I" KEY="SEL" ENTRY="Criterios de selección" LENGTH="22 "/>
   <textElement ID="I" KEY="TXD" ENTRY="Texto Destino" LENGTH="23 "/>
   <textElement ID="R" ENTRY="Traducciones" LENGTH="12 "/>
   <textElement ID="S" KEY="P_DTEL" ENTRY="        Elementos de datos" LENGTH="26 "/>
   <textElement ID="S" KEY="P_FICHE" ENTRY="        Fichero" LENGTH="15 "/>
   <textElement ID="S" KEY="P_LANGD" ENTRY="        Idioma destino" LENGTH="22 "/>
   <textElement ID="S" KEY="P_LANGO" ENTRY="        Idioma origen" LENGTH="21 "/>
   <textElement ID="S" KEY="P_PROG" ENTRY="        Programas" LENGTH="17 "/>
   <textElement ID="S" KEY="P_VARI" ENTRY="D       Layout" LENGTH="16 "/>
   <textElement ID="S" KEY="S_DEVCL" ENTRY="D       Paquete" LENGTH="16 "/>
   <textElement ID="S" KEY="S_DTEL" ENTRY="D       Elemento datos" LENGTH="22 "/>
   <textElement ID="S" KEY="S_NAME" ENTRY="D       Programa" LENGTH="16 "/>
   <textElement ID="I" KEY="LTB" ENTRY="La tabla" LENGTH="8 "/>
   <textElement ID="I" KEY="NET" ENTRY="No existe tabla" LENGTH="15 "/>
   <textElement ID="I" KEY="CTR" ENTRY="Carga Traducción" LENGTH="16 "/>
   <textElement ID="I" KEY="TXO" ENTRY="Texto Origen" LENGTH="12 "/>
   <textElement ID="I" KEY="IDO" ENTRY="Idioma Origen" LENGTH="13 "/>
   <textElement ID="I" KEY="IDD" ENTRY="Idioma Destino" LENGTH="14 "/>
   <textElement ID="I" KEY="DCP" ENTRY="Descarga propuestas" LENGTH="19 "/>
   <textElement ID="I" KEY="IMP" ENTRY="Importar propuestas" LENGTH="19 "/>
   <textElement ID="I" KEY="BTS" ENTRY="Buscar textos sin elemento" LENGTH="26 "/>
   <textElement ID="I" KEY="TAB" ENTRY="Tabla" LENGTH="5 "/>
   <textElement ID="I" KEY="GIN" ENTRY="Generando informe" LENGTH="17 "/>
   <textElement ID="I" KEY="NIC" ENTRY="no tiene campo de idioma en la clave" LENGTH="36 "/>
   <textElement ID="I" KEY="GTR" ENTRY="Grabar traducción" LENGTH="17 "/>
   <textElement ID="I" KEY="PRO" ENTRY="Propuesta" LENGTH="9 "/>
   <textElement ID="I" KEY="TRA" ENTRY="Traducir" LENGTH="8 "/>
   <textElement ID="I" KEY="ISP" ENTRY="Informe selección de programas" LENGTH="30 "/>
   <textElement ID="I" KEY="ISE" ENTRY="Informe selección de elementos de datos" LENGTH="39 "/>
   <textElement ID="I" KEY="PTE" ENTRY="Proponer elementos de texto" LENGTH="27 "/>
  </language>
 </textPool>
 <dynpros>
  <dynpro PROG="ZRBC0004" DNUM="0100" FNUM="0100" BZMX="57 " BZBR="255 " MILI="192 " MICO="37 " MALI="0 " MACO="0 " NOLI="57 " NOCO="255 " VALP="0 " CUAN="G" SPRA="S" DTEXT="Grid">
   <dynprofield FNAM="GRID" DIDX="0039" FLG1="00" FLG2="30" FLG3="00" FILL="U" FMB1="30" FMB2="00" LENG="FF" LINE="01" COLN="02" LANF="00" LBLK="00" LREP="00" AUTH="101" AGLT="14" ADEZ="63"/>
   <dynprofield DIDX="0000" FLG1="80" FLG2="10" FLG3="00" FMB1="00" FMB2="00" LENG="14" LINE="FF" COLN="01" LTYP="O" LANF="00" LBLK="00" LREP="00" TYPE="CHAR" AGLT="00" ADEZ="00" STXT="____________________"/>
   <dynproflowsource>PROCESS BEFORE OUTPUT.
 MODULE STATUS_0100.
*
PROCESS AFTER INPUT.
 MODULE USER_COMMAND_0100 AT EXIT-COMMAND.

 MODULE USER_COMMAND_0100.</dynproflowsource>
  </dynpro>
 </dynpros>
 <source>&quot; ----------------------------------------------------------------------
&quot; TIPO : LISTADO
&quot; TITULO : Traducciones de programas
&quot;
&quot; AUTOR: Andrés Picazo                                FECHA: 11/11/2025
&quot;
&quot; ----------------------------------------------------------------------
REPORT zrbc0004.

&quot; ------TABLAS/ESTRUCTURAS-----------------------------------------------
TABLES: trdir, dd04l, tadir.

&quot; ------TABLAS INTERNAS--------------------------------------------------

CONSTANTS: c_textos TYPE string VALUE &apos;Textos&apos; ##NO_TEXT,
           c_tabla  TYPE string VALUE &apos;Tabla&apos; ##NO_TEXT.

CLASS lcl_event_grid DEFINITION INHERITING FROM zcl_ap_alv_grid_eventos FINAL.
  PUBLIC SECTION.
    METHODS data_changed REDEFINITION.
ENDCLASS.

TYPES: BEGIN OF t_carga,
         name          TYPE trdir-name,
         tipo          TYPE srmelemtyp,
         lango         TYPE char1,
         id            TYPE string,             &quot; textpoolid,
         key           TYPE string,             &quot; textpoolky,
         campo_texto   TYPE fieldname,
         entry         TYPE textpooltx,
         length        TYPE textpoolln,
         langd         TYPE sy-langu,
         sobreescribir TYPE abadrflagoverwrite,
         entry_d       TYPE textpool-entry,
       END OF t_carga.

DATA i_carga TYPE TABLE OF t_carga.

DATA ls_e071 TYPE e071.
DATA lt_e071 TYPE tr_objects.


&quot; -----------------------------------------------------------------------
&quot; CLASS zcl_report DEFINITION
&quot; -----------------------------------------------------------------------
&quot;
&quot; -----------------------------------------------------------------------
CLASS zcl_report DEFINITION INHERITING FROM zcl_ap_dev FINAL.
  PUBLIC SECTION.
    TYPES: BEGIN OF t_listado,
             check         TYPE xfeld,
             lights        TYPE zico_estado_mensaje,
             name          TYPE trdir-name,
             tipo          TYPE srmelemtyp,
             lango         TYPE sy-langu,
             id            TYPE string, &quot; textpoolid,
             key           TYPE string, &quot; textpoolky,
             campo_texto   TYPE fieldname,
             entry         TYPE textpooltx,
             length        TYPE textpoolln,
             langd         TYPE sy-langu,
             sobreescribir TYPE tabadrsf-overwrite,
             entry_d       TYPE textpool-entry,
             message       TYPE bapi_msg,
             tabix         TYPE sy-tabix,
           END OF t_listado,
           tt_listado TYPE STANDARD TABLE OF t_listado.
    TYPES: BEGIN OF t_conf_tabla,
             name         TYPE trdir-name,
             campo_idioma TYPE dd03l-fieldname,
             long_clave   TYPE int4,
             pos_idioma   TYPE int4,
           END OF t_conf_tabla.

    DATA i_listado     TYPE tt_listado.
    DATA i_listado_ini TYPE tt_listado.
    DATA i_conf_tabla  TYPE TABLE OF t_conf_tabla.
    DATA o_alv         TYPE REF TO zcl_ap_alv_grid.
    DATA o_event       TYPE REF TO lcl_event_grid.

    METHODS main.

    METHODS seleccionar_datos.

    METHODS grabar_textos
      IMPORTING !name TYPE progname
                !lang TYPE sy-langu.

    METHODS grabar_cuad
      IMPORTING !name TYPE progname
                !lang TYPE sy-langu.

    METHODS grabar_dynpro
      IMPORTING !name TYPE progname
                !lang TYPE syst_langu
                !id   TYPE string.

    METHODS grabar_dtel
      IMPORTING !name TYPE progname
                !lang TYPE sy-langu.

    METHODS grabar_propuesta
      IMPORTING lango TYPE char1
                texto TYPE textpooltx
                langd TYPE syst_langu
                textd TYPE textpooltx.

    METHODS get_traduccion
      IMPORTING lango             TYPE char1
                langd             TYPE syst_langu
                texto             TYPE textpooltx
      RETURNING VALUE(traduccion) TYPE string.

    METHODS get_idioma_2 IMPORTING !lang           TYPE sy-langu
                         RETURNING VALUE(idioma_2) TYPE char2.

    METHODS status_dynpro_0100.
    METHODS command_dynpro_0100.

  PRIVATE SECTION.
    METHODS get_tabla
      IMPORTING tabla       TYPE string
                campo_clave TYPE string
                campo_texto TYPE string
                !name       TYPE progname.

    METHODS grabar_registro
      IMPORTING tabla       TYPE string
                clave       TYPE string
                idioma      TYPE any
                campo_texto TYPE fieldname
                texto       TYPE textpooltx
      RETURNING VALUE(ok)   TYPE abap_bool.

    METHODS propuesta.

ENDCLASS.

&quot; ------VARIABLES--------------------------------------------------------
DATA o_prog TYPE REF TO zcl_report.


&quot; ------PARAMETER/SELECT-OPTIONS EN PANTALLA-----------------------------
SELECTION-SCREEN BEGIN OF BLOCK b01 WITH FRAME TITLE TEXT-sel.
  PARAMETERS p_prog RADIOBUTTON GROUP g DEFAULT &apos;X&apos; USER-COMMAND g.
  SELECT-OPTIONS: s_name FOR trdir-name,
                  s_devcl FOR tadir-devclass.
  PARAMETERS p_dtel RADIOBUTTON GROUP g.
  SELECT-OPTIONS s_dtel FOR dd04l-rollname.
  SELECTION-SCREEN SKIP 1.
  PARAMETERS: p_lango TYPE sy-langu DEFAULT sy-langu OBLIGATORY,
              p_langd TYPE sy-langu OBLIGATORY.
  SELECTION-SCREEN SKIP 1.
  PARAMETERS p_fiche TYPE text255.
  SELECTION-SCREEN SKIP 1.
  PARAMETERS p_vari LIKE disvariant-variant.
SELECTION-SCREEN END OF BLOCK b01.
__botones_plantilla.


&quot; -----------------------------------------------------------------------
&quot;
&quot; LOGICA DEL PROGRAMA
&quot;
&quot; -----------------------------------------------------------------------
CLASS lcl_event_grid IMPLEMENTATION.
  METHOD data_changed.
    ini_data_changed( cambios = er_data_changed-&gt;mt_good_cells ).
    LOOP AT i_cambios_celda INTO cambio_celda.
      AT NEW row_id.
        READ TABLE o_prog-&gt;i_listado INTO DATA(l_listado_ini) INDEX cambio_celda-row_id. &quot;#EC CI_SUBRC
        DATA(l_listado) = l_listado_ini.
      ENDAT.

      set_valor_mod( CHANGING datos = l_listado ).

      AT END OF row_id.
        IF l_listado-entry_d &lt;&gt; &apos;&apos;.
          IF l_listado-entry_d = o_prog-&gt;i_listado_ini[ tabix = l_listado-tabix ]-entry_d.
            l_listado-lights = icon_equal_green.
          ELSE.
            l_listado-lights = icon_change.
          ENDIF.

          MODIFY o_prog-&gt;i_listado FROM l_listado INDEX cambio_celda-row_id.
          actualizar_fila( fila_ini        = l_listado_ini
                           fila_fin        = l_listado
                           er_data_changed = er_data_changed
                           fila            = cambio_celda-row_id ).
        ENDIF.
      ENDAT.
    ENDLOOP.
  ENDMETHOD.
ENDCLASS.


&quot; -----------------------------------------------------------------------
&quot; CLASS zcl_report IMPLEMENTATION
&quot; -----------------------------------------------------------------------
&quot;
&quot; -----------------------------------------------------------------------
CLASS zcl_report IMPLEMENTATION.
  METHOD main.
    seleccionar_datos( ).
    IF sy-batch IS INITIAL.
      CALL SCREEN 0100.
    ELSE.
      MESSAGE &apos;Este programa no se puede ejecutar en fondo&apos;(pnf) TYPE &apos;E&apos;.
    ENDIF.
  ENDMETHOD.                    &quot; REPORT

  METHOD seleccionar_datos.
    DATA text_o TYPE TABLE OF textpool.
    DATA text_d TYPE TABLE OF textpool.

    IF p_prog = &apos;X&apos;.
      sgpi_texto( &apos;Seleccionando datos&apos;(sda) ).
      SELECT DISTINCT name
        FROM trdir
               JOIN
                 tadir ON trdir~name = tadir~obj_name  &quot;#EC CI_BUFFJOIN
        INTO TABLE @DATA(i_prog)
        WHERE name     IN @s_name
          AND devclass IN @s_devcl.

      SELECT obj_name AS name FROM tadir                &quot;#EC CI_GENBUFF
        INTO TABLE @DATA(i_prog_class)
        WHERE obj_name IN @s_name
          AND pgmid     = &apos;R3TR&apos;
          AND object    = &apos;CLAS&apos;
          AND devclass IN @s_devcl.
      LOOP AT i_prog_class ASSIGNING FIELD-SYMBOL(&lt;cl&gt;).
        DATA(l_cl) = |{ &lt;cl&gt;-name }%CP|.
        SELECT name FROM trdir
          INTO @DATA(l_name)
          UP TO 1 ROWS
          WHERE name LIKE @l_cl
          ORDER BY PRIMARY KEY.
        ENDSELECT.
        IF sy-subrc = 0.
          APPEND l_name TO i_prog.                      &quot;#EC CI_CONV_OK
        ENDIF.
      ENDLOOP.

      SORT i_prog BY name.
      DELETE ADJACENT DUPLICATES FROM i_prog COMPARING ALL FIELDS.

      o_prog-&gt;o_sgpi-&gt;get_filas_tabla( i_prog[] ).
      LOOP AT i_prog ASSIGNING FIELD-SYMBOL(&lt;prog&gt;).
        sgpi_texto( texto1    = &apos;Procesando datos&apos;(pda)
                    cant_porc = 100 ).

        READ TEXTPOOL &lt;prog&gt;-name INTO text_o LANGUAGE p_lango.
        READ TEXTPOOL &lt;prog&gt;-name INTO text_d LANGUAGE p_langd.
        LOOP AT text_o ASSIGNING FIELD-SYMBOL(&lt;to&gt;) WHERE entry &lt;&gt; &apos;D       .&apos;.
          APPEND INITIAL LINE TO i_listado ASSIGNING FIELD-SYMBOL(&lt;listado&gt;).
          &lt;listado&gt;-name = &lt;prog&gt;-name.
          &lt;listado&gt;-tipo = c_textos.
          MOVE-CORRESPONDING &lt;to&gt; TO &lt;listado&gt;.
          &lt;listado&gt;-lango = p_lango.
          &lt;listado&gt;-langd = p_langd.
          ASSIGN text_d[ key = &lt;to&gt;-key
                         id  = &lt;to&gt;-id ] TO FIELD-SYMBOL(&lt;td&gt;).
          IF sy-subrc = 0.
            IF &lt;td&gt;-entry &lt;&gt; &apos;ALV Template&apos;.
              &lt;listado&gt;-entry_d = &lt;td&gt;-entry.
              &lt;listado&gt;-lights  = icon_yellow_light.
            ENDIF.
          ENDIF.

          IF &lt;listado&gt;-id = &apos;S&apos;.
            &lt;listado&gt;-entry   = &lt;listado&gt;-entry+8.
            &lt;listado&gt;-entry_d = &lt;listado&gt;-entry_d+8.
          ENDIF.
        ENDLOOP.

        get_tabla( tabla       = &apos;RSMPTEXTS&apos;
                   campo_clave = &apos;PROGNAME&apos;
                   campo_texto = &apos;TEXT&apos;
                   name        = &lt;prog&gt;-name ).

        get_tabla( tabla       = &apos;D021T&apos;
                   campo_clave = &apos;PROG&apos;
                   campo_texto = &apos;DTXT&apos;
                   name        = &lt;prog&gt;-name ).

      ENDLOOP.

      DELETE i_listado WHERE tipo = c_tabla AND id = &apos;D021T-1000&apos; AND key CS &apos;%&apos;.
    ELSEIF p_dtel = &apos;X&apos;.
      SELECT rollname
        FROM dd04l
               JOIN
                 tadir ON  dd04l~rollname = tadir~obj_name &quot;#EC CI_BUFFJOIN
                       AND tadir~object   = &apos;DTEL&apos;
        INTO TABLE @DATA(i_dtel)
        WHERE rollname IN @s_dtel
          AND devclass IN @s_devcl.

      o_prog-&gt;o_sgpi-&gt;get_filas_tabla( i_dtel[] ).
      LOOP AT i_dtel ASSIGNING FIELD-SYMBOL(&lt;dtel&gt;).
        sgpi_texto( texto1    = &apos;Procesando datos&apos;(pda)
                    cant_porc = 100 ).

        get_tabla( tabla       = &apos;DD04T&apos;
                   campo_clave = &apos;ROLLNAME&apos;
                   campo_texto = &apos;DDTEXT&apos;
                   name        = CONV #( &lt;dtel&gt;-rollname ) ).

        get_tabla( tabla       = &apos;DD04T&apos;
                   campo_clave = &apos;ROLLNAME&apos;
                   campo_texto = &apos;REPTEXT&apos;
                   name        = CONV #( &lt;dtel&gt;-rollname ) ).

        get_tabla( tabla       = &apos;DD04T&apos;
                   campo_clave = &apos;ROLLNAME&apos;
                   campo_texto = &apos;SCRTEXT_M&apos;
                   name        = CONV #( &lt;dtel&gt;-rollname ) ).

        get_tabla( tabla       = &apos;DD04T&apos;
                   campo_clave = &apos;ROLLNAME&apos;
                   campo_texto = &apos;SCRTEXT_L&apos;
                   name        = CONV #( &lt;dtel&gt;-rollname ) ).

        get_tabla( tabla       = &apos;DD04T&apos;
                   campo_clave = &apos;ROLLNAME&apos;
                   campo_texto = &apos;SCRTEXT_S&apos;
                   name        = CONV #( &lt;dtel&gt;-rollname ) ).

      ENDLOOP.
    ENDIF.

    SORT i_listado BY name
                      lango
                      id
                      key.

    LOOP AT i_listado ASSIGNING &lt;listado&gt;.
      &lt;listado&gt;-tabix = sy-tabix.
    ENDLOOP.
    i_listado_ini = i_listado.
  ENDMETHOD.

  METHOD grabar_textos.
    DATA: text      TYPE TABLE OF textpool,
          text_orig TYPE TABLE OF textpool.

    READ TEXTPOOL name INTO text LANGUAGE lang.
    READ TEXTPOOL name INTO text_orig LANGUAGE p_lango.

    LOOP AT i_listado ASSIGNING FIELD-SYMBOL(&lt;listado&gt;)
         WHERE name = name AND tipo = c_textos AND langd = lang AND lights = icon_change AND entry_d IS NOT INITIAL.
      ASSIGN text[ id  = &lt;listado&gt;-id
                   key = &lt;listado&gt;-key ] TO FIELD-SYMBOL(&lt;t&gt;).
      IF sy-subrc = 0.
        &lt;t&gt;-entry = &lt;listado&gt;-entry_d.
      ELSE.
        APPEND INITIAL LINE TO text ASSIGNING &lt;t&gt;.
        MOVE-CORRESPONDING &lt;listado&gt; TO &lt;t&gt;.
        &lt;t&gt;-entry = &lt;listado&gt;-entry_d.
      ENDIF.

      IF &lt;listado&gt;-entry &lt;&gt; &apos;&apos;.
        ASSIGN text_orig[ id  = &lt;listado&gt;-id
                          key = &lt;listado&gt;-key ] TO FIELD-SYMBOL(&lt;to&gt;).
        IF sy-subrc &lt;&gt; 0.
          APPEND INITIAL LINE TO text_orig ASSIGNING &lt;to&gt;.
          MOVE-CORRESPONDING &lt;listado&gt; TO &lt;to&gt;.
          DATA(l_new) = abap_true.
        ENDIF.
      ENDIF.

      &lt;listado&gt;-lights = icon_green_light.
    ENDLOOP.
    IF sy-subrc = 0.

      LOOP AT text ASSIGNING &lt;t&gt;.
        IF &lt;listado&gt;-id = &apos;S&apos;.
          &lt;t&gt;-entry+8 = &lt;t&gt;-entry.
          CLEAR &lt;t&gt;-entry(8).
        ENDIF.

        IF strlen( &lt;t&gt;-entry ) &gt; &lt;t&gt;-length.
          &lt;t&gt;-length = strlen( &lt;t&gt;-entry ).
        ENDIF.

      ENDLOOP.

      INSERT TEXTPOOL name FROM text LANGUAGE lang.
      IF sy-subrc &lt;&gt; 0.
        MESSAGE ID sy-msgid TYPE &apos;E&apos; NUMBER sy-msgno
                WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      ELSE.
        IF l_new = abap_true.
          INSERT TEXTPOOL name FROM text_orig LANGUAGE p_lango.
        ENDIF.

        ls_e071-as4pos   = 1.
        ls_e071-pgmid    = &apos;LIMU&apos;.
        ls_e071-object   = &apos;REPT&apos;.
        ls_e071-obj_name = name.
        ls_e071-lang     = sy-langu.
        INSERT ls_e071 INTO TABLE lt_e071.
      ENDIF.
    ENDIF.

    &quot; Grabamos la traducción estándar para que SAP refresque
    DATA(o_bi) = NEW zcl_ap_batch_input( ).

    o_bi-&gt;inicio( ).

    o_bi-&gt;dynpro( program = &apos;SAPLWBABAP&apos;
                  dynpro  = &apos;0100&apos;
                  okcode  = &apos;=TRAN&apos; ).
    o_bi-&gt;campos( campo = &apos;RS38M-PROGRAMM&apos;
                  valor = name ). &quot; Nombre de programa ABAP
    o_bi-&gt;campos( campo = &apos;RS38M-FUNC_EDIT&apos;
                  valor = &apos;&apos; ). &quot; Editor
    o_bi-&gt;campos( campo = &apos;RS38M-FUNC_TEXT&apos;
                  valor = &apos;X&apos; ). &quot; Elementos de texto

    &quot; Idioma objetivo para la traducción
    o_bi-&gt;dynpro( program = &apos;SAPLSETX&apos;
                  dynpro  = &apos;0200&apos;
                  okcode  = &apos;=NEXT&apos; ).
    WRITE p_lango TO aux1.
    WRITE p_langd TO aux2.
    o_bi-&gt;campos( campo = &apos;RSETX-MASTERLANG&apos;
                  valor = aux1 ). &quot; Clave de idioma
    o_bi-&gt;campos( campo = &apos;RSETX-TARG_LANGU&apos;
                  valor = aux2 ). &quot; Clave de idioma

    o_bi-&gt;dynpro( program = &apos;SAPMSSY0&apos;
                  dynpro  = &apos;0120&apos;
                  okcode  = &apos;=SAVE&apos; ).

    o_bi-&gt;llamar_transaccion( tcode = &apos;SE38&apos;
                              modo  = &apos;N&apos; ).
  ENDMETHOD.

  METHOD grabar_cuad.
    LOOP AT i_listado ASSIGNING FIELD-SYMBOL(&lt;listado&gt;)
         WHERE name = name AND tipo = c_tabla AND id = &apos;RSMPTEXTS&apos; AND langd = lang AND lights = icon_change AND entry_d IS NOT INITIAL.

      IF grabar_registro( tabla       = &apos;RSMPTEXTS&apos;
                          clave       = &lt;listado&gt;-key
                          idioma      = p_langd
                          campo_texto = &apos;TEXT&apos;
                          texto       = &lt;listado&gt;-entry_d ) = abap_true.
        &lt;listado&gt;-lights = icon_green_light.
        DATA(ok) = abap_true.
      ELSE.
        &lt;listado&gt;-lights = icon_red_light.
      ENDIF.
    ENDLOOP.
    IF ok = abap_false.
      RETURN.
    ENDIF.

    ls_e071-as4pos   = 1.
    ls_e071-pgmid    = &apos;LIMU&apos;.
    ls_e071-object   = &apos;CUAD&apos;.
    ls_e071-obj_name = name.
    ls_e071-lang     = sy-langu.
    INSERT ls_e071 INTO TABLE lt_e071.

    &quot; Grabamos la traducción estándar para que SAP refresque
    DATA(o_bi) = NEW zcl_ap_batch_input( ).

    o_bi-&gt;inicio( ).

    o_bi-&gt;dynpro( program = &apos;SAPMSMPE&apos;
                  dynpro  = &apos;0100&apos;
                  okcode  = &apos;=TRAN&apos; ).
    o_bi-&gt;campos( campo = &apos;RSMPE-PROGRAM&apos;
                  valor = name ). &quot; Nombre de programa ABAP

    &quot; Idioma objetivo para la traducción
    o_bi-&gt;dynpro( program = &apos;SAPLSETX&apos;
                  dynpro  = &apos;0200&apos;
                  okcode  = &apos;=NEXT&apos; ).
    WRITE p_lango TO aux1.
    WRITE p_langd TO aux2.
    o_bi-&gt;campos( campo = &apos;RSETX-MASTERLANG&apos;
                  valor = aux1 ). &quot; Clave de idioma
    o_bi-&gt;campos( campo = &apos;RSETX-TARG_LANGU&apos;
                  valor = aux2 ). &quot; Clave de idioma

    o_bi-&gt;dynpro( program = &apos;SAPMSSY0&apos;
                  dynpro  = &apos;0120&apos;
                  okcode  = &apos;=SEQU&apos; ).

    o_bi-&gt;dynpro( program = &apos;SAPMSSY0&apos;
                  dynpro  = &apos;0120&apos;
                  okcode  = &apos;=SAVE&apos; ).

    o_bi-&gt;llamar_transaccion( tcode = &apos;SE41&apos;
                              modo  = &apos;N&apos; ).
  ENDMETHOD.

  METHOD grabar_dtel.
    LOOP AT i_listado ASSIGNING FIELD-SYMBOL(&lt;listado&gt;)
         WHERE name = name AND tipo = c_tabla AND id = &apos;DD04T&apos; AND langd = lang AND lights = icon_change AND entry_d IS NOT INITIAL.

      IF grabar_registro( tabla       = &apos;DD04T&apos;
                          clave       = &lt;listado&gt;-key
                          idioma      = p_langd
                          campo_texto = &lt;listado&gt;-campo_texto
                          texto       = &lt;listado&gt;-entry_d ) = abap_true.
        &lt;listado&gt;-lights = icon_green_light.
        DATA(ok) = abap_true.
      ELSE.
        &lt;listado&gt;-lights = icon_red_light.
      ENDIF.
    ENDLOOP.
    IF ok = abap_false.
      RETURN.
    ENDIF.

    ls_e071-as4pos   = 1.
    ls_e071-pgmid    = &apos;LIMU&apos;.
    ls_e071-object   = &apos;DTED&apos;.
    ls_e071-obj_name = name.
    ls_e071-lang     = sy-langu.
    INSERT ls_e071 INTO TABLE lt_e071.

    &quot; Grabamos la traducción estándar para que SAP refresque
    DATA(o_bi) = NEW zcl_ap_batch_input( ).

    o_bi-&gt;inicio( ).

    o_bi-&gt;dynpro( program = &apos;SAPLSD_ENTRY&apos;
                  dynpro  = &apos;1000&apos;
                  okcode  = &apos;=CHANGE_RADIO&apos; ).
    o_bi-&gt;campos( campo = &apos;RSRD1-TBMA&apos;
                  valor = &apos;&apos; ).
    o_bi-&gt;campos( campo = &apos;RSRD1-DDTYPE&apos;
                  valor = &apos;X&apos; ).

    o_bi-&gt;dynpro( program = &apos;SAPLSD_ENTRY&apos;
                  dynpro  = &apos;1000&apos;
                  okcode  = &apos;=WB_TRANSLATION&apos; ).
    o_bi-&gt;campos( campo = &apos;RSRD1-DDTYPE_VAL&apos;
                  valor = name ).

    &quot; Idioma objetivo para la traducción
    o_bi-&gt;dynpro( program = &apos;SAPLSETX&apos;
                  dynpro  = &apos;0200&apos;
                  okcode  = &apos;=NEXT&apos; ).
    WRITE p_lango TO aux1.
    WRITE p_langd TO aux2.
    o_bi-&gt;campos( campo = &apos;RSETX-MASTERLANG&apos;
                  valor = aux1 ). &quot; Clave de idioma
    o_bi-&gt;campos( campo = &apos;RSETX-TARG_LANGU&apos;
                  valor = aux2 ). &quot; Clave de idioma
    o_bi-&gt;dynpro( program = &apos;SAPMSSY0&apos;
                  dynpro  = &apos;0120&apos;
                  okcode  = &apos;=SAVE&apos; ).

    o_bi-&gt;llamar_transaccion( tcode = &apos;SE11&apos;
                              modo  = &apos;N&apos; ).
  ENDMETHOD.

  METHOD get_tabla.
    DATA lr_struct_descr TYPE REF TO cl_abap_structdescr.
    DATA lr_table_descr  TYPE REF TO cl_abap_tabledescr.
    DATA lr_data         TYPE REF TO data.
    DATA where           TYPE string.

    FIELD-SYMBOLS &lt;tabla&gt; TYPE ANY TABLE.

    SELECT fieldname, keyflag, leng, position, languflag
      FROM dd03l
      INTO TABLE @DATA(i_campos_tabla)
      WHERE tabname = @tabla
      ORDER BY position.
    IF sy-subrc &lt;&gt; 0.
      MESSAGE |{ &apos;No existe tabla&apos;(net) } { tabla }| TYPE &apos;E&apos;.
    ENDIF.

    APPEND INITIAL LINE TO i_conf_tabla ASSIGNING FIELD-SYMBOL(&lt;conf&gt;).
    &lt;conf&gt;-name = tabla.
    LOOP AT i_campos_tabla ASSIGNING FIELD-SYMBOL(&lt;campo&gt;) WHERE keyflag = &apos;X&apos;.
      IF &lt;campo&gt;-languflag = &apos;X&apos;.
        &lt;conf&gt;-campo_idioma = &lt;campo&gt;-fieldname.
        &lt;conf&gt;-pos_idioma   = &lt;conf&gt;-long_clave.
      ENDIF.
      &lt;conf&gt;-long_clave = &lt;conf&gt;-long_clave + &lt;campo&gt;-leng. &quot;#EC CI_CONV_OK
    ENDLOOP.

    IF &lt;conf&gt;-campo_idioma IS INITIAL.
      MESSAGE |{ &apos;La tabla&apos;(ltb) } { tabla } { &apos;no tiene campo de idioma en la clave&apos;(nic) }| TYPE &apos;E&apos;.
    ENDIF.

    TRY.
        lr_struct_descr ?= cl_abap_typedescr=&gt;describe_by_name( tabla ).
        lr_table_descr = cl_abap_tabledescr=&gt;create( p_line_type = lr_struct_descr ).
        CREATE DATA lr_data TYPE HANDLE lr_table_descr.
        ASSIGN lr_data-&gt;* TO &lt;tabla&gt;.
      CATCH cx_root INTO DATA(lx_type) ##CATCH_ALL.
        MESSAGE lx_type-&gt;get_text( ) TYPE &apos;E&apos;.
    ENDTRY.

    where = |{ &lt;conf&gt;-campo_idioma } = &apos;{ p_lango }&apos;|.
    IF campo_clave IS NOT INITIAL.
      where = |{ where } AND { campo_clave } = &apos;{ name }&apos;|.
    ENDIF.
    SELECT * FROM (tabla) INTO TABLE @&lt;tabla&gt; WHERE (where).
    LOOP AT &lt;tabla&gt; ASSIGNING FIELD-SYMBOL(&lt;wa&gt;).
      APPEND INITIAL LINE TO i_listado ASSIGNING FIELD-SYMBOL(&lt;listado&gt;).
      &lt;listado&gt;-name        = name.
      &lt;listado&gt;-lango       = p_lango.
      &lt;listado&gt;-langd       = p_langd.
      &lt;listado&gt;-tipo        = c_tabla.
      &lt;listado&gt;-campo_texto = campo_texto.
      &lt;listado&gt;-id          = tabla.
      IF tabla = &apos;D021T&apos;.
        ASSIGN COMPONENT &apos;DYNR&apos; OF STRUCTURE &lt;wa&gt; TO FIELD-SYMBOL(&lt;dynr&gt;).
        IF sy-subrc = 0.
          &lt;listado&gt;-id = |{ tabla }-{ &lt;dynr&gt; }|.
        ENDIF.
      ENDIF.
      &lt;listado&gt;-key = &lt;wa&gt;(&lt;conf&gt;-long_clave).
      ASSIGN COMPONENT campo_texto OF STRUCTURE &lt;wa&gt; TO FIELD-SYMBOL(&lt;texto&gt;).
      IF sy-subrc = 0.
        &lt;listado&gt;-entry = &lt;texto&gt;.
      ENDIF.

      CLEAR where.
      LOOP AT i_campos_tabla ASSIGNING &lt;campo&gt; WHERE keyflag = &apos;X&apos;.
        ASSIGN COMPONENT &lt;campo&gt;-fieldname OF STRUCTURE &lt;wa&gt; TO FIELD-SYMBOL(&lt;clave&gt;).
        IF &lt;campo&gt;-fieldname = &lt;conf&gt;-campo_idioma.
          ASSIGN p_langd TO &lt;clave&gt;.
        ENDIF.
        IF where IS INITIAL.
          where = |{ &lt;campo&gt;-fieldname } = &apos;{ &lt;clave&gt; }&apos;|.
        ELSE.
          where = |{ where } and { &lt;campo&gt;-fieldname } = &apos;{ &lt;clave&gt; }&apos;|.
        ENDIF.
      ENDLOOP.

      SELECT SINGLE (campo_texto) FROM (tabla) INTO @&lt;listado&gt;-entry_d WHERE (where).
      IF &lt;listado&gt;-entry_d IS NOT INITIAL.
        &lt;listado&gt;-lights = icon_yellow_light.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.

  METHOD grabar_registro.
    DATA lr_row     TYPE REF TO data.
    DATA where      TYPE string.
    DATA where_dest TYPE string.

    FIELD-SYMBOLS &lt;row&gt; TYPE any.

    CLEAR ok.

    ASSIGN i_conf_tabla[ name = tabla ] TO FIELD-SYMBOL(&lt;conf&gt;).

    TRY.
        cl_abap_typedescr=&gt;describe_by_name( tabla ).

        CREATE DATA lr_row TYPE (tabla).
        ASSIGN lr_row-&gt;* TO &lt;row&gt;.

        &lt;row&gt; = clave.

        SELECT fieldname, keyflag, leng, position, languflag
          FROM dd03l
          INTO TABLE @DATA(i_campos_tabla)
          WHERE tabname = @tabla
          ORDER BY position.
        IF sy-subrc &lt;&gt; 0.
          MESSAGE |{ &apos;No existe tabla&apos;(net) } { tabla }| TYPE &apos;E&apos;.
        ENDIF.

        CLEAR: where,
               where_dest.
        LOOP AT i_campos_tabla ASSIGNING FIELD-SYMBOL(&lt;campo&gt;) WHERE keyflag = &apos;X&apos;.
          ASSIGN COMPONENT &lt;campo&gt;-fieldname OF STRUCTURE &lt;row&gt; TO FIELD-SYMBOL(&lt;clave&gt;).
          IF where IS INITIAL.
            where = |{ &lt;campo&gt;-fieldname } = &apos;{ &lt;clave&gt; }&apos;|.
          ELSE.
            where = |{ where } and { &lt;campo&gt;-fieldname } = &apos;{ &lt;clave&gt; }&apos;|.
          ENDIF.

          IF &lt;campo&gt;-fieldname = &lt;conf&gt;-campo_idioma.
            ASSIGN idioma TO &lt;clave&gt;.
          ENDIF.
          IF where_dest IS INITIAL.
            where_dest = |{ &lt;campo&gt;-fieldname } = &apos;{ &lt;clave&gt; }&apos;|.
          ELSE.
            where_dest = |{ where_dest } and { &lt;campo&gt;-fieldname } = &apos;{ &lt;clave&gt; }&apos;|.
          ENDIF.
        ENDLOOP.

        &quot; Si existe en idioma destino, modificamos
        SELECT SINGLE * FROM (tabla) INTO &lt;row&gt; WHERE (where_dest).
        IF sy-subrc = 0.
          DATA(l_mod) = abap_true.
        ELSE.
          &quot; Si no, copiamos del origen
          SELECT SINGLE * FROM (tabla) INTO &lt;row&gt; WHERE (where).
        ENDIF.
        IF &lt;row&gt; IS NOT INITIAL.
          ASSIGN COMPONENT &lt;conf&gt;-campo_idioma OF STRUCTURE &lt;row&gt; TO FIELD-SYMBOL(&lt;idioma&gt;).
          &lt;idioma&gt; = idioma.
          ASSIGN COMPONENT campo_texto OF STRUCTURE &lt;row&gt; TO FIELD-SYMBOL(&lt;texto&gt;).
          IF &lt;texto&gt; &lt;&gt; texto OR l_mod = abap_false.
            &lt;texto&gt; = texto.

            MODIFY (tabla) FROM &lt;row&gt;.
            IF sy-subrc = 0.
              ok = abap_true.
            ENDIF.
          ENDIF.
        ENDIF.
      CATCH cx_root INTO DATA(lx_root) ##CATCH_ALL.
        MESSAGE lx_root-&gt;get_text( ) TYPE &apos;E&apos;.
    ENDTRY.
  ENDMETHOD.

  METHOD grabar_propuesta.
    DATA ztemps TYPE ztemps.

    ztemps-clave    = &apos;TRAD&apos;.
    ztemps-subclave = |{ lango }-{ langd }-{ texto }|.
    ztemps-valor    = |{ lango }-{ langd }|.
    ztemps-texto    = texto.
    ztemps-string   = textd.

    SELECT string FROM ztemps
      INTO @DATA(texto_bd)
      UP TO 1 ROWS
      WHERE clave = &apos;TRAD&apos; AND valor = @ztemps-valor AND subclave = @ztemps-subclave
      ORDER BY PRIMARY KEY.
    ENDSELECT.
    IF sy-subrc &lt;&gt; 0 OR texto_bd &lt;&gt; textd.
      ztemps-permanente = &apos;X&apos;.
      ztemps-erdat      = sy-datum.
      ztemps-erzet      = sy-uzeit.
      ztemps-ernam      = sy-uname.
      MODIFY ztemps FROM ztemps.
    ENDIF.
  ENDMETHOD.

  METHOD status_dynpro_0100.
    status_dynpro( EXPORTING cprog     = &apos;ZAP_STATUS&apos;
                             status    = &apos;ST_DYN&apos;
                   CHANGING  i_listado = i_listado ).
    IF inicio IS NOT INITIAL.
      RETURN.
    ENDIF.

    inicio = &apos;X&apos;.
    o_alv-&gt;add_button( button = &apos;F01&apos;
                       text   = &apos;Excel&apos;
                       icon   = icon_xls
                       ucomm  = &apos;EXCELC&apos; ) ##NO_TEXT.
    o_alv-&gt;add_button( button = &apos;F02&apos;
                       text   = &apos;Carga Traducción&apos;(ctr)
                       icon   = icon_import
                       ucomm  = &apos;CARGAR&apos; ).
    o_alv-&gt;add_button( button = &apos;F03&apos;
                       text   = &apos;Grabar traducción&apos;(gtr)
                       icon   = icon_system_save
                       ucomm  = &apos;GRABAR&apos; ).
    o_alv-&gt;add_button( button = &apos;F04&apos;
                       text   = &apos;Propuesta&apos;(pro)
                       icon   = icon_execute_object
                       ucomm  = &apos;PROP&apos; ).
    o_alv-&gt;add_button( button = &apos;F05&apos;
                       text   = &apos;Traducir&apos;(tra)
                       icon   = icon_url
                       ucomm  = &apos;TRADUCIR&apos; ).
    o_alv-&gt;add_button( button = &apos;M01&apos;
                       text   = &apos;Descarga propuestas&apos;(dcp)
                       ucomm  = &apos;DESC_PROP&apos; ).
    o_alv-&gt;add_button( button = &apos;M02&apos;
                       text   = &apos;Importar propuestas&apos;(imp)
                       ucomm  = &apos;SUBIR_PROP&apos; ).

    o_alv-&gt;add_button( button = &apos;M03&apos;
                       text   = &apos;Buscar textos sin elemento&apos;(bts)
                       ucomm  = &apos;TEXT_ELM&apos; ).
    o_alv-&gt;add_button( button = &apos;M04&apos;
                       text   = &apos;Proponer elementos de texto&apos;(pte)
                       ucomm  = &apos;PROP_TEXT_ELM&apos; ).
    o_alv-&gt;add_button( button = &apos;M05&apos;
                       text   = &apos;Buscar mensajes&apos;(msg)
                       ucomm  = &apos;MESSAGES&apos; ).

    o_alv-&gt;variant-variant = p_vari.
    o_alv-&gt;registrar_mod( ).
    o_alv-&gt;set_layout( no_rowmove = &apos;X&apos;
                       no_rowins  = &apos;X&apos; ).
    o_alv-&gt;quitar_opciones( cl_gui_alv_grid=&gt;mc_fc_refresh ).
    o_alv-&gt;set_campos_tabint( i_listado[] ).
    o_alv-&gt;set_field_quitar( &apos;CHECK,TABIX&apos; ).
    o_alv-&gt;set_field( campo = &apos;SOBREESCRIBIR&apos;
                      op    = &apos;CHECKBOX&apos; ).
    o_alv-&gt;set_field_input( &apos;SOBREESCRIBIR,ENTRY_D&apos; ).
    o_alv-&gt;set_orden( &apos;NAME,TIPO,LANGO,ID,KEY&apos; ).
    o_alv-&gt;set_field_text( campo = &apos;KEY&apos;
                           valor = &apos;Clave&apos;(cla) ).
    o_alv-&gt;set_field_text( campo = &apos;ID&apos;
                           valor = &apos;Tabla&apos;(tab) ).
    o_alv-&gt;set_field_text( campo = &apos;ENTRY&apos;
                           valor = &apos;Texto Origen&apos;(txo) ).
    o_alv-&gt;set_field_text( campo  = &apos;LANGO&apos;
                           valor  = &apos;IOrig&apos;
                           valor2 = &apos;Idioma Origen&apos;(ido) ).
    o_alv-&gt;set_field_text( campo = &apos;ENTRY_D&apos;
                           valor = &apos;Texto Destino&apos;(txd) ).
    o_alv-&gt;set_field_text( campo  = &apos;LANGD&apos;
                           valor  = &apos;IDest&apos;
                           valor2 = &apos;Idioma Destino&apos;(idd) ).

    sgpi_texto( &apos;Generando informe&apos;(gin) ).
    o_alv-&gt;show( CHANGING tabla = i_listado ).

    o_alv-&gt;set_seleccion( CHANGING t_tabla = i_listado ).
  ENDMETHOD.

  METHOD command_dynpro_0100.
    TYPES: BEGIN OF t_elm,
             check    TYPE xfeld,
             linea    TYPE sy-tabix,
             key      TYPE rs38m-itex132,
             text     TYPE rs38m-itex132,
             text_sap TYPE rs38m-itex132,
           END OF t_elm.

    DATA l_comm_sel  TYPE string VALUE &apos;PROP,TRADUCIR,TEXT_ELM,PROP_TEXT_ELM,MESSAGES&apos;.
    DATA l_hay_sel   TYPE c LENGTH 1.
    DATA i_sel       TYPE TABLE OF t_carga.
    DATA i_list_proc TYPE TABLE OF t_listado.
    DATA i_content   TYPE TABLE OF text255.
    DATA i_elm       TYPE TABLE OF t_elm.
    DATA: l_linea_completa TYPE string,
          l_original       TYPE string,
          l_nuevo          TYPE string,
          l_msg            TYPE bapi_msg.
    DATA l_n1 TYPE numc1.
    DATA: l_var_msg TYPE string,
          lv_inner  TYPE string.
    DATA: lt_parts TYPE TABLE OF string,
          lv_part  TYPE string.

    command_dynpro( EXPORTING o_alv         = o_alv
                              seleccion     = l_comm_sel
                    CHANGING  i_listado     = i_listado
                              i_listado_ini = i_listado_ini
                              hay_sel       = l_hay_sel ).

    CASE ucomm.
      WHEN &apos;EXCELC&apos;.
        MOVE-CORRESPONDING o_prog-&gt;i_listado TO i_carga.
        DATA(o_alvl) = NEW zcl_ap_alv( tabla  = &apos;I_CARGA&apos;
                                       handle = &apos;CARG&apos; ).

        IF p_fiche IS INITIAL.
          zcl_ap_abap2xls=&gt;alv_2_xls( alv              = o_alvl-&gt;o_alv
                                      refresh_metadata = &apos;X&apos;
                                      tabla            = i_carga
                                      abrir            = &apos;X&apos;
                                      nombre_fichero   = &apos;Traducciones.xlsx&apos; ) ##NO_TEXT.
        ELSE.
          zcl_ap_abap2xls=&gt;alv_2_xls( alv              = o_alvl-&gt;o_alv
                                      refresh_metadata = &apos;X&apos;
                                      tabla            = i_carga
                                      abrir            = &apos;X&apos;
                                      ruta             = p_fiche ).
        ENDIF.
      WHEN &apos;CARGAR&apos;.
        NEW zcl_ap_abap2xls( )-&gt;lee_fichero( EXPORTING popup_select_file = &apos;X&apos;
                                                       get_datos         = &apos;X&apos;
                                                       huge              = &apos;X&apos;
                                                       mostrar_error     = &apos;X&apos;
                                                       fichero           = p_fiche
                                             IMPORTING datos             = i_carga ).

        LOOP AT i_carga ASSIGNING FIELD-SYMBOL(&lt;carga&gt;) WHERE entry_d &lt;&gt; &apos;&apos;.
          IF &lt;carga&gt;-sobreescribir = abap_true.
            ASSIGN o_prog-&gt;i_listado[ name        = &lt;carga&gt;-name
                                      langd       = &lt;carga&gt;-langd
                                      id          = &lt;carga&gt;-id
                                      key         = &lt;carga&gt;-key
                                      campo_texto = &lt;carga&gt;-campo_texto ] TO FIELD-SYMBOL(&lt;listado&gt;).
            IF sy-subrc = 0.
              IF &lt;listado&gt;-entry_d &lt;&gt; &lt;carga&gt;-entry_d.
                &lt;listado&gt;-entry_d = &lt;carga&gt;-entry_d.
                &lt;listado&gt;-lights  = icon_change.
              ENDIF.
            ENDIF.
          ELSE.
            ASSIGN o_prog-&gt;i_listado[ name        = &lt;carga&gt;-name
                                      langd       = &lt;carga&gt;-langd
                                      id          = &lt;carga&gt;-id
                                      key         = &lt;carga&gt;-key
                                      campo_texto = &lt;carga&gt;-campo_texto
                                      entry_d     = &apos;&apos; ] TO &lt;listado&gt;.
            IF sy-subrc = 0.
              &lt;listado&gt;-entry_d = &lt;carga&gt;-entry_d.
              &lt;listado&gt;-lights  = icon_change.
            ENDIF.
          ENDIF.
        ENDLOOP.

        o_alv-&gt;refrescar_grid( ).

      WHEN &apos;GRABAR&apos;.
        CLEAR lt_e071.
        LOOP AT o_prog-&gt;i_listado ASSIGNING &lt;listado&gt; WHERE lights = icon_change.

          IF &lt;listado&gt;-tipo = c_textos.
            IF NOT line_exists( i_sel[ name  = &lt;listado&gt;-name
                                       langd = &lt;listado&gt;-langd
                                       tipo  = c_textos ] ).
              APPEND VALUE #( name  = &lt;listado&gt;-name
                              langd = &lt;listado&gt;-langd
                              tipo  = c_textos ) TO i_sel.
              o_prog-&gt;grabar_textos( name = &lt;listado&gt;-name
                                     lang = &lt;listado&gt;-langd ).
            ENDIF.
          ENDIF.

          IF &lt;listado&gt;-tipo = c_tabla AND &lt;listado&gt;-id = &apos;RSMPTEXTS&apos;.
            IF NOT line_exists( i_sel[ name  = &lt;listado&gt;-name
                                       langd = &lt;listado&gt;-langd
                                       tipo  = c_tabla
                                       id    = &apos;RSMPTEXTS&apos; ] ).
              APPEND VALUE #( name  = &lt;listado&gt;-name
                              langd = &lt;listado&gt;-langd
                              tipo  = c_tabla
                              id    = &apos;RSMPTEXTS&apos; ) TO i_sel.
              o_prog-&gt;grabar_cuad( name = &lt;listado&gt;-name
                                   lang = &lt;listado&gt;-langd ).
            ENDIF.
          ENDIF.

          IF &lt;listado&gt;-id CS &apos;D021T&apos;.
            IF NOT line_exists( i_sel[ name  = &lt;listado&gt;-name
                                       langd = &lt;listado&gt;-langd
                                       tipo  = c_tabla
                                       id    = &lt;listado&gt;-id ] ).
              APPEND VALUE #( name  = &lt;listado&gt;-name
                              langd = &lt;listado&gt;-langd
                              tipo  = c_tabla
                              id    = &lt;listado&gt;-id ) TO i_sel.
              o_prog-&gt;grabar_dynpro( name = &lt;listado&gt;-name
                                     lang = &lt;listado&gt;-langd
                                     id   = &lt;listado&gt;-id ).
            ENDIF.
          ENDIF.

          IF &lt;listado&gt;-tipo = c_tabla AND &lt;listado&gt;-id = &apos;DD04T&apos;.
            IF NOT line_exists( i_sel[ name  = &lt;listado&gt;-name
                                       langd = &lt;listado&gt;-langd
                                       tipo  = c_tabla
                                       id    = &apos;DD04T&apos; ] ).
              APPEND VALUE #( name  = &lt;listado&gt;-name
                              langd = &lt;listado&gt;-langd
                              tipo  = c_tabla
                              id    = &apos;DD04T&apos; ) TO i_sel.

              o_prog-&gt;grabar_dtel( name = &lt;listado&gt;-name
                                   lang = &lt;listado&gt;-langd ).
            ENDIF.
          ENDIF.

          IF NOT &lt;listado&gt;-entry_d IS INITIAL.
            o_prog-&gt;grabar_propuesta( lango = p_lango
                                      texto = &lt;listado&gt;-entry
                                      langd = p_langd
                                      textd = &lt;listado&gt;-entry_d ).
          ENDIF.

        ENDLOOP.
        i_listado_ini = i_listado.

        IF lt_e071 IS NOT INITIAL.
          SORT lt_e071.
          DELETE ADJACENT DUPLICATES FROM lt_e071.
          CALL FUNCTION &apos;TR_REQUEST_CHOICE&apos;
            EXPORTING
*             IV_REQUEST_TYPES =
*             IV_CLI_DEP = &apos; &apos;
*             IV_REQUEST = &apos; &apos;
              it_e071 = lt_e071
*             IV_LOCK_OBJECTS = &apos; &apos;
*             IV_NO_OWNER_CHECK = &apos; &apos;
            EXCEPTIONS
              OTHERS  = 1.
          IF sy-subrc &lt;&gt; 0.
            MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                    WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
          ENDIF.
        ENDIF.

        o_alv-&gt;refrescar_grid( ).

      WHEN &apos;PROP&apos;.
        propuesta( ).

      WHEN &apos;TRADUCIR&apos;.
        LOOP AT o_prog-&gt;i_listado ASSIGNING &lt;listado&gt; WHERE check = &apos;X&apos; AND ( sobreescribir = abap_true OR entry_d IS INITIAL ).
          DATA(l_trad) = o_prog-&gt;get_traduccion( lango = &lt;listado&gt;-lango
                                                 langd = &lt;listado&gt;-langd
                                                 texto = &lt;listado&gt;-entry ).
          IF l_trad &lt;&gt; &apos;&apos;.
            IF &lt;listado&gt;-entry_d &lt;&gt; l_trad.
              &lt;listado&gt;-entry_d = l_trad.
              &lt;listado&gt;-lights  = icon_change.
            ENDIF.
          ENDIF.
        ENDLOOP.
        o_alv-&gt;refrescar_grid( ).

      WHEN &apos;DESC_PROP&apos;.
        SELECT * FROM ztemps                  &quot;#EC CI_ALL_FIELDS_NEEDED
          INTO TABLE @DATA(i_trad)
          WHERE clave = &apos;TRAD&apos;.
        zcl_ap_segw=&gt;get_json( EXPORTING datos = i_trad
                               IMPORTING json  = DATA(l_json) ).
        IF l_json IS NOT INITIAL.
          zcl_ap_ficheros=&gt;grabar_xstring( fichero = &apos;propuesta traducciones.json&apos;
                                           dialogo = &apos;X&apos;
                                           string  = l_json ) ##NO_TEXT.
        ENDIF.

      WHEN &apos;SUBIR_PROP&apos;.
        zcl_ap_ficheros=&gt;leer_xstring( EXPORTING popup_select_fichero = &apos;X&apos;
                                                 get_string           = &apos;X&apos;
                                       IMPORTING string               = l_json ).
        zcl_ap_segw=&gt;set_json( EXPORTING json  = l_json
                               IMPORTING datos = i_trad ).

        MODIFY ztemps FROM TABLE i_trad.

      WHEN &apos;TEXT_ELM&apos; OR &apos;PROP_TEXT_ELM&apos; OR &apos;MESSAGES&apos;.
        LOOP AT i_listado ASSIGNING &lt;listado&gt; WHERE check = &apos;X&apos; AND tipo = c_textos.
          IF NOT line_exists( i_list_proc[ name = &lt;listado&gt;-name ] ).
            APPEND VALUE #( name = &lt;listado&gt;-name
                            tipo = c_textos ) TO i_list_proc.
            DATA(l_clase) = &apos;&apos;.
            IF &lt;listado&gt;-name CS &apos;=&apos;.
              l_clase = &apos;X&apos;.
              SPLIT &lt;listado&gt;-name AT &apos;=&apos; INTO DATA(obj_name) aux1.
              i_content = zcl_ap_dev=&gt;get_objeto_como_texto( tipo   = &apos;CLAS&apos;
                                                             nombre = obj_name ).
            ELSE.
              READ REPORT &lt;listado&gt;-name INTO i_content.
            ENDIF.
            IF NOT i_content IS INITIAL.
              CLEAR i_elm.
              LOOP AT i_content ASSIGNING FIELD-SYMBOL(&lt;linea&gt;).
                DATA(l_tabix) = sy-tabix.

                DATA(l_lineam) = to_upper( &lt;linea&gt; ).
                IF ucomm = &apos;TEXT_ELM&apos;.
                  DATA(matcher) = cl_abap_matcher=&gt;create_pcre( pattern = &apos;&apos;&apos;([^&apos;&apos;]*)&apos;&apos;\(([A-Za-z0-9]{3})\)&apos; text = &lt;linea&gt; ) ##NO_TEXT.
                  WHILE matcher-&gt;find_next( ) = abap_true.
                    DATA(l_id) = to_upper( matcher-&gt;get_submatch( 2 ) ).
                    DATA(l_text) = matcher-&gt;get_submatch( 1 ).
                    ASSIGN i_listado[ name = &lt;listado&gt;-name
                                      tipo = c_textos
                                      key  = l_id ] TO FIELD-SYMBOL(&lt;sap&gt;).
                    IF sy-subrc &lt;&gt; 0.
                      APPEND VALUE #( text  = l_text
                                      key   = l_id
                                      check = &apos;X&apos; ) TO i_elm.
                    ELSEIF &lt;sap&gt;-entry &lt;&gt; l_text.
                      APPEND VALUE #( text     = l_text
                                      text_sap = &lt;sap&gt;-entry
                                      key      = l_id
                                      check    = &apos;&apos; ) TO i_elm.
                    ENDIF.
                  ENDWHILE.
                ELSEIF ucomm = &apos;PROP_TEXT_ELM&apos;.
*                  IF l_tabix = 233.
*                    BREAK-POINT.
*                  ENDIF.
                  IF    &lt;linea&gt;  CS &apos;NO_TEXT&apos; OR l_lineam CS ` AND `
                     OR l_lineam CS &apos;WHEN&apos;    OR l_lineam CS &apos;CALL FUNCTION&apos;.
                    CONTINUE.
                  ENDIF.
*                  matcher = cl_abap_matcher=&gt;create_pcre( pattern = |&apos;([^&apos;]*)&apos;(?!\\()| text = &lt;linea&gt; ).
                  matcher = cl_abap_matcher=&gt;create_pcre( pattern = &apos;&apos;&apos;([^&apos;&apos;]*)&apos;&apos;(?!\()&apos; text = &lt;linea&gt; ) ##NO_TEXT.
                  WHILE matcher-&gt;find_next( ) = abap_true.
                    l_text = matcher-&gt;get_submatch( 1 ).
                    IF    strlen( l_text )  = 1   OR l_text  = to_upper( l_text ) OR l_text CS &apos;=&apos; OR l_text(1)  = &apos;(&apos;
                       OR l_text           CS &apos;-&apos; OR l_text CS &apos;{&apos;                OR l_text CS &apos;|&apos; OR l_text    CS &apos;@&apos;.
                      CONTINUE.
                    ENDIF.

                    CLEAR l_linea_completa.
                    l_linea_completa = &lt;linea&gt;.

                    DATA(l_prev) = l_tabix.
                    DO 3 TIMES.
                      l_prev = l_prev - 1.
                      ASSIGN i_content[ l_prev ] TO FIELD-SYMBOL(&lt;prev_line&gt;).
                      IF sy-subrc = 0.
                        IF    zcl_ap_string=&gt;ultimo_caracter( &lt;prev_line&gt; ) = &apos;.&apos;
                           OR &lt;prev_line&gt;(1) = &apos;*&apos;.
                          EXIT.
                        ELSE.
                          l_linea_completa = condense( |{ &lt;prev_line&gt; }#{ l_linea_completa }| ).
                        ENDIF.
                      ELSE.
                        EXIT.
                      ENDIF.
                    ENDDO.

                    IF zcl_ap_string=&gt;ultimo_caracter( &lt;linea&gt; ) &lt;&gt; &apos;.&apos;.
                      DATA(l_next) = l_tabix.
                      DO 3 TIMES.
                        l_next = l_next + 1.
                        ASSIGN i_content[ l_next ] TO FIELD-SYMBOL(&lt;next_line&gt;).
                        IF sy-subrc = 0.
                          l_linea_completa = condense( |{ l_linea_completa }#{ &lt;next_line&gt; }| ).
                          IF    zcl_ap_string=&gt;ultimo_caracter( &lt;next_line&gt; ) = &apos;.&apos;
                             OR &lt;next_line&gt;(1) = &apos;*&apos;.
                            EXIT.
                          ENDIF.

                        ELSE.
                          EXIT.
                        ENDIF.
                      ENDDO.
                    ENDIF.

                    DATA(l_texto_may) = to_upper( l_linea_completa ).
                    IF    l_texto_may CS &apos;NO_TEXT&apos; OR l_texto_may CS ` AND ` OR l_texto_may CS &apos;~&apos;
                       OR l_texto_may CS &apos;WHEN&apos;    OR l_texto_may CS &apos;CALL FUNCTION&apos;
                       OR l_texto_may CS &apos;REPLACE&apos;.
                      CONTINUE.
                    ENDIF.

                    CLEAR string.
                    DATA(l_nuevo_id) = abap_false.
                    ASSIGN i_listado[ name = &lt;listado&gt;-name
                                      tipo = c_textos
                                      entry = l_text ] TO &lt;sap&gt;.
                    IF sy-subrc = 0.
                      string = &lt;sap&gt;-key.
                    ELSE.
                      ASSIGN i_elm[ text = l_text ] TO FIELD-SYMBOL(&lt;elm&gt;).
                      IF sy-subrc = 0.
                        string = &lt;elm&gt;-key.
                      ELSE.
                        l_nuevo_id = abap_true.
                        SPLIT l_text AT &apos; &apos; INTO TABLE DATA(i_palabras).
                        IF lines( i_palabras ) = 1.
                          string = i_palabras[ 1 ].
                        ELSE.
                          LOOP AT i_palabras ASSIGNING FIELD-SYMBOL(&lt;palabra&gt;).
                            IF &lt;palabra&gt;(1) &lt;&gt; &apos;.&apos; AND &lt;palabra&gt;(1) &lt;&gt; ` `.
                              IF string IS INITIAL.
                                string = &lt;palabra&gt;(1).
                              ELSE.
                                string = |{ string }{ &lt;palabra&gt;(1) }|.
                              ENDIF.
                            ENDIF.
                          ENDLOOP.
                        ENDIF.
                        IF strlen( string ) = 1.
                          string = |{ string }01|.
                        ELSEIF strlen( string ) = 2.
                          string = |{ string }1|.
                        ENDIF.
                      ENDIF.
                    ENDIF.

                    aux1 = to_upper( string ).
                    aux1 = aux1(3).
                    REPLACE ALL OCCURRENCES OF &apos;.&apos; IN aux1 WITH &apos;P&apos;.
                    REPLACE ALL OCCURRENCES OF &apos;%&apos; IN aux1 WITH &apos;P&apos;.
                    REPLACE ALL OCCURRENCES OF &apos;?&apos; IN aux1 WITH &apos;Q&apos;.
                    REPLACE ALL OCCURRENCES OF &apos;¿&apos; IN aux1 WITH &apos;Q&apos;.
                    REPLACE ALL OCCURRENCES OF &apos;/&apos; IN aux1 WITH &apos;S&apos;.
                    REPLACE ALL OCCURRENCES OF &apos;º&apos; IN aux1 WITH &apos;O&apos;.

                    string = aux1.
                    zcl_ap_string=&gt;quitar_caracteres_extranos( CHANGING string = string ).
                    aux1 = string.

                    IF l_nuevo_id = abap_true.
                      DO 9 TIMES.
                        l_n1 = sy-index.
                        DATA(l_existe) = abap_false.
                        ASSIGN i_listado[ name = &lt;listado&gt;-name
                                          tipo = c_textos
                                          key  = aux1 ] TO &lt;sap&gt;.
                        IF sy-subrc = 0.
                          l_existe = abap_true.
                        ELSE.
                          ASSIGN i_elm[ key = aux1 ] TO &lt;elm&gt;.
                          IF sy-subrc = 0.
                            l_existe = abap_true.
                          ENDIF.
                        ENDIF.
                        IF l_existe = abap_false.
                          EXIT.
                        ELSE.
                          aux1+2(1) = l_n1.
                        ENDIF.
                      ENDDO.
                    ENDIF.

                    APPEND VALUE #( text     = l_text
                                    linea    = l_tabix
                                    key      = aux1(3)
                                    text_sap = l_linea_completa ) TO i_elm.
                  ENDWHILE.
                ELSEIF ucomm = &apos;MESSAGES&apos;.
                  IF l_lineam CS &apos;MESSAGE&apos; AND l_lineam CS &apos;|&apos; AND l_lineam CS &apos;TYPE&apos; AND NOT l_lineam CS &apos;(&apos;.
                    &quot; 1) Extraer entre pipes
                    FIND REGEX &apos;\|([^|]+)\|&apos; IN &lt;linea&gt; SUBMATCHES l_var_msg.
                    &quot; lv_inner = &apos;INICIO { VARIABLE } FINAL&apos;
                    lv_inner = l_var_msg.

                    &quot; 2) Reemplazar todos los bloques { ... } por un separador literal
                    REPLACE ALL OCCURRENCES OF REGEX &apos;\{[^}]*\}&apos; IN lv_inner WITH &apos;&lt;&lt;&lt;SEP&gt;&gt;&gt;&apos;.
                    &quot; ahora lv_inner = &apos;INICIO &lt;&lt;&lt;SEP&gt;&gt;&gt; FINAL&apos;

                    IF NOT lv_inner CS &apos;&lt;&lt;&lt;SEP&gt;&gt;&gt;&apos;.
                      string = lv_inner.
                    ELSE.
                      &quot; 3) Separar por el separador (SPLIT por texto funciona bien)
                      SPLIT lv_inner AT &apos;&lt;&lt;&lt;SEP&gt;&gt;&gt;&apos; INTO TABLE lt_parts.

                      CLEAR string.
                      LOOP AT lt_parts INTO lv_part.
                        CONDENSE lv_part. &quot; quita espacios repetidos y bordes
                        lv_part = |{ lv_part } &amp;_|.
                        IF sy-tabix = 1.
                          string = lv_part.
                        ELSE.
                          string = |{ string }%{ lv_part }|.
                        ENDIF.
                      ENDLOOP.
                      string = |{ string }%|.
                    ENDIF.
                    IF string IS NOT INITIAL.
                      SELECT arbgb, msgnr, text FROM t100
                        INTO TABLE @DATA(i_msg)
                        WHERE sprsl    = @p_lango
                          AND text  LIKE @string.
                      IF sy-subrc &lt;&gt; 0.
                        APPEND VALUE #( text     = l_var_msg
                                        linea    = l_tabix
                                        key      = &apos;ERROR&apos;
                                        text_sap = &apos;NADA&apos; ) TO i_elm.
                      ELSE.
                        LOOP AT i_msg ASSIGNING FIELD-SYMBOL(&lt;msg&gt;).
                          SPLIT l_lineam AT `TYPE &apos;` INTO aux1 aux2.

                          APPEND VALUE #( text     = l_var_msg
                                          linea    = l_tabix
                                          key      = |{ aux2(1) }{ &lt;msg&gt;-msgnr }({ &lt;msg&gt;-arbgb })|
                                          text_sap = &lt;msg&gt;-text ) TO i_elm.
                        ENDLOOP.
                      ENDIF.
                    ENDIF.
                  ENDIF.
                ENDIF.
              ENDLOOP.
              IF i_elm IS NOT INITIAL.
                zcl_ap_popup=&gt;popup_alv_seleccion( EXPORTING campos_texto = &apos;TEXT=Código&amp;&amp;TEXT_SAP=Actual&apos;
                                                   IMPORTING cancelado    = DATA(l_cancelado)
                                                   CHANGING  tabla        = i_elm ).
                IF l_cancelado = abap_false.
                  IF ucomm = &apos;PROP_TEXT_ELM&apos; OR ucomm = &apos;MESSAGES&apos;.
                    DELETE i_elm WHERE check = &apos;&apos; OR key = &apos;ERROR&apos;.
                    DELETE ADJACENT DUPLICATES FROM i_elm COMPARING linea key.
                  ENDIF.
                  LOOP AT i_elm ASSIGNING &lt;elm&gt; WHERE check = &apos;X&apos;.
                    IF ucomm = &apos;PROP_TEXT_ELM&apos; OR ucomm = &apos;MESSAGES&apos;.
                      IF l_clase = &apos;X&apos;.
                        MESSAGE &apos;No es posible modificar clases&apos; TYPE &apos;I&apos;.
                        RETURN.
                      ENDIF.
                      DATA(l_mod_report) = abap_true.
                      ASSIGN i_content[ &lt;elm&gt;-linea ] TO FIELD-SYMBOL(&lt;linea_mod&gt;).
                      IF sy-subrc = 0.
                        IF ucomm = &apos;PROP_TEXT_ELM&apos;.
                          l_original = |&apos;{ &lt;elm&gt;-text }&apos;|.
                          l_nuevo = |&apos;{ &lt;elm&gt;-text }&apos;({ &lt;elm&gt;-key })|.
                          REPLACE l_original IN &lt;linea_mod&gt; WITH l_nuevo.
                          IF NOT &lt;linea_mod&gt; CS l_nuevo.
                            MESSAGE |No se ha podido modificar la línea { &lt;elm&gt;-linea } { &lt;elm&gt;-text } | TYPE &apos;E&apos;.
                          ENDIF.
                        ELSEIF ucomm = &apos;MESSAGES&apos;.
                          &lt;linea_mod&gt; = |{ &lt;linea_mod&gt; } &quot;TODO { &lt;elm&gt;-key } { &lt;elm&gt;-text_sap }|.
                        ENDIF.
                      ENDIF.
                    ELSEIF ucomm = &apos;TEXT_ELM&apos;.
                      APPEND INITIAL LINE TO i_listado ASSIGNING FIELD-SYMBOL(&lt;listado2&gt;).
                      &lt;listado2&gt; = &lt;listado&gt;.
                      &lt;listado2&gt;-check  = &apos;&apos;.
                      &lt;listado2&gt;-id     = &apos;I&apos;.
                      &lt;listado2&gt;-key    = &lt;elm&gt;-key.
                      &lt;listado2&gt;-entry  = &lt;elm&gt;-text.
                      &lt;listado2&gt;-length = strlen( &lt;elm&gt;-text ).
                      CLEAR &lt;listado2&gt;-entry_d.
                    ENDIF.
                  ENDLOOP.

                  IF l_mod_report = abap_true.
                    CALL FUNCTION &apos;EDITOR_SYNTAX_CHECK&apos;
                      EXPORTING
                        i_program       = &lt;listado&gt;-name
                      IMPORTING
                        o_error_message = l_msg
                        o_error_line    = l_tabix
                      TABLES
                        i_source        = i_content.

                    IF NOT l_msg IS INITIAL.
                      MESSAGE |Linea { l_tabix } { l_msg }| TYPE &apos;I&apos;.
                    ELSE.
                      INSERT REPORT &lt;listado&gt;-name FROM i_content.
                    ENDIF.
                  ELSE.
                    o_alv-&gt;refrescar_grid( ).
                  ENDIF.
                ENDIF.
              ENDIF.
            ENDIF.
          ENDIF.
        ENDLOOP.

      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.

  METHOD grabar_dynpro.
    LOOP AT i_listado ASSIGNING FIELD-SYMBOL(&lt;listado&gt;)
         WHERE name = name AND tipo = c_tabla AND id = id AND langd = lang AND lights = icon_change AND entry_d IS NOT INITIAL.

      IF grabar_registro( tabla       = &apos;D021T&apos;
                          clave       = &lt;listado&gt;-key
                          idioma      = p_langd
                          campo_texto = &apos;DTXT&apos;
                          texto       = &lt;listado&gt;-entry_d ) = abap_true.
        &lt;listado&gt;-lights = icon_green_light.
        DATA(ok) = abap_true.
      ELSE.
        &lt;listado&gt;-lights = icon_red_light.
      ENDIF.
    ENDLOOP.
    IF ok = abap_false.
      RETURN.
    ENDIF.

    ls_e071-as4pos = 1.
    ls_e071-pgmid  = &apos;LIMU&apos;.
    ls_e071-object = &apos;DYNP&apos;.

    DATA(l_dynpro) = id+6.
    ls_e071-obj_name = |{ name } { l_dynpro }|.
    ls_e071-lang     = sy-langu.
    INSERT ls_e071 INTO TABLE lt_e071.

    &quot; Grabamos la traducción estándar para que SAP refresque
    DATA(o_bi) = NEW zcl_ap_batch_input( ).

    o_bi-&gt;inicio( ).

    o_bi-&gt;dynpro( program = &apos;SAPLWBSCREEN&apos;
                  dynpro  = &apos;0010&apos;
                  okcode  = &apos;=TRAN&apos; ).
    o_bi-&gt;campos( campo = &apos;RS37A-DYNPROG&apos;
                  valor = name ). &quot; Nombre de programa ABAP
    o_bi-&gt;campos( campo = &apos;FELD-DYNNR&apos;
                  valor = l_dynpro ).

    &quot; Idioma objetivo para la traducción
    o_bi-&gt;dynpro( program = &apos;SAPLSETX&apos;
                  dynpro  = &apos;0200&apos;
                  okcode  = &apos;=NEXT&apos; ).
    WRITE p_lango TO aux1.
    WRITE p_langd TO aux2.
    o_bi-&gt;campos( campo = &apos;RSETX-MASTERLANG&apos;
                  valor = aux1 ). &quot; Clave de idioma
    o_bi-&gt;campos( campo = &apos;RSETX-TARG_LANGU&apos;
                  valor = aux2 ). &quot; Clave de idioma

    o_bi-&gt;dynpro( program = &apos;SAPMSSY0&apos;
                  dynpro  = &apos;0120&apos;
                  okcode  = &apos;=SEQU&apos; ).

    o_bi-&gt;dynpro( program = &apos;SAPMSSY0&apos;
                  dynpro  = &apos;0120&apos;
                  okcode  = &apos;=SAVE&apos; ).

    o_bi-&gt;llamar_transaccion( tcode = &apos;SE51&apos;
                              modo  = &apos;N&apos; ).
  ENDMETHOD.

  METHOD get_traduccion.
    TYPES: BEGIN OF ty_response_data,
             translatedtext TYPE string,
             match          TYPE i,
           END OF ty_response_data.

    TYPES: BEGIN OF ty_match,
             id               TYPE string,
             segment          TYPE string,
             translation      TYPE string,
             source           TYPE string,
             target           TYPE string,
             quality          TYPE i,
             reference        TYPE string,
             usage_count      TYPE i,
             subject          TYPE string,
             created_by       TYPE string,
             last_updated_by  TYPE string,
             create_date      TYPE string,
             last_update_date TYPE string,
             match            TYPE i,
             penalty          TYPE i,
           END OF ty_match.

    TYPES ty_matches_table TYPE STANDARD TABLE OF ty_match WITH EMPTY KEY.

    TYPES: BEGIN OF ty_root,
             responsedata    TYPE ty_response_data,
             quotafinished   TYPE abap_bool,
             mtlangsupported TYPE string,
             responsedetails TYPE string,
             responsestatus  TYPE i,
             responderid     TYPE string,
             exception_code  TYPE string,
             matches         TYPE ty_matches_table,
           END OF ty_root.

    DATA l_valor TYPE ty_root.

    CLEAR traduccion.
    IF texto IS INITIAL.
      RETURN.
    ENDIF.

    DATA(l_texto) = cl_http_utility=&gt;escape_url( CONV #( texto ) ).
    DATA(l_peticion) = |q={ l_texto }&amp;langpair={ get_idioma_2( lango ) }%&amp;%{ get_idioma_2( langd ) }|.
    REPLACE &apos;%&amp;%&apos; IN l_peticion WITH &apos;|&apos;.

    NEW zcl_ap_odata( host = &apos;https://api.mymemory.translated.net/&apos; )-&gt;get_odata( ##NO_TEXT
      EXPORTING
        servicio  = |get?{ l_peticion }| ##NO_TEXT
*       popup_respuesta = &apos;X&apos;
        get_datos = &apos;X&apos;
      IMPORTING
        datos     = l_valor ).

    IF l_valor-responsestatus = &apos;200&apos;.
      IF l_valor-responsedata-translatedtext &lt;&gt; &apos;&apos; AND l_valor-responsedata-match &gt;= 1.
        traduccion = l_valor-responsedata-translatedtext.
      ENDIF.
    ENDIF.
  ENDMETHOD.

  METHOD get_idioma_2.
    WRITE lang TO idioma_2.
  ENDMETHOD.

  METHOD propuesta.
    DATA i_tradsap TYPE STANDARD TABLE OF lxe_pcx_s1.

    FIELD-SYMBOLS &lt;listado&gt; TYPE zcl_report=&gt;t_listado.

    LOOP AT o_prog-&gt;i_listado ASSIGNING &lt;listado&gt; WHERE check = &apos;X&apos; AND ( sobreescribir = abap_true OR entry_d IS INITIAL ) AND entry IS NOT INITIAL.

* Intento ver si hay alguna traducción en la tabla ZTEMPS primero
      DATA(l_idiomas) = |{ &lt;listado&gt;-lango }-{ &lt;listado&gt;-langd }|.
      SELECT string FROM ztemps
        INTO @DATA(texto)
        UP TO 1 ROWS
        WHERE clave = &apos;TRAD&apos; AND valor = @l_idiomas AND texto = @&lt;listado&gt;-entry
        ORDER BY PRIMARY KEY.
      ENDSELECT.
      IF sy-subrc = 0 AND texto IS NOT INITIAL.
        IF &lt;listado&gt;-entry_d &lt;&gt; texto.
          &lt;listado&gt;-entry_d = texto.
          &lt;listado&gt;-lights  = icon_change.
        ENDIF.
      ELSE.
* Si no, busco si hay algo confirmado en el módulo de traducciones estándar
        DATA(l_src_lang) = COND lxeisolang( WHEN &lt;listado&gt;-lango = &apos;E&apos; THEN &apos;enUS&apos;
                                            WHEN &lt;listado&gt;-lango = &apos;S&apos; THEN &apos;esES&apos;
                                            WHEN &lt;listado&gt;-lango = &apos;F&apos; THEN &apos;frFR&apos; ).
        DATA(l_dst_lang) = COND lxeisolang( WHEN &lt;listado&gt;-langd = &apos;E&apos; THEN &apos;enUS&apos;
                                            WHEN &lt;listado&gt;-langd = &apos;S&apos; THEN &apos;esES&apos;
                                            WHEN &lt;listado&gt;-langd = &apos;F&apos; THEN &apos;frFR&apos; ).
        DATA(l_src_text) = &lt;listado&gt;-entry.
        CALL FUNCTION &apos;LXE_PP1_SEARCH_ENTRY&apos;
          EXPORTING
            s_lang    = l_src_lang
            t_lang    = l_dst_lang
            s_text    = l_src_text
          TABLES
            ex_pcx_s1 = i_tradsap.
        ASSIGN i_tradsap[ s_text = l_src_text ] TO FIELD-SYMBOL(&lt;trad&gt;).
        IF sy-subrc = 0.
          IF &lt;listado&gt;-entry_d &lt;&gt; &lt;trad&gt;-t_text AND &lt;trad&gt;-t_text &lt;&gt; &apos;&apos;.
            &lt;listado&gt;-entry_d = &lt;trad&gt;-t_text.
            &lt;listado&gt;-lights  = icon_change.
          ENDIF.
        ENDIF.
      ENDIF.

* Si no, miro lo puedo sacar a partir de valores similares en elementos de datos
      IF strlen( &lt;listado&gt;-entry ) &lt; 55.
        IF &lt;listado&gt;-entry_d IS INITIAL AND &lt;listado&gt;-lights &lt;&gt; icon_change.
          SELECT rollname FROM dd04t
            INTO @DATA(l_dominio)
            UP TO 1 ROWS
            WHERE ddlanguage = @&lt;listado&gt;-lango
              AND ddtext     = @&lt;listado&gt;-entry
            ORDER BY PRIMARY KEY.
          ENDSELECT.
          IF sy-subrc = 0.
            SELECT SINGLE ddtext FROM dd04t
              INTO &lt;listado&gt;-entry_d
              WHERE rollname   = l_dominio
                AND ddlanguage = &lt;listado&gt;-langd.
            IF &lt;listado&gt;-entry_d &lt;&gt; &apos;&apos;.
              &lt;listado&gt;-lights = icon_change.
            ENDIF.
          ENDIF.

* Si no, miro lo puedo sacar a partir de valores similares de dominios
          IF &lt;listado&gt;-entry_d IS INITIAL AND &lt;listado&gt;-lights &lt;&gt; icon_change.
            SELECT domname, valpos FROM dd07t
              INTO (@l_dominio, @DATA(l_valpos))
              UP TO 1 ROWS
              WHERE ddlanguage = @&lt;listado&gt;-lango
                AND ddtext     = @&lt;listado&gt;-entry
              ORDER BY PRIMARY KEY.
            ENDSELECT.
            IF sy-subrc = 0.
              SELECT SINGLE ddtext FROM dd07t
                INTO &lt;listado&gt;-entry_d
                WHERE domname    = l_dominio
                  AND valpos     = l_valpos
                  AND ddlanguage = &lt;listado&gt;-langd.
              IF &lt;listado&gt;-entry_d &lt;&gt; &apos;&apos;.
                &lt;listado&gt;-lights = icon_change.
              ENDIF.
            ENDIF.
          ENDIF.

* Si no, miro lo puedo sacar a partir de valores similares demensajes
          IF &lt;listado&gt;-entry_d IS INITIAL AND &lt;listado&gt;-lights &lt;&gt; icon_change.
            SELECT * FROM t100
              INTO @DATA(l_t100)
              UP TO 1 ROWS
              WHERE sprsl = @&lt;listado&gt;-lango
                AND text     = @&lt;listado&gt;-entry
              ORDER BY PRIMARY KEY.
            ENDSELECT.
            IF sy-subrc = 0.
              SELECT SINGLE text FROM t100
                INTO &lt;listado&gt;-entry_d
                WHERE sprsl = &lt;listado&gt;-langd
                  AND arbgb = l_t100-arbgb
                  AND msgnr = l_t100-msgnr.
              IF &lt;listado&gt;-entry_d &lt;&gt; &apos;&apos;.
                &lt;listado&gt;-lights = icon_change.
              ENDIF.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDLOOP.
    o_alv-&gt;refrescar_grid( ).
  ENDMETHOD.
ENDCLASS.

&quot; -----------------------------------------------------------------------
&quot; INITIALIZATION
&quot; -----------------------------------------------------------------------

INITIALIZATION.
  o_prog = NEW #( status       = &apos;INICIO_DYN&apos;
                  status_prog  = &apos;ZAP_STATUS&apos;
                  no_param     = &apos;X&apos;
                  guardar_logz = &apos;&apos; ).

  PERFORM add_button IN PROGRAM zap_status
          USING &apos;M01&apos;
                &apos;Log&apos;(log)
                &apos;&apos;
                &apos;&apos;.

  IF sy-batch IS INITIAL.
    o_prog-&gt;o_event = NEW #( boton_refrescar = &apos;X&apos;
                             boton_excel     = &apos;Y&apos;
                             o_prog          = o_prog ).

    o_prog-&gt;o_alv = NEW #( estructura = &apos;&apos;
                           o_event    = o_prog-&gt;o_event ).

    p_vari = o_prog-&gt;o_alv-&gt;get_default_layout( ).
  ENDIF.

  o_prog-&gt;initialization_i( CHANGING sscrfields = sscrfields ).

AT SELECTION-SCREEN OUTPUT.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  p_vari = o_prog-&gt;o_alv-&gt;get_f4_layout( ).

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_fiche.
  p_fiche = zcl_ap_ficheros=&gt;popup_select_fichero( file_filter = zcl_ap_ficheros=&gt;c_filtro_xlsx ).

  &quot; -----------------------------------------------------------------------
  &quot; AT SELECTION-SCREEN.
  &quot; -----------------------------------------------------------------------

AT SELECTION-SCREEN.
  CASE sy-ucomm.
    WHEN &apos;ONLI&apos;.
      IF p_prog = &apos;X&apos; AND s_name[] IS INITIAL.
        MESSAGE &apos;Informe selección de programas&apos;(isp) TYPE &apos;E&apos;.
      ELSEIF p_dtel = &apos;X&apos; AND s_dtel[] IS INITIAL.
        MESSAGE &apos;Informe selección de elementos de datos&apos;(ise) TYPE &apos;E&apos;.
      ENDIF.

      o_prog-&gt;validar_seleccion_obligatoria( campos_or = &apos;*&apos;
                                             msgty     = &apos;W&apos; ).
    WHEN OTHERS.
      o_prog-&gt;at_selection( ).
  ENDCASE.

AT SELECTION-SCREEN ON EXIT-COMMAND.
  o_prog-&gt;at_selection( ).

  &quot; ----------------------------------------------------------------------
  &quot; START-OF-SELECTION.
  &quot; -----------------------------------------------------------------------

START-OF-SELECTION.
  o_prog-&gt;main( ).

*&amp;---------------------------------------------------------------------*
*&amp;      Module  STATUS_0100  OUTPUT
*&amp;---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.

  o_prog-&gt;status_dynpro_0100( ).

ENDMODULE.
*&amp;---------------------------------------------------------------------*
*&amp;      Module  USER_COMMAND_0100  INPUT
*&amp;---------------------------------------------------------------------*
MODULE user_command_0100 INPUT.

  o_prog-&gt;command_dynpro_0100( ).


ENDMODULE.</source>
</PROG>

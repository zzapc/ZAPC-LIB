<?xml version="1.0" encoding="utf-8"?>
<PROG NAME="ZRSTPDA_SCRIPT_AP_ENH_SEARCH" VARCL="X" DBAPL="S" SUBC="S" APPL="X" RMAND="100" FIXPT="X" LDBNAME="D$S" UCCHECK="X">
 <textPool>
  <language SPRAS="S">
   <textElement ID="R" ENTRY="Watchpoints" LENGTH="33 "/>
  </language>
 </textPool>
 <source>*&lt;SCRIPT:PERSISTENT&gt;
REPORT  rstpda_script_template.

*&lt;SCRIPT:HEADER&gt;
*&lt;SCRIPTNAME&gt;RSTPDA_SCRIPT_TEMPLATE&lt;/SCRIPTNAME&gt;
*&lt;SCRIPT_CLASS&gt;LCL_DEBUGGER_SCRIPT&lt;/SCRIPT_CLASS&gt;
*&lt;SCRIPT_COMMENT&gt;Debugger Skript: Default Template&lt;/SCRIPT_COMMENT&gt;
*&lt;SINGLE_STEP&gt;X&lt;/SINGLE_STEP&gt;

*&lt;/SCRIPT:HEADER&gt;

*&lt;SCRIPT:PRESETTINGS&gt;

*&lt;/SCRIPT:PRESETTINGS&gt;

*&lt;SCRIPT:SCRIPT_CLASS&gt;
CLASS lcl_debugger_script DEFINITION INHERITING FROM  cl_tpda_script_class_super  .

  PUBLIC SECTION.
    METHODS: init    REDEFINITION,
      script  REDEFINITION,
      end     REDEFINITION,
      get_abap_src_info RETURNING VALUE(rs_info) TYPE tpda_scr_prg_info RAISING cx_tpda_src_info,
      check_stack,
      check_breakpoints,
      check_bp IMPORTING iv_bp_type TYPE string
                         it_stack   TYPE tpda_stack_view_it.

    METHODS: check_events_in_stack IMPORTING is_stack_view TYPE tpda_stack_view.
  PRIVATE SECTION.
    TYPES:
      BEGIN OF ts_script_params,
        trace          TYPE flag,
        debug          TYPE flag,
        customer_exits TYPE flag,
        new_badi       TYPE flag,
        old_badi       TYPE flag,
        enhancements   TYPE flag,
        bte            TYPE flag,
        user_exits     TYPE flag,
      END OF ts_script_params.

    DATA: mt_functions    TYPE RANGE OF tpda_event.
    DATA: mt_forms        TYPE RANGE OF tpda_event.
    DATA: mt_progs_cache  TYPE HASHED TABLE OF tpda_scr_prg_info WITH UNIQUE KEY program include line.

    DATA:
      ms_script_params TYPE ts_script_params.
ENDCLASS.                    &quot;lcl_debugger_script DEFINITION
*---------------------------------------------------------------------*
*       CLASS lcl_debugger_script IMPLEMENTATION
*---------------------------------------------------------------------*
*
*---------------------------------------------------------------------*
CLASS lcl_debugger_script IMPLEMENTATION.

  METHOD init.
    ms_script_params-trace = ms_script_params-customer_exits = ms_script_params-old_badi =
    ms_script_params-new_badi = ms_script_params-enhancements = ms_script_params-bte = abap_true.

    cl_ci_query_attributes=&gt;generic(
      EXPORTING
        p_name       = CONV #( sy-repid )
        p_title      = &apos;Script options&apos;
        p_attributes = VALUE #(

                                ( kind  = &apos;G&apos;
                                  text  = &apos;Script options&apos;
                                  ref   = REF #( sy-subrc ) )

                                ( ref   = REF #( ms_script_params-trace )
                                  kind  = &apos;R&apos;
                                  text  = &apos;Create trace file&apos; )

                                ( ref   = REF #( ms_script_params-debug )
                                  kind  = &apos;R&apos;
                                  text  = &apos;Stop with debugger&apos; )

                                ( kind  = &apos;G&apos;
                                  text  = &apos;Enhancements types&apos;
                                  ref   = REF #( sy-subrc ) )

                                ( ref   = REF #( ms_script_params-customer_exits )
                                  kind  = &apos;C&apos;
                                  text  = &apos;Customer Exits&apos; )

                                ( ref   = REF #( ms_script_params-new_badi )
                                  kind  = &apos;C&apos;
                                  text  = &apos;New BAdI&apos; )

                                ( ref   = REF #( ms_script_params-old_badi )
                                  kind  = &apos;C&apos;
                                  text  = &apos;Old BAdI&apos; )

                                ( ref   = REF #( ms_script_params-enhancements )
                                  kind  = &apos;C&apos;
                                  text  = &apos;Enhancements&apos; )

                                ( ref   = REF #( ms_script_params-bte )
                                  kind  = &apos;C&apos;
                                  text  = &apos;BTE (OpenFI)&apos; )

                                ( ref   = REF #( ms_script_params-user_exits )
                                  kind  = &apos;C&apos;
                                  text  = &apos;User Exits&apos; )
        )

        p_display    = &apos;&apos; ).

    TRY.
        IF ms_script_params-old_badi = abap_true.
          cl_tpda_script_bp_services=&gt;set_bp_method_global_class(
            p_methodname = &apos;GET_INSTANCE&apos;
            p_classname  = &apos;CL_EXITHANDLER&apos; ).
        ENDIF.

        IF ms_script_params-customer_exits = abap_true.
          cl_tpda_script_bp_services=&gt;set_bp_statement( p_statement = &apos;CALL CUSTOMER-FUNCTION&apos; ).
        ENDIF.

        IF ms_script_params-enhancements = abap_true.
          cl_tpda_script_bp_services=&gt;set_bp_statement( p_statement = &apos;ENHANCEMENT&apos; ).
        ENDIF.

        IF ms_script_params-new_badi = abap_true.
          cl_tpda_script_bp_services=&gt;set_bp_statement( p_statement = &apos;CALL BADI&apos; ).
          cl_tpda_script_bp_services=&gt;set_bp_statement( p_statement = &apos;GET BADI&apos; ).
        ENDIF.

        cl_tpda_script_bp_services=&gt;set_bp_stack_change( cl_tpda_script_bp_services=&gt;c_script_bp ).
      CATCH cx_tpda_scr_bp.
    ENDTRY.

    IF ms_script_params-bte = abap_true.
      mt_functions = VALUE #( ( sign = &apos;I&apos; option = &apos;CP&apos; low = &apos;OPEN_FI_PERFORM_*&apos; )
                              ( sign = &apos;I&apos; option = &apos;CP&apos; low = &apos;OUTBOUND_CALL_*&apos;   )
                              ( sign = &apos;I&apos; option = &apos;CP&apos; low = &apos;EXIT*&apos; ) ).
    ENDIF.

    IF ms_script_params-user_exits = abap_true.
      mt_forms = VALUE #( ( sign = &apos;I&apos; option = &apos;CP&apos; low = &apos;USEREXIT*&apos; ) ).
    ENDIF.
  ENDMETHOD.                    &quot;init

  METHOD check_bp.
    CASE iv_bp_type.
      WHEN &apos;CALL CUSTOMER-FUNCTION&apos;.
        CHECK ms_script_params-customer_exits = abap_true.
      WHEN &apos;ENHANCEMENT&apos;.
        CHECK ms_script_params-enhancements = abap_true.
      WHEN &apos;CALL BADI&apos; OR &apos;GET BADI&apos;.
        CHECK ms_script_params-new_badi = abap_true.
      WHEN OTHERS.
        RETURN.
    ENDCASE.

    IF ms_script_params-debug = abap_true.
      me-&gt;break( ).
    ENDIF.

    IF ms_script_params-trace = abap_true.
      TRY.
          DATA(ls_program_info) = get_abap_src_info( ).
        CATCH cx_tpda_src_info.
          RETURN.
      ENDTRY.

      trace-&gt;add_custom_info(
        EXPORTING
          p_trace_entry = VALUE #( value = |{ iv_bp_type } AT: { it_stack[ 1 ]-eventtype } { it_stack[ 1 ]-eventname }, line: { it_stack[ 1 ]-line }, program:{ it_stack[ 1 ]-program }|
                                   type = iv_bp_type ) ).

      trace-&gt;add_src_info( ).
      trace-&gt;add_event_info(
        EXPORTING
          p_abap_only = abap_true ).
    ENDIF.
  ENDMETHOD.

  METHOD get_abap_src_info.
    cl_tpda_script_abapdescr=&gt;get_abap_src_info( IMPORTING p_prg_info = rs_info ).
    READ TABLE mt_progs_cache WITH TABLE KEY program = rs_info-program
                                             include = rs_info-include
                                             line    = rs_info-line
      TRANSPORTING NO FIELDS.

    IF sy-subrc = 0.
      RAISE EXCEPTION TYPE cx_tpda_src_info.
    ELSE.
      INSERT rs_info INTO TABLE mt_progs_cache.
    ENDIF.
  ENDMETHOD.

  METHOD check_stack.
    TRY.
        DATA(lt_stack) = cl_tpda_script_abapdescr=&gt;get_abap_dynp_stack( ).
      CATCH cx_tpda_stack_error.
    ENDTRY.

    check_events_in_stack( is_stack_view = lt_stack[ 1 ] ).
  ENDMETHOD.

  METHOD check_breakpoints.
    TRY.
        DATA(lt_stack) = cl_tpda_script_abapdescr=&gt;get_abap_dynp_stack( ).
      CATCH cx_tpda_stack_error.
        RETURN.
    ENDTRY.

    TRY.
        DATA(lt_breakpoints) = cl_tpda_script_bp_services=&gt;get_reached_script_bps( ).
      CATCH cx_root.
        RETURN.
    ENDTRY.

    LOOP AT lt_breakpoints ASSIGNING FIELD-SYMBOL(&lt;ls_breakpoint&gt;) WHERE statementsta IS NOT INITIAL.
      check_bp( it_stack = lt_stack
                iv_bp_type = CONV #( &lt;ls_breakpoint&gt;-statementsta ) ).
    ENDLOOP.
  ENDMETHOD.

  METHOD script.
    check_stack( ).
    check_breakpoints( ).
  ENDMETHOD.                    &quot;script

  METHOD end.
  ENDMETHOD.                    &quot;end

  METHOD check_events_in_stack.
    DATA:
      ls_program_info TYPE tpda_scr_prg_info.

    CASE is_stack_view-eventtype.
      WHEN &apos;FUNCTION&apos;.
        CHECK is_stack_view-eventname IN mt_functions AND ms_script_params-bte = abap_true.

        IF ms_script_params-trace = abap_true.
          TRY.
              ls_program_info = get_abap_src_info( ).
            CATCH cx_tpda_src_info.
              RETURN.
          ENDTRY.

          trace-&gt;add_custom_info(
            EXPORTING
              p_trace_entry = VALUE #( value = |FM { is_stack_view-eventname }|
                                       type = &apos;BTE(FM)&apos; ) ).
        ENDIF.

      WHEN &apos;FORM&apos;.
        CHECK is_stack_view-eventname IN mt_forms AND ms_script_params-user_exits = abap_true.

        IF ms_script_params-trace = abap_true.
          TRY.
              ls_program_info = get_abap_src_info( ).
            CATCH cx_tpda_src_info.
              RETURN.
          ENDTRY.

          trace-&gt;add_custom_info(
            EXPORTING
              p_trace_entry = VALUE #( value = |PROGRAM: { ls_program_info-program }, INCLUDE: { ls_program_info-include }, FORM { is_stack_view-eventname }|
                                       type = &apos;USER-EXIT&apos; ) ).
        ENDIF.

      WHEN &apos;METHOD&apos;.
        IF is_stack_view-eventname = &apos;GET_INSTANCE&apos; AND is_stack_view-program CP &apos;CL_EXITHANDLER*&apos; AND ms_script_params-old_badi = abap_true.
          TRY.
              DATA(lv_exit_name) = cl_tpda_script_data_descr=&gt;get_simple_value( &apos;EXIT_NAME&apos; ).
            CATCH cx_tpda_varname cx_tpda_script_no_simple_type.
          ENDTRY.

          IF ms_script_params-trace = abap_true.
            TRY.
                ls_program_info = get_abap_src_info( ).
              CATCH cx_tpda_src_info.
                RETURN.
            ENDTRY.
          ENDIF.

          IF NOT lv_exit_name IS INITIAL.
            IF ms_script_params-trace = abap_true.
              trace-&gt;add_custom_info(
                EXPORTING
                  p_trace_entry = VALUE #( value = | OLD BADI: { lv_exit_name }|
                                           type = &apos;OLD_BADI&apos; ) ).
            ENDIF.
          ELSE.
            TRY.
                IF ms_script_params-trace = abap_true.
                  DATA(ls_instance) = cl_tpda_script_data_descr=&gt;get_variable_info( p_var_name  = &apos;INSTANCE&apos; ).
                  trace-&gt;add_custom_info(
                    EXPORTING
                      p_trace_entry = VALUE #( value = |OLD BADI : { ls_instance-vartype } |
                                               type = &apos;OLD_BADI&apos; ) ).
                ENDIF.
              CATCH cx_tpda_varname .
                RETURN.
            ENDTRY.
          ENDIF.
        ELSEIF ( is_stack_view-eventname CP  &apos;IPR_*&apos; OR
                 is_stack_view-eventname CP  &apos;IPO_*&apos; OR
                 is_stack_view-eventname CP  &apos;IWO_*&apos; ) AND ms_script_params-enhancements = abap_true.

          IF ms_script_params-trace = abap_true.
            DATA(lv_type) =  SWITCH string( is_stack_view-eventname+0(4)
                                            WHEN &apos;IPR_&apos; THEN &apos;PreExit Method&apos;
                                            WHEN &apos;IPO_&apos; THEN &apos;PostExit Method&apos;
                                            WHEN &apos;IWO_&apos; THEN &apos;OverWrite-Exit Method&apos;
                                            ELSE &apos;&apos; ).

            TRY.
                ls_program_info = get_abap_src_info( ).
              CATCH cx_tpda_src_info.
                RETURN.
            ENDTRY.

            SPLIT ls_program_info-program AT &apos;=&apos; INTO sy-msgv1 sy-msgv2 IN CHARACTER MODE.
            SPLIT ls_program_info-eventname AT &apos;~&apos; INTO sy-msgv2 sy-msgv3 IN CHARACTER MODE.

            trace-&gt;add_custom_info(
              EXPORTING
                p_trace_entry = VALUE #( value = |Class: { sy-msgv1 }, Method: { sy-msgv3  } |
                                             type = lv_type ) ).
          ENDIF.
        ELSE.
          RETURN.
        ENDIF.
      WHEN OTHERS.
        RETURN.
    ENDCASE.

    IF ms_script_params-trace = abap_true.
      trace-&gt;add_src_info( ).
      trace-&gt;add_event_info(
        EXPORTING
          p_abap_only = abap_true ).
    ENDIF.

    IF ms_script_params-debug = abap_true.
      me-&gt;break( ).
    ENDIF.
  ENDMETHOD.

ENDCLASS.                    &quot;lcl_debugger_script IMPLEMENTATION
*&lt;/SCRIPT:SCRIPT_CLASS&gt;

*&lt;/SCRIPT:PERSISTENT&gt;</source>
</PROG>

***********************************************************************
* TIPO : LISTADO
* TITULO : Actualización emails
* DESCRIPCION : Actualización emails
*
* AUTOR: Andrés Picazo                                FECHA: 03/02/2022
* ANALISTA: José Pavón
*
***********************************************************************
REPORT zcambio_emails.

*------TABLAS/ESTRUCTURAS----------------------------------------------*


*------TABLAS INTERNAS-------------------------------------------------*
*----------------------------------------------------------------------*
* CLASS lcl_alv DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv DEFINITION INHERITING FROM zcl_ap_alv_check FINAL.
  PUBLIC SECTION.
    METHODS: handle_user_command REDEFINITION.
    METHODS: visualizar_objeto REDEFINITION.
ENDCLASS.                    "lcl_alv DEFINITION

*----------------------------------------------------------------------*
*       CLASS zcl_report DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report DEFINITION INHERITING FROM zcl_ap_dev FINAL.
  PUBLIC SECTION.
    TYPES: BEGIN OF t_listado,
             check       TYPE xfeld,
             lights      TYPE zico_estado_mensaje,
             origen      TYPE string,
             clave       TYPE string,
             tabla       TYPE tabname,
             campo       TYPE fieldname,
             begda       TYPE begda,
             endda       TYPE endda,
             valor       TYPE string,
             nuevo_email TYPE string,
             origen2     TYPE string,
             clave2      TYPE string,
             no          TYPE abap_bool,
             message     TYPE bapi_msg,
           END OF t_listado,
           tt_listado TYPE STANDARD TABLE OF t_listado,
           BEGIN OF t_cambios,
             origen  TYPE string,
             destino TYPE string,
           END OF t_cambios.
    DATA: i_listado      TYPE tt_listado,
          i_cambios      TYPE TABLE OF t_cambios,
          i_cambios_todo TYPE TABLE OF t_cambios,
          o_alv          TYPE REF TO lcl_alv.


    METHODS: main.

    METHODS:  listado,
      seleccionar_datos.
  PRIVATE SECTION.
    METHODS buscar_it_0105.
    METHODS buscar_adr6.
    METHODS buscar_ausp.
    METHODS buscar_jobs.
    METHODS buscar_zparametros.
    METHODS buscar_tablas.

ENDCLASS.                    "REPORT DEFINITION

*------VARIABLES-------------------------------------------------------*
DATA: o_prog       TYPE REF TO zcl_report,
      r_email      TYPE RANGE OF zconf_web-email,
      r_email_todo TYPE RANGE OF zconf_web-email.

DATA: zconf_web TYPE zconf_web,
      dd03l     TYPE dd03l.

*------PARAMETER/SELECT-OPTIONS EN PANTALLA----------------------------*
SELECTION-SCREEN BEGIN OF BLOCK b01 WITH FRAME TITLE TEXT-sel.
SELECT-OPTIONS: s_email FOR zconf_web-email DEFAULT '*@gva.es' OPTION CP.
PARAMETERS: p_fecha TYPE sy-datum DEFAULT sy-datum.
SELECTION-SCREEN: SKIP 1.
PARAMETERS: p_i0105 AS CHECKBOX DEFAULT 'X',
            p_adr6  AS CHECKBOX DEFAULT 'X',
            p_param AS CHECKBOX DEFAULT 'X',
            p_clas  AS CHECKBOX DEFAULT 'X',
            p_jobs  AS CHECKBOX DEFAULT 'X'.
SELECT-OPTIONS: s_tabla FOR dd03l-tabname.
SELECTION-SCREEN: SKIP 1.
PARAMETERS: p_file TYPE localfile.
SELECTION-SCREEN: SKIP 1.
PARAMETERS: p_vari LIKE disvariant-variant.
SELECTION-SCREEN END OF BLOCK b01.
__botones_plantilla.


************************************************************************
*
* LOGICA DEL PROGRAMA
*
************************************************************************

*----------------------------------------------------------------------*
* CLASS lcl_alv IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv IMPLEMENTATION.

  METHOD visualizar_objeto.
    DATA l_list TYPE o_prog->t_listado.
    l_list = list.
    CASE column.
*      WHEN 'BUKRS'.
*        MESSAGE l_list-bukrs TYPE 'I'.
      WHEN OTHERS. message = 'No implementado'.
    ENDCASE.
  ENDMETHOD. "handle_double_click


  METHOD handle_user_command.
    DATA: l_pakey     TYPE pakey,
          pa0105      TYPE pa0105,
          ausp        TYPE ausp,
          adr6        TYPE adr6,
          zparametros TYPE zparametros,
          l_fecha     TYPE dats,
          l_set       TYPE string.

    check_ucomm_sel = 'EJEC'.

    super->handle_user_command( e_salv_function ).

    CASE ucomm.
      WHEN 'EJEC'.
        IF line_exists( o_prog->i_listado[ check = 'X' no = 'X' ] ).
          MESSAGE 'No seleccione líneas que no es posible modificar automáticamente' TYPE 'I'.
          RETURN.
        ENDIF.

        LOOP AT o_prog->i_listado ASSIGNING FIELD-SYMBOL(<listado>) WHERE check = 'X'.
          DATA(l_icono) = icon_okay.
          CASE <listado>-tabla.
            WHEN 'PA0105'.
              IF p_fecha IS INITIAL.
                l_icono = icon_red_light.
                <listado>-message = 'Sólo modificamos infotipo si se indica fecha de corte'.
              ELSE.
                l_pakey = <listado>-clave.
                SELECT SINGLE * FROM  pa0105
                  INTO pa0105
                 WHERE pernr  = l_pakey-pernr
                   AND subty  = l_pakey-subty
                   AND objps  = l_pakey-objps
                   AND sprps  = l_pakey-sprps
                   AND endda  = l_pakey-endda
                   AND begda  = l_pakey-begda
                   AND seqnr  = l_pakey-seqnr.
                IF sy-subrc NE 0.
                  l_icono = icon_red_light.
                  <listado>-message = 'Error recuparando infotipo'.
                ELSE.
                  l_fecha = p_fecha - 1.
                  IF l_fecha < pa0105-begda.
                    l_icono = icon_red_light.
                    <listado>-message = 'No es posible delimitar infotipo'.
                  ELSE.
                    UPDATE pa0105
                      SET endda = l_fecha
                     WHERE pernr  = l_pakey-pernr
                       AND subty  = l_pakey-subty
                       AND objps  = l_pakey-objps
                       AND sprps  = l_pakey-sprps
                       AND endda  = l_pakey-endda
                       AND begda  = l_pakey-begda
                       AND seqnr  = l_pakey-seqnr.
                    IF sy-subrc NE 0.
                      l_icono = icon_red_light.
                      <listado>-message = 'Error delimitando infotipo'.
                    ELSE.
                      pa0105-begda = p_fecha.
                      IF pa0105-endda >= pa0105-begda.
                        IF NOT pa0105-usrid_long IS INITIAL.
                          pa0105-usrid_long  = <listado>-nuevo_email.
                        ENDIF.
                        IF NOT pa0105-usrid IS INITIAL.
                          IF strlen( <listado>-nuevo_email ) <= 30.
                            pa0105-usrid  = <listado>-nuevo_email.
                          ELSE.
                            CLEAR pa0105-usrid.
                            pa0105-usrid_long  = <listado>-nuevo_email.
                          ENDIF.
                        ENDIF.
                        INSERT pa0105 FROM pa0105.
                        IF sy-subrc NE 0.
                          l_icono = icon_red_light.
                          <listado>-message = 'Error grabando nuevo infotipo'.
                          ROLLBACK WORK.
                        ENDIF.
                      ENDIF.
                    ENDIF.
                  ENDIF.
                ENDIF.
              ENDIF.
            WHEN 'ZPARAMETROS'.
              zparametros = <listado>-clave.
              SELECT SINGLE * FROM  zparametros
                INTO zparametros
               WHERE clave   = zparametros-clave
                 AND campo   = zparametros-campo
                 AND valor   = zparametros-valor
                 AND valor2  = zparametros-valor2.
              IF sy-subrc NE 0.
                l_icono = icon_red_light.
                <listado>-message = 'Error recuparando tabla parametros'.
              ELSE.
                l_set = |{ <listado>-campo } = '{ <listado>-nuevo_email }'|.
                UPDATE zparametros
                  SET (l_set)
                 WHERE clave   = zparametros-clave
                   AND campo   = zparametros-campo
                   AND valor   = zparametros-valor
                   AND valor2  = zparametros-valor2.
                IF sy-subrc NE 0.
                  l_icono = icon_red_light.
                  <listado>-message = 'Error actualizando tabla parámetros'.
                ENDIF.
              ENDIF.
            WHEN 'AUSP'.
              ausp(71) = <listado>-clave.
              SELECT SINGLE * FROM  ausp
                INTO ausp
                     WHERE  objek  = ausp-objek
                     AND    atinn  = ausp-atinn
                     AND    atzhl  = ausp-atzhl
                     AND    mafid  = ausp-mafid
                     AND    klart  = ausp-klart
                     AND    adzhl  = ausp-adzhl.
              IF sy-subrc NE 0.
                l_icono = icon_red_light.
                <listado>-message = 'Error recuparando tabla AUSP'.
              ELSE.
                UPDATE ausp
                  SET atwrt = <listado>-nuevo_email
                     WHERE  objek  = ausp-objek
                     AND    atinn  = ausp-atinn
                     AND    atzhl  = ausp-atzhl
                     AND    mafid  = ausp-mafid
                     AND    klart  = ausp-klart
                     AND    adzhl  = ausp-adzhl.
                IF sy-subrc NE 0.
                  l_icono = icon_red_light.
                  <listado>-message = 'Error actualizando tabla AUSP'.
                ENDIF.
              ENDIF.
            WHEN 'ADR6'.
              adr6+3(31) = <listado>-clave.
              SELECT SINGLE * FROM  adr6
                INTO adr6
                     WHERE  addrnumber  = adr6-addrnumber
                     AND    persnumber  = adr6-persnumber
                     AND    date_from   = adr6-date_from
                     AND    consnumber  = adr6-consnumber.

              IF sy-subrc NE 0.
                l_icono = icon_red_light.
                <listado>-message = 'Error recuparando tabla ADR6'.
              ELSE.
                l_set = to_upper( <listado>-nuevo_email ).

                UPDATE adr6
                  SET smtp_addr = <listado>-nuevo_email
                      smtp_srch = l_set
                     WHERE  addrnumber  = adr6-addrnumber
                     AND    persnumber  = adr6-persnumber
                     AND    date_from   = adr6-date_from
                     AND    consnumber  = adr6-consnumber.
                IF sy-subrc NE 0.
                  l_icono = icon_red_light.
                  <listado>-message = 'Error actualizando tabla ADR6'.
                ENDIF.
              ENDIF.
          ENDCASE.
          <listado>-lights = zcl_ap_alv=>set_icono( icono = l_icono mensaje = <listado>-message ).
          IF l_icono = icon_okay.
            o_prog->message( p1 = |Se modifica { <listado>-tabla } { <listado>-valor } por { <listado>-nuevo_email }| type = 'S' ).
            zcl_ap_temp=>set_st( clave = 'CAMB_MAIL' subclave = <listado>-clave
                                 texto = <listado>-valor texto2 = <listado>-nuevo_email ).
            COMMIT WORK AND WAIT.
          ENDIF.
        ENDLOOP.
        IF sy-subrc = 0.
          refresh( ).
        ENDIF.
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD. "handle_USER_COMMAND
ENDCLASS. "lcl_alv IMPLEMENTATION

*----------------------------------------------------------------------*
*       CLASS zcl_report IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report IMPLEMENTATION.
  METHOD main.

    IF NOT p_file IS INITIAL.
      REFRESH s_email.
      TYPES: BEGIN OF t_fichero,
               pernr    TYPE persno,
               ename    TYPE pa0001-ename,
               mail_new TYPE string,
               mail_old TYPE string,
               resto    TYPE string,
             END OF t_fichero.
      DATA i_fichero TYPE TABLE OF t_fichero.
      DATA(o_excel) = NEW zcl_ap_abap2xls( ).
      o_excel->lee_fichero( EXPORTING fichero = p_file
                                      get_datos = 'X'
                                      huge      = 'X'
                                      mostrar_error = 'X'
                            IMPORTING datos   = i_fichero
                                      errores = DATA(i_errores) ).
      LOOP AT i_fichero ASSIGNING FIELD-SYMBOL(<fichero>) WHERE mail_old CS '@' AND mail_new CS '@'.
        CONCATENATE '|' <fichero>-mail_new INTO DATA(l_high).
        APPEND VALUE #( option = 'BT' sign = 'I' low = <fichero>-mail_old high = l_high ) TO s_email.
      ENDLOOP.
      IF s_email[] IS INITIAL.
        MESSAGE 'No se han recuperado mails correctos del fichero' TYPE 'I'.
        LEAVE PROGRAM.
      ENDIF.
    ENDIF.

    CLEAR r_email.
    LOOP AT s_email INTO DATA(l_email).
      DATA(l_todo) = ''.
      IF l_email-high(1) = '|'.
        IF l_email-low CS '*' AND l_email-high CS '*'.
          APPEND VALUE #( origen  = to_lower( l_email-low )
                          destino = to_lower( l_email-high+1 ) ) TO i_cambios_todo.
          l_todo = 'X'.
        ELSE.
          APPEND VALUE #( origen  = to_lower( l_email-low )
                          destino = to_lower( l_email-high+1 ) ) TO i_cambios.
        ENDIF.
        CLEAR l_email-high.
        IF l_email-option = 'BT'.
          IF l_email-low CS '*'.
            l_email-option = 'CP'.
          ELSE.
            l_email-option = 'EQ'.
          ENDIF.
        ENDIF.
      ENDIF.
      TRANSLATE: l_email-low TO LOWER CASE,
                 l_email-high TO LOWER CASE.
      APPEND l_email TO r_email.
      IF l_todo = 'X'. APPEND l_email TO r_email_todo. ENDIF.
      TRANSLATE: l_email-low TO UPPER CASE,
                 l_email-high TO UPPER CASE.
      APPEND l_email TO r_email.
      IF l_todo = 'X'. APPEND l_email TO r_email_todo. ENDIF.
      IF  l_email-low CS '@' AND l_email-high IS INITIAL.
        SPLIT l_email-low AT '@' INTO aux1 aux2.
        APPEND VALUE #( option = l_email-option sign = l_email-sign low = |{ to_lower( aux1 ) }@{ to_upper( aux2 ) }| ) TO r_email.
        APPEND VALUE #( option = l_email-option sign = l_email-sign low = |{ to_upper( aux1 ) }@{ to_lower( aux2 ) }| ) TO r_email.
      ENDIF.
    ENDLOOP.
    SORT r_email.
    DELETE ADJACENT DUPLICATES FROM r_email.

    seleccionar_datos( ).
    listado( ).
  ENDMETHOD.                    "REPORT

  METHOD seleccionar_datos.
    DATA l_icono TYPE icon_d.

    sgpi_texto( 'Seleccionando datos'(sda) ).
    IF p_i0105 = 'X'.
      buscar_it_0105(  ).
    ENDIF.

    IF p_adr6 = 'X'.
      buscar_adr6(  ).
    ENDIF.

    IF p_param = 'X'.
      buscar_zparametros(  ).
    ENDIF.

    IF p_clas = 'X'.
      buscar_ausp(  ).
    ENDIF.

    IF p_jobs = 'X'.
      buscar_jobs(  ).
    ENDIF.

    IF NOT s_tabla[] IS INITIAL.
      buscar_tablas( ).
    ENDIF.

    o_prog->o_sgpi->get_filas_tabla( i_listado[] ).
    LOOP AT i_listado ASSIGNING FIELD-SYMBOL(<listado>).
      sgpi_texto( texto1 = 'Procesando datos'(pda) cant_porc = 100 ).
      CLEAR l_icono.

      aux1 = to_lower( <listado>-valor ).
      READ TABLE i_cambios ASSIGNING FIELD-SYMBOL(<cambio>) WITH KEY origen = aux1.
      IF sy-subrc = 0.
        IF <listado>-valor = aux1.
          <listado>-nuevo_email = <cambio>-destino.
        ELSE.
          <listado>-nuevo_email = to_upper( <cambio>-destino ).
        ENDIF.
      ELSE.
        IF NOT i_cambios_todo IS INITIAL.
          IF <listado>-valor IN r_email_todo.
            LOOP AT i_cambios_todo ASSIGNING <cambio> WHERE origen IN r_email_todo.
              EXIT.
            ENDLOOP.
            IF sy-subrc = 0.
              SPLIT <cambio>-origen AT '*' INTO aux1 DATA(l_origen).
              SPLIT <cambio>-destino AT '*' INTO aux1 DATA(l_destino).
              IF NOT l_origen IS INITIAL AND NOT l_destino IS INITIAL.
                <listado>-nuevo_email = <listado>-valor.
                REPLACE ALL OCCURRENCES OF l_origen IN <listado>-nuevo_email WITH l_destino.
              ENDIF.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.

      IF <listado>-nuevo_email IS INITIAL.
        l_icono = icon_dummy.
        <listado>-no = 'X'.
        <listado>-message = 'No se ha indicado mail destino'.
      ELSE.
        IF <listado>-origen = 'REPORT' OR <listado>-origen = 'VARIANTE' OR <listado>-origen = 'TABLA'.
          l_icono = icon_physical_sample.
          <listado>-message = 'Estos valores se deben de modificar manualmente'.
        ELSE.
          IF to_lower( <listado>-valor ) = to_lower(  <listado>-nuevo_email ).
            l_icono = icon_green_light.
          ELSE.
            IF <listado>-origen = 'CLASIFICACION' AND strlen( <listado>-nuevo_email ) > 30.
              l_icono = icon_message_critical.
              <listado>-message = 'Nuevo email en clasificación no puede superar 30 carácteres'.
              <listado>-no = 'X'.
            ELSE.
              l_icono = icon_change.
              <listado>-check = 'X'.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.

      set_status_list( EXPORTING icono = l_icono message = <listado>-message CHANGING list = <listado> ).
    ENDLOOP.

    SORT i_listado.

  ENDMETHOD.                    "seleccionar_datos


  METHOD listado.

    sgpi_texto( 'Generando informe'(gin) ).

    o_alv->add_button( button = 'F01' text = 'Modificar emails'  icon = icon_execute_object ucomm = 'EJEC' ).
    o_alv->add_button( button = 'F02' text = 'Excel'  icon = icon_xls ucomm = 'EXCEL' ).

    o_alv->set_layout( p_vari ).
    o_alv->set_field_text( 'ORIGEN,CLAVE,ORIGEN2,CLAVE2,VALOR,NUEVO_EMAIL').

    o_alv->set_top_of_page(
*    i_param = VALUE #( ( tipo = 'R' param = 'S_CREDAT' texto = 'F.Creación IDOC' tabla = '' campo = '' )
                          ).

*    o_alv->set_field_hotspot( campo = 'EBELN' auto = 'X' ).

    o_alv->set_field( campo = 'LIGHTS' op = 'KEY' ).
    o_alv->set_field_quitar( 'CHECK,NO' ).

*    o_alv->set_orden( '' ).
    o_alv->get_datos_layout( EXPORTING reordenar_tabla = 'X' tabla_ref = 'X' CHANGING t_tabla = i_listado ).
    o_alv->set_seleccion( CHANGING t_tabla = i_listado ).

    o_alv->show( ).


  ENDMETHOD.                    "


  METHOD buscar_it_0105.
    DATA: l_pakey   TYPE pakey,
          l_listado TYPE t_listado,
          i_pa0105  TYPE TABLE OF pa0105.

    IF NOT p_fecha IS INITIAL.
      DATA(l_where) = |ENDDA >= '{ p_fecha }' AND BEGDA <= '{ p_fecha }'|.
    ENDIF.
    SELECT pernr, subty, objps, sprps, endda, begda, seqnr,usrid, usrid_long FROM pa0105
      APPENDING CORRESPONDING FIELDS OF TABLE @i_pa0105
     WHERE ( usrid IN @r_email
          OR usrid_long IN @r_email )
       AND (l_where).

    CLEAR l_listado.
    l_listado-origen = 'IT COMUNICACIONES'.
    l_listado-tabla  = 'PA0105'.


    LOOP AT i_pa0105 ASSIGNING FIELD-SYMBOL(<pa0105>).
      MOVE-CORRESPONDING <pa0105> TO l_pakey.
      l_listado-clave = l_pakey.
      l_listado-begda = <pa0105>-begda.
      l_listado-endda = <pa0105>-endda.
      IF NOT <pa0105>-usrid IS INITIAL.
        IF <pa0105>-usrid IN r_email.
          l_listado-campo = 'USRID'.
          l_listado-valor = <pa0105>-usrid.
          APPEND l_listado TO i_listado.
          CONTINUE.
        ENDIF.
      ENDIF.
      IF NOT <pa0105>-usrid_long IS INITIAL.
        IF <pa0105>-usrid_long IN r_email.

          string = to_lower( <pa0105>-usrid_long ).
          READ TABLE i_cambios ASSIGNING FIELD-SYMBOL(<cambio>) WITH KEY origen = string.
          IF sy-subrc = 0.
            string = to_upper( <cambio>-destino ).
            SELECT SINGLE pernr FROM pa0105
              INTO <pa0105>-pernr
             WHERE pernr = <pa0105>-pernr
               AND subty = <pa0105>-subty
               AND usrid_long = string
               AND (l_where).
            IF sy-subrc = 0.
              CONTINUE.
            ENDIF.
          ENDIF.


          l_listado-campo = 'USRID_LONG'.
          l_listado-valor = <pa0105>-usrid_long.
          APPEND l_listado TO i_listado.
        ENDIF.
      ENDIF.
    ENDLOOP.

  ENDMETHOD.


  METHOD buscar_adr6.
    DATA: l_listado TYPE t_listado.


    IF NOT p_fecha IS INITIAL.
      DATA(l_where) = |DATE_FROM <= '{ p_fecha }'|.
    ENDIF.
    SELECT addrnumber, persnumber, date_from, consnumber, smtp_addr, smtp_srch FROM adr6
      INTO TABLE @DATA(i_adr6)
     WHERE smtp_addr IN @r_email
       AND (l_where).

    CLEAR l_listado.
    l_listado-origen = 'DIRECCIONES'.
    l_listado-tabla  = 'ADR6'.

    LOOP AT i_adr6 ASSIGNING FIELD-SYMBOL(<adr6>).
      l_listado-clave = <adr6>(31).
      l_listado-campo = 'SMTP_ADDR'.
      l_listado-begda = <adr6>-date_from.
      l_listado-endda = '99991231'.
      l_listado-valor = <adr6>-smtp_addr.

      CLEAR: l_listado-origen2, l_listado-clave2.

      SELECT SINGLE appl_table FROM adrv
        INTO l_listado-origen2
       WHERE addrnumber = <adr6>-addrnumber.
      IF l_listado-origen2 = 'SOPR'. "Mails
        CONTINUE.
      ENDIF.


*      IF NOT <adr6>-persnumber IS INITIAL AND NOT <adr6>-addrnumber IS INITIAL.
*        SELECT SINGLE bname FROM usr21
*          INTO l_listado-clave2
*         WHERE persnumber = <adr6>-persnumber
*           AND addrnumber = <adr6>-addrnumber.
*        IF sy-subrc = 0.
*          l_listado-origen2 = 'USUARIO SAP'.
*        ENDIF.
*
*        IF l_listado-origen2  IS INITIAL.
*          SELECT SINGLE so_key FROM adcp
*            INTO l_listado-clave2
*           WHERE persnumber = <adr6>-persnumber
*             AND addrnumber = <adr6>-addrnumber.
*          IF sy-subrc = 0.
*            l_listado-origen2 = 'ADCP '.
*          ENDIF.
*        ENDIF.
*      ELSE.
*        SELECT SINGLE lifnr FROM lfa1                   "#EC CI_NOFIELD
*          INTO l_listado-clave2
*         WHERE adrnr = <adr6>-addrnumber.
*        IF sy-subrc = 0.
*          l_listado-origen2 = 'PROVEEDOR'.
*        ENDIF.
*        IF l_listado-clave2 IS INITIAL.
*          SELECT SINGLE kunnr FROM kna1                 "#EC CI_NOFIELD
*            INTO l_listado-clave2
*           WHERE adrnr = <adr6>-addrnumber.
*          IF sy-subrc = 0.
*            l_listado-origen2 = 'CLIENTE'.
*          ENDIF.
*        ENDIF.
*        IF l_listado-clave2 IS INITIAL.
*          SELECT SINGLE appl_table FROM adrv
*            INTO l_listado-origen2
*           WHERE ADDRNUMBER = <adr6>-addrnumber.
*        ENDIF.
**        ENDIF.
**        IF L_LISTADO-CLAVE2 IS INITIAL.
**        SELECT SINGLE COUNTER FROM SOGR
**          INTO l_listado-CLAVE2
**         WHERE SND_ADRNO = <adr6>-addrnumber.
**        IF sy-subrc = 0.
**          l_listado-origen2 = 'SOGR'.
**        ENDIF.
**        ENDIF.
*      ENDIF.



      APPEND l_listado TO i_listado.
    ENDLOOP.
  ENDMETHOD.


  METHOD buscar_ausp.
    DATA: l_listado TYPE t_listado,
          i_ausp    TYPE TABLE OF ausp.



    SELECT objek, atinn, atzhl, mafid, klart, adzhl, atwrt FROM ausp "#EC CI_NOFIELD "#EC CI_NOFIRST
      INTO CORRESPONDING FIELDS OF TABLE @i_ausp
     WHERE atwrt IN @r_email.

    CLEAR l_listado.
    l_listado-origen = 'CLASIFICACION'.
    l_listado-tabla  = 'AUSP'.

    LOOP AT i_ausp ASSIGNING FIELD-SYMBOL(<ausp>).
      l_listado-clave = <ausp>(71).
      l_listado-campo = 'ATWRT'.
      l_listado-valor = <ausp>-atwrt.

      CLEAR: l_listado-origen2, l_listado-clave2.
      IF NOT  <ausp>-klart IS INITIAL.
        SELECT SINGLE artxt FROM tclat
           INTO l_listado-origen2
          WHERE spras = sy-langu
           AND klart = <ausp>-klart.
      ENDIF.


      APPEND l_listado TO i_listado.
    ENDLOOP.
  ENDMETHOD.


  METHOD buscar_jobs.
    DATA: i_tabla   TYPE TABLE OF text255,
          l_listado TYPE t_listado.

    SELECT DISTINCT progname, variant, jobname FROM tbtcp
      INTO TABLE @DATA(i_jobs)
     WHERE progname LIKE 'Z%'
       AND status = 'P'
    ORDER BY progname, variant, jobname.

    DATA(o_var) = NEW zcl_ap_variante( ).
    LOOP AT i_jobs ASSIGNING FIELD-SYMBOL(<job>).
      DATA(l_job) = <job>.
      AT NEW progname.
        CLEAR l_listado.
        l_listado-origen = 'REPORT'.
        l_listado-clave  = <job>-progname.
        l_listado-no = 'X'.
        CLEAR: i_tabla.
        READ REPORT <job>-progname INTO  i_tabla.
        DATA(string) = zcl_ap_string=>tabla2string( i_tabla ).
        DATA(emails) = zcl_ap_regexp=>buscar_patron( string = string patron = '\w+(\.\w+)*@(\w+\.)+(\w{2,4})' ).
        SORT emails.
        DELETE ADJACENT DUPLICATES FROM emails.
        LOOP AT emails ASSIGNING FIELD-SYMBOL(<email>).
          IF <email> IN r_email.
            l_listado-valor = <email>.
            l_listado-origen2 = 'JOB'.
            l_listado-clave2 = l_job-jobname.
            APPEND l_listado TO i_listado.
          ENDIF.
        ENDLOOP.
      ENDAT.

      AT NEW variant.
        CLEAR l_listado.
        l_listado-origen = 'VARIANTE'.
        l_listado-clave  = <job>-progname.
        l_listado-tabla  = <job>-variant.
        l_listado-no = 'X'.
        o_var->get_contenidos( report = <job>-progname variant = <job>-variant ).
        LOOP AT o_var->i_valores ASSIGNING FIELD-SYMBOL(<valor>) WHERE low IN r_email.
          l_listado-valor = <valor>-low.
          l_listado-origen2 = 'JOB'.
          l_listado-clave2 = l_job-jobname.
          APPEND l_listado TO i_listado.
        ENDLOOP.
      ENDAT.

    ENDLOOP.

  ENDMETHOD.


  METHOD buscar_zparametros.
    DATA: l_listado TYPE t_listado,
          i_param   TYPE TABLE OF zparametros.

    SELECT clave, campo, valor, valor2, atributo1, atributo2 FROM zparametros "#EC CI_GENBUFF.
      INTO CORRESPONDING FIELDS OF TABLE @i_param
     WHERE campo IN @r_email
        OR valor IN @r_email
        OR valor2 IN @r_email
        OR atributo1 IN @r_email
        OR atributo2 IN @r_email.

    CLEAR l_listado.
    l_listado-origen = 'PARAMETROS'.
    l_listado-tabla  = 'ZPARAMETROS'.

    LOOP AT i_param ASSIGNING FIELD-SYMBOL(<param>).
      l_listado-clave = <param>(108).
      IF NOT <param>-campo IS INITIAL.
        IF <param>-campo IN r_email.
          l_listado-campo = 'CAMPO'.
          l_listado-valor = <param>-campo.
          APPEND l_listado TO i_listado.
        ENDIF.
      ENDIF.
      IF NOT <param>-valor IS INITIAL.
        IF <param>-valor IN r_email.
          l_listado-campo = 'VALOR'.
          l_listado-valor = <param>-valor.
          APPEND l_listado TO i_listado.
        ENDIF.
      ENDIF.
      IF NOT <param>-valor2 IS INITIAL.
        IF <param>-valor2 IN r_email.
          l_listado-campo = 'VALOR2'.
          l_listado-valor = <param>-valor2.
          APPEND l_listado TO i_listado.
        ENDIF.
      ENDIF.
      IF NOT <param>-atributo1 IS INITIAL.
        IF <param>-atributo1 IN r_email.
          l_listado-campo = 'ATRIBUTO1'.
          l_listado-valor = <param>-atributo1.
          APPEND l_listado TO i_listado.
        ENDIF.
      ENDIF.
      IF NOT <param>-atributo2 IS INITIAL.
        IF <param>-atributo2 IN r_email.
          l_listado-campo = 'ATRIBUTO2'.
          l_listado-valor = <param>-atributo2.
          APPEND l_listado TO i_listado.
        ENDIF.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.


  METHOD buscar_tablas.
    DATA: l_listado TYPE t_listado,
          var       TYPE REF TO data,
          tabla     TYPE REF TO data.
    FIELD-SYMBOLS: <fs>        TYPE any,
                   <contenido> TYPE STANDARD TABLE.

    LOOP AT s_tabla ASSIGNING FIELD-SYMBOL(<tabla>).
      zcl_ap_fs=>create_it_from_struc( EXPORTING i_struc = <tabla>-low
                                       IMPORTING e_table = tabla e_workarea = var ).

      ASSIGN tabla->* TO <contenido>.
      ASSIGN var->* TO <fs>.

      SELECT * FROM (<tabla>-low)
        INTO TABLE <contenido>.

      LOOP AT <contenido> ASSIGNING FIELD-SYMBOL(<cont>).
        string = <cont>.
        DATA(emails) = zcl_ap_regexp=>buscar_patron( string = string patron = '\w+(\.\w+)*@(\w+\.)+(\w{2,4})' ).
        SORT emails.
        DELETE ADJACENT DUPLICATES FROM emails.
        LOOP AT emails ASSIGNING FIELD-SYMBOL(<email>).
          IF <email> IN r_email.
            CLEAR l_listado.
            l_listado-origen = 'TABLA'.
            l_listado-tabla = <tabla>-low.
            l_listado-clave = string.
            l_listado-valor = <email>.
            l_listado-no = 'X'.
            APPEND l_listado TO i_listado.
          ENDIF.
        ENDLOOP.
      ENDLOOP.

*    CLEAR l_listado.
*    l_listado-origen = 'CLASIFICACION'.
*    l_listado-tabla  = 'AUSP'.
*
*    LOOP AT i_ausp ASSIGNING FIELD-SYMBOL(<ausp>).
*      l_listado-clave = <ausp>(71).
*      l_listado-campo = 'ATWRT'.
*      l_listado-valor = <ausp>-atwrt.
*
*      CLEAR: l_listado-origen2, l_listado-clave2.
*      IF NOT  <ausp>-klart IS INITIAL.
*        SELECT SINGLE artxt FROM tclat
*           INTO l_listado-origen2
*          WHERE spras = sy-langu
*           AND klart = <ausp>-klart.
*      ENDIF.
*
*
*      APPEND l_listado TO i_listado.
*    ENDLOOP.
    ENDLOOP.
  ENDMETHOD.


ENDCLASS.                    "REPORT IMPLEMENTATION

*----------------------------------------------------------------------*
* INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

  o_prog = NEW #( status       = 'INICIO_DYN'
                  status_prog  = 'ZAP_STATUS'
                  no_param     = 'X'
                  guardar_logz = 'X' ).

  PERFORM add_button IN PROGRAM zap_status USING 'M01' 'Log'(log) '' ''.

  o_prog->o_alv =  NEW #( status             = 'STANDARD_ALV_DYN'
                          status_prog        = 'ZAP_STATUS'
                          top_of_page_auto   = 'X'
                          top_of_page_titulo = 'X'
                          o_dev              = o_prog ).


  p_vari = o_prog->o_alv->get_default_layout( ).

  o_prog->initialization_i( CHANGING sscrfields = sscrfields ).

AT SELECTION-SCREEN OUTPUT.



AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  p_vari = o_prog->o_alv->get_f4_layout( ).

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file.
  p_file = zcl_ap_ficheros=>popup_select_fichero( file_filter = zcl_ap_ficheros=>c_filtro_xls ).


************************************************************************
* AT SELECTION-SCREEN.
************************************************************************
AT SELECTION-SCREEN.
  CASE sy-ucomm.
    WHEN 'ONLI'.
      o_prog->validar_seleccion_obligatoria( campos_or = 'S_EMAIL,P_FILE' msgty = 'E' ).
    WHEN OTHERS.
      o_prog->at_selection( ).
  ENDCASE.

AT SELECTION-SCREEN ON EXIT-COMMAND.
  o_prog->at_selection( ).

*----------------------------------------------------------------------
* START-OF-SELECTION.
*----------------------------------------------------------------------*
START-OF-SELECTION.

  o_prog->main( ).

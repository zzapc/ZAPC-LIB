************************************************************************
* TITULO      : Include para manejo de SAPGUI_PROGRESS_INDICATOR
* DESCRIPCION :                                                        *
* AUTOR       : Andrés Picazo                   FECHA: 25/11/99        *
* OT : D01K912189
* TAREAS: 474722 - 601875 - ...(include genérico, será utilizado
*                               por más tareas)
*
*  FORM SGPI_REFRESH    > Borra la anterior línea de estado
*  FORM SGPI_TEXTO      > Imprime un texto en la línea de estado
*  FORM SGPI_PORCENTAJE > Muestra texto y porcentaje en la línea de esta
*  FORM SGPI_INI_PORC   > Inicializa los contadores
*  FORM SGPI_PORC_CANT  > Muestra el el porcentaje a intervalos regulare
*
* EJEMPLO:
*
*  data: l_cont type i.
*  describe table i_plan lines l_cont.
*  perform sgpi_ini_porc using l_cont.
*  loop at i_plan.
*    perform sgpi_porc_cant using 'Generando Batch Input' 100.
*    ...
*  endloop.
*
* O TAMBIEN:
*  perform sgpi_ini_porc2 tables i_plan.
*  loop at i_plan.
*    perform sgpi_porc_cant using 'Generando Batch Input' 100.
*    ...
*  endloop.

*----------------------------------------------------------------------*
* Modificado:                                     OT: D01K912239       *
* 451574-230703-APC (Altimia)                                          *
* Comentarios: Si se lanza en fondo, en lugar de la función            *
*              de indicador de progreso, muestra un mensaje en el log  *
************************************************************************
DATA: d_sgpi_cont TYPE i,"Contador
       d_sgpi_aux TYPE i,"Auxiliar para marcar el nuevo salto
       d_sgpi_total TYPE i,"Total de elementos en la tabla
       d_sgpi_commit_work VALUE ''. "Indicador de si realizar commit


*---------------------------------------------------------------------*
*       FORM SGPI_REFRESH                                             *
*---------------------------------------------------------------------*
*       Borra la anterior línea de estado
*---------------------------------------------------------------------*
FORM sgpi_refresh.
  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'.
ENDFORM.

*---------------------------------------------------------------------*
*       FORM SGPI_TEXTO                                               *
*---------------------------------------------------------------------*
*       Imprime un texto en la línea de estado
*---------------------------------------------------------------------*
*  -->  PE_TEXTO                                                      *
*---------------------------------------------------------------------*
FORM sgpi_texto USING pe_texto.

  IF sy-batch IS INITIAL.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
         EXPORTING
              text   = pe_texto
         EXCEPTIONS
              OTHERS = 1.
  ELSE.
    MESSAGE i003(aq) WITH pe_texto.
    IF d_sgpi_commit_work = 'X'.
      commit work AND wait.
    ENDIF.
  ENDIF.


ENDFORM.

*---------------------------------------------------------------------*
*       FORM SGPI_TEXTO2                                              *
*---------------------------------------------------------------------*
*       Imprime un texto en la línea de estado con 2 parámetros
*---------------------------------------------------------------------*
*  -->  PE_TEXTO                                                      *
*---------------------------------------------------------------------*
FORM sgpi_texto2 USING pe_texto1 pe_texto2.
  DATA l_linea(100).

  CONCATENATE pe_texto1 pe_texto2 INTO l_linea SEPARATED BY space.
  IF sy-batch IS INITIAL.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
         EXPORTING
              text   = l_linea
         EXCEPTIONS
              OTHERS = 1.
  ELSE.
    MESSAGE i003(aq) WITH l_linea.
    IF d_sgpi_commit_work = 'X'.
      commit work AND wait.
    ENDIF.
  ENDIF.

ENDFORM.

*---------------------------------------------------------------------*
*       FORM SGPI_TEXTO2                                              *
*---------------------------------------------------------------------*
*       Imprime un texto en la línea de estado con 2 parámetros
*---------------------------------------------------------------------*
*  -->  PE_TEXTO                                                      *
*---------------------------------------------------------------------*
FORM sgpi_texto3 USING pe_texto1 pe_texto2 pe_texto3.
  DATA l_linea(100).

  IF sy-batch IS INITIAL.
    CONCATENATE pe_texto1 pe_texto2 pe_texto3
                                    INTO l_linea SEPARATED BY space.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
         EXPORTING
              text   = l_linea
         EXCEPTIONS
              OTHERS = 1.
  ELSE.
    MESSAGE i003(aq) WITH l_linea.
    IF d_sgpi_commit_work = 'X'.
      commit work AND wait.
    ENDIF.
  ENDIF.

ENDFORM.

*---------------------------------------------------------------------*
*       FORM SGPI_PORCENTAJE                                          *
*---------------------------------------------------------------------*
*       Muestra texto y porcentaje en la línea de estado
*---------------------------------------------------------------------*
*  -->  PE_TEXTO                                                      *
*  -->  PE_PORC                                                       *
*---------------------------------------------------------------------*
FORM sgpi_porcentaje USING pe_texto
                            pe_porc.

  IF sy-batch IS INITIAL.
    PERFORM sgpi_refresh.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
         EXPORTING
              percentage = pe_porc
              text       = pe_texto
         EXCEPTIONS
              OTHERS     = 1.
  ELSE.
    MESSAGE i003(aq) WITH pe_texto pe_porc '%'.
    IF d_sgpi_commit_work = 'X'.
      commit work AND wait.
    ENDIF.
  ENDIF.

ENDFORM.

*---------------------------------------------------------------------*
*       FORM SGPI_INI_PORC                                            *
*---------------------------------------------------------------------*
*       Inicializa los contadores utilizados por el form
*       SGPI_PORC_CANT
*---------------------------------------------------------------------*
*  -->  PE_TOTAL  Nº total de registros
*---------------------------------------------------------------------*
FORM sgpi_ini_porc USING pe_total.

  CLEAR: d_sgpi_cont,
          d_sgpi_aux.

  d_sgpi_total = pe_total.

ENDFORM.

*---------------------------------------------------------------------*
*       FORM SGPI_INI_PORC2                                           *
*---------------------------------------------------------------------*
*       Inicializa los contadores utilizados por el form
*       SGPI_PORC_CANT
*---------------------------------------------------------------------*
*  -->  PE_TABLA - Tabla con los registros a procesar.
*---------------------------------------------------------------------*
FORM sgpi_ini_porc2 TABLES pe_tabla.
  DATA l_cont TYPE i.
  CLEAR: d_sgpi_cont,
          d_sgpi_aux.

  DESCRIBE TABLE pe_tabla LINES l_cont.
  d_sgpi_total = l_cont.

ENDFORM.


*---------------------------------------------------------------------*
*       FORM SGPI_PORC_CANT                                           *
*---------------------------------------------------------------------*
*       Muestra el el porcentaje a intervalos regulares
*---------------------------------------------------------------------*
*  -->  PE_TEXTO  Texto
*  -->  PE_CANT   Cantidad a la cual mostrar el texto
*---------------------------------------------------------------------*
FORM sgpi_porc_cant USING pe_texto
                           pe_cant.
  DATA l_porc TYPE i.

  CHECK d_sgpi_total > 0.
  ADD 1 TO d_sgpi_cont.
  l_porc = ( d_sgpi_cont * 100 ) / d_sgpi_total.
  IF d_sgpi_cont > d_sgpi_aux.
    PERFORM sgpi_porcentaje USING pe_texto
                                   l_porc.
    ADD pe_cant TO d_sgpi_aux.
  ENDIF.

ENDFORM.

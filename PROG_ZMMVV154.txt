***********************************************************************
* TIPO : LISTADO
* TITULO : Automatización lanzamiento COGI
* DESCRIPCION : Automatización lanzamiento COGI
*
* AUTOR: Andrés Picazo                                FECHA: 07/02/2024
* ANALISTA: Lucía Rodriguez
*
***********************************************************************
REPORT zmmvv154.

*------INCLUDES--------------------------------------------------------*

*------TABLAS/ESTRUCTURAS----------------------------------------------*
TABLES: mseg, affw.

*------TABLAS INTERNAS-------------------------------------------------*

*----------------------------------------------------------------------*
* CLASS lcl_alv DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv DEFINITION INHERITING FROM zcl_ap_alv_check.
  PUBLIC SECTION.
    METHODS: handle_double_click REDEFINITION.
    METHODS: handle_user_command REDEFINITION.

ENDCLASS. "lcl_alv DEFINITION

*----------------------------------------------------------------------*
*       CLASS zcl_report DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report DEFINITION INHERITING FROM zcl_ap_dev.
  PUBLIC SECTION.
    TYPES: BEGIN OF t_listado,
             check   TYPE xfeld,
             lights  TYPE zico_estado_mensaje,
             clave   TYPE string,
             matnr   TYPE matnr,
             maktx   TYPE maktx,
             werks   TYPE werks_d,
             lgort   TYPE lgort_d,
             meins   TYPE mara-meins,
             sobkz   TYPE mseg-sobkz,
             kdauf   TYPE mseg-kdauf,
             kdpos   TYPE mseg-kdpos,
             altas   TYPE mseg-menge,
             labst   TYPE mard-labst,
             cogi    TYPE mard-labst,
             message TYPE bapi_msg,
           END OF t_listado,
           tt_listado TYPE TABLE OF t_listado.
    DATA: i_listado TYPE tt_listado,
          l_listado TYPE t_listado.

    TYPES: BEGIN OF t_detalle,
             check   TYPE xfeld,
             clave   TYPE string,
             matnr   TYPE matnr,
             maktx   TYPE maktx,
             meins   TYPE mara-meins,
             werks   TYPE werks_d,
             lgort   TYPE lgort_d,
             sobkz   TYPE mseg-sobkz,
             kdauf   TYPE mseg-kdauf,
             kdpos   TYPE mseg-kdpos,
             weblnr  TYPE affw-weblnr,
             weblpos TYPE affw-weblpos,
             aufnr   TYPE affw-aufnr,
             bwart   TYPE affw-bwart,
             erfmg   TYPE affw-erfmg,
             erfme   TYPE affw-erfme,
             ersda   TYPE affw-ersda,
             ernam   TYPE affw-ernam,
             fwdat   TYPE affw-fwdat,
             msgid   TYPE affw-msgid,
             msgno   TYPE affw-msgno,
             msgty   TYPE affw-msgty,
             msgv1   TYPE affw-msgv1,
             msgv2   TYPE affw-msgv2,
             msgv3   TYPE affw-msgv3,
             msgv4   TYPE affw-msgv4,
             message TYPE bapi_msg,
           END OF t_detalle,
           tt_detalle TYPE TABLE OF t_detalle.
    DATA: i_detalle TYPE tt_detalle,
          l_detalle TYPE t_detalle.
    DATA: i_detalle_todo TYPE tt_detalle,
          v_detalle_init.

    METHODS: main.

    METHODS:  listado,
      listado_detalle IMPORTING clave TYPE any OPTIONAL,
      seleccionar_datos,
      ejecutar_cogi CHANGING list TYPE t_listado.

ENDCLASS.                    "REPORT DEFINITION


*------VARIABLES-------------------------------------------------------*
DATA: o_prog TYPE REF TO zcl_report,
      o_alv  TYPE REF TO lcl_alv,
      o_alvd TYPE REF TO lcl_alv.

*------PARAMETER/SELECT-OPTIONS EN PANTALLA----------------------------*
SELECTION-SCREEN BEGIN OF BLOCK mov WITH FRAME TITLE TEXT-mov.
SELECT-OPTIONS: s_matnr FOR mseg-matnr,
                s_budat FOR mseg-budat_mkpf,
                s_werks FOR mseg-werks DEFAULT '0002',
                s_lgort FOR mseg-lgort,
                s_kdauf FOR mseg-kdauf.
SELECTION-SCREEN END OF BLOCK mov.
SELECTION-SCREEN BEGIN OF BLOCK cog WITH FRAME TITLE TEXT-cog.
SELECT-OPTIONS: s_fwdat FOR affw-fwdat,
                s_aufnr FOR affw-aufnr NO-DISPLAY.
SELECTION-SCREEN END OF BLOCK cog.
SELECTION-SCREEN BEGIN OF BLOCK opc WITH FRAME TITLE TEXT-opc.
PARAMETERS: p_budat TYPE mkpf-budat OBLIGATORY DEFAULT sy-datum MODIF ID lis,
            p_ejec  AS CHECKBOX MODIF ID lis,
            p_email TYPE text255 MODIF ID lis.
SELECTION-SCREEN: SKIP 1.

PARAMETERS: p_list RADIOBUTTON GROUP g USER-COMMAND g DEFAULT 'X',
            p_deta RADIOBUTTON GROUP g.
SELECTION-SCREEN: SKIP 1.
PARAMETERS: p_varil LIKE disvariant-variant MODIF ID lis,
            p_varid LIKE disvariant-variant MODIF ID det.
SELECTION-SCREEN END OF BLOCK opc.
__botones_plantilla.



************************************************************************
*
* LOGICA DEL PROGRAMA
*
************************************************************************

*----------------------------------------------------------------------*
* CLASS lcl_alv IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv IMPLEMENTATION.
  METHOD handle_double_click.
    IF nombre_tabla CS 'LISTADO'.
      READ TABLE o_prog->i_listado INTO o_prog->l_listado INDEX row.
      IF sy-subrc = 0.
        CASE column.
          WHEN 'ALTAS'.
            DATA r_mov TYPE RANGE OF bwart.
            r_mov = VALUE #( ( option = 'BT' sign = 'I' low = '101' high = '102' ) ).
            SUBMIT rm07docs
                 WITH matnr = o_prog->l_listado-matnr
                 WITH werks = o_prog->l_listado-werks
                 WITH lgort = o_prog->l_listado-lgort
                 WITH budat IN s_budat
                 WITH bwart IN r_mov
                 AND RETURN.
          WHEN 'LABST'.
            zcl_material=>ver_stocks( matnr = o_prog->l_listado-matnr
                                      werks = o_prog->l_listado-werks
                                      lgort = o_prog->l_listado-lgort ).
          WHEN OTHERS.
            o_prog->listado_detalle( o_prog->l_listado-clave ).
        ENDCASE.
      ENDIF.
    ELSE.
      READ TABLE o_prog->i_detalle INTO o_prog->l_detalle INDEX row.
      IF sy-subrc = 0.
      ENDIF.
    ENDIF.
  ENDMETHOD. "handle_double_click
  METHOD handle_user_command.
    check_ucomm_sel = 'COGI'.

    super->handle_user_command( e_salv_function ).

    CASE ucomm.
      WHEN 'COGI'.
        LOOP AT o_prog->i_listado ASSIGNING FIELD-SYMBOL(<listado>) WHERE check = 'X'. "#EC CI_STDSEQ
          o_prog->ejecutar_cogi( CHANGING list = <listado> ).
        ENDLOOP.
        IF sy-subrc NE 0.
          MESSAGE 'Seleccione algún registro' TYPE 'I'.
        ELSE.
          refresh( ).
        ENDIF.

      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD. "handle_USER_COMMAND


ENDCLASS. "lcl_alv IMPLEMENTATION

*----------------------------------------------------------------------*
*       CLASS zcl_report IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report IMPLEMENTATION.
  METHOD main.

    seleccionar_datos( ).

    IF p_list = 'X'.
      IF p_ejec = 'X'.
        LOOP AT i_listado ASSIGNING FIELD-SYMBOL(<listado>).
          o_prog->ejecutar_cogi( CHANGING list = <listado> ).
          IF <listado>-lights(3) = icon_red_light(3).
            DATA(l_errores) = 'X'.
          ENDIF.
        ENDLOOP.
      ENDIF.
      listado( ).

      IF NOT p_email IS INITIAL AND NOT l_errores IS INITIAL.
        DATA(l_asunto) = |Errores en ajecución automática COGI { sy-datum  DATE = USER }|.
        DATA(l_nombre) = |Resultado ejecucion COGI.xlsx|.
        o_alv->set_field_quitar( 'LIGHTS' ).
        zcl_ap_abap2xls=>mail( direccion = p_email
                               subject = l_asunto
                               o_alv1 = o_alv
                               tabla_alv1 = i_listado
                               nombre_alv1 = l_nombre ).
      ENDIF.

    ELSE.
      listado_detalle( ).
    ENDIF.

  ENDMETHOD.                    "REPORT

  METHOD seleccionar_datos.
    DATA: l_listado TYPE t_listado,
          l_detalle TYPE t_detalle.

    sgpi_texto( 'Seleccionando datos' ).
    SELECT matnr, werks, lgort, SUM( menge ) AS altas, meins, bwart, sobkz, kdauf, kdpos FROM mseg
      INTO TABLE @DATA(i_mseg)
     WHERE budat_mkpf IN @s_budat
       AND matnr IN @s_matnr
       AND werks IN @s_werks
       AND lgort IN @s_lgort
       AND kdauf IN @s_kdauf
       AND bwart IN ('101', '102')
     GROUP BY matnr, werks, lgort, meins, bwart, sobkz, kdauf, kdpos
      ORDER BY matnr, werks, lgort, meins, bwart, sobkz, kdauf, kdpos.


    o_prog->o_sgpi->get_filas_tabla( i_mseg[] ).
    LOOP AT i_mseg ASSIGNING FIELD-SYMBOL(<mseg>).
      sgpi_texto( texto1 = 'Procesando datos' cant_porc = 100 ).
      CLEAR l_listado.
      DATA(l_umb) = zcl_material=>get_unidad_base( <mseg>-matnr ).
      IF l_umb NE <mseg>-meins.
        <mseg>-altas = zcl_material=>convertir_unidad( matnr = <mseg>-matnr cantidad = <mseg>-altas unidad_origen = <mseg>-meins unidad_destino = l_umb ).
        <mseg>-meins = l_umb.
      ENDIF.

      IF <mseg>-bwart = '102'.
        <mseg>-altas = - <mseg>-altas.
      ENDIF.
      MOVE-CORRESPONDING <mseg> TO l_listado.
      COLLECT l_listado INTO i_listado.
    ENDLOOP.
    DELETE i_listado WHERE altas = 0.

    LOOP AT i_listado ASSIGNING FIELD-SYMBOL(<listado>).
      <listado>-clave = <listado>-matnr && <listado>-werks && <listado>-lgort && <listado>-sobkz && <listado>-kdauf && <listado>-kdpos.
      <listado>-maktx = get( tabla = 'MAKT' clave = <listado>-matnr ).
      IF <listado>-sobkz = 'E'.
        SELECT SINGLE kalab FROM mska
          INTO <listado>-labst
         WHERE matnr = <listado>-matnr
           AND werks = <listado>-werks
           AND lgort = <listado>-lgort
           AND sobkz = <listado>-sobkz
           AND vbeln = <listado>-kdauf
           AND posnr = <listado>-kdpos.
      ELSE.
        SELECT SINGLE labst FROM mard
          INTO <listado>-labst
         WHERE matnr = <listado>-matnr
           AND werks = <listado>-werks
           AND lgort = <listado>-lgort.
      ENDIF.

      IF <listado>-labst IS INITIAL.
        DELETE i_listado.
        CONTINUE.
      ENDIF.

      SELECT * FROM affw
        INTO TABLE @DATA(i_affw)
       WHERE matnr = @<listado>-matnr
         AND werks = @<listado>-werks
         AND lgort = @<listado>-lgort
         AND sobkz = @<listado>-sobkz
         AND kdauf = @<listado>-kdauf
         AND kdpos = @<listado>-kdpos
         AND fwdat IN @s_fwdat
         AND aufnr IN @s_aufnr.
      IF sy-subrc NE 0.
        DELETE i_listado.
        CONTINUE.
      ENDIF.

      LOOP AT i_affw ASSIGNING FIELD-SYMBOL(<affw>).
        CLEAR l_detalle.
        MOVE-CORRESPONDING <listado> TO l_detalle.
        MOVE-CORRESPONDING <affw> TO l_detalle.

        IF NOT <affw>-msgid IS INITIAL.
          MESSAGE ID <affw>-msgid TYPE 'S' NUMBER <affw>-msgno WITH <affw>-msgv1 <affw>-msgv2 <affw>-msgv3 <affw>-msgv4 INTO l_detalle-message.
        ENDIF.

        ADD <affw>-erfmg TO <listado>-cogi.
        APPEND l_detalle TO i_detalle_todo.
      ENDLOOP.
    ENDLOOP.

  ENDMETHOD.                    "seleccionar_datos


  METHOD listado.

    sgpi_texto( 'Generando informe' ).

    o_alv->add_button( button = 'F01' text = 'Ejecutar'  icon = '@15@' qinfo = 'Ejecutar' ucomm = 'COGI' ).
    o_alv->add_button( button = 'M01' text = 'LOG'  icon = '@15@' qinfo = 'Log' ).

    o_alv->set_layout( p_varil ).
    o_alv->get_datos_layout( EXPORTING reordenar_tabla = 'X' CHANGING t_tabla = i_listado ).
    o_alv->set_top_of_page( ).

    o_alv->set_field_quitar( 'CLAVE,CHECK' ).
    o_alv->set_field( campo = 'LIGHTS' op = 'KEY' ).
    o_alv->set_field_text( 'ALTAS' ).
    o_alv->set_field_text( campo = 'COGI' valor = 'COGI' ).

    o_alv->show( ).
    o_alv->set_seleccion( CHANGING t_tabla = i_listado ).

  ENDMETHOD.                    "listado

  METHOD listado_detalle.
    FIELD-SYMBOLS <detalle> TYPE t_detalle.

    sgpi_texto( 'Generando informe' ).

    CLEAR i_detalle.
    IF clave IS INITIAL.
      i_detalle = i_detalle_todo.
    ELSE.
      LOOP AT i_detalle_todo ASSIGNING <detalle> WHERE clave = clave. "#EC CI_STDSEQ
        APPEND <detalle> TO i_detalle.
      ENDLOOP.
    ENDIF.

    IF v_detalle_init IS INITIAL.
      v_detalle_init = 'X'.
      o_alvd->set_layout( p_varid ).
      o_alvd->get_datos_layout( EXPORTING reordenar_tabla = 'X' CHANGING t_tabla = i_detalle ).
      o_alvd->set_top_of_page( ).

      o_alvd->set_field_noout( 'CLAVE,CHECK' ).
    ENDIF.
    o_alvd->show( ).


  ENDMETHOD.


  METHOD ejecutar_cogi.
    DATA: o_bi      TYPE REF TO zcl_ap_batch_input,
          l_mensaje TYPE bapireturn1-message.

    LOOP AT i_detalle_todo ASSIGNING FIELD-SYMBOL(<detalle>) WHERE clave = list-clave.
      CREATE OBJECT o_bi.

      o_bi->inicio( ).

      o_bi->dynpro( program = 'CORUAFFW' dynpro = '1000' okcode = '=ONLI').
      o_bi->campos( campo = 'S_WERKS-LOW' valor = list-werks ).
      o_bi->campos( campo = 'S_LGORT-LOW' valor = list-lgort ).
      o_bi->campos( campo = 'S_MATNR-LOW' valor = list-matnr ).
      o_bi->campos( campo = 'S_BELNR-LOW' valor = <detalle>-weblnr ).
      o_bi->campos( campo = 'S_AUFNR-LOW' valor = <detalle>-aufnr ).

      IF NOT s_fwdat[] IS INITIAL.
        zcl_ap_fechas=>rango2fechas( EXPORTING r_fechas = s_fwdat[]
                                     IMPORTING fecha_desde = DATA(l_fini)
                                               fecha_hasta = DATA(l_ffin)
                                               ).
        o_bi->campos( campo = 'P_DATUV' valor = l_fini ).
        o_bi->campos( campo = 'P_DATUB' valor = l_ffin ).
      ENDIF.
      o_bi->campos( campo = 'P_BKAUF' valor = list-kdauf ).
      o_bi->campos( campo = 'P_BKPOS' valor = list-kdpos ).
      o_bi->campos( campo = 'R_SINGLE' valor = 'X').
      o_bi->campos( campo = 'R_CUMUL' valor = '').
      o_bi->campos( campo = 'P_OLD' valor = 'X').

* .
      o_bi->dynpro( program = 'SAPMSSY0' dynpro = '0120' okcode = '=AMAK').

* .
      o_bi->dynpro( program = 'SAPMSSY0' dynpro = '0120' okcode = '=BUDA').

* Tratami. posterior de mov.mercancíar erróneso: fecha actual.
      o_bi->dynpro( program = 'CORUAFFW' dynpro = '0100' okcode = '=ENTR').
      o_bi->campos( campo = 'AFFWB-BLDAT' valor = p_budat ). " Fecha de documento en documento
      o_bi->campos( campo = 'AFFWB-BUDAT' valor = p_budat ). " Fecha de contabilización en el documento

* .
      o_bi->dynpro( program = 'SAPMSSY0' dynpro = '0120' okcode = '=BU').

      l_mensaje = o_bi->llamar_transaccion( tcode = 'COGI' modo = modo_ct ).

*    LOOP AT i_detalle_todo ASSIGNING FIELD-SYMBOL(<detalle>) WHERE clave = list-clave.
      SELECT SINGLE * FROM affw
        INTO CORRESPONDING FIELDS OF <detalle>
       WHERE weblnr = <detalle>-weblnr
         AND weblpos = <detalle>-weblpos.
      IF sy-subrc NE 0.
        DATA(l_ok) = 'X'.
        SUBTRACT <detalle>-erfmg FROM list-cogi.
      ELSE.
        IF NOT <detalle>-msgid IS INITIAL.
          MESSAGE ID <detalle>-msgid TYPE 'S' NUMBER <detalle>-msgno WITH <detalle>-msgv1 <detalle>-msgv2 <detalle>-msgv3 <detalle>-msgv4 INTO <detalle>-message.
          IF <detalle>-msgty = 'E'.
            DATA(l_error) = <detalle>-message.
          ENDIF.
        ENDIF.
      ENDIF.

    ENDLOOP.

    IF NOT l_error IS INITIAL.
      list-message = l_error.
      list-lights = zcl_ap_alv=>set_icono( icono = icon_red_light mensaje = l_error ).
    ELSE.
      IF l_ok = 'X'.
        list-message = 'Se ha ejecutado la COGI correctamente'.
        list-lights = zcl_ap_alv=>set_icono( icono = icon_green_light mensaje = list-message ).
      ENDIF.
    ENDIF.

  ENDMETHOD.
ENDCLASS.                    "REPORT IMPLEMENTATION

*----------------------------------------------------------------------*
* INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

  CREATE OBJECT o_prog
    EXPORTING
      status        = 'INICIO_DYN'
      get_nombre_pc = 'X'
      no_param      = 'X'
      status_prog   = 'ZAP_STATUS'.

*  PERFORM add_button IN PROGRAM zap_status USING 'M01' 'Log' '' ''.

  CREATE OBJECT o_alv
    EXPORTING
      status             = 'STANDARD_ALV_DYN'
      status_prog        = 'ZAP_STATUS'
      o_dev              = o_prog
      top_of_page_auto   = 'X'
      top_of_page_titulo = 'X'.

  p_varil = o_alv->get_default_layout( ).

  CREATE OBJECT o_alvd
    EXPORTING
      status             = 'STANDARD_ALV_DYN'
      tabla              = 'I_DETALLE'
      top_of_page_auto   = 'X'
      top_of_page_titulo = 'X'.

  p_varid = o_alvd->get_default_layout( ).

  o_prog->initialization_i( CHANGING sscrfields = sscrfields ).
  zcl_ap_dynpro=>set_primer_radiobutton( campos = 'P_LIST,P_DETA' ).

AT SELECTION-SCREEN OUTPUT.

  zcl_ap_dynpro=>screen_visible( group1 = 'LIS' variable = p_list ).
  zcl_ap_dynpro=>screen_visible( group1 = 'DET' variable = p_deta ).


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_varil.
  p_varil = o_alv->get_f4_layout( ).

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_varid.
  p_varid = o_alvd->get_f4_layout( ).

************************************************************************
* AT SELECTION-SCREEN.
************************************************************************
AT SELECTION-SCREEN.

  o_prog->at_selection( ).

AT SELECTION-SCREEN ON EXIT-COMMAND.
  o_prog->at_selection( ).

*----------------------------------------------------------------------
* START-OF-SELECTION.
*----------------------------------------------------------------------*
START-OF-SELECTION.

  o_prog->main( ).
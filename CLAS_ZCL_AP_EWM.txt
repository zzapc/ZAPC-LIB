
class ZCL_AP_EWM definition
  public
  create public .

public section.

  class-methods GET_STOCK
    importing
      !LGNUM type /SCWM/LAGP-LGNUM
      !R_MATNR type /SCWM/TT_MATNR_R optional
      !R_CHARG type /SCWM/TT_CHARG_R optional
      !R_LGTYP type /SCWM/TT_LGTYP_R optional
      !R_HUIDENT type /SCWM/TT_HUIDENT_R optional
      !R_CAT type /SCWM/TT_CAT_R optional
      !EXIDV type EXIDV
    returning
      value(I_STOCK) type /SCWM/TT_STOCK_MON .
  class-methods GET_STOCK_SIMPLE
    importing
      !LGNUM type /SCWM/LAGP-LGNUM
      !R_MATNR type /SCWM/TT_MATNR_R optional
      !R_CHARG type /SCWM/TT_CHARG_R optional
      !R_LGTYP type /SCWM/TT_LGTYP_R optional
      !R_HUIDENT type /SCWM/TT_HUIDENT_R optional
      !R_CAT type /SCWM/TT_CAT_R optional
      !EXIDV type EXIDV
    returning
      value(STOCK) type /SCWM/S_STOCK_MON .
  class-methods CONTABILIZAR_SM
    importing
      !LGNUM type /SCWM/LGNUM
      !EXIDV type EXIDV default ''
      !REASON type /LIME/REASON default ''
      !REASON_WM type /SCWM/DE_REASON default ''
      !UMLGO type UMLGO default ''
      !O_LOG type ref to ZCL_AP_LOG optional
    exporting
      !TANUM type /SCWM/TANUM
      !MESSAGE type BAPI_MSG
      !I_RETURN type BAPIRETTAB .
  class-methods BORRAR_HU
    importing
      !LGNUM type /SCWM/LGNUM
      !EXIDV type EXIDV
    returning
      value(MESSAGE) type BAPI_MSG .
  class-methods MATID_2_MATNR
    importing
      !MATID type ANY
    returning
      value(MATNR) type MATNR .
protected section.
private section.
endclass. "ZCL_AP_EWM definition
class ZCL_AP_EWM implementation.
  METHOD borrar_hu.
* COPIADO DE /SCWM/HU_DELETE_MON, PERO SIN EL POPUP DE CONFIRMACION

    DATA: ls_hu_mon   TYPE /scwm/s_huhdr_mon,
          lt_hu_mon   TYPE /scwm/tt_huhdr_mon,
          lv_lgnum    TYPE /scwm/lgnum,
          ls_huident  TYPE /scwm/s_huident,
          lt_huident  TYPE /scwm/tt_huident,
          lv_continue TYPE char1,
          lt_huhdr    TYPE /scwm/tt_huhdr_int,
          lv_error    TYPE boole_d.

    DATA: lo_wm_packing   TYPE REF TO /scwm/cl_wm_packing.

    FIELD-SYMBOLS: <fs_wo>     TYPE any,
                   <huhdr_mon> TYPE /scwm/s_huhdr_mon,
                   <huhdr>     TYPE /scwm/s_huhdr_int.

    CALL METHOD /scwm/cl_tm=>set_lgnum
      EXPORTING
        iv_lgnum = '0005'.


    APPEND VALUE #( lgnum = lgnum huident = exidv ) TO lt_huident.

    CALL FUNCTION '/SCWM/HU_GT_FILL'
      EXPORTING
        it_huident = lt_huident
      IMPORTING
        et_huhdr   = lt_huhdr
      EXCEPTIONS
        error      = 1
        OTHERS     = 2.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE 'S' NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
              INTO message.
      RETURN.
    ENDIF.

    IF lt_huhdr IS INITIAL.
      message = 'No existen las HUs a borrar'.
      RETURN.
    ENDIF.

    LOOP AT lt_huhdr ASSIGNING <huhdr>.
      READ TABLE lt_huident TRANSPORTING NO FIELDS WITH KEY
                 huident = <huhdr>-huident.
      IF NOT sy-subrc IS INITIAL.
        DELETE lt_huhdr.
        CONTINUE.
      ENDIF.
      IF <huhdr>-vhi = wmegc_vhi_plan.
        MESSAGE s162(/scwm/ui_packing) WITH <huhdr>-huident
                     INTO message.
        RETURN.
      ENDIF.
    ENDLOOP.
    IF lt_huhdr IS INITIAL.
      message = 'No existen HUs válidas a borrar'.
      RETURN.
    ENDIF.


    CREATE OBJECT lo_wm_packing.
    LOOP AT lt_huhdr ASSIGNING <huhdr>.
      CALL METHOD lo_wm_packing->/scwm/if_pack_bas~delete_hu
        EXPORTING
          iv_hu  = <huhdr>-guid_hu
        EXCEPTIONS
          error  = 1
          OTHERS = 2.
      IF sy-subrc <> 0.
        IF sy-msgid = '/SCWM/L3' AND sy-msgno = '054'.
          MESSAGE s083(/scwm/delivery) INTO message.
        ELSE.
          MESSAGE ID sy-msgid TYPE 'S' NUMBER sy-msgno
                   WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
                   INTO message.
        ENDIF.
        lv_error = abap_true.
        EXIT.
      ENDIF.
    ENDLOOP.
    IF lv_error = abap_true.
      RETURN.
    ENDIF.

    CALL METHOD lo_wm_packing->/scwm/if_pack_bas~save
      EXPORTING
        iv_commit = 'X'
        iv_wait   = 'X'
      EXCEPTIONS
        error     = 1
        OTHERS    = 2.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE 'S' NUMBER sy-msgno
                 WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
                 INTO message.
    ENDIF.

  ENDMETHOD.
  METHOD contabilizar_sm.
    DATA iv_call     TYPE char1.
    DATA is_header   TYPE /scwm/s_gmheader.
    DATA it_item     TYPE /scwm/tt_gmitem.
    DATA et_result   TYPE /scwm/tt_spresult.
    DATA ev_severity TYPE bapi_mtype.
    DATA l_item TYPE /scwm/s_gmitem.

    CLEAR: tanum, message, i_return.

    is_header-lgnum = lgnum.
    is_header-created_by = sy-uname.
    is_header-code = '/SCWM/RGI_SCRAP'.
    is_header-post = 'X'.

    l_item-id = '000001'.
    l_item-id_group = '000001'.
    l_item-direction = 'O'.
    l_item-huident = exidv.
    l_item-reason = reason.
    l_item-reason_wm = reason_wm..


    IF NOT exidv IS INITIAL.
      DATA(stock_hu) = get_stock_simple( lgnum = lgnum exidv = exidv ).
      IF stock_hu IS INITIAL.
* Verificamos si tiene hijas para saber dónde están
        SELECT venum FROM vekp
          INTO @DATA(l_venum)
          UP TO 1 ROWS
         WHERE exidv = @exidv
           AND status NE '0060'
         ORDER BY venum DESCENDING.
        ENDSELECT.
        IF sy-subrc = 0 AND NOT l_venum IS INITIAL.
          SELECT unvel FROM vepo
            INTO TABLE @DATA(i_hijas)
           WHERE venum = @l_venum
             AND velin = '3'.
          LOOP AT i_hijas ASSIGNING FIELD-SYMBOL(<hijas>).
            SELECT SINGLE exidv FROM vekp
              INTO @DATA(l_exidv_hija)
             WHERE venum = @<hijas>-unvel.
            IF sy-subrc = 0.
              stock_hu = get_stock_simple( lgnum = lgnum exidv = l_exidv_hija ).
              IF NOT stock_hu-lgtyp IS INITIAL.
                EXIT.
              ENDIF.
            ENDIF.
          ENDLOOP.
        ENDIF.
      ENDIF.

      IF stock_hu IS INITIAL.
        message = |SSCC { exidv  ALPHA = OUT } no tiene stock en WM. No podemos determinar ubicación origen|.
        IF NOT o_log IS INITIAL.
          o_log->log( p1 = message ).
        ENDIF.
        RETURN.
      ENDIF.
      l_item-loc-idx_loc = 'W01'.
      l_item-loc-lgnum  = lgnum.
      l_item-loc-lgtyp = stock_hu-lgtyp.
      l_item-loc-lgpla = stock_hu-lgpla.
      IF NOT o_log IS INITIAL.
        o_log->log( p1 = |Sacamos de { stock_hu-lgtyp } { stock_hu-lgpla } Ctd = { stock_hu-quan } { stock_hu-meins }| msgty = 'I'  ).
      ENDIF.

      APPEND VALUE #( unit = stock_hu-meins quan = stock_hu-quan ) TO l_item-t_quan.

    ENDIF.

    IF NOT umlgo IS INITIAL.
      APPEND VALUE #( param_name = 'NWMLOC' param_value = umlgo param_group = 'SCWM' ) TO l_item-t_param.
    ENDIF.

    APPEND l_item TO it_item.

    TRY.
        CALL FUNCTION '/SCWM/GM_CLEANUP'
* EXPORTING
*   IV_SAVE          = ' '
*   IV_RESTORE       = ' '
*   IV_CLEAR         = ' '
          .


        CALL FUNCTION '/SCWM/GM_CREATE'
          EXPORTING
            iv_call     = ' '
            is_header   = is_header
            it_item     = it_item
          IMPORTING
            ev_tanum    = tanum
            et_result   = et_result
            et_bapiret  = i_return
            ev_severity = ev_severity.
      CATCH /scwm/cx_core.
    ENDTRY.

    IF tanum IS INITIAL.
      LOOP AT i_return ASSIGNING FIELD-SYMBOL(<ret>).
        __concat_a message <ret>-message.
      ENDLOOP.
    ELSE.
      IF NOT o_log IS INITIAL.
        o_log->log( p1 = 'Se ha creado OT' p2 = tanum msgty = 'S' ).
      ENDIF.
      CALL FUNCTION '/SCWM/GM_POST'.

      COMMIT WORK AND WAIT.
    ENDIF.

    IF NOT o_log IS INITIAL.
      LOOP AT i_return ASSIGNING <ret>.
        o_log->log( msgid = <ret>-id msgno = <ret>-number  msgty = 'S'
                    msgv1 = <ret>-message_v1 msgv2 = <ret>-message_v2
                    msgv3 = <ret>-message_v3 msgv4 = <ret>-message_v4 ).
      ENDLOOP.
    ENDIF.
*
  ENDMETHOD.
  METHOD get_stock.


    DATA(r_hu) = r_huident.
    IF r_hu IS INITIAL AND NOT exidv IS INITIAL.
      APPEND VALUE #( option = 'EQ' sign = 'I' low = exidv ) TO r_hu.
    ENDIF.


    TRY.
        NEW /scwm/cl_mon_stock(
          iv_lgnum        = lgnum
*      is_read_options =
        )->get_physical_stock(
          EXPORTING
            iv_drill_down    = abap_false       " Drill Down from Available Stock
*        iv_skip_bin      = abap_false       " Get No Stock on Storage Bins
            iv_skip_resource = abap_true       " Get No Stock on Resources
            iv_skip_tu       = abap_true       " Get No Stock on Transportation Units
*        iv_bbd           = abap_false       " Called from BBD query
*        iv_kit           = abap_false       " Products are KITs
            iv_skip_logpos   = abap_false                 " Get no LOGPOS for stock
            it_matnr_r       = r_matnr                 " Range Table Type for Product
            it_cat_r         = r_cat                 " RANGES Table Type for Field Name CAT
*        it_owner_r       =                  " RANGES Table Type for Field Name OWNER
*        it_entitled_r    =                  " RANGES Table Type for Field Name ENTITLED
            it_charg_r       = r_charg                 " Range Table Type for Batch
*        it_idplate_r     =                  " Range Table for Stock Identification Number
*        it_serid_r       =                  " Serial Number Interval
            it_huident_r     = r_hu                 " Ranges Table Type for Field Name HUIDENT
            it_lgtyp_r       = r_lgtyp                 " Range Table Type for Field Name LGTYP
*        it_lgber_r       =                  " Range Table Type for Storage Section
*        it_lgpla_r       =                  " Range Table Type for Field Name LGPLA
*        it_lptyp_r       =                  " Range Table Type for Storage Bin Type
*        it_aisle_r       =                  " Range Table Type for Aisle in a Storage Bin
*        it_stack_r       =                  " Range Table Type for Stack in a Storage Bin
*        it_level_r       =                  " Range Table Type for Level in a Storage Bin
*        it_binsc_r       =                  " Range Table Type for Bin Section of a Storage Bin
*        it_depth_r       =                  " Range Table Type for Bin Depth
*        it_psa_r         =                  " Range for PSA
*        it_rsrc_r        =                  " Range Table Type for Field Name RSRC
*        it_tu_num_ext_r  =                  " Selection: External Transportation Unit Number
*        it_tsp_r         =                  " Selection: Transportation Service Provider
          IMPORTING
            et_stock_mon     =  i_stock                " Stock in Warehouse Monitor
            ev_error         =  DATA(lv_err)
         ).
      CATCH cx_root INTO DATA(o_root).

    ENDTRY.
    IF sy-msgty = 'E' OR sy-msgno = '036'. "Ha veces se retorna un mensaje de error raro
      MESSAGE s398(00) WITH ''.
    ENDIF.

  ENDMETHOD.
  METHOD get_stock_simple.

    IF r_matnr IS INITIAL AND r_charg IS INITIAL AND r_huident IS INITIAL AND exidv IS INITIAL.
      RETURN.
    ENDIF.

    DATA(i_stock) = get_stock( lgnum     = lgnum
                               r_matnr   = r_matnr
                               r_charg   = r_charg
                               r_lgtyp   = r_lgtyp
                               r_huident = r_huident
                               r_cat     = r_cat
                               exidv     = exidv ).

    READ TABLE i_stock INTO stock INDEX 1.

  ENDMETHOD.
  METHOD MATID_2_MATNR.

    CALL FUNCTION 'CONVERSION_EXIT_MDLPD_OUTPUT'
      EXPORTING
        input  = matid
      IMPORTING
        output = matnr.

    IF NOT matnr IS INITIAL.
      __formatear_material matnr.
    ENDIF.

  ENDMETHOD.

*----------------------------------------------------------------------*
*       CLASS ZCL_AP_MGRAPH  DEFINITIO
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
class ZCL_AP_MGRAPH definition
  public
  final
  create public .

public section.

  types:
    BEGIN OF t_attachment,
             odata_type             TYPE string,
             odata_mediacontenttype TYPE string,
             id                     TYPE string,
             lastmodifieddatetime   TYPE string,
             name                   TYPE string,
             contenttype            TYPE string,
             size                   TYPE int4,
             isinline               TYPE xfeld,
             contentid              TYPE string,
             contentlocation        TYPE string,
             contentbytes           TYPE string,
           END OF t_attachment .
  types:
    tt_attachments TYPE STANDARD TABLE OF t_attachment WITH KEY id .
  types:
    BEGIN OF t_application,
             id          TYPE string,
             displayname TYPE string,
     END OF t_application .
  types:
    BEGIN OF t_user,
             email       TYPE string,
             id          TYPE string,
             displayname TYPE string,
      END OF t_user .
  types:
    BEGIN OF t_app_user,
             application TYPE t_application,
             user        TYPE t_user,
      END OF t_app_user .
  types:
    BEGIN OF t_file_reference,
             drivetype TYPE string,
             driveid   TYPE string,
             id        TYPE string,
             path      TYPE string,
             siteid    TYPE string,
      END OF t_file_reference .
  types:
    BEGIN OF t_hashes,
            quickxorhash TYPE string,
      END OF t_hashes .
  types:
    tt_hashes TYPE STANDARD TABLE OF t_hashes WITH KEY quickxorhash .
  types:
    BEGIN OF t_file_mime,
             mimetype TYPE string,
             hashes   TYPE tt_hashes,
      END OF t_file_mime .
  types:
    BEGIN OF t_filesysteminfo,
             createddatetime      TYPE string,
             lastmodifieddatetime TYPE string,
      END OF t_filesysteminfo .
  types:
    BEGIN OF t_info_fichero,
             createddatetime      TYPE string,
             etag                 TYPE string,
             id                   TYPE string,
             lastmodifieddatetime TYPE string,
             name                 TYPE string,
             weburl               TYPE string,
             ctag                 TYPE string,
             size                 TYPE string,
             createdby            TYPE t_app_user,
             lastmodifiedby       TYPE t_user,
             parentreference      TYPE t_file_reference,
             file                 TYPE t_file_mime,
             filesysteminfo       TYPE t_filesysteminfo,
      END OF t_info_fichero .
  types:
    BEGIN OF t_info_xlsx,
             id         TYPE string,
             name       TYPE string,
             position   TYPE int4,
             visibility TYPE string,
      END OF t_info_xlsx .
  types:
    tt_info_xlsx TYPE STANDARD TABLE OF t_info_xlsx WITH KEY id .
  types:
    BEGIN OF t_info_xlsx_t,
            value TYPE tt_info_xlsx,
      END OF t_info_xlsx_t .
  types ABAP_BOOL type XFELD .
  types:
    BEGIN OF t_token_data,
             token_type     TYPE string,
             expires_in     TYPE int4,
             ext_expires_in TYPE int4,
             access_token   TYPE string,
      END OF t_token_data .
  types:
    BEGIN OF t_innererror,
             date            TYPE string,
             requestid       TYPE string,
             clientrequestid TYPE string,
      END OF t_innererror .
  types:
    BEGIN OF t_error_cod,
             code       TYPE string,
             message    TYPE string,
             innererror TYPE t_innererror,
      END OF t_error_cod .
  types:
    BEGIN OF t_error,
            error TYPE t_error_cod,
      END OF t_error .
  types:
    BEGIN OF t_email_add,
             name    TYPE string,
             address TYPE string,
      END OF t_email_add .
  types:
    BEGIN OF t_email_address,
            emailaddress TYPE t_email_add,
      END OF t_email_address .
  types:
    tt_email_address TYPE STANDARD TABLE OF t_email_address WITH KEY emailaddress .
  types:
    BEGIN OF t_body,
             contenttype TYPE string,
             content     TYPE string,
      END OF t_body .
  types:
    BEGIN OF t_datetime,
             datetime TYPE string,
             timezone TYPE string,
      END OF t_datetime .
  types:
    BEGIN OF t_flag_status,
             flagstatus        TYPE string,  "flagged / compleet
             completeddatetime TYPE t_datetime,
      END OF t_flag_status .
  types:
    BEGIN OF t_email,
             id                         TYPE string,
             createddatetime            TYPE string,
             lastmodifieddatetime       TYPE string,
             changekey                  TYPE string,
             categories                 TYPE table_of_strings,
             receiveddatetime           TYPE string,
             sentdatetime               TYPE string,
             internetmessageid          TYPE string,
             subject                    TYPE string,
             bodypreview                TYPE string,
             importance                 TYPE string,
             parentfolderid             TYPE string,
             conversationid             TYPE string,
             conversationindex          TYPE string,
             isdeliveryreceiptrequested TYPE abap_bool,
             isreadreceiptrequested     TYPE abap_bool,
             hasattachments             TYPE abap_bool,
             idread                     TYPE abap_bool,
             isdraft                    TYPE string,
             weblink                    TYPE string,
             body                       TYPE t_body,
             sender                     TYPE t_email_address,
             from                       TYPE t_email_address,
             torecipients               TYPE tt_email_address,
             ccrecipients               TYPE tt_email_address,
             bcrecipients               TYPE tt_email_address,
             flag                       TYPE t_flag_status,
             fecha_creacion             TYPE erdat,
             hora_creacion              TYPE erzet,
      END OF t_email .
  types:
    tt_email TYPE STANDARD TABLE OF t_email WITH KEY id .
  types:
    BEGIN OF t_workbook_range,
             address        TYPE string,
             address_local  TYPE string,
             column_count   TYPE i,
             cell_count     TYPE i,
             column_hidden  TYPE abap_bool,
             row_hidden     TYPE abap_bool,
             number_format  TYPE table_of_strings,
             column_index   TYPE i,
             text           TYPE table_of_strings,
             formulas       TYPE table_of_strings,
             formulas_local TYPE table_of_strings,
             formulas_r1c1  TYPE table_of_strings,
             hidden         TYPE abap_bool,
             row_count      TYPE i,
             row_index      TYPE i,
             value_types    TYPE table_of_strings,
             values         TYPE table_of_strings,
      END OF t_workbook_range .
  types:
    BEGIN OF t_workbook_values,
             fila    TYPE int4,
             columna TYPE int4,
             valor   TYPE string,
      END OF t_workbook_values .
  types:
    tt_workbook_values TYPE STANDARD TABLE OF t_workbook_values WITH KEY fila columna .
  types:
    BEGIN OF ty_responsestatus,
             response TYPE string,
             time     TYPE string,
      END OF ty_responsestatus .
  types:
    BEGIN OF ty_emailaddress,
             name    TYPE string,
             address TYPE string,
      END OF ty_emailaddress .
  types:
    BEGIN OF ty_attendees,
             type         TYPE string,
             status       TYPE ty_responsestatus,
             emailaddress TYPE ty_emailaddress,
      END OF ty_attendees .
  types:
    tt_attendees TYPE STANDARD TABLE OF ty_attendees WITH NON-UNIQUE KEY type .
  types:
    BEGIN OF ty_location,
             displayname  TYPE string,
             locationtype TYPE string,
             uniqueid     TYPE string,
             uniqueidtype TYPE string,
      END OF ty_location .
  types:
    tt_location TYPE STANDARD TABLE OF ty_location WITH NON-UNIQUE KEY displayname .
  types:
    BEGIN OF ty_event,
             etag                       TYPE string,
             id                         TYPE string,
             createddatetime            TYPE string,
             lastmodifieddatetime       TYPE string,
             changekey                  TYPE string,
             categories                 TYPE table_of_strings,
             transactionid              TYPE string,
             originalstarttimezone      TYPE string,
             originalendtimezone        TYPE string,
             icaluid                    TYPE string,
             uid                        TYPE string,
             reminderminutesbeforestart TYPE i,
             isreminderon               TYPE abap_bool,
             hasattachments             TYPE abap_bool,
             subject                    TYPE string,
             bodypreview                TYPE string,
             importance                 TYPE string,
             sensitivity                TYPE string,
             isallday                   TYPE abap_bool,
             iscancelled                TYPE abap_bool,
             isorganizer                TYPE abap_bool,
             responserequested          TYPE abap_bool,
             seriesmasterid             TYPE string,
             showas                     TYPE string,
             type                       TYPE string,
             weblink                    TYPE string,
             onlinemeetingurl           TYPE string,
             isonlinemeeting            TYPE abap_bool,
             onlinemeetingprovider      TYPE string,
             allownewtimeproposals      TYPE abap_bool,
             occurrenceid               TYPE string,
             isdraft                    TYPE abap_bool,
             hideattendees              TYPE abap_bool,
             responsestatus             TYPE ty_responsestatus,
             body                       TYPE string,
             start                      TYPE t_datetime,
      end                        TYPE t_datetime,
             location                   TYPE ty_location,
             locations                  TYPE tt_location,
             recurrence                 TYPE string,
             attendees                  TYPE tt_attendees,
             fecha_inicio               TYPE begda,
             hora_inicio                TYPE beguz,
             fecha_fin                  TYPE endda,
             hora_fin                   TYPE enduz,
      END OF ty_event .
  types:
    tt_event TYPE STANDARD TABLE OF ty_event WITH NON-UNIQUE KEY id .
  types:
    BEGIN OF ty_calendarview,
             context TYPE string,
             value   TYPE tt_event,
      END OF ty_calendarview .

  data O_WS type ref to ZCL_AP_ODATA .
  data MESSAGE type STRING .

  methods CONSTRUCTOR
    importing
      !CLAVE type OBJKEY
      !VERSION type STRING default 'v1.0'
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !USAR_HTTP type ABAP_BOOL default ''
      !DELEGADO type ABAP_BOOL default ''
      !USER_ID type STRING default ''
      !JSON_PARAMETROS type STRING default '' .
  class-methods SAVE_TOKEN
    importing
      !CLAVE type OBJKEY .
  methods GET
    importing
      !SERVICIO type STRING
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !FILTER type STRING default ''
      !TOP type INT4 default 0
      !SELECT type STRING default ''
      !ORDERBY type STRING default ''
      !SEARCH type STRING default ''
      !PARAMETRO type STRING default ''
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING
      !XSTRING type XSTRING .
  methods PATCH
    importing
      !SERVICIO type STRING
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !PETICION type STRING
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING .
  methods PETICION
    importing
      !SERVICIO type STRING
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !METHOD type STRING default 'GET'
      !PETICION type STRING default ''
      !FILTER type STRING default ''
      !TOP type INT4 default 0
      !SELECT type STRING default ''
      !ORDERBY type STRING default ''
      !SEARCH type STRING default ''
      !CONTENT_TYPE type STRING default ''
      !PETICIONX type XSTRING optional
      !PARAMETRO type STRING default ''
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING
      !ERROR type T_ERROR
      !XSTRING type XSTRING .
  methods GET_MAILS
    importing
      !SERVICIO type STRING default ''
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !FILTER type STRING default ''
      !TOP type INT4 default 100
      !SELECT type STRING default ''
      !ORDERBY type STRING default 'receivedDateTime desc'
      !QUITAR_CONFIRMACIONES type ABAP_BOOL default 'X'
      !SOLO_RECIBIDOS type ABAP_BOOL default ''
      !BANDEJA type STRING default ''
      !SEARCH type STRING default ''
      !GET_ADJUNTOS type ABAP_BOOL default ''
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING
      !I_EMAILS type TT_EMAIL
      !I_ATTACHMENTS type TT_ATTACHMENTS .
  methods CATEGORIZAR_MAIL
    importing
      !SERVICIO type STRING default ''
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !ID type STRING
      !CATEGORIA type STRING
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING .
  methods GET_TOKEN
    importing
      !CLIENT_ID type STRING
      !CLIENT_SECRET type STRING
      !CODE type STRING default '12345'
      !TENANT_ID type STRING
      !SCOPE type STRING default 'https://graph.microsoft.com/.default'
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !GRANT_TYPE type STRING default 'client_credentials'
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING
      !TOKEN_DATA type T_TOKEN_DATA .
  methods REFRESCAR_TOKEN
    importing
      !URL type ABAP_BOOL default ''
      !POPUP_RESPUESTA type ABAP_BOOL default ''
    exporting
      !TOKEN_DATA type T_TOKEN_DATA
      !MESSAGE type STRING .
  methods GET_USER_PREFIX
    importing
      !USER_ID type STRING default ''
    exporting
      !MESSAGE type STRING
      value(PREFIX) type STRING .
  methods FORMAT_SERVICIO
    importing
      !USER_ID type STRING default ''
      !SERVICIO type STRING optional
      !SERVICIO_FORZADO type STRING default ''
    exporting
      !MESSAGE type STRING
      !SERVICIO_FINAL type STRING .
  methods DELETE
    importing
      !SERVICIO type STRING
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !PETICION type STRING
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING .
  methods BORRAR_MAIL
    importing
      !SERVICIO type STRING default ''
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !ID type STRING
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING .
  methods JSON2TABLA
    importing
      !JSON type STRING
    exporting
      !TABLA type TABLE
      !MESSAGE type STRING .
  methods GET_CONTENIDO_FICHERO
    importing
      !SERVICIO type STRING default ''
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !ID type STRING optional
      !NOMBRE type STRING optional
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING
      !CONTENIDO type XSTRING
      !TABLA_XLSX type TABLE .
  methods GET_INFO_FICHERO
    importing
      !SERVICIO type STRING default ''
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !ID type STRING optional
      !NOMBRE type STRING optional
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING
      !INFO_FICHERO type T_INFO_FICHERO .
  methods JSON2DATOS
    importing
      !JSON type STRING
    exporting
      !DATOS type ANY
      !MESSAGE type STRING .
  methods GRABAR_FICHERO
    importing
      !SERVICIO type STRING default ''
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !ID type STRING optional
      !NOMBRE type STRING optional
      !CONTENIDO type STRING optional
      !CONTENT_TYPE type STRING default ''
      !CONTENIDOX type XSTRING optional
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING
      !INFO_FICHERO type T_INFO_FICHERO .
  methods PUT
    importing
      !SERVICIO type STRING
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !PETICION type STRING
      !CONTENT_TYPE type STRING default ''
      !PETICIONX type XSTRING
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING .
  methods GET_ID_WORKSHEET
    importing
      !SERVICIO type STRING default ''
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !ID type STRING optional
      !NOMBRE type STRING optional
      !HOJA type STRING default ''
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING
      !INFO_XLSX type TT_INFO_XLSX
      !ID_WORKSHEET type STRING .
  methods GET_RANGO_WORKSHEET
    importing
      !SERVICIO type STRING default ''
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !ID type STRING optional
      !NOMBRE type STRING optional
      !RANGO type STRING
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING
      !I_VALORES type TT_WORKBOOK_VALUES .
  methods GET_CALENDARIO
    importing
      !SERVICIO type STRING default ''
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !FILTER type STRING default ''
      !SELECT type STRING default ''
      !ORDERBY type STRING default 'createdDateTime desc'
      !QUITAR_CANCELADOS type ABAP_BOOL default 'X'
      !SEARCH type STRING default ''
      !PARAMETRO type STRING default ''
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING
      !I_EVENTOS type TT_EVENT .
  methods GET_ATTACHMENTS
    importing
      !SERVICIO type STRING default ''
      !POPUP_RESPUESTA type ABAP_BOOL default ''
      !ID type STRING default ''
    exporting
      !RESPUESTA type STRING
      !MESSAGE type STRING
      !I_ATTACHMENTS type TT_ATTACHMENTS .
protected section.
private section.

  data TOKEN type STRING .
  data VERSION type STRING .
  data O_PAR type ref to ZCL_AP_PARAMETROS .
  data CLAVE type STRING .
  data USAR_HTTP type ABAP_BOOL .
  data USER_ID type STRING .
  data DELEGADO type ABAP_BOOL .
endclass. "ZCL_AP_MGRAPH definition
class ZCL_AP_MGRAPH implementation.
METHOD borrar_mail.
  DATA: l_peticion TYPE string,
        l_servicio TYPE string.
  CLEAR: respuesta, message.

  format_servicio( EXPORTING servicio = 'messages' user_id = user_id servicio_forzado = servicio
                   IMPORTING servicio_final = l_servicio
                             message = message ).
  IF NOT message IS INITIAL.
    RETURN.
  ENDIF.

  CONCATENATE l_servicio id INTO l_servicio SEPARATED BY '/'.
  delete( EXPORTING servicio  = l_servicio
                   popup_respuesta = popup_respuesta
                   peticion = l_peticion
         IMPORTING respuesta = respuesta
                   message   = message ).
ENDMETHOD.
METHOD categorizar_mail.
  DATA: l_peticion TYPE string,
        l_servicio TYPE string.

  CONCATENATE '{ "categories": [  "' categoria '" ] }' INTO l_peticion.

  CLEAR: respuesta, message.

  format_servicio( EXPORTING servicio = 'messages' user_id = user_id servicio_forzado = servicio
                   IMPORTING servicio_final = l_servicio
                             message = message ).
  IF NOT message IS INITIAL.
    RETURN.
  ENDIF.

  CONCATENATE l_servicio id INTO l_servicio SEPARATED BY '/'.
  patch( EXPORTING servicio  = l_servicio
                   popup_respuesta = popup_respuesta
                   peticion = l_peticion
         IMPORTING respuesta = respuesta
                   message   = message ).
ENDMETHOD.
METHOD constructor.
  DATA: zcache  TYPE zcache,
        l_token TYPE zcl_ap_mgraph=>t_token_data,
        l_seg   TYPE int4,
        l_host  TYPE string.

  CLEAR message.
  me->clave     = clave.
  me->usar_http = usar_http.
  IF popup_respuesta IS INITIAL.
    SELECT SINGLE fecha hora aux1 string FROM zcache
      INTO CORRESPONDING FIELDS OF zcache
     WHERE report = 'TOKEN_MGRAPH'
       AND clave = clave.
    IF NOT zcache-string IS INITIAL AND zcache-aux1 CO '0123456789 '.
      GET TIME.
      l_seg = zcl_ap_fechas=>get_duracion_intervalo_sec( fini = zcache-fecha hini = zcache-hora
                                                         ffin = sy-datum hfin = sy-uzeit ).
      IF l_seg >= zcache-aux1.
        CLEAR zcache-string.
      ENDIF.
    ENDIF.
  ENDIF.

  CREATE OBJECT o_par
    EXPORTING
      clave        = clave
      fichero_json = json_parametros.

  IF NOT zcache-string IS INITIAL.
    l_token-access_token = zcache-string.
  ELSE.
    refrescar_token( EXPORTING popup_respuesta = popup_respuesta
                     IMPORTING token_data = l_token
                               message    = message ).
    IF l_token IS INITIAL AND NOT message IS INITIAL.
      RETURN.
    ENDIF.
  ENDIF.

  IF l_token IS INITIAL.
    message = 'No existe el token'.
    RETURN.
  ENDIF.

  me->version  = version.
  me->delegado = delegado.
  IF delegado IS INITIAL.
    IF user_id IS INITIAL.
      me->user_id = o_par->get_atr1( campo = 'USER_ID' ).
    ELSE.
      me->user_id = user_id.
    ENDIF.
  ENDIF.

  IF usar_http IS INITIAL.
    l_host = 'https://graph.microsoft.com'.
  ELSE.
    l_host = 'http://graph.microsoft.com'.
  ENDIF.

  CREATE OBJECT o_ws
    EXPORTING
      host = l_host
      auth = l_token-access_token.

* https://developer.microsoft.com/en-us/graph/graph-explorer
* https://portal.azure.com/#view/Microsoft_AAD_IAM/ActiveDirectoryMenuBlade/~/RegisteredApps
ENDMETHOD.
METHOD delete.

  CLEAR: respuesta, message.
  peticion( EXPORTING method    = 'DELETE'
                      popup_respuesta = popup_respuesta
                      servicio  = servicio
                      peticion  = peticion
            IMPORTING respuesta = respuesta
                      message   = message ).

ENDMETHOD.
METHOD format_servicio.
  DATA l_prefix TYPE string.

  CLEAR: servicio_final, message.

  IF NOT servicio_forzado IS INITIAL.
    servicio_final = servicio_forzado.
  ELSE.
    IF servicio IS INITIAL.
      message = 'Debe indicar servicio'.
    ELSE.
      get_user_prefix( EXPORTING user_id = user_id
                       IMPORTING prefix  = l_prefix
                                 message = message ).
      IF NOT message IS INITIAL.
        RETURN.
      ENDIF.
      IF servicio(1) = '/'.
        CONCATENATE l_prefix servicio INTO servicio_final.
      ELSE.
        CONCATENATE l_prefix '/' servicio INTO servicio_final.
      ENDIF.
    ENDIF.
  ENDIF.

ENDMETHOD.
METHOD get.

  CLEAR: respuesta, message.
  peticion( EXPORTING method    = 'GET'
                      popup_respuesta = popup_respuesta
                      servicio  = servicio
                      filter    = filter
                      top       = top
                      select    = select
                      orderby   = orderby
                      search    = search
                      parametro = parametro
            IMPORTING respuesta = respuesta
                      message   = message
                      xstring   = xstring ).

ENDMETHOD.
METHOD get_attachments.
  DATA: l_json      TYPE string,
        l_aux       TYPE string,
        l_long      TYPE i,
        l_msg2      TYPE bapi_msg,
        l_excepcion TYPE REF TO cx_root,
        l_servicio  TYPE string.

  FIELD-SYMBOLS <mail> TYPE t_email.

  CLEAR: respuesta, message, i_attachments.

  format_servicio( EXPORTING servicio = 'messages' user_id = user_id servicio_forzado = servicio
                   IMPORTING servicio_final = l_servicio
                             message = message ).
  IF NOT message IS INITIAL.
    RETURN.
  ENDIF.

  IF NOT id IS INITIAL.
    CONCATENATE l_servicio '/' id '/attachments' INTO l_servicio.
  ENDIF.

  get( EXPORTING servicio  = l_servicio
                 popup_respuesta = popup_respuesta
       IMPORTING respuesta = respuesta
                 message   = message ).

  IF NOT respuesta IS INITIAL AND message IS INITIAL.
    TRY.
        SPLIT respuesta AT '"value":[' INTO l_aux l_json.
        l_long = STRLEN( l_json ) - 1.
        CONCATENATE '[' l_json(l_long) INTO l_json.
        replace all OCCURRENCES OF '@odata.type' in l_json with 'odata_type'.
        replace all OCCURRENCES OF '@odata.mediaContentType' in l_json with 'odata_mediaContentType'.

        json2tabla( EXPORTING json = l_json IMPORTING tabla = i_attachments message = message ).


      CATCH cx_root  INTO l_excepcion.
        message = l_excepcion->get_longtext( ).
        IF message IS INITIAL.
          message = l_excepcion->get_text( ).
        ENDIF.
    ENDTRY.
  ENDIF.


ENDMETHOD.
METHOD get_calendario.
  DATA: l_json      TYPE string,
        l_aux       TYPE string,
        l_long      TYPE i,
        l_msg2      TYPE bapi_msg,
        l_excepcion TYPE REF TO cx_root,
        l_servicio  TYPE string.

  FIELD-SYMBOLS <evento> TYPE ty_event.

  CLEAR: respuesta, message, i_eventos.

  format_servicio( EXPORTING servicio = 'calendarview' user_id = user_id servicio_forzado = servicio
                   IMPORTING servicio_final = l_servicio
                             message = message ).
  IF NOT message IS INITIAL.
    RETURN.
  ENDIF.

  get( EXPORTING servicio  = l_servicio
                 popup_respuesta = popup_respuesta
                 filter    = filter
                 parametro = parametro
                 select    = select
                 orderby   = orderby
                 search    = search
       IMPORTING respuesta = respuesta
                 message   = message ).

  IF NOT respuesta IS INITIAL AND message IS INITIAL.
    TRY.
        SPLIT respuesta AT '"value":[' INTO l_aux l_json.
        l_long = STRLEN( l_json ) - 1.
        CONCATENATE '[' l_json(l_long) INTO l_json.

        json2tabla( EXPORTING json = l_json IMPORTING tabla = i_eventos message = message ).

        IF quitar_cancelados = 'X'.
          DELETE i_eventos WHERE iscancelled = 'X'.
        ENDIF.


        LOOP AT i_eventos ASSIGNING <evento>.
          zcl_ap_fechas=>datetimews_2_fechahora( EXPORTING fecha_ws = <evento>-start-datetime
                                                 IMPORTING fecha = <evento>-fecha_inicio hora = <evento>-hora_inicio ).
          zcl_ap_fechas=>datetimews_2_fechahora( EXPORTING fecha_ws = <evento>-end-datetime
                                                 IMPORTING fecha = <evento>-fecha_fin hora = <evento>-hora_fin ).
          IF <evento>-start-timezone = 'UTC'.
            <evento>-hora_inicio = <evento>-hora_inicio + 3600.
            <evento>-hora_fin    = <evento>-hora_fin + 3600.
          ENDIF.
        ENDLOOP.

      CATCH cx_root  INTO l_excepcion.
        message = l_excepcion->get_longtext( ).
        IF message IS INITIAL.
          message = l_excepcion->get_text( ).
        ENDIF.
    ENDTRY.
  ENDIF.

ENDMETHOD.
METHOD get_contenido_fichero.
  DATA: l_peticion TYPE string,
        l_servicio TYPE string.

  CLEAR: respuesta, message, contenido.


  IF NOT id IS INITIAL.
    format_servicio( EXPORTING servicio = 'drive/items' user_id = user_id servicio_forzado = servicio
                     IMPORTING servicio_final = l_servicio
                               message = message ).
    CONCATENATE l_servicio id INTO l_servicio SEPARATED BY '/'.
  ELSEIF NOT nombre IS INITIAL.
    format_servicio( EXPORTING servicio = 'drive/root:' user_id = user_id servicio_forzado = servicio
                     IMPORTING servicio_final = l_servicio
                               message = message ).
    CONCATENATE l_servicio nombre INTO l_servicio SEPARATED BY '/'.
    CONCATENATE l_servicio ':' INTO l_servicio.
  ELSE.
    message = 'Debe indicar ID o nombre fichero'.
  ENDIF.


  CONCATENATE l_servicio id 'content' INTO l_servicio SEPARATED BY '/'.

  get( EXPORTING servicio  = l_servicio
                 popup_respuesta = popup_respuesta
       IMPORTING respuesta = respuesta
                 xstring   = contenido
                 message   = message ).

  IF tabla_xlsx IS SUPPLIED AND NOT contenido IS INITIAL.
    DATA: o_xls TYPE REF TO zcl_ap_abap2xls,
          l_msg2 type bapi_msg.

    CREATE OBJECT o_xls.
    o_xls->lee_fichero( EXPORTING xstring = contenido IMPORTING message = l_msg2 ).
    IF NOT l_msg2 IS INITIAL.
      message = l_msg2.
    ELSE.
      o_xls->get_tabla( IMPORTING et_table = tabla_xlsx ).
    ENDIF.
  ENDIF.

ENDMETHOD.
METHOD get_id_worksheet.
  DATA: l_peticion     TYPE string,
        l_servicio     TYPE string,
        l_excepcion    TYPE REF TO cx_root,
        l_info_fichero TYPE t_info_fichero,
        l_respuesta    TYPE string,
        l_info_xlsx    TYPE t_info_xlsx,
        l_info         TYPE t_info_xlsx_t.

  CLEAR: respuesta, message, id_worksheet, info_xlsx.

  CALL METHOD get_info_fichero
    EXPORTING
      servicio        = ''
      popup_respuesta = popup_respuesta
      id              = id
      nombre          = nombre
    IMPORTING
      respuesta       = l_respuesta
      message         = message
      info_fichero    = l_info_fichero.

  CHECK message IS INITIAL.


  format_servicio( EXPORTING servicio = 'drive/root:' user_id = user_id servicio_forzado = servicio
                   IMPORTING servicio_final = l_servicio
                             message = message ).

  CHECK message IS INITIAL.
  CONCATENATE l_servicio nombre INTO l_servicio SEPARATED BY '/'.
  CONCATENATE l_servicio ':/workbook/worksheets' INTO l_servicio.

  get( EXPORTING servicio  = l_servicio
                 popup_respuesta = popup_respuesta
       IMPORTING respuesta = respuesta
                 message   = message ).

  IF NOT respuesta IS INITIAL AND message IS INITIAL.
    TRY.
        json2datos( EXPORTING json = respuesta IMPORTING datos = l_info message = message ).
        info_xlsx = l_info-value.

        IF NOT nombre IS INITIAL.
          READ TABLE info_xlsx INTO l_info_xlsx WITH KEY name = hoja.
          IF sy-subrc = 0.
            id_worksheet = l_info_xlsx-id.
          ELSE.
            CONCATENATE 'No existe la hoja' hoja INTO message SEPARATED BY space.
          ENDIF.
        ELSE.
          READ TABLE info_xlsx INTO l_info_xlsx INDEX 1.
          IF sy-subrc = 0.
            id_worksheet = l_info_xlsx-id.
          ENDIF.
        ENDIF.
      CATCH cx_root  INTO l_excepcion.
        message = l_excepcion->get_longtext( ).
        IF message IS INITIAL.
          message = l_excepcion->get_text( ).
        ENDIF.
    ENDTRY.
  ENDIF.
ENDMETHOD.
METHOD get_info_fichero.
    DATA: l_peticion  TYPE string,
          l_servicio  TYPE string,
          l_excepcion TYPE REF TO cx_root.

    CLEAR: respuesta, message, info_fichero.

    IF NOT id IS INITIAL.
      format_servicio( EXPORTING servicio = 'drive/items' user_id = user_id servicio_forzado = servicio
                       IMPORTING servicio_final = l_servicio
                                 message = message ).
      CONCATENATE l_servicio id INTO l_servicio SEPARATED BY '/'.
    ELSEIF NOT nombre IS INITIAL.
      format_servicio( EXPORTING servicio = 'drive/root:' user_id = user_id servicio_forzado = servicio
                       IMPORTING servicio_final = l_servicio
                                 message = message ).
      CONCATENATE l_servicio nombre INTO l_servicio SEPARATED BY '/'.
    ELSE.
      message = 'Debe indicar ID o nombre fichero'.
    ENDIF.
    IF NOT message IS INITIAL.
      RETURN.
    ENDIF.



    get( EXPORTING servicio  = l_servicio
                   popup_respuesta = popup_respuesta
         IMPORTING respuesta = respuesta
                   message   = message ).

    IF NOT respuesta IS INITIAL AND message IS INITIAL.
      TRY.
          json2datos( EXPORTING json = respuesta IMPORTING datos = info_fichero message = message ).
        CATCH cx_root  INTO l_excepcion.
          message = l_excepcion->get_longtext( ).
          IF message IS INITIAL.
            message = l_excepcion->get_text( ).
          ENDIF.
      ENDTRY.
    ENDIF.
  ENDMETHOD.
METHOD get_mails.
  DATA: l_json      TYPE string,
        l_aux       TYPE string,
        l_long      TYPE i,
        l_msg2      TYPE bapi_msg,
        l_excepcion TYPE REF TO cx_root,
        l_servicio  TYPE string.

  FIELD-SYMBOLS <mail> TYPE t_email.

  CLEAR: respuesta, message, i_emails, i_attachments.

  format_servicio( EXPORTING servicio = 'messages' user_id = user_id servicio_forzado = servicio
                   IMPORTING servicio_final = l_servicio
                             message = message ).
  IF NOT message IS INITIAL.
    RETURN.
  ENDIF.

  IF NOT bandeja IS INITIAL.
    DATA l_bandeja TYPE string.
    CONCATENATE '/mailFolders/' bandeja '/messages' INTO l_bandeja.
    REPLACE '/messages' IN l_servicio WITH l_bandeja.
  ENDIF.
  get( EXPORTING servicio  = l_servicio
                 popup_respuesta = popup_respuesta
                 filter    = filter
                 top       = top
                 select    = select
                 orderby   = orderby
                 search    = search
       IMPORTING respuesta = respuesta
                 message   = message ).

  IF NOT respuesta IS INITIAL AND message IS INITIAL.
    TRY.
        SPLIT respuesta AT '"value":[' INTO l_aux l_json.
        l_long = STRLEN( l_json ) - 1.
        CONCATENATE '[' l_json(l_long) INTO l_json.

        json2tabla( EXPORTING json = l_json IMPORTING tabla = i_emails message = message ).

        IF quitar_confirmaciones = 'X'.
          DELETE i_emails WHERE sender-emailaddress-address CS 'MicrosoftExchangE' AND sender-emailaddress-address CS '.onmicrosoft.com'
                            AND ( subject CS 'Entregado:' OR subject CS 'Recibido:' OR subject CS 'Leído:'  ).
          DELETE i_emails WHERE ( subject CS 'Leído:'  AND bodypreview CS 'El mensaje' AND bodypreview CS 'fue leído el' ).

        ENDIF.


        LOOP AT i_emails ASSIGNING <mail>.
          zcl_ap_fechas=>datetimews_2_fechahora( EXPORTING fecha_ws = <mail>-createddatetime
                                                 IMPORTING fecha = <mail>-fecha_creacion hora = <mail>-hora_creacion ).
        ENDLOOP.

      CATCH cx_root  INTO l_excepcion.
        message = l_excepcion->get_longtext( ).
        IF message IS INITIAL.
          message = l_excepcion->get_text( ).
        ENDIF.
    ENDTRY.
  ENDIF.

  CHECK message IS INITIAL.

  IF get_adjuntos = 'X'.
    DATA i_ata TYPE tt_attachments.
    FIELD-SYMBOLS <ata> TYPE t_attachment.
    LOOP AT i_emails ASSIGNING <mail> WHERE hasattachments = 'X'.
      CLEAR i_ata.
      get_attachments( EXPORTING id = <mail>-id
           IMPORTING i_attachments = i_ata
                     respuesta = respuesta
                     message   = message ).
      LOOP AT i_ata ASSIGNING <ata> WHERE isinline = ''.
        APPEND <ata> TO i_attachments.
      ENDLOOP.
    ENDLOOP.
  ENDIF.

ENDMETHOD.
METHOD get_rango_worksheet.
  DATA: l_peticion     TYPE string,
        l_servicio     TYPE string,
        l_excepcion    TYPE REF TO cx_root,
        l_info_fichero TYPE t_info_fichero,
        l_respuesta    TYPE string,
        l_info_rango   TYPE t_workbook_range,
        l_aux1         TYPE string,
        l_valores      TYPE string,
        i_valoresl     TYPE TABLE OF string,
        i_columnas     TYPE TABLE OF string,
        l_fila         TYPE int4,
        l_col          TYPE int4,
        l_fila_wb      TYPE t_workbook_values,
        l_long         TYPE int4.

  CLEAR: respuesta, message, i_valores.

  format_servicio( EXPORTING servicio = 'drive/root:' user_id = user_id servicio_forzado = servicio
                  IMPORTING servicio_final = l_servicio
                            message = message ).

  CHECK message IS INITIAL.
  CONCATENATE l_servicio nombre INTO l_servicio SEPARATED BY '/'.
  CONCATENATE l_servicio ':/workbook/worksheets(''' id ''')/range(address=''' rango ''')' INTO l_servicio.

  get( EXPORTING servicio  = l_servicio
                 popup_respuesta = popup_respuesta
       IMPORTING respuesta = respuesta
                 message   = message ).

  IF NOT respuesta IS INITIAL AND message IS INITIAL.
    TRY.
        json2datos( EXPORTING json = respuesta IMPORTING datos = l_info_rango message = message ).
        IF NOT l_info_rango-values IS INITIAL.
          READ TABLE l_info_rango-values INTO l_valores INDEX 1.
          IF l_valores IS INITIAL.
            SPLIT respuesta AT ']],"values":[[' INTO l_aux1 l_valores.
            SPLIT l_valores AT ']]' INTO l_valores l_aux1.
            SPLIT l_valores AT '],[' INTO TABLE i_valoresl.
            CLEAR l_fila.
            LOOP AT i_valoresl INTO l_valores.
              ADD 1 TO l_fila.
              CLEAR l_fila_wb.
              l_fila_wb-fila = l_fila.
              i_columnas     = zcl_ap_regexp=>buscar_patron( string = l_valores patron = '"([^"]*)"' ).
              CLEAR l_col.
              LOOP AT i_columnas INTO l_aux1.
                ADD 1 TO l_col.
                l_fila_wb-columna = l_col.
                IF l_aux1 CS '"'.
                  l_aux1 = l_aux1+1.
                  IF l_aux1 CS '"'.
                    l_long = STRLEN( l_aux1 ) - 1.
                    IF l_aux1+l_long(1) = '"'.
                      l_aux1 = l_aux1(l_long).
                    ENDIF.
                  ENDIF.
                ENDIF.
                l_fila_wb-valor = l_aux1.
                APPEND l_fila_wb TO i_valores.
              ENDLOOP.
            ENDLOOP.
          ENDIF.
        ENDIF.
      CATCH cx_root  INTO l_excepcion.
        message = l_excepcion->get_longtext( ).
        IF message IS INITIAL.
          message = l_excepcion->get_text( ).
        ENDIF.
    ENDTRY.
  ENDIF.
ENDMETHOD.
METHOD get_token.
  DATA: l_servicio TYPE string,
        i_form     TYPE apb_lpd_t_key_value,
        l_form     TYPE wdy_key_value,
        l_host     TYPE string.

  l_form-key = 'grant_type'. l_form-value = grant_type. APPEND l_form TO i_form.
  l_form-key = 'client_id'. l_form-value = client_id. APPEND l_form TO i_form.
  l_form-key = 'client_secret'. l_form-value = client_secret. APPEND l_form TO i_form.
  l_form-key = 'scope'. l_form-value = scope. APPEND l_form TO i_form.

  CLEAR: respuesta, message.

  IF usar_http IS INITIAL.
    l_host = 'https://login.microsoftonline.com/'.
  ELSE.
    l_host = 'http://login.microsoftonline.com/'.
  ENDIF.

  CREATE OBJECT o_ws
    EXPORTING
      host = l_host.

  CONCATENATE tenant_id '/oauth2/v2.0/token' INTO l_servicio.
  o_ws->get_odata( EXPORTING servicio = l_servicio
                             entidad = '' odata_sap = ''
                             method = 'POST'
                             popup_respuesta = popup_respuesta
                             get_datos = 'X'
                             content_type = 'application/x-www-form-urlencoded'
                             i_form = i_form
                   IMPORTING respuesta = respuesta
                             message   = message
                             datos     = token_data ).


ENDMETHOD.
METHOD get_user_prefix.

  CLEAR: prefix, message.

  IF delegado = 'X'.
    prefix = 'me'.
  ELSE.
    IF user_id IS INITIAL.
      message = 'No ha indicado USER_ID'.
    ELSE.
      CONCATENATE '/users/' user_id INTO prefix.
    ENDIF.
  ENDIF.

ENDMETHOD.
METHOD grabar_fichero.
    DATA: l_peticion  TYPE string,
          l_servicio  TYPE string,
          l_nombre    TYPE string,
          l_excepcion TYPE REF TO cx_root.

    CLEAR: respuesta, message.

    IF NOT id IS INITIAL.
      format_servicio( EXPORTING servicio = 'drive/items' user_id = user_id servicio_forzado = servicio
                       IMPORTING servicio_final = l_servicio
                                 message = message ).
      CONCATENATE l_servicio id INTO l_servicio SEPARATED BY '/'.
    ELSEIF NOT nombre IS INITIAL.
      format_servicio( EXPORTING servicio = 'drive/root:' user_id = user_id servicio_forzado = servicio
                       IMPORTING servicio_final = l_servicio
                                 message = message ).
      CONCATENATE nombre ':' INTO l_nombre.
      CONCATENATE l_servicio l_nombre 'content' INTO l_servicio SEPARATED BY '/'.
    ELSE.
      message = 'Debe indicar ID o nombre fichero'.
    ENDIF.
    IF NOT message IS INITIAL.
      RETURN.
    ENDIF.



    put( EXPORTING servicio  = l_servicio
                   popup_respuesta = popup_respuesta
                   peticion = contenido
                   peticionx = contenidox
                   content_type = content_type
         IMPORTING respuesta = respuesta
                   message   = message ).

    IF NOT respuesta IS INITIAL AND message IS INITIAL.
      TRY.
          json2datos( EXPORTING json = respuesta IMPORTING datos = info_fichero message = message ).
        CATCH cx_root  INTO l_excepcion.
          message = l_excepcion->get_longtext( ).
          IF message IS INITIAL.
            message = l_excepcion->get_text( ).
          ENDIF.
      ENDTRY.
    ENDIF.
  ENDMETHOD.
METHOD json2datos.
  DATA: l_clase TYPE seoclass-clsname,
        o_root  TYPE REF TO cx_root.

  CLEAR: message, datos.
  SELECT SINGLE clsname FROM seoclass
    INTO l_clase
   WHERE clsname = '/UI2/CL_JSON'.
  IF sy-subrc NE 0.
    SELECT SINGLE clsname FROM seoclass
      INTO l_clase
     WHERE clsname = 'ZCL_JSON_UI2'.
  ENDIF.

  IF NOT l_clase IS INITIAL.
    TRY.
        CALL METHOD (l_clase)=>deserialize
          EXPORTING
            json = json
          CHANGING
            data = datos.
      CATCH cx_root INTO o_root.
        message = o_root->get_text( ).
    ENDTRY.
  ELSE.
    DATA l_json2 TYPE string.

    CALL FUNCTION 'SCP_TRANSLATE_CHARS'
      EXPORTING
        inbuff           = json
*       INBUFFLG         = 0
        incode           = '4110'
*       OUTBUFFLG        = 0
        outcode          = '1100'
*       CSUBST           = 'X'
*       SUBSTC_HASH      = ' '
*       SUBSTC_DOT       = ' '
*       SUBSTC_SPACE     = ' '
*       SUBSTC           = '00035'
      IMPORTING
*       INUSED           =
        outbuff          = l_json2
*       OUTOVERFLOW      =
*       OUTUSED          =
*       SUBSTED          =
*       INPUT_ENDS_IN_CHAR       =
*       ERRMSG           =
      EXCEPTIONS
        invalid_codepage = 1
        internal_error   = 2
        cannot_convert   = 3
        fields_bad_type  = 4
        OTHERS           = 5.

    DATA l_msg TYPE bapi_msg.
    zcl_ap_segw=>json2datos( EXPORTING json = l_json2
                              IMPORTING datos = datos
                                        message = l_msg ).
    message = l_msg.
  ENDIF.

ENDMETHOD.
METHOD json2tabla.
  DATA: l_clase TYPE seoclass-clsname,
        o_root  TYPE REF TO cx_root.

  CLEAR: message, tabla.
  SELECT SINGLE clsname FROM seoclass
    INTO l_clase
   WHERE clsname = '/UI2/CL_JSON'.
  IF sy-subrc NE 0.
    SELECT SINGLE clsname FROM seoclass
      INTO l_clase
     WHERE clsname = 'ZCL_JSON_UI2'.
  ENDIF.

  IF NOT l_clase IS INITIAL.
    TRY.
        CALL METHOD (l_clase)=>deserialize
          EXPORTING
            json = json
          CHANGING
            data = tabla.
      CATCH cx_root INTO o_root.
        message = o_root->get_text( ).
    ENDTRY.
  ELSE.
    DATA l_json2 TYPE string.

    CALL FUNCTION 'SCP_TRANSLATE_CHARS'
      EXPORTING
        inbuff           = json
*         INBUFFLG         = 0
        incode           = '4110'
*         OUTBUFFLG        = 0
        outcode          = '1100'
*         CSUBST           = 'X'
*         SUBSTC_HASH      = ' '
*         SUBSTC_DOT       = ' '
*         SUBSTC_SPACE     = ' '
*         SUBSTC           = '00035'
      IMPORTING
*         INUSED           =
        outbuff          = l_json2
*         OUTOVERFLOW      =
*         OUTUSED          =
*         SUBSTED          =
*         INPUT_ENDS_IN_CHAR       =
*         ERRMSG           =
      EXCEPTIONS
        invalid_codepage = 1
        internal_error   = 2
        cannot_convert   = 3
        fields_bad_type  = 4
        OTHERS           = 5.

    DATA l_msg TYPE bapi_msg.
    zcl_ap_segw=>json2tabla( EXPORTING json = l_json2
                              IMPORTING tabla = tabla
                                        message = l_msg ).
    message = l_msg.
  ENDIF.

ENDMETHOD.
METHOD patch.

    CLEAR: respuesta, message.
    peticion( EXPORTING method    = 'PATCH'
                        popup_respuesta = popup_respuesta
                        servicio  = servicio
                        peticion  = peticion
              IMPORTING respuesta = respuesta
                        message   = message ).

  ENDMETHOD.
METHOD peticion.
  DATA: l_servicio   TYPE string,
        l_parametros TYPE string,
        l_aux        TYPE string.

  CLEAR: respuesta, message.

  IF o_ws IS INITIAL. message = 'No se especifico token'. RETURN. ENDIF.

  IF version IS INITIAL.
    l_servicio = servicio.
  ELSE.
    DATA l_ini TYPE c.
    l_ini = servicio.
    IF l_ini = '/'.
      CONCATENATE version servicio INTO l_servicio.
    ELSE.
      CONCATENATE version servicio INTO l_servicio SEPARATED BY '/'.
    ENDIF.
  ENDIF.

  DEFINE set_par.
    IF l_parametros IS INITIAL.
      l_parametros = l_aux.
    ELSE.
      CONCATENATE l_parametros l_aux INTO l_parametros SEPARATED BY '&'.
    ENDIF.
  END-OF-DEFINITION.

  IF NOT parametro IS INITIAL.
    l_aux = parametro. CONDENSE l_aux NO-GAPS.
    set_par.
  ENDIF.

  IF NOT top IS INITIAL.
    l_aux = top. CONDENSE l_aux NO-GAPS.
    CONCATENATE '$top=' l_aux INTO l_aux.
    set_par.
  ENDIF.

  IF NOT select IS INITIAL.
    CONCATENATE '$select=' select INTO l_aux.
    set_par.
  ENDIF.

  IF NOT orderby IS INITIAL AND search IS INITIAL.
    CONCATENATE '$orderby=' orderby INTO l_aux.
    set_par.
  ENDIF.

  IF NOT search IS INITIAL.
    IF search(1) = '"'.
      CONCATENATE '$search=' search INTO l_aux.
    ELSE.
      CONCATENATE '$search="' search '"' INTO l_aux.
    ENDIF.
    set_par.
  ELSEIF NOT filter IS INITIAL.
    CONCATENATE '$filter=' filter INTO l_aux.
    set_par.
  ENDIF.



  o_ws->get_odata( EXPORTING servicio = l_servicio
                             parametros = l_parametros
                             peticion = peticion
                             peticionx = peticionx
                             entidad = '' odata_sap = ''
                             method = method
                             popup_respuesta = popup_respuesta
                             get_datos = ''
                             content_type = content_type
                   IMPORTING respuesta = respuesta
                             xstring = xstring
                             message = message ).

  IF NOT respuesta IS INITIAL.
    TRY.
        zcl_ap_segw=>json2datos( EXPORTING json = respuesta
                                 IMPORTING datos = error ).

        IF message IS INITIAL AND NOT error-error-message IS INITIAL.
          message = error-error-message.

          IF popup_respuesta = 'E'.
            o_ws->get_odata( EXPORTING servicio = l_servicio
                                       parametros = l_parametros
                                       peticion = peticion
                                       peticionx = peticionx
                                       entidad = '' odata_sap = ''
                                       method = method
                                       popup_respuesta = 'X'
                                       get_datos = ''
                                       content_type = content_type ).
          ENDIF.
        ENDIF.
    ENDTRY.
  ENDIF.

ENDMETHOD.
METHOD put.

  CLEAR: respuesta, message.
  peticion( EXPORTING method    = 'PUT'
                      popup_respuesta = popup_respuesta
                      servicio  = servicio
                      peticion  = peticion
                      peticionx = peticionx
                      content_type = content_type
            IMPORTING respuesta = respuesta
                      message   = message ).

ENDMETHOD.
METHOD refrescar_token.
  DATA: zcache          TYPE zcache,
        l_tenant_id     TYPE string,
        l_client_id     TYPE string,
        l_client_secret TYPE string,
        l_scope         TYPE string,
        l_url           TYPE string,
        l_respuesta     TYPE string.

  l_tenant_id = o_par->get_atr1( campo = 'TENANT_ID' ).
  IF l_tenant_id IS INITIAL.
    message = 'Falta informar TENANT_ID'. RETURN.
  ENDIF.
  l_client_id = o_par->get_atr1( campo = 'CLIENT_ID' ).
  IF l_client_id IS INITIAL.
    message = 'Falta informar CLIENT_ID'. RETURN.
  ENDIF.
  l_client_secret = o_par->get_atr1( campo = 'CLIENT_SECRET' ).
  IF l_client_secret IS INITIAL.
    message = 'Falta informar CLIENT_SECRET'. RETURN.
  ENDIF.
  l_scope = o_par->get_atr1( campo = 'SCOPE' ).
  IF l_scope IS INITIAL.
    l_scope = 'https://graph.microsoft.com/.default'.
  ENDIF.


  IF url = 'X'.
*    l_url = |https://LOGIN.microsoftonline.com/{ l_tenant_id }/oauth2/v2.0/authorize?response_type=code&client_id={ l_client_id }&state=12345&scope=openid%20mail.readwrite&redirect_uri=https%3a%2f%2foauth.pstmn.io%2fv1%2fcallback|.
    zcl_ap_gos=>visualizar_fichero_st( l_url ).
  ELSE.
    get_token( EXPORTING client_id = l_client_id
                         tenant_id = l_tenant_id
                         client_secret = l_client_secret
                         scope     = l_scope
                         popup_respuesta = popup_respuesta
               IMPORTING token_data = token_data
                         message = message
                         respuesta = l_respuesta ).

    IF NOT token_data-access_token IS INITIAL.
      CONCATENATE token_data-token_type token_data-access_token INTO token_data-access_token SEPARATED BY space.
      o_ws->auth = token_data-access_token.
      zcl_ap_cache=>set_cache( report = 'TOKEN_MGRAPH' clave = clave string = o_ws->auth aux1 = token_data-expires_in max_duracion = 720 ).
    ELSEIF message IS INITIAL.
      IF NOT l_respuesta IS INITIAL.
        message = l_respuesta.
      ELSE.
        message = 'No existe el token'.
      ENDIF.
    ENDIF.
  ENDIF.

ENDMETHOD.
METHOD save_token.
    DATA string TYPE string.

    IF clave IS INITIAL.
      MESSAGE 'Informe clave' TYPE 'I'.
      RETURN.
    ENDIF.

    zcl_ap_string=>from_clipboard( IMPORTING string = string ).
    IF string IS INITIAL.
      MESSAGE 'No se ha recuperado nada' TYPE 'I'.
      RETURN.
    ELSEIF strlen( string ) < 1000.
      MESSAGE 'Token erróneo' TYPE 'I'.
      RETURN.
    ENDIF.

    IF string(6) NE 'Bearer'.
      CONCATENATE 'Bearer' string into string SEPARATED BY space.
    ENDIF.

    zcl_ap_cache=>set_cache( report = 'TOKEN' clave = clave string = string max_duracion = 999999 ).
    CONCATENATE 'Se ha grabado token' string into string SEPARATED BY space.
    MESSAGE string TYPE 'I'.

  ENDMETHOD.

***********************************************************************
* APLICACION : BC
* TIPO : LISTADO
* TITULO : Gestión documentos
* *
* DESCRIPCION : Gestión documentos
*
*
* AUTOR: Andrés Picazo                                FECHA: 19/05/2011
* ANALISTA: Andrés Picazo
*
* MODIFICACIONES
*
***********************************************************************
REPORT zdocumentos.

*------INCLUDES--------------------------------------------------------*

*------TABLAS/ESTRUCTURAS----------------------------------------------*
TABLES: zest_documento, *zest_documento, sscrfields.


CLASS lcl_alv DEFINITION INHERITING FROM zcl_ap_alv_check.
  PUBLIC SECTION.
    METHODS: handle_double_click REDEFINITION.
    METHODS: handle_user_command REDEFINITION.
ENDCLASS. "lcl_alv DEFINITION

CLASS zcl_report DEFINITION INHERITING FROM zcl_ap_dev FINAL.
  PUBLIC SECTION.
    TYPES: BEGIN OF t_listado,
             check      TYPE xfeld,
             lights     TYPE zico_estado_mensaje,
             tcode      LIKE zest_documento-tcode,
             nombre     LIKE zest_documento-nombre,
             extension  LIKE zest_documento-extension,
             comprimido LIKE zest_documento-comprimido,
             longitud   LIKE zest_documento-longitud,
             fichero    LIKE zest_documento-fichero,
             obsoleto   LIKE zest_documento-obsoleto,
             erdat      LIKE zest_documento-erdat,
             erzet      LIKE zest_documento-erzet,
             ernam      LIKE zest_documento-ernam,
             aedat      LIKE zest_documento-aedat,
             aezet      LIKE zest_documento-aezet,
             aenam      LIKE zest_documento-aenam,
           END OF t_listado,
           tt_listado TYPE STANDARD TABLE OF t_listado.
    DATA: i_listado TYPE tt_listado,
          o_alv     TYPE REF TO lcl_alv.                    "#EC NEEDED

    METHODS: main.

    METHODS:  listado,
      seleccionar_datos.

ENDCLASS.                    "REPORT DEFINITION

*------VARIABLES-------------------------------------------------------*
DATA: o_prog TYPE REF TO zcl_report.



DATA: documento    TYPE zdocumentos,
      l_string     TYPE string,
      v_directorio TYPE string,
      v_fichero    TYPE string,
      l_return,
      l_sistema    TYPE char40.


*------PARAMETER/SELECT-OPTIONS EN PANTALLA----------------------------*
SELECTION-SCREEN BEGIN OF BLOCK 001 WITH FRAME.
SELECT-OPTIONS: s_tcode  FOR zest_documento-tcode,
                s_nombre FOR zest_documento-nombre,
                s_exten  FOR zest_documento-extension,
                s_erdat  FOR zest_documento-erdat,
                s_erzet  FOR zest_documento-erzet,
                s_ernam  FOR zest_documento-ernam,
                s_obsol  FOR zest_documento-obsoleto DEFAULT ''.
SELECTION-SCREEN: SKIP 1.
PARAMETERS: p_vari LIKE disvariant-variant.
SELECTION-SCREEN END OF BLOCK 001.
SELECTION-SCREEN: FUNCTION KEY 1.
SELECTION-SCREEN: FUNCTION KEY 2.
SELECTION-SCREEN: FUNCTION KEY 3.




************************************************************************
*
* LOGICA DEL PROGRAMA
*
************************************************************************

*----------------------------------------------------------------------*
* CLASS lcl_alv IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_alv IMPLEMENTATION.
  METHOD handle_double_click.
    READ TABLE o_prog->i_listado INDEX row INTO DATA(l_listado).
    IF sy-subrc = 0.
      zcl_ap_documentos=>visualizar_documento( tcode = l_listado-tcode
                                          nombre = l_listado-nombre ).
    ENDIF.
  ENDMETHOD. "handle_double_click
  METHOD handle_user_command.
    check_ucomm_sel = 'EDITAR,BORRAR,DESCARGAR,OT,OBSOLETO'.

    super->handle_user_command( e_salv_function ).

    DATA: l_row         TYPE i,
          l_sistema     TYPE rfcdest,
          l_editor      TYPE string,
          l_xstring     TYPE xstring,
          l_xstring_ini TYPE xstring,
          l_mod         TYPE xfeld.

    IF sy-sysid = zcl_c=>entorno_desarrollo.
      l_sistema = zcl_c=>rfc_produccion.
    ELSE.
      l_sistema = zcl_c=>rfc_desarrollo.
    ENDIF.

    CASE ucomm.
      WHEN 'EXCEL'.
        exportar_excel( ).
      WHEN 'NUEVO'.
        DATA l_tcode TYPE zrnrangetcode.
        CLEAR zest_documento.
        READ TABLE s_tcode INTO l_tcode INDEX 1.
        IF sy-subrc = 0.
          zest_documento-tcode = l_tcode-low.
          READ TABLE s_tcode INTO l_tcode INDEX 2.
          IF sy-subrc = 0.
            zest_documento-nombre = l_tcode-low.
          ENDIF.
        ENDIF.
        zcl_ap_documentos=>popup_documento( EXPORTING operacion = 'C'
                                CHANGING documento = zest_documento ).
        o_prog->seleccionar_datos( ).
        refresh( ).

      WHEN 'EDITAR'.
        get_seleccion( ).
        LOOP AT o_prog->i_listado INTO DATA(l_listado) WHERE check = 'X'.
          DATA(l_fichero) = zcl_ap_documentos=>grabar_documento_temporal( tcode = l_listado-tcode  nombre = l_listado-nombre ).
          CLEAR l_editor.
          DATA(l_extension) = zcl_ap_ficheros=>get_extension( fichero = l_fichero ).
          TRANSLATE l_extension TO UPPER CASE.
          CASE l_extension.
            WHEN 'DOC' OR 'DOCX'.
              l_editor = 'winword.exe'.
            WHEN 'XLS' OR 'XLSX'.
              l_editor = 'excel.exe'.
          ENDCASE.
          IF l_editor IS INITIAL.
            MESSAGE |No es posible editar ficheros { l_extension }| TYPE 'I'.
          ELSE.
            zcl_ap_ficheros=>leer_xstring( EXPORTING fichero = l_fichero
                                           IMPORTING xstring  = l_xstring_ini ).
            IF l_xstring_ini IS INITIAL.
              MESSAGE 'Error recuperando fichero' TYPE 'I'.
            ELSE.
              DATA(l_fichero_edt) = '"' && l_fichero && '"'.

              CALL METHOD cl_gui_frontend_services=>execute
                EXPORTING
*                 document               =
                  application            = l_editor
                  parameter              = l_fichero_edt
*                 default_directory      =
*                 maximized              =
*                 minimized              =
                  synchronous            = 'X'
*                 operation              = 'OPEN'
                EXCEPTIONS
                  cntl_error             = 1
                  error_no_gui           = 2
                  bad_parameter          = 3
                  file_not_found         = 4
                  path_not_found         = 5
                  file_extension_unknown = 6
                  error_execute_failed   = 7
                  synchronous_failed     = 8
                  not_supported_by_gui   = 9
                  OTHERS                 = 10.
              IF sy-subrc NE 0.
                MESSAGE 'Error editando fichero' TYPE 'I'.
              ELSE.
                zcl_ap_ficheros=>leer_xstring( EXPORTING fichero = l_fichero
                                               IMPORTING xstring  = l_xstring ).

                IF NOT l_xstring IS INITIAL.
                  IF l_xstring NE l_xstring_ini.
                    l_mod = 'X'.
                    DATA(l_long) = xstrlen( l_xstring ).
                    UPDATE zdocumentos
                       SET xstring = l_xstring
                           longitud = l_long
                           aenam    = sy-uname
                           aedat    = sy-datum
                           aezet    = sy-uzeit
                     WHERE tcode = l_listado-tcode
                       AND nombre = l_listado-nombre.
                  ENDIF.
                ENDIF.
              ENDIF.
            ENDIF.
          ENDIF.
        ENDLOOP.
        IF l_mod = 'X'.
          o_prog->seleccionar_datos( ).
          refresh( ).
        ENDIF.

      WHEN 'ACTUALIZAR'.
        get_seleccion( ).
        LOOP AT o_prog->i_listado INTO l_listado WHERE check = 'X'.
          CLEAR zest_documento.
          MOVE-CORRESPONDING l_listado TO zest_documento.
          zcl_ap_documentos=>popup_documento( EXPORTING operacion = 'M'
                                  CHANGING documento = zest_documento ).
        ENDLOOP.
        o_prog->seleccionar_datos( ).
        refresh( ).
      WHEN 'BORRAR'.
        get_seleccion( ).
        LOOP AT o_prog->i_listado INTO l_listado WHERE check = 'X'.
          DELETE FROM zdocumentos
           WHERE tcode = l_listado-tcode
             AND nombre = l_listado-nombre.
        ENDLOOP.
        o_prog->seleccionar_datos( ).
        refresh( ).
      WHEN 'OBSOLETO'.
        get_seleccion( ).
        LOOP AT o_prog->i_listado ASSIGNING FIELD-SYMBOL(<listado>) WHERE check = 'X'.
          IF <listado>-obsoleto IS INITIAL.
            <listado>-obsoleto = 'X'.
          ELSE.
            <listado>-obsoleto = ''.
          ENDIF.
          UPDATE zdocumentos
             SET obsoleto = <listado>-obsoleto
           WHERE tcode = <listado>-tcode
             AND nombre = <listado>-nombre.
        ENDLOOP.
        refresh( ).
      WHEN 'GET_DES'.
        zcl_ap_popup=>popup_usuario( EXPORTING campo1 =
        'ZEST_DOCUMENTO-TCODE'
                                               titulo =
  'Recupera datos de desarrollo'
                                     IMPORTING return = l_return
                                     CHANGING  valor1 =
                                     zest_documento-tcode ).
        IF l_return = '' AND NOT zest_documento-tcode IS INITIAL.
          l_string = zest_documento-tcode.
          IF sy-sysid = zcl_c=>entorno_produccion.
            l_sistema = zcl_c=>rfc_desarrollo.
          ELSE.
            l_sistema = zcl_c=>rfc_produccion.
          ENDIF.
          CALL FUNCTION 'Z_RFC_GET_TABLA'
            EXPORTING
              tabla      = 'ZDOCUMENTOS'
              clave      = l_string
              actualizar = 'X'
              sistema    = l_sistema.
        ENDIF.


      WHEN 'DESCARGAR'.

        CALL METHOD cl_gui_frontend_services=>directory_browse
          EXPORTING
            window_title         = 'Selecciona un directorio'
            initial_folder       = 'C:\'
          CHANGING
            selected_folder      = v_directorio
          EXCEPTIONS
            cntl_error           = 1
            error_no_gui         = 2
            not_supported_by_gui = 3
            OTHERS               = 4.

        get_seleccion( ).
        LOOP AT o_prog->i_listado INTO l_listado WHERE check = 'X'.
          CLEAR zest_documento.
          MOVE-CORRESPONDING l_listado TO zest_documento.
          SELECT SINGLE * FROM zdocumentos
            INTO documento
           WHERE tcode  = l_listado-tcode
             AND nombre = l_listado-nombre.
          IF l_listado-nombre IS INITIAL.
            l_listado-nombre = 'noname'.
          ENDIF.
          v_fichero = zcl_ap_ficheros=>concat_ruta(
                                       directorio = v_directorio
                                       fichero    = l_listado-nombre
                                       extension  = l_listado-extension ).
          zcl_ap_documentos=>grabar_documento( documento = documento
                                            fichero = v_fichero ).
        ENDLOOP.

      WHEN 'COPIAR_REM'.
        SET PARAMETER ID 'Z_NO_VAR_DEF' FIELD 'X'.          "#EC EXISTS
        SUBMIT zap_comparar_tablas
         VIA SELECTION-SCREEN
         WITH p_target = l_sistema
         WITH s_table = 'ZDOCUMENTOS'.
        SET PARAMETER ID 'Z_NO_VAR_DEF' FIELD ''.           "#EC EXISTS

      WHEN 'OT'.
        DATA: l_key(120),
            i_keys TYPE TABLE OF string.

        get_seleccion( ).
        LOOP AT o_prog->i_listado INTO l_listado WHERE check = 'X'.
          l_key(3) = sy-mandt.
          l_key+3(20) = l_listado-tcode.
          l_key+23(40) = l_listado-nombre.
          APPEND l_key TO i_keys.
        ENDLOOP.

        zcl_ap_utils=>grabar_tabla_en_ot( tabla = 'ZDOCUMENTOS' i_keys = i_keys ).
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD. "handle_USER_COMMAND
ENDCLASS. "lcl_alv IMPLEMENTATION

CLASS zcl_report IMPLEMENTATION.
  METHOD main.
    seleccionar_datos( ).
    listado( ).
  ENDMETHOD.                    "REPORT

  METHOD seleccionar_datos.

    CLEAR i_listado.
    SELECT * FROM zdocumentos                 "#EC CI_ALL_FIELDS_NEEDED
      INTO CORRESPONDING FIELDS OF TABLE i_listado
     WHERE tcode IN s_tcode
       AND nombre IN s_nombre
       AND extension IN s_exten
       AND erdat IN s_erdat
       AND erzet IN s_erzet
       AND ernam IN s_ernam
       AND obsoleto IN s_obsol
     ORDER BY tcode nombre.

  ENDMETHOD.                    "seleccionar_datos


  METHOD listado.

    sgpi_texto( 'Generando informe'(gin) ).

    o_alv->add_button( button = 'F01' text = 'Nuevo'  icon = icon_create ucomm = 'NUEVO' ).
    o_alv->add_button( button = 'F02' text = 'Actualizar'  icon = icon_read_file ucomm = 'ACTUALIZAR' ).
    o_alv->add_button( button = 'F03' text = 'Editar'  icon = icon_change ucomm = 'EDITAR' ).
    o_alv->add_button( button = 'F04' text = 'Borrar'  icon = icon_delete ucomm = 'BORRAR' ).
    o_alv->add_button( button = 'F05' text = 'Descargar'  icon = icon_write_file ucomm = 'DESCARGAR' ).
    o_alv->add_button( button = 'F06' qinfo = 'Copiar de sistema remoto'  icon = icon_system_copy ucomm = 'COPIAR_REM' ).
    o_alv->add_button( button = 'F07' qinfo = 'Incluir en orden de transporte'  icon = icon_transport ucomm = 'OT' ).
    o_alv->add_button( button = 'F08' qinfo = 'Marcar como obsoleto'  icon = icon_erase ucomm = 'OBSOLETO' ).

    o_alv->set_layout( p_vari ).

*    o_alv->set_top_of_page( ).

    o_alv->set_field_noout( 'COMPRIMIDO,LIGHTS' ).
    o_alv->set_field_text( 'NOMBRE,EXTENSION,LONGITUD' ).
    o_alv->set_field( campo = 'OBSOLETO' op = 'CHECKBOX' ).
    o_alv->set_field( campo = 'ERZET,AEZET' op = 'NO_CERO' ).

    o_alv->set_orden( 'TCODE,NOMBRE' ).

    o_alv->get_datos_layout( EXPORTING reordenar_tabla = 'X' tabla_ref = 'X' CHANGING t_tabla = i_listado ).
    o_alv->set_seleccion( CHANGING t_tabla = i_listado ).

    o_alv->show( ).


  ENDMETHOD.                    "

ENDCLASS.                    "REPORT IMPLEMENTATION


*----------------------------------------------------------------------*
* INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.


  o_prog = NEW #( status       = 'INICIO_DYN'
                  status_prog  = 'ZAP_STATUS'
                  no_param     = 'X'
                  guardar_logz = '' ).

  o_prog->o_alv =  NEW #( status             = 'STANDARD_ALV_DYN'
                          status_prog        = 'ZAP_STATUS'
                          top_of_page_auto   = 'X'
                          top_of_page_titulo = 'X'
                          o_dev              = o_prog ).

  p_vari = o_prog->o_alv->get_default_layout( ).

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.

  p_vari = o_prog->o_alv->get_f4_layout( ).

************************************************************************
* AT SELECTION-SCREEN.
************************************************************************
AT SELECTION-SCREEN.

*+*+*+*+ PLANTILLA *+*+*+*+
  zcl_ap_dev=>at_selection_screen( ).
*+*+*+*+ PLANTILLA *+*+*+*+

*----------------------------------------------------------------------
* START-OF-SELECTION.
*----------------------------------------------------------------------*
START-OF-SELECTION.

  o_prog->main( ).

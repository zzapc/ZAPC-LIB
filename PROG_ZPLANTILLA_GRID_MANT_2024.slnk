<?xml version="1.0" encoding="utf-8"?>
<PROG NAME="ZPLANTILLA_GRID_MANT_2024" VARCL="X" SUBC="1" RMAND="100" RLOAD="S" FIXPT="X" UCCHECK="X">
 <textPool>
  <language SPRAS="E">
   <textElement ID="I" KEY="EJE" ENTRY="Execute" LENGTH="18 "/>
   <textElement ID="I" KEY="PDA" ENTRY="Processing data" LENGTH="26 "/>
   <textElement ID="I" KEY="PNF" ENTRY="This program can&apos;t run in batch process" LENGTH="84 "/>
   <textElement ID="I" KEY="SDA" ENTRY="Selecting Data" LENGTH="29 "/>
   <textElement ID="R" ENTRY="ALV Template" LENGTH="70 "/>
   <textElement ID="S" KEY="P_VARI" ENTRY="D       ." LENGTH="14 "/>
   <textElement ID="S" KEY="S_VBELN" ENTRY="D       ." LENGTH="15 "/>
  </language>
  <language SPRAS="S">
   <textElement ID="I" KEY="EJE" ENTRY="Ejecutar" LENGTH="18 "/>
   <textElement ID="I" KEY="PDA" ENTRY="Procesando datos" LENGTH="26 "/>
   <textElement ID="I" KEY="PNF" ENTRY="Este programa no se puede ejecutar en fondo" LENGTH="84 "/>
   <textElement ID="I" KEY="SDA" ENTRY="Seleccionando datos" LENGTH="29 "/>
   <textElement ID="R" ENTRY="Plantilla ALV GRID" LENGTH="18 "/>
   <textElement ID="S" KEY="P_VARI" ENTRY="D       ." LENGTH="14 "/>
   <textElement ID="S" KEY="S_BUKRS" ENTRY="D       ." LENGTH="9 "/>
  </language>
 </textPool>
 <dynpros>
  <dynpro PROG="ZPLANTILLA_GRID_MANT_2024" DNUM="0100" FNUM="0100" BZMX="57 " BZBR="255 " MILI="192 " MICO="37 " MALI="0 " MACO="0 " NOLI="57 " NOCO="255 " VALP="0 " CUAN="G" SPRA="S" DTEXT="Grid">
   <dynprofield FNAM="GRID" DIDX="0039" FLG1="00" FLG2="30" FLG3="00" FILL="U" FMB1="30" FMB2="00" LENG="FF" LINE="01" COLN="02" LANF="00" LBLK="00" LREP="00" AUTH="101" AGLT="14" ADEZ="63"/>
   <dynprofield DIDX="0000" FLG1="80" FLG2="10" FLG3="00" FMB1="00" FMB2="00" LENG="14" LINE="FF" COLN="01" LTYP="O" LANF="00" LBLK="00" LREP="00" TYPE="CHAR" AGLT="00" ADEZ="00" STXT="____________________"/>
   <dynproflowsource>PROCESS BEFORE OUTPUT.
 MODULE STATUS_0100.
*
PROCESS AFTER INPUT.
 MODULE USER_COMMAND_0100 AT EXIT-COMMAND.

 MODULE USER_COMMAND_0100.</dynproflowsource>
  </dynpro>
 </dynpros>
 <source>***********************************************************************
* TIPO : MANTENIMIENTO
* TITULO : Mantenimiento
* DESCRIPCION : Mantenimiento
*
* AUTOR: Andres Picazo                                FECHA: 23/10/2024
* ANALISTA: Andres Picazo
*
***********************************************************************
REPORT  zplantilla_grid_mant_20024.

TABLES ztemp.

CONSTANTS tabla TYPE tabname VALUE &apos;ZTEMP&apos;.

CLASS lcl_event_grid DEFINITION INHERITING FROM zcl_ap_alv_grid_eventos FINAL.
  PUBLIC SECTION.
    METHODS: data_changed REDEFINITION,
      data_changed_finished REDEFINITION,
      user_command REDEFINITION,
      toolbar      REDEFINITION.
ENDCLASS.


*----------------------------------------------------------------------*
*       CLASS zcl_report DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report DEFINITION INHERITING FROM zcl_ap_dev FINAL.
  PUBLIC SECTION.
    TYPES: BEGIN OF  t_listado,
             check         TYPE xfeld,
             lights        TYPE zico_estado_mensaje.
             INCLUDE TYPE ztemp.
             TYPES:   message       TYPE bapi_msg,
             clave_interna TYPE string,
             style         TYPE lvc_t_styl,
             color         TYPE lvc_t_scol,
             updkz         TYPE char1,
             tabix         TYPE int4,
           END OF t_listado,
           tt_listado TYPE STANDARD TABLE OF t_listado.

    DATA: i_listado           TYPE tt_listado,
          i_listado_ini       TYPE tt_listado,
          i_listado_mod       TYPE tt_listado,
          tabix               TYPE sy-tabix,
          campos_clave        TYPE string,
          campos_input        TYPE string,
          campos_obligatorios TYPE string,
          longitud_clave      TYPE int4,
          i_campos_tabla      TYPE TABLE OF dd03l,
          i_campos_alv        TYPE lvc_t_fcat,
          where               TYPE string.

    DATA: o_alv   TYPE REF TO zcl_ap_alv_grid,
          o_event TYPE REF TO lcl_event_grid.

    METHODS: buscar_datos REDEFINITION,

      validaciones IMPORTING !mod    TYPE abap_bool DEFAULT &apos;&apos;
                   CHANGING  listado TYPE t_listado ##NEEDED,

      status_dynpro_0100,
      command_dynpro_0100,
      configuracion,

      where_clave IMPORTING listado      TYPE t_listado
                  RETURNING VALUE(where) TYPE string.

ENDCLASS.

DATA o_prog TYPE REF TO zcl_report ##NEEDED.

SELECTION-SCREEN BEGIN OF BLOCK 001 WITH FRAME.
SELECT-OPTIONS: s_clave FOR ztemp-clave,
                s_subcl FOR ztemp-subclave.
SELECTION-SCREEN SKIP.
PARAMETERS p_vari LIKE disvariant-variant.
SELECTION-SCREEN END OF BLOCK 001.
PARAMETERS p_vis NO-DISPLAY. &quot; Si queremos opción de no editar.
__botones_plantilla.


************************************************************************
*
* LOGICA DEL PROGRAMA
*
************************************************************************
CLASS lcl_event_grid IMPLEMENTATION.
  METHOD toolbar.
    super-&gt;toolbar( e_object = e_object e_interactive = e_interactive ).

    add_boton( function = &apos;COPIAR&apos; icon = icon_system_copy text = &apos;Copiar&apos; quickinfo = &apos;Copiar&apos; e_object = e_object ).
  ENDMETHOD.

  METHOD user_command.
    DATA: l_listado TYPE o_prog-&gt;t_listado,
          l_hay_sel TYPE c LENGTH 1.

    CASE e_ucomm.
      WHEN &apos;NUEVO&apos;.
        o_prog-&gt;tabix = o_prog-&gt;tabix + 1.
        l_listado-tabix = o_prog-&gt;tabix.
        o_prog-&gt;validaciones( EXPORTING mod = &apos;I&apos; CHANGING listado = l_listado ).
        APPEND l_listado TO o_prog-&gt;i_listado.

        o_alv-&gt;refrescar_grid( ).
      WHEN &apos;BORRAR&apos;.
        o_alv-&gt;comprobar_cambios( ).
        o_alv-&gt;set_marca_filas_sel( EXPORTING validar_seleccion = &apos;X&apos; CHANGING t_tabla = o_prog-&gt;i_listado hay_sel = l_hay_sel ).
        IF l_hay_sel = &apos;X&apos;.
          LOOP AT o_prog-&gt;i_listado ASSIGNING FIELD-SYMBOL(&lt;listado&gt;) WHERE check = &apos;X&apos;.
            IF &lt;listado&gt;-updkz = &apos;I&apos;.
              DELETE o_prog-&gt;i_listado.
            ELSEIF &lt;listado&gt;-updkz = &apos;D&apos;.
              CLEAR: &lt;listado&gt;-updkz, &lt;listado&gt;-lights, &lt;listado&gt;-check.
              o_prog-&gt;validaciones( EXPORTING mod = &apos;U&apos; CHANGING listado = &lt;listado&gt; ).
            ELSE.
              o_prog-&gt;validaciones( EXPORTING mod = &apos;D&apos; CHANGING listado = &lt;listado&gt; ).
            ENDIF.
          ENDLOOP.
          o_alv-&gt;refrescar_grid( ).
        ENDIF.

      WHEN &apos;COPIAR&apos;.
        o_alv-&gt;comprobar_cambios( ).
        o_alv-&gt;set_marca_filas_sel( EXPORTING validar_seleccion = &apos;X&apos; CHANGING t_tabla = o_prog-&gt;i_listado hay_sel = l_hay_sel ).
        IF l_hay_sel = &apos;X&apos;.
          LOOP AT o_prog-&gt;i_listado ASSIGNING &lt;listado&gt; WHERE check = &apos;X&apos;.
            CLEAR &lt;listado&gt;-check.
            l_listado = &lt;listado&gt;.
            o_prog-&gt;validaciones( EXPORTING mod = &apos;I&apos; CHANGING listado = l_listado ).
            APPEND l_listado TO o_prog-&gt;i_listado.
          ENDLOOP.
          SORT o_prog-&gt;i_listado BY tabix.
          LOOP AT o_prog-&gt;i_listado ASSIGNING &lt;listado&gt;.
            &lt;listado&gt;-tabix = sy-tabix.
          ENDLOOP.
          o_alv-&gt;refrescar_grid( soft_refresh = &apos;&apos; ).
        ENDIF.
      WHEN OTHERS.
        super-&gt;user_command( e_ucomm = e_ucomm ).
    ENDCASE.
  ENDMETHOD.

  METHOD data_changed.
    ini_data_changed( cambios = er_data_changed-&gt;mt_good_cells  ).

    LOOP AT i_cambios_celda INTO cambio_celda.
      AT NEW row_id.
        READ TABLE o_prog-&gt;i_listado INTO DATA(l_listado_ini) INDEX cambio_celda-row_id. &quot;#EC CI_SUBRC
        DATA(l_listado) = l_listado_ini.
      ENDAT.

      set_valor_mod( CHANGING datos = l_listado ).

      AT END OF row_id.
        o_prog-&gt;validaciones( EXPORTING mod = &apos;U&apos; CHANGING listado = l_listado ).
        MODIFY o_prog-&gt;i_listado FROM l_listado INDEX cambio_celda-row_id.
        actualizar_fila( fila_ini = l_listado_ini fila_fin = l_listado er_data_changed = er_data_changed fila = cambio_celda-row_id ).
        IF l_listado-style NE l_listado_ini-style OR l_listado-color NE l_listado_ini-color.
          APPEND l_listado TO o_prog-&gt;i_listado_mod. &quot;No me guarda los cambios en los estilos
        ENDIF.
      ENDAT.
    ENDLOOP.
  ENDMETHOD.

  METHOD data_changed_finished.

    IF NOT tabla_data_changed IS INITIAL.
      LOOP AT o_prog-&gt;i_listado_mod ASSIGNING FIELD-SYMBOL(&lt;list_mod&gt;).
        READ TABLE o_prog-&gt;i_listado ASSIGNING FIELD-SYMBOL(&lt;list&gt;) WITH KEY tabix = &lt;list_mod&gt;-tabix.
        IF sy-subrc = 0.
          &lt;list&gt; = &lt;list_mod&gt;.
        ENDIF.
        DELETE o_prog-&gt;i_listado_mod.
      ENDLOOP.
      IF sy-subrc = 0.
        o_alv-&gt;refrescar_grid( soft_refresh = &apos;X&apos; ).
      ENDIF.
      CLEAR tabla_data_changed.
    ENDIF.

  ENDMETHOD.
ENDCLASS.


*----------------------------------------------------------------------*
*       CLASS zcl_report IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS zcl_report IMPLEMENTATION.
  METHOD buscar_datos.
    sgpi_texto( &apos;Seleccionando datos&apos;(sda) ).

    CLEAR i_listado.
    SELECT * FROM (tabla)
      INTO CORRESPONDING FIELDS OF TABLE i_listado
     WHERE clave IN s_clave
       AND subclave IN s_subcl
      ORDER BY PRIMARY KEY.

    o_prog-&gt;o_sgpi-&gt;get_filas_tabla( i_listado[] ).
    LOOP AT i_listado ASSIGNING FIELD-SYMBOL(&lt;listado&gt;).
      tabix = sy-tabix.
      &lt;listado&gt;-tabix = tabix.
      sgpi_texto( texto1 = &apos;Procesando datos&apos;(pda) cant_porc = 100 ).

      validaciones( CHANGING listado = &lt;listado&gt; ).
    ENDLOOP.
    SORT i_listado BY clave_interna.
    i_listado_ini = i_listado.
  ENDMETHOD.                                               &quot; seleccionar_datos

  METHOD status_dynpro_0100.
    IF inicio IS NOT INITIAL.
      RETURN.
    ENDIF.

    status_dynpro( EXPORTING cprog = &apos;ZAP_STATUS&apos; status = &apos;ST_DYN&apos; titulo = string CHANGING i_listado = i_listado ).

    inicio = &apos;X&apos;.
    o_alv-&gt;variant-variant = p_vari.
    o_alv-&gt;registrar_mod( ).
    o_alv-&gt;set_layout( ancho_optimizado = &apos;X&apos; no_rowmove = &apos;X&apos; no_rowins = &apos;X&apos; style = &apos;STYLE&apos; colort = &apos;COLOR&apos; ).
    o_alv-&gt;quitar_opciones( cl_gui_alv_grid=&gt;mc_fc_refresh ).
    o_alv-&gt;set_campos_tabint( i_listado[] ).
    o_alv-&gt;set_field_quitar( &apos;CHECK,MANDT,MESSAGE,TABIX,UPDKZ,CLAVE_INTERNA&apos; ).

    IF p_vis IS INITIAL.
      o_alv-&gt;add_button( button = &apos;F01&apos; text = &apos;Grabar&apos; icon = icon_system_save ucomm = &apos;GRABAR&apos; ).

      IF sy-sysid = zcl_c=&gt;entorno_desarrollo.
        o_alv-&gt;add_button( button = &apos;F02&apos; text = &apos;Crear OT&apos;  icon =  icon_import_transport_request ucomm = &apos;OT&apos; ).
        o_alv-&gt;add_button( button = &apos;F03&apos; text = &apos;Comparar con producción&apos;  icon =  icon_compare ucomm = &apos;COMP&apos; ).
      ELSE.
        o_alv-&gt;add_button( button = &apos;F03&apos; text = &apos;Comparar con desarrollo&apos;  icon =  icon_compare ucomm = &apos;COMP&apos; ).
      ENDIF.
      o_alv-&gt;set_field_input( campos_input ).
    ENDIF.

    o_alv-&gt;set_orden( campos_clave ).

    sgpi_texto( &apos;Generando informe&apos; ).
    o_alv-&gt;show( CHANGING tabla = i_listado ).

    i_campos_alv = o_alv-&gt;get_fcat_inicial( ).
  ENDMETHOD.

  METHOD command_dynpro_0100.
    DATA: l_comm_sel TYPE string VALUE &apos;OT,LIST_EMP,SIMULAR&apos;,
          l_hay_sel  TYPE c LENGTH 1,
          l_cont     TYPE i.
    DATA: i_list_dup TYPE TABLE OF t_listado,
          l_list_dup TYPE t_listado.
    DATA: l_key  TYPE c LENGTH 120,
          i_keys TYPE TABLE OF string.

    command_dynpro( EXPORTING o_alv = o_alv seleccion = l_comm_sel
                            CHANGING i_listado = i_listado i_listado_ini = i_listado_ini hay_sel = l_hay_sel ).

    CASE ucomm.
      WHEN &apos;GRABAR&apos;.
        LOOP AT i_listado ASSIGNING FIELD-SYMBOL(&lt;listado&gt;).
          IF line_exists( i_list_dup[ clave_interna = &lt;listado&gt;-clave_interna  ] ).
            i_list_dup[ clave_interna = &lt;listado&gt;-clave_interna  ]-tabix = 2.
          ELSE.
            l_list_dup-clave_interna = &lt;listado&gt;-clave_interna.
            l_list_dup-tabix = 1.
            APPEND l_list_dup TO i_list_dup.
          ENDIF.
          CLEAR &lt;listado&gt;-color.
        ENDLOOP.
        LOOP AT i_list_dup ASSIGNING FIELD-SYMBOL(&lt;list&gt;) WHERE tabix &gt; 1.
          LOOP AT i_listado ASSIGNING &lt;listado&gt; WHERE clave_interna = &lt;list&gt;-clave_interna.
            set_status_list( EXPORTING color = &apos;R&apos; CHANGING list = &lt;listado&gt; ).
          ENDLOOP.
        ENDLOOP.
        IF sy-subrc = 0.
          MESSAGE &apos;No puede grabar si hay duplicados&apos; TYPE &apos;I&apos;.
          o_alv-&gt;refrescar_grid( ).
          RETURN.
        ENDIF.

        LOOP AT i_listado ASSIGNING &lt;listado&gt; WHERE updkz &lt;&gt; &apos;&apos; AND NOT message IS INITIAL.
          MESSAGE &apos;No puede grabar si hay errores&apos; TYPE &apos;I&apos;.
          RETURN.
        ENDLOOP.

        LOOP AT i_listado ASSIGNING &lt;listado&gt; WHERE updkz &lt;&gt; &apos;&apos;.

          MOVE-CORRESPONDING &lt;listado&gt; TO ztemp.
          l_cont = l_cont + 1.
          IF &lt;listado&gt;-updkz = &apos;D&apos;.
            DATA(l_where_delete) = where_clave( &lt;listado&gt; ).
            DELETE FROM (tabla)
             WHERE (l_where_delete).
            IF sy-subrc = 0.
              DELETE i_listado.
            ENDIF.
          ELSE.
            CLEAR: &lt;listado&gt;-updkz, &lt;listado&gt;-lights, &lt;listado&gt;-message, &lt;listado&gt;-style.
            CLEAR ztemp.
            MOVE-CORRESPONDING &lt;listado&gt; TO ztemp.
            MODIFY ztemp FROM ztemp.
          ENDIF.
        ENDLOOP.

        IF l_cont = 0.
          MESSAGE &apos;No había cambios&apos; TYPE &apos;S&apos;.
        ELSE.
          MESSAGE |Se han grabado { l_cont } registros| TYPE &apos;S&apos;.
          i_listado_ini = i_listado.
        ENDIF.
        o_alv-&gt;refrescar_grid( ).

      WHEN &apos;OT&apos;.
        LOOP AT i_listado ASSIGNING &lt;listado&gt; WHERE check = &apos;X&apos;.
          MOVE-CORRESPONDING &lt;listado&gt; TO ztemp.
          ztemp-mandt = sy-mandt.
          l_key = ztemp(longitud_clave).
          APPEND l_key TO i_keys.
        ENDLOOP.

        zcl_ap_utils=&gt;grabar_tabla_en_ot( tabla = tabla i_keys = i_keys ).

      WHEN &apos;COMP&apos;.
        SUBMIT zap_comparar_tablas
          AND RETURN
               WITH s_table = tabla.
*             WITH p_clave = p_cprog. &quot;CAMBIAR

    ENDCASE.
  ENDMETHOD.

  METHOD validaciones.
    DATA: campos_edit  TYPE string,
          campos_error TYPE string.

    CLEAR: listado-message, listado-style, listado-color, listado-lights.

    CASE mod.
      WHEN &apos;I&apos;.
        listado-updkz = &apos;I&apos;.
      WHEN &apos;D&apos;.
        listado-updkz = &apos;D&apos;.
      WHEN &apos;U&apos;.
        IF listado-updkz IS INITIAL.
          ASSIGN i_listado_ini[ tabix = listado-tabix ] TO FIELD-SYMBOL(&lt;ini&gt;).
          IF sy-subrc &lt;&gt; 0.
            listado-updkz = &apos;U&apos;.
          ELSEIF &lt;ini&gt; &lt;&gt; listado.
            listado-updkz = &apos;U&apos;.
          ENDIF.
        ENDIF.
    ENDCASE.

    CASE listado-updkz.
      WHEN &apos;I&apos;.
        DATA(l_icono) = icon_create.
        campos_edit = campos_clave.
      WHEN &apos;D&apos;.
        l_icono = icon_delete.
      WHEN &apos;U&apos;.
        l_icono = icon_change.
    ENDCASE.

    IF NOT listado-updkz IS INITIAL.
      SPLIT campos_obligatorios AT &apos;,&apos; INTO TABLE DATA(i_campos_obl).
      LOOP AT i_campos_obl ASSIGNING FIELD-SYMBOL(&lt;obl&gt;).
        ASSIGN COMPONENT &lt;obl&gt; OF STRUCTURE listado TO FIELD-SYMBOL(&lt;fs&gt;).
        IF sy-subrc &lt;&gt; 0.
          CONTINUE.
        ENDIF.

        IF &lt;fs&gt; IS NOT INITIAL.
          CONTINUE.
        ENDIF.

        __add_lista campos_error &lt;obl&gt;.
        IF listado-message IS NOT INITIAL.
          CONTINUE.
        ENDIF.

        ASSIGN i_campos_alv[ fieldname = &lt;obl&gt; ] TO FIELD-SYMBOL(&lt;alv&gt;).
        IF sy-subrc &lt;&gt; 0.
          CONTINUE.
        ENDIF.

        DATA(l_desc_campo) = &lt;alv&gt;-scrtext_m.
        IF l_desc_campo IS INITIAL.
          SELECT SINGLE ddtext FROM dd03t
           INTO l_desc_campo
          WHERE tabname    = tabla
            AND ddlanguage = sy-langu
            AND fieldname  = &lt;obl&gt;.
          IF sy-subrc &lt;&gt; 0.
            ASSIGN i_campos_tabla[ fieldname = &lt;obl&gt; ] TO FIELD-SYMBOL(&lt;campo&gt;).
            SELECT ddtext FROM dd04t
              INTO l_desc_campo
              UP TO 1 ROWS
             WHERE ddlanguage = sy-langu
               AND rollname   = &lt;campo&gt;-rollname
              ORDER BY PRIMARY KEY.
            ENDSELECT.
          ENDIF.
          IF l_desc_campo IS INITIAL.
            l_desc_campo = &lt;obl&gt;.
          ENDIF.
        ENDIF.

        listado-message = |Informe campo { l_desc_campo }|.
        l_icono = icon_red_light.
      ENDLOOP.
    ENDIF.

    CLEAR: listado-clave_interna.
    LOOP AT i_campos_tabla ASSIGNING &lt;campo&gt; WHERE keyflag = &apos;X&apos;.
      ASSIGN COMPONENT &lt;campo&gt;-fieldname OF STRUCTURE listado TO &lt;fs&gt;.
      IF sy-subrc = 0.
        CONCATENATE listado-clave_interna &lt;fs&gt; INTO listado-clave_interna SEPARATED BY &apos;-&apos;.
      ENDIF.
    ENDLOOP.

    set_status_list( EXPORTING message = listado-message
                               icono = l_icono
                               campos_editables = campos_edit
                               resaltar_campos = campos_error
                               color_resalte = &apos;R&apos;
                     CHANGING list = listado ).
  ENDMETHOD.

  METHOD configuracion.
    CLEAR: campos_clave, i_campos_tabla, i_campos_alv.

    i_campos_alv = get_fieldcatalog_tabla_alv( tabla = i_listado ).

    SELECT fieldname keyflag leng position FROM dd03l
      INTO CORRESPONDING FIELDS OF TABLE i_campos_tabla
     WHERE tabname = tabla
       AND fieldname NE &apos;MANDT&apos;
     ORDER BY position.
    IF sy-subrc &lt;&gt; 0.
      MESSAGE |No existe tabla { tabla }| TYPE &apos;E&apos;.
    ENDIF.

    LOOP AT i_campos_tabla ASSIGNING FIELD-SYMBOL(&lt;campo&gt;).
      IF line_exists(  i_campos_alv[ fieldname = &lt;campo&gt;-fieldname ] ).
        IF &lt;campo&gt;-keyflag = &apos;X&apos;.
          IF campos_clave IS INITIAL.
            campos_clave = &lt;campo&gt;-fieldname.
          ELSE.
            CONCATENATE campos_clave &lt;campo&gt;-fieldname INTO campos_clave SEPARATED BY &apos;,&apos;.
          ENDIF.
          longitud_clave = longitud_clave + &lt;campo&gt;-leng.
        ELSE.
          __add_lista campos_input &lt;campo&gt;-fieldname.
        ENDIF.
      ENDIF.
    ENDLOOP.

    campos_obligatorios = campos_clave.
  ENDMETHOD.

  METHOD where_clave.
    CLEAR where.
    LOOP AT i_campos_tabla ASSIGNING FIELD-SYMBOL(&lt;campo&gt;) WHERE keyflag = &apos;X&apos;.
      ASSIGN COMPONENT &lt;campo&gt;-fieldname OF STRUCTURE listado TO FIELD-SYMBOL(&lt;fs&gt;).
      DATA(l_w) = |{ &lt;campo&gt;-fieldname } = &apos;{ &lt;fs&gt; }&apos;|.
      IF where IS INITIAL.
        where = l_w.
      ELSE.
        where = |{ where } AND { l_w }|.
      ENDIF.
    ENDLOOP.
  ENDMETHOD.
ENDCLASS.

INITIALIZATION.
  o_prog = NEW #( status       = &apos;INICIO_DYN&apos;
                  guardar_logz = &apos;X&apos;
                  status_prog  = &apos;ZAP_STATUS&apos; ).

  IF sy-batch IS INITIAL.
    o_prog-&gt;o_event = NEW #( boton_refrescar = &apos;X&apos;
                             boton_excel     = &apos;Y&apos;
                             boton_nuevo     = &apos;X&apos;
                             boton_borrar    = &apos;X&apos;
                             o_prog          = o_prog ).

    o_prog-&gt;o_alv   = NEW #( estructura = &apos;&apos;
                             o_event    = o_prog-&gt;o_event ).

    p_vari = o_prog-&gt;o_alv-&gt;get_default_layout( ).
  ENDIF.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.

  IF NOT o_prog-&gt;o_alv IS INITIAL.
    p_vari = o_prog-&gt;o_alv-&gt;get_f4_layout( ).
  ENDIF.



START-OF-SELECTION.
  o_prog-&gt;configuracion( ).
  o_prog-&gt;buscar_datos( ).

  IF sy-batch IS INITIAL.
    CALL SCREEN 0100.
  ELSE.
    MESSAGE &apos;Este programa no se puede ejecutar en fondo&apos;(pnf) TYPE &apos;E&apos;.
  ENDIF.

*&amp;---------------------------------------------------------------------*
*&amp;      Module  STATUS_0100  OUTPUT
*&amp;---------------------------------------------------------------------*

MODULE status_0100 OUTPUT.

  o_prog-&gt;status_dynpro_0100( ).

ENDMODULE.
*&amp;---------------------------------------------------------------------*
*&amp;      Module  USER_COMMAND_0100  INPUT
*&amp;---------------------------------------------------------------------*
MODULE user_command_0100 INPUT.

  o_prog-&gt;command_dynpro_0100( ).


ENDMODULE.</source>
</PROG>
